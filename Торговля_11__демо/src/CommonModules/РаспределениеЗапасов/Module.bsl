////////////////////////////////////////////////////////////////////////////////
// Модуль "Распределение запасов", содержит процедуры и функции
// чтения и записи в регистр сведений "Распределение запасов".
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Определяет включен ли в базе производительный режим распределения запасов. В случае, если производительный режим
// включен, в запросах получения остатков необходимо максимально задействовать регистр накопления Запасы и потребности.
// Использовать регистр сведений Распределение запасов нужно только для получения информации о распределении запасов
// на конкретные заказы на отгрузку.
// Возвращаемое значение:
//  Булево - Истина, если производительный режим распределения запасов включен, Ложь - в противном случае.
Функция ЭтоПроизводительныйРежим() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Возврат Константы.ИспользоватьПроизводительныйРежимЗаписиИнформацииОДоступностиТоваровИРабот.Получить();
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура АктуализацияПотребностейПоГраницеОбеспеченияРегламентноеЗадание() Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(
		Метаданные.РегламентныеЗадания.АктуализацияПотребностейПоГраницеОбеспечения);
		
	УстановитьПривилегированныйРежим(Истина);
	
	Если РаспределениеЗапасов.ЭтоПроизводительныйРежим() Тогда
		Текст =
			"ВЫБРАТЬ
			|	ДанныеИБ.Номенклатура КАК Номенклатура,
			|	ДанныеИБ.Характеристика КАК Характеристика,
			|	ДанныеИБ.Склад КАК Склад,
			|	ДанныеИБ.Назначение КАК Назначение,
			|	ДанныеИБ.Заказ КАК Заказ,
			|	ДанныеИБ.ДатаСобытия КАК ДатаСобытия,
			|	ДанныеИБ.РезервироватьПоМереПоступленияОстаток КАК РезервироватьПоМереПоступления,
			|	ДанныеИБ.ОтложитьРезервированиеОстаток КАК ОтложитьРезервирование
			|ПОМЕСТИТЬ ДанныеЗаказов
			|ИЗ
			|	РегистрНакопления.ЗапасыИПотребности.Остатки(, ДатаСобытия > ДАТАВРЕМЯ(1,1,1)) КАК ДанныеИБ
			|;
			|
			|/////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	Товары.Номенклатура КАК Номенклатура,
			|	Товары.Характеристика КАК Характеристика,
			|	Товары.Склад КАК Склад
			|ПОМЕСТИТЬ РазличныеТовары
			|ИЗ
			|	ДанныеЗаказов КАК Товары
			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура, Характеристика, Склад
			|;
			|
			|/////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	РазличныеТовары.Номенклатура КАК Номенклатура,
			|	РазличныеТовары.Характеристика КАК Характеристика,
			|	РазличныеТовары.Склад КАК Склад,
			|	РасчетПереопределяемый.ГраницаПериода КАК Дата
			|ПОМЕСТИТЬ ГраницыПериодаОбеспечения
			|ИЗ
			|	РазличныеТовары КАК РазличныеТовары
			|		
			|		ЛЕВОЕ СОЕДИНЕНИЕ РасчетПереопределяемый ПО ИСТИНА
			|ГДЕ
			|	ЕСТЬNULL(РасчетПереопределяемый.ГраницаПериода, ДАТАВРЕМЯ(1, 1, 1)) > &НачалоТекущегоДня
			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура, Характеристика, Склад
			|;
			|
			|/////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	Движения.Регистратор КАК Регистратор
			|ИЗ
			|	ДанныеЗаказов КАК ДанныеЗаказов
			|		ЛЕВОЕ СОЕДИНЕНИЕ ГраницыПериодаОбеспечения КАК ГраницыПериода
			|		ПО ГраницыПериода.Номенклатура = ДанныеЗаказов.Номенклатура
			|		 И ГраницыПериода.Характеристика = ДанныеЗаказов.Характеристика
			|		 И ГраницыПериода.Склад = ДанныеЗаказов.Склад
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗапасыИПотребности КАК Движения
			|		ПО Движения.Заказ = ДанныеЗаказов.Заказ
			|ГДЕ
			|	ДанныеЗаказов.ОтложитьРезервирование <> 0
			|			И (ГраницыПериода.Дата ЕСТЬ NULL ИЛИ ГраницыПериода.Дата >= ДанныеЗаказов.ДатаСобытия)
			|		ИЛИ ДанныеЗаказов.РезервироватьПоМереПоступления <> 0
			|			И ГраницыПериода.Дата < ДанныеЗаказов.ДатаСобытия";
		
		Подстановки = ОбеспечениеВДокументахСервер.ПодстановкиГраницыОбеспечиваемогоПериода(
			"РазличныеТовары.Номенклатура", "РазличныеТовары.Характеристика", "РазличныеТовары.Склад", "&НачалоТекущегоДня", "ИСТИНА");
		
		Текст = СтрЗаменить(Текст, "РасчетПереопределяемый.ГраницаПериода", Подстановки.Поле);
		Текст = СтрЗаменить(Текст, "ЛЕВОЕ СОЕДИНЕНИЕ РасчетПереопределяемый ПО ИСТИНА", Подстановки.Соединения);
		
		Запрос = Новый Запрос(Текст);
		Запрос.УстановитьПараметр("НачалоТекущегоДня", НачалоДня(ТекущаяДатаСеанса()));
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Набор = РегистрыНакопления.ЗапасыИПотребности.СоздатьНаборЗаписей();
		Пока Выборка.Следующий() Цикл
			
			НачатьТранзакцию();
			Попытка
				МетаданныеДокумента = Выборка.Регистратор.Метаданные(); // ОбъектМетаданныхДокумент -
				БлокировкаДанных = Новый БлокировкаДанных();
				ЭлементБлокировки = БлокировкаДанных.Добавить(МетаданныеДокумента.ПолноеИмя());
				ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Регистратор);
				ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
				БлокировкаДанных.Заблокировать();
				
				ЗаписатьДвижения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Выборка.Регистратор, "Проведен");
				Если ТипЗнч(ЗаписатьДвижения) <> Тип("Булево") Тогда
					ЗаписатьДвижения = Ложь;
				КонецЕсли;
				Если ЗаписатьДвижения Тогда
					НаборЗаписей = РегистрыНакопления.ЗапасыИПотребности.СоздатьНаборЗаписей();
					НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
					Набор.Отбор.Регистратор.Значение = Выборка.Регистратор;
					Набор.Прочитать();
					ВсегоСтрок = Набор.Количество();
					Для Счетчик = 1 По ВсегоСтрок Цикл
						СтрокаНабора = Набор[ВсегоСтрок - Счетчик];
						Если СтрокаНабора.ТипЗаписи <> Перечисления.ТипыЗаписейЗапасыИПотребности.ПервичнаяЗапись Тогда
							Набор.Удалить(СтрокаНабора);
						КонецЕсли;
					КонецЦикла;
					МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
					Набор.ДополнительныеСвойства.Вставить("РассчитыватьИзменения", Истина);
					Набор.ДополнительныеСвойства.Вставить("СвойстваДокумента", Новый Структура("ЭтоНовый", Ложь));
					Набор.ДополнительныеСвойства.Вставить("МенеджерВременныхТаблиц", МенеджерВременныхТаблиц);
					Набор.ДополнительныеСвойства.Вставить("ТаблицыКонтроля", Новый Структура);
					Набор.ДополнительныеСвойства.Вставить("АктуализацияОтложенногоОбеспечения");
					Набор.Записать();
					ДокументСтруктура = Новый Структура("Ссылка", Неопределено);
					РаспределениеЗапасовДвижения.ОтразитьЗаданияКРаспределениюЗапасов(ДокументСтруктура, МенеджерВременныхТаблиц);
				КонецЕсли;
			ЗафиксироватьТранзакцию();
			Исключение
			
				ОтменитьТранзакцию();
				ТекстСообщения = НСтр("ru = 'Регламентное задание ""Актуализация потребностей по границе обеспечения"" завершилось неудачно по причине: %Причина%'");
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				СобытиеЖурнала = НСтр("ru = 'Распределение запасов'", ОбщегоНазначения.КодОсновногоЯзыка());
				ЗаписьЖурналаРегистрации(СобытиеЖурнала,
				                         УровеньЖурналаРегистрации.Ошибка,
				                         Метаданные.РегламентныеЗадания.АктуализацияПотребностейПоГраницеОбеспечения,
				                         ,
				                         ТекстСообщения);
				
			КонецПопытки;
		КонецЦикла;
		Возврат;
	КонецЕсли;
	
	ДосчитыватьРегистрРегламентнымЗаданием = ДосчитыватьРегистрРегламентнымЗаданием();
	
	Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеИБ.Номенклатура    КАК Номенклатура,
		|	ДанныеИБ.Характеристика  КАК Характеристика,
		|	ДанныеИБ.Склад           КАК Склад,
		|	ДанныеИБ.Назначение      КАК Назначение
		|ИЗ
		|	РегистрСведений.РаспределениеЗапасов КАК ДанныеИБ
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РасчетПереопределяемый ПО ИСТИНА
		|		
		|ГДЕ
		|	ДанныеИБ.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемаяОтгрузка)
		|		И ВЫБОР КОГДА ЕСТЬNULL(РасчетПереопределяемый.ГраницаПериода, ДанныеИБ.ЖелаемаяДатаОтгрузки) >= ДанныеИБ.ЖелаемаяДатаОтгрузки
		|						ИЛИ РасчетПереопределяемый.ГраницаПериода = ДАТАВРЕМЯ(1, 1, 1) ТОГДА
		|					ДанныеИБ.ОтложитьРезервирование > 0
		|				ИНАЧЕ
		|					ДанныеИБ.РезервироватьПоМереПоступления > 0
		|			КОНЕЦ";
	
	Подстановки = ОбеспечениеВДокументахСервер.ПодстановкиГраницыОбеспечиваемогоПериода(
		"ДанныеИБ.Номенклатура", "ДанныеИБ.Характеристика", "ДанныеИБ.Склад", "&НачалоТекущегоДня", "ИСТИНА");
	
	Текст = СтрЗаменить(Текст, "РасчетПереопределяемый.ГраницаПериода",             Подстановки.Поле);
	Текст = СтрЗаменить(Текст, "ЛЕВОЕ СОЕДИНЕНИЕ РасчетПереопределяемый ПО ИСТИНА", Подстановки.Соединения);
	
	Запрос = Новый Запрос(Текст);
	Запрос.УстановитьПараметр("НачалоТекущегоДня", НачалоДня(ТекущаяДатаСеанса()));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Текст =
		"ВЫБРАТЬ
		|	ДанныеИБ.Состояние                     КАК Состояние,
		|	ДанныеИБ.Номенклатура                  КАК Номенклатура,
		|	ДанныеИБ.Характеристика                КАК Характеристика,
		|	ДанныеИБ.Склад                         КАК Склад,
		|	ДанныеИБ.Назначение                    КАК Назначение,
		|	ДанныеИБ.ЗаказНаОтгрузку               КАК ЗаказНаОтгрузку,
		|	ДанныеИБ.ЖелаемаяДатаОтгрузки          КАК ЖелаемаяДатаОтгрузки,
		|	ДанныеИБ.ЗаказНаПоступление            КАК ЗаказНаПоступление,
		|	ДанныеИБ.ДатаПоступления               КАК ДатаПоступления,
		|	ДанныеИБ.ТипЗаписиРаспределенияЗапасов КАК ТипЗаписиРаспределенияЗапасов,
		|	ДанныеИБ.Резервировать                 КАК Резервировать,
		|	ДанныеИБ.НеОбеспечивать                КАК НеОбеспечивать,
		// Ресурсы меняются местами.
		|	ДанныеИБ.ОтложитьРезервирование        КАК РезервироватьПоМереПоступления,
		|	ДанныеИБ.КОбеспечениюБезРезерва        КАК КОбеспечению,
		|	ДанныеИБ.РезервироватьПоМереПоступления КАК ОтложитьРезервирование
		|ПОМЕСТИТЬ ТаблицаИзменений
		|ИЗ
		|	РегистрСведений.РаспределениеЗапасов КАК ДанныеИБ
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РасчетПереопределяемый ПО ИСТИНА
		|		
		|ГДЕ
		|	ДанныеИБ.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемаяОтгрузка)
		|		И ВЫБОР КОГДА ЕСТЬNULL(РасчетПереопределяемый.ГраницаПериода, ДанныеИБ.ЖелаемаяДатаОтгрузки) >= ДанныеИБ.ЖелаемаяДатаОтгрузки
		|						ИЛИ РасчетПереопределяемый.ГраницаПериода = ДАТАВРЕМЯ(1, 1, 1) ТОГДА
		|					ДанныеИБ.ОтложитьРезервирование > 0
		|				ИНАЧЕ
		|					ДанныеИБ.РезервироватьПоМереПоступления > 0
		|			КОНЕЦ
		|		И ДанныеИБ.Номенклатура = &Номенклатура
		|		И ДанныеИБ.Характеристика = &Характеристика
		|		И ДанныеИБ.Склад = &Склад
		|		И ДанныеИБ.Назначение = &Назначение
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение, ЗаказНаОтгрузку
		|;
		|
		|/////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Таблица.Номенклатура                  КАК Номенклатура,
		|	Таблица.Характеристика                КАК Характеристика,
		|	Таблица.Склад                         КАК Склад,
		|	Таблица.Назначение                    КАК Назначение,
		|	Таблица.ЗаказНаОтгрузку               КАК ЗаказНаОтгрузку,
		|	МАКСИМУМ(ЕСТЬNULL(ДанныеИБ.РезервироватьПоМереПоступленияИтогНаДату, 0)) КАК РезервироватьПоМереПоступленияИтогНаДату
		|ПОМЕСТИТЬ ПотребностиРезервироватьПоМереПоступленияИтогНаДату
		|ИЗ
		|	ТаблицаИзменений КАК Таблица
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаспределениеЗапасов КАК ДанныеИБ
		|		ПО ДанныеИБ.Номенклатура = Таблица.Номенклатура
		|		 И ДанныеИБ.Характеристика = Таблица.Характеристика
		|		 И ДанныеИБ.Склад = Таблица.Склад
		|		 И ДанныеИБ.Назначение = Таблица.Назначение
		|		 И ДанныеИБ.ЗаказНаОтгрузку = Таблица.ЗаказНаОтгрузку
		|		 И ДанныеИБ.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемаяОтгрузка)
		|СГРУППИРОВАТЬ ПО
		|	Таблица.Номенклатура,
		|	Таблица.Характеристика,
		|	Таблица.Склад,
		|	Таблица.Назначение,
		|	Таблица.ЗаказНаОтгрузку
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение, ЗаказНаОтгрузку
		|;
		|
		|/////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеИБ.Состояние                     КАК Состояние,
		|	ДанныеИБ.Номенклатура                  КАК Номенклатура,
		|	ДанныеИБ.Характеристика                КАК Характеристика,
		|	ДанныеИБ.Склад                         КАК Склад,
		|	ДанныеИБ.Назначение                    КАК Назначение,
		|	ДанныеИБ.ЗаказНаОтгрузку               КАК ЗаказНаОтгрузку,
		|	ДанныеИБ.ЖелаемаяДатаОтгрузки          КАК ЖелаемаяДатаОтгрузки,
		|	ДанныеИБ.ЗаказНаПоступление            КАК ЗаказНаПоступление,
		|	ДанныеИБ.ДатаПоступления               КАК ДатаПоступления,
		|	ДанныеИБ.ТипЗаписиРаспределенияЗапасов КАК ТипЗаписиРаспределенияЗапасов,
		|	ДанныеИБ.Резервировать                 КАК Резервировать,
		|	ДанныеИБ.НеОбеспечивать                КАК НеОбеспечивать,
		|	ДанныеИБ.КОбеспечению                  КАК КОбеспечениюБезРезерва,
		|	ДанныеИБ.РезервироватьПоМереПоступления КАК РезервироватьПоМереПоступления,
		|	ДанныеИБ.ОтложитьРезервирование        КАК ОтложитьРезервирование,
		|	Таблица.РезервироватьПоМереПоступленияИтогНаДату КАК РезервироватьПоМереПоступленияИтогНаДату
		|ИЗ
		|	ТаблицаИзменений КАК ДанныеИБ
		|		
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПотребностиРезервироватьПоМереПоступленияИтогНаДату КАК Таблица
		|		ПО Таблица.Номенклатура = ДанныеИБ.Номенклатура
		|		 И Таблица.Характеристика = ДанныеИБ.Характеристика
		|		 И Таблица.Склад = ДанныеИБ.Склад
		|		 И Таблица.Назначение = ДанныеИБ.Назначение
		|		 И Таблица.ЗаказНаОтгрузку = ДанныеИБ.ЗаказНаОтгрузку
		|УПОРЯДОЧИТЬ ПО
		|	ЗаказНаОтгрузку, ЖелаемаяДатаОтгрузки";
	
	Подстановки = ОбеспечениеВДокументахСервер.ПодстановкиГраницыОбеспечиваемогоПериода(
		"ДанныеИБ.Номенклатура", "ДанныеИБ.Характеристика", "ДанныеИБ.Склад", "&НачалоТекущегоДня", "ИСТИНА");
	
	Текст = СтрЗаменить(Текст, "РасчетПереопределяемый.ГраницаПериода",             Подстановки.Поле);
	Текст = СтрЗаменить(Текст, "ЛЕВОЕ СОЕДИНЕНИЕ РасчетПереопределяемый ПО ИСТИНА", Подстановки.Соединения);
	
	Запрос.Текст = Текст;
	
	Набор = РегистрыСведений.РаспределениеЗапасов.СоздатьНаборЗаписей();
	НаборРаспределенияЗапасов = РегистрыСведений.РаспределениеЗапасов.СоздатьНаборЗаписей();
	
	НовыйРазделитель = Новый УникальныйИдентификатор();
	НаборЗаданий = РегистрыСведений.ЗаданияКРаспределениюЗапасов.СоздатьНаборЗаписей();
	НаборЗаданий.Отбор.РазделительЗаписи.Установить(НовыйРазделитель);
	ТекущаяДатаСеанса = ТекущаяДатаСеанса();
	
	РезультатРаспределенияЗапасов = РегистрыСведений.РаспределениеЗапасов.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	ЗаказыНаОтгрузку = Новый Соответствие();
	МассивЗаказов = Новый Массив();
	
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		Попытка
			
			БлокировкаДанных = Новый БлокировкаДанных();
			
			ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.РаспределениеЗапасов");
			ЭлементБлокировки.УстановитьЗначение("Номенклатура", Выборка.Номенклатура);
			ЭлементБлокировки.УстановитьЗначение("Характеристика", Выборка.Характеристика);
			ЭлементБлокировки.УстановитьЗначение("Склад", Выборка.Склад);
			ЭлементБлокировки.УстановитьЗначение("Назначение", Выборка.Назначение);
			ЭлементБлокировки.УстановитьЗначение("ТипЗаписиРаспределенияЗапасов", 1);
			БлокировкаДанных.Заблокировать();
			
			Запрос.УстановитьПараметр("Номенклатура", Выборка.Номенклатура);
			Запрос.УстановитьПараметр("Характеристика", Выборка.Характеристика);
			Запрос.УстановитьПараметр("Склад", Выборка.Склад);
			Запрос.УстановитьПараметр("Назначение", Выборка.Назначение);
			
			Запись = Запрос.Выполнить().Выбрать();
			
			ЗаказыНаОтгрузку.Очистить();
			МассивЗаказов.Очистить();
			ТекущийЗаказ = Неопределено;
			ТекущийИтог = 0;
			Пока Запись.Следующий() Цикл
				
				Если ТекущийЗаказ <> Запись.ЗаказНаОтгрузку Тогда
					ТекущийЗаказ = Запись.ЗаказНаОтгрузку;
					ТекущийИтог = Запись.РезервироватьПоМереПоступленияИтогНаДату;
				КонецЕсли;
				
				Набор.Очистить();
				Набор.Отбор.ТипЗаписиРаспределенияЗапасов.Установить(Запись.ТипЗаписиРаспределенияЗапасов);
				Набор.Отбор.Состояние.Установить(Запись.Состояние);
				Набор.Отбор.Номенклатура.Установить(Запись.Номенклатура);
				Набор.Отбор.Характеристика.Установить(Запись.Характеристика);
				Набор.Отбор.Склад.Установить(Запись.Склад);
				Набор.Отбор.Назначение.Установить(Запись.Назначение);
				Набор.Отбор.ЗаказНаОтгрузку.Установить(Запись.ЗаказНаОтгрузку);
				Набор.Отбор.ЖелаемаяДатаОтгрузки.Установить(Запись.ЖелаемаяДатаОтгрузки);
				Набор.Отбор.ЗаказНаПоступление.Значение = Запись.ЗаказНаПоступление;
				Набор.Отбор.ЗаказНаПоступление.Использование = Истина;
				Набор.Отбор.ДатаПоступления.Установить(Запись.ДатаПоступления);
				
				НоваяСтрока = Набор.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Запись);
				Если НоваяСтрока.ОтложитьРезервирование > 0 Тогда
					НоваяСтрока.РезервироватьПоМереПоступленияИтогНаДату = 0;
				Иначе
					ТекущийИтог = ТекущийИтог + НоваяСтрока.РезервироватьПоМереПоступления;
					НоваяСтрока.РезервироватьПоМереПоступленияИтогНаДату = ТекущийИтог;
				КонецЕсли;
				
				Набор.Записать();
				
				Если ЗаказыНаОтгрузку.Получить(Запись.ЗаказНаОтгрузку) = Неопределено Тогда
					
					ЗаказыНаОтгрузку.Вставить(Запись.ЗаказНаОтгрузку, 1);
					Если ДосчитыватьРегистрРегламентнымЗаданием Тогда
						
						Задание = НаборЗаданий.Добавить();
						ЗаполнитьЗначенияСвойств(Задание, Выборка);
						Задание.ЗаказНаОтгрузку = Запись.ЗаказНаОтгрузку;
						Задание.РазделительЗаписи = НовыйРазделитель;
						Задание.ДатаЗаписи = ТекущаяДатаСеанса;
						
					Иначе
						МассивЗаказов.Добавить(Запись.ЗаказНаОтгрузку);
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЦикла;
			
			ЗапросТоваров = Новый Запрос();
			ЗапросТоваров.УстановитьПараметр("Номенклатура", Выборка.Номенклатура);
			ЗапросТоваров.УстановитьПараметр("Характеристика", Выборка.Характеристика);
			ЗапросТоваров.УстановитьПараметр("Склад", Выборка.Склад);
			ЗапросТоваров.УстановитьПараметр("Назначение", Выборка.Назначение);
			ЗапросТоваров.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
			ЗапросТоваров.Текст =
				"ВЫБРАТЬ
				|	&Номенклатура   КАК Номенклатура,
				|	&Характеристика КАК Характеристика,
				|	&Склад          КАК Склад,
				|	&Назначение     КАК Назначение,
				|	ЛОЖЬ            КАК РаспределятьЕслиОстатокНеотрицательный
				|ПОМЕСТИТЬ РазличныеТовары";
				
			ЗапросТоваров.Выполнить();
			РаспределениеЗапасовДвижения.ЗаписатьРаспределениеЗапасовНаПотребностиПоТаблицеРазличныеТовары(ЗапросТоваров);
			
			Если Не ДосчитыватьРегистрРегламентнымЗаданием Тогда
				
				РаспределениеЗапасовДвижения.ДобавитьПустуюТаблицуИзменениеОстаткаПоТоварамДляКонтроляОстатков(ЗапросТоваров);
				
				ЗапросТоваров.Текст = ТекстЗапросаРаспределенияЗапасов(Истина, "РазличныеТовары");
				ЗапросТоваров.УстановитьПараметр("НачалоТекущегоДня", НачалоДня(ТекущаяДатаСеанса()));
				Пакет = ЗапросТоваров.ВыполнитьПакет();
				Таблица = Пакет[10].Выгрузить();
				График = Пакет[11].Выгрузить();
				
				РезультатРаспределенияЗапасов.Очистить();
				РаспределитьЗапасыВТаблицу(РезультатРаспределенияЗапасов, Таблица, График, Ложь);
				НаборРаспределенияЗапасов.Отбор.ТипЗаписиРаспределенияЗапасов.Установить(0);
				НаборРаспределенияЗапасов.Отбор.Номенклатура.Установить(Запись.Номенклатура);
				НаборРаспределенияЗапасов.Отбор.Характеристика.Установить(Запись.Характеристика);
				НаборРаспределенияЗапасов.Отбор.Склад.Установить(Запись.Склад);
				НаборРаспределенияЗапасов.Отбор.Назначение.Установить(Запись.Назначение);
				
				НаборРаспределенияЗапасов.Загрузить(РезультатРаспределенияЗапасов);
				НаборРаспределенияЗапасов.Записать();
				
				ИсключитьСсылкиТребующиеОтложенногоОбновления(МассивЗаказов);
				РегистрыСведений.СостоянияЗаказовКлиентов.ОтразитьСостояниеЗаказа(МассивЗаказов, Ложь, Ложь);
				РегистрыСведений.СостоянияВнутреннихЗаказов.ОтразитьСостояниеЗаказа(МассивЗаказов);
				
			КонецЕсли;
			
			ЗапросТоваров.МенеджерВременныхТаблиц.Закрыть();
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			ТекстСообщения = НСтр("ru = 'Регламентное задание ""Актуализация потребностей по границе обеспечения"" завершилось неудачно по причине: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			СобытиеЖурнала = НСтр("ru = 'Распределение запасов'", ОбщегоНазначения.КодОсновногоЯзыка());
			ЗаписьЖурналаРегистрации(СобытиеЖурнала,
			                         УровеньЖурналаРегистрации.Ошибка,
			                         Метаданные.РегламентныеЗадания.АктуализацияПотребностейПоГраницеОбеспечения,
			                         ,
			                         ТекстСообщения);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Если ДосчитыватьРегистрРегламентнымЗаданием Тогда
		
		НаборЗаданий.Записать();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура РаспределениеЗапасовРегламентноеЗадание() Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.РаспределениеЗапасов);
	Отбор = Новый Структура;
	Отбор.Вставить("Ключ", Метаданные.РегламентныеЗадания.РаспределениеЗапасов.Ключ);
	Отбор.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
	ТекущиеФоновыеЗадания = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);
	
	Если ТекущиеФоновыеЗадания.Количество() < 2 Тогда // РЗ имеет такой же ключ как и ФЗ, поэтому тоже учитывается.
		
		ФоновыеЗадания.Выполнить(
			"РаспределениеЗапасов.ВыполнитьРаспределениеВФоне",
			Новый Массив(),
			Метаданные.РегламентныеЗадания.РаспределениеЗапасов.Ключ,
			НСтр("ru = 'Выполняется распределение запасов'"));
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьРаспределениеВФоне() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Если БлокироватьРаспределениеЗапасов() Тогда
		Возврат;
	КонецЕсли;
	
	Пока Истина Цикл
		
		Запрос = Новый Запрос();
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
		
		Запрос.Текст =
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	Задания.Номенклатура КАК Номенклатура,
			|	Задания.Характеристика КАК Характеристика,
			|	Задания.Склад КАК Склад,
			|	Задания.Назначение КАК Назначение
			|ПОМЕСТИТЬ ТоварыДляРаспределенияЗапасов
			|ИЗ(
			|	ВЫБРАТЬ ПЕРВЫЕ 1000
			|		Задания.Номенклатура КАК Номенклатура,
			|		Задания.Характеристика КАК Характеристика,
			|		Задания.Склад КАК Склад,
			|		Задания.Назначение КАК Назначение,
			|		Задания.ДатаЗаписи КАК ДатаЗаписи
			|	ИЗ
			|		РегистрСведений.ЗаданияКРаспределениюЗапасов КАК Задания
			|	УПОРЯДОЧИТЬ ПО
			|		ДатаЗаписи) КАК Задания
			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура, Характеристика, Склад, Назначение
			|;
			|
			|////////////////////////////////////////
			|ВЫБРАТЬ
			|	Задания.Номенклатура КАК Номенклатура,
			|	Задания.Характеристика КАК Характеристика,
			|	Задания.Склад КАК Склад,
			|	Задания.Назначение КАК Назначение,
			|	Задания.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
			|	Задания.РазделительЗаписи КАК РазделительЗаписи,
			|	Задания.КСнятиюРезерва КАК КСнятиюРезерва
			|ПОМЕСТИТЬ ЗаданияКРаспределениюЗапасов
			|ИЗ
			|	ТоварыДляРаспределенияЗапасов КАК Товары
			|		
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗаданияКРаспределениюЗапасов КАК Задания
			|		ПО Задания.Номенклатура = Товары.Номенклатура
			|		 И Задания.Характеристика = Товары.Характеристика
			|		 И Задания.Склад = Товары.Склад
			|		 И Задания.Назначение = Товары.Назначение
			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура, Характеристика, Склад, Назначение
			|;
			|
			|//////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Задания.Номенклатура КАК Номенклатура,
			|	Задания.Характеристика КАК Характеристика,
			|	Задания.Склад КАК Склад,
			|	Задания.Назначение КАК Назначение,
			|	Задания.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
			|	СУММА(Задания.КСнятиюРезерва) КАК Отгрузка
			|ПОМЕСТИТЬ ИзменениеОстаткаПоТоварамДляКонтроляОстатков
			|ИЗ
			|	ЗаданияКРаспределениюЗапасов КАК Задания
			|ГДЕ
			|	Задания.КСнятиюРезерва > 0
			|СГРУППИРОВАТЬ ПО
			|	Задания.Номенклатура,
			|	Задания.Характеристика,
			|	Задания.Склад,
			|	Задания.Назначение,
			|	Задания.ЗаказНаОтгрузку
			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура, Характеристика, Склад, Назначение, ЗаказНаОтгрузку";
		
		РезультатЗапроса = Запрос.ВыполнитьПакет();
		
		Если РезультатЗапроса[1].Выгрузить()[0].Количество = 0 Тогда
			Прервать;
		КонецЕсли;
		
		Набор = РегистрыСведений.РаспределениеЗапасов.СоздатьНаборЗаписей();
		ТаблицаРезультат = Набор.ВыгрузитьКолонки();
		ТаблицаРезультат.Колонки.Добавить("Пустая", Новый ОписаниеТипов("Булево"));
		Запрос.УстановитьПараметр("НачалоТекущегоДня", НачалоДня(ТекущаяДатаСеанса()));
		ЭтоПроизводительныйРежим = ЭтоПроизводительныйРежим();
		Если ЭтоПроизводительныйРежим Тогда
			
			ТаблицаДляОбновленияРесурсаВНаличии = ВременныеТаблицыДанныхРегистра(Запрос);
			ВременнаяТаблицаПотребностиРезервироватьПоМере(Запрос);
			ВременнаяТаблицаЗапасыИДоступностьПоДатам(Запрос);
			ВременнаяТаблицаРезультатРаспределенияЗапасовНаПотребности(Запрос);
			ТаблицаРезультат = ДанныеДляЗаписиТоваровВПроизводительномРежиме(Запрос);
			
		Иначе
			
			Набор.Отбор.ТипЗаписиРаспределенияЗапасов.Установить(0);
			
			Запрос.Текст = ТекстЗапросаРаспределенияЗапасов();
			Пакет = Запрос.ВыполнитьПакет();
			
			Таблица = Пакет[10].Выгрузить();
			График = Пакет[11].Выгрузить();
			
			РаспределитьЗапасыВТаблицу(ТаблицаРезультат, Таблица, График, Истина);
			
		КонецЕсли;
		
		Отбор = Новый Структура();
		Отбор.Вставить("Номенклатура");
		Отбор.Вставить("Характеристика");
		Отбор.Вставить("Склад");
		Отбор.Вставить("Назначение");
		НаборЗаписанЦеликом = Ложь;
		ТаблицаОшибок = Новый ТаблицаЗначений();
		ТаблицаОшибок.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
		ТаблицаОшибок.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
		ТаблицаОшибок.Колонки.Добавить("Склад", Новый ОписаниеТипов("СправочникСсылка.Склады"));
		ТаблицаОшибок.Колонки.Добавить("Назначение", Новый ОписаниеТипов("СправочникСсылка.Назначения"));
		Если ЭтоПроизводительныйРежим Тогда
			
			НачатьТранзакцию();
			Попытка
				
				Для Индекс = 0 По ТаблицаРезультат.Количество() - 1 Цикл
					
					Строка = ТаблицаРезультат[Индекс];
					
					Если Отбор.Номенклатура <> Строка.Номенклатура
							Или Отбор.Характеристика <> Строка.Характеристика
							Или Отбор.Склад <> Строка.Склад
							Или Отбор.Назначение <> Строка.Назначение Тогда
								
								Если Отбор.Номенклатура <> Неопределено Тогда
									Набор.Записать();
								КонецЕсли;
								
								ЗаполнитьЗначенияСвойств(Отбор, Строка);
								
								Набор.Очистить();
								Набор.Отбор.Номенклатура.Установить(Отбор.Номенклатура);
								Набор.Отбор.Характеристика.Установить(Отбор.Характеристика);
								Набор.Отбор.Склад.Установить(Отбор.Склад);
								Набор.Отбор.Назначение.Установить(Отбор.Назначение);
								
					КонецЕсли;
					
					Если Не Строка.Пустая Тогда
						ЗаполнитьЗначенияСвойств(Набор.Добавить(), Строка);
					КонецЕсли;
					
				КонецЦикла;
				
				Если ТаблицаРезультат.Количество() > 0 Тогда
					Набор.Записать();
				КонецЕсли;
				
				Для Индекс = 0 По ТаблицаДляОбновленияРесурсаВНаличии.Количество() - 1 Цикл
					
					Строка = ТаблицаДляОбновленияРесурсаВНаличии[Индекс];
					Набор.Очистить();
					Набор.Отбор.Номенклатура.Установить(Строка.Номенклатура);
					Набор.Отбор.Характеристика.Установить(Строка.Характеристика);
					Набор.Отбор.Склад.Установить(Строка.Склад);
					Набор.Отбор.Назначение.Установить(Строка.Назначение);
					Набор.Отбор.Состояние.Установить(Строка.Состояние);
					
					Если Не Строка.Пустая Тогда
						ЗаполнитьЗначенияСвойств(Набор.Добавить(), Строка);
					КонецЕсли;
					Набор.Записать();
					
				КонецЦикла;
				
				НаборЗаписанЦеликом = Истина;
				ЗафиксироватьТранзакцию();
				
			Исключение
				ОтменитьТранзакцию();
				СообщитьОНеудачнойОбработкеНоменклатуры();
				НаборЗаписанЦеликом = Ложь;
			КонецПопытки;
			
		Иначе
			Для Индекс = 0 По ТаблицаРезультат.Количество() - 1 Цикл
				
				Строка = ТаблицаРезультат[Индекс];
				
				Если Отбор.Номенклатура <> Строка.Номенклатура
						Или Отбор.Характеристика <> Строка.Характеристика
						Или Отбор.Склад <> Строка.Склад
						Или Отбор.Назначение <> Строка.Назначение Тогда
							
							Если Отбор.Номенклатура <> Неопределено Тогда
								
								НачатьТранзакцию();
								Попытка
									Набор.Записать();
									ЗафиксироватьТранзакцию();
								Исключение
									ОтменитьТранзакцию();
									СообщитьОНеудачнойОбработкеНоменклатуры();
									ЗаполнитьЗначенияСвойств(ТаблицаОшибок.Добавить(), Отбор);
								КонецПопытки;
								
							КонецЕсли;
							
							ЗаполнитьЗначенияСвойств(Отбор, Строка);
							
							Набор.Очистить();
							Набор.Отбор.Номенклатура.Установить(Отбор.Номенклатура);
							Набор.Отбор.Характеристика.Установить(Отбор.Характеристика);
							Набор.Отбор.Склад.Установить(Отбор.Склад);
							Набор.Отбор.Назначение.Установить(Отбор.Назначение);
							
				КонецЕсли;
				
				Если Не Строка.Пустая Тогда
					ЗаполнитьЗначенияСвойств(Набор.Добавить(), Строка);
				КонецЕсли;
				
			КонецЦикла;
			
			Если ТаблицаРезультат.Количество() > 0 Тогда
				НачатьТранзакцию();
				Попытка
					Набор.Записать();
					ЗафиксироватьТранзакцию();
				Исключение
					ОтменитьТранзакцию();
					СообщитьОНеудачнойОбработкеНоменклатуры();
					ЗаполнитьЗначенияСвойств(ТаблицаОшибок.Добавить(), Отбор);
				КонецПопытки;
			КонецЕсли;
			
		КонецЕсли;
		
		Если Не ЭтоПроизводительныйРежим Или НаборЗаписанЦеликом Тогда
			
			Запрос.Текст =
				"ВЫБРАТЬ РАЗЛИЧНЫЕ
				|	Набор.Ссылка КАК Ссылка
				|ПОМЕСТИТЬ ВсеЗаказы
				|ИЗ(
				|	ВЫБРАТЬ
				|		Задания.ЗаказНаОтгрузку КАК Ссылка
				|	ИЗ
				|		ЗаданияКРаспределениюЗапасов КАК Задания
				|	ГДЕ
				|		Задания.ЗаказНаОтгрузку <> НЕОПРЕДЕЛЕНО
				|	
				|	ОБЪЕДИНИТЬ ВСЕ
				|	
				|	ВЫБРАТЬ
				|		ОжидаемаяОтгрузка.ЗаказНаОтгрузку КАК Ссылка
				|	ИЗ
				|		ЗаданияКРаспределениюЗапасов КАК Заказы
				|			
				|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаспределениеЗапасов КАК ОжидаемаяОтгрузка
				|			ПО ОжидаемаяОтгрузка.Номенклатура = Заказы.Номенклатура
				|			 И ОжидаемаяОтгрузка.Характеристика = Заказы.Характеристика
				|			 И ОжидаемаяОтгрузка.Склад = Заказы.Склад
				|			 И ОжидаемаяОтгрузка.Назначение = Заказы.Назначение
				|	ГДЕ
				|		ОжидаемаяОтгрузка.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемаяОтгрузка)) КАК Набор
				|ИНДЕКСИРОВАТЬ ПО
				|	Ссылка
				|;
				|
				|/////////////////////////////////////////////
				|ВЫБРАТЬ
				|	ВсеЗаказы.Ссылка КАК Ссылка
				|ИЗ
				|	ВсеЗаказы КАК ВсеЗаказы
				|ГДЕ
				|	НЕ ИСТИНА В(
				|		ВЫБРАТЬ ПЕРВЫЕ 1
				|			ИСТИНА КАК ЗаказЕстьВНеобработанныхЗаданиях
				|		ИЗ
				|			РегистрСведений.ЗаданияКРаспределениюЗапасов КАК ВсеЗадания
				|		ГДЕ
				|			ВсеЗадания.ЗаказНаОтгрузку = ВсеЗаказы.Ссылка
				|				И НЕ ИСТИНА В(
				|					ВЫБРАТЬ
				|						ИСТИНА КАК ЭтаНоменклатураОтработана
				|					ИЗ
				|						ЗаданияКРаспределениюЗапасов КАК ОтработанныеЗадания
				|					ГДЕ
				|						ОтработанныеЗадания.Номенклатура = ВсеЗадания.Номенклатура
				|							И ОтработанныеЗадания.Характеристика = ВсеЗадания.Характеристика
				|							И ОтработанныеЗадания.Склад = ВсеЗадания.Склад
				|							И ОтработанныеЗадания.Назначение = ВсеЗадания.Назначение))
				|	И НЕ ИСТИНА В(
				|		ВЫБРАТЬ ПЕРВЫЕ 1
				|			ИСТИНА КАК НоменклатураЗаказаЕстьВНеотработанныхЗаданиях
				|		ИЗ
				|			РегистрСведений.РаспределениеЗапасов КАК ОжидаемаяОтгрузка
				|		ГДЕ
				|			ОжидаемаяОтгрузка.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемаяОтгрузка)
				|				И ОжидаемаяОтгрузка.ЗаказНаОтгрузку = ВсеЗаказы.Ссылка
				|				И ИСТИНА В(
				|					ВЫБРАТЬ
				|						ИСТИНА КАК ЭтаНоменклатураЕстьВЗаданиях
				|					ИЗ
				|						РегистрСведений.ЗаданияКРаспределениюЗапасов КАК ВсеЗадания
				|					ГДЕ
				|						ВсеЗадания.Номенклатура = ОжидаемаяОтгрузка.Номенклатура
				|							И ВсеЗадания.Характеристика = ОжидаемаяОтгрузка.Характеристика
				|							И ВсеЗадания.Склад = ОжидаемаяОтгрузка.Склад
				|							И ВсеЗадания.Назначение = ОжидаемаяОтгрузка.Назначение)
				|					
				|				И НЕ ИСТИНА В(
				|					ВЫБРАТЬ
				|						ИСТИНА КАК ЭтаНоменклатураОтработана
				|					ИЗ
				|						ЗаданияКРаспределениюЗапасов КАК ОтработанныеЗадания
				|					ГДЕ
				|						ОтработанныеЗадания.Номенклатура = ОжидаемаяОтгрузка.Номенклатура
				|							И ОтработанныеЗадания.Характеристика = ОжидаемаяОтгрузка.Характеристика
				|							И ОтработанныеЗадания.Склад = ОжидаемаяОтгрузка.Склад
				|							И ОтработанныеЗадания.Назначение = ОжидаемаяОтгрузка.Назначение))";
			
			Заказы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
			ИсключитьСсылкиТребующиеОтложенногоОбновления(Заказы);
			РегистрыСведений.СостоянияЗаказовКлиентов.ОтразитьСостояниеЗаказа(Заказы, Ложь, Ложь);
			РегистрыСведений.СостоянияВнутреннихЗаказов.ОтразитьСостояниеЗаказа(Заказы);
			
			РезультатЗапроса = Запрос.МенеджерВременныхТаблиц.Таблицы["ЗаданияКРаспределениюЗапасов"].ПолучитьДанные(); // РезультатЗапроса
			ВыборкаЗаданий = РезультатЗапроса.Выбрать();
			
			ТаблицаОшибок.Индексы.Добавить("Номенклатура,Характеристика,Склад,Назначение");
			СтруктураОтбора = Новый Структура("Номенклатура,Характеристика,Склад,Назначение");
			Пока ВыборкаЗаданий.Следующий() Цикл
				
				Если ТаблицаОшибок.Количество() > 0 Тогда
					ЗаполнитьЗначенияСвойств(СтруктураОтбора, ВыборкаЗаданий);
					Если ТаблицаОшибок.НайтиСтроки(СтруктураОтбора).Количество() > 0 Тогда
						Продолжить;
					КонецЕсли;
				КонецЕсли;
				НаборЗаданий = РегистрыСведений.ЗаданияКРаспределениюЗапасов.СоздатьНаборЗаписей();
				НаборЗаданий.Отбор.Номенклатура.Установить(ВыборкаЗаданий.Номенклатура);
				НаборЗаданий.Отбор.Характеристика.Установить(ВыборкаЗаданий.Характеристика);
				НаборЗаданий.Отбор.Склад.Установить(ВыборкаЗаданий.Склад);
				НаборЗаданий.Отбор.Назначение.Установить(ВыборкаЗаданий.Назначение);
				НаборЗаданий.Отбор.ЗаказНаОтгрузку.Установить(ВыборкаЗаданий.ЗаказНаОтгрузку);
				НаборЗаданий.Отбор.РазделительЗаписи.Установить(ВыборкаЗаданий.РазделительЗаписи);
				НаборЗаданий.Записать();
				
			КонецЦикла;
			
		КонецЕсли;
		Запрос.МенеджерВременныхТаблиц.Закрыть();
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаписатьРаспределениеЗапасовВТранзакцииПроведения(Запрос) Экспорт
	
	ТаблицаДляОбновленияРесурсаВНаличии = ВременныеТаблицыДанныхРегистра(Запрос);
	ВременнаяТаблицаПотребностиРезервироватьПоМере(Запрос);
	ВременнаяТаблицаЗапасыИДоступностьПоДатам(Запрос);
	ВременнаяТаблицаРезультатРаспределенияЗапасовНаПотребности(Запрос);
	Результат = ДанныеДляЗаписиТоваровВПроизводительномРежиме(Запрос);
	
	ВсегоСтрок = Результат.Количество();
	
	Номенклатура   = Неопределено;
	Характеристика = Неопределено;
	Склад          = Неопределено;
	Назначение     = Неопределено;
	
	Набор = РегистрыСведений.РаспределениеЗапасов.СоздатьНаборЗаписей();
	
	Для Индекс = 0 По ВсегоСтрок - 1 Цикл
		
		ТекСтрока = Результат[Индекс];
		Если Номенклатура <> ТекСтрока.Номенклатура
				Или Характеристика <> ТекСтрока.Характеристика
				Или Склад <> ТекСтрока.Склад
				Или Назначение <> ТекСтрока.Назначение Тогда
					
					Если Номенклатура <> Неопределено Тогда
						Набор.Записать();
						Набор.Очистить();
					КонецЕсли;
					
					Номенклатура   = ТекСтрока.Номенклатура;
					Характеристика = ТекСтрока.Характеристика;
					Склад          = ТекСтрока.Склад;
					Назначение     = ТекСтрока.Назначение;
					
					Набор.Отбор.Номенклатура.Установить(Номенклатура);
					Набор.Отбор.Характеристика.Установить(Характеристика);
					Набор.Отбор.Склад.Установить(Склад);
					Набор.Отбор.Назначение.Установить(Назначение);
		КонецЕсли;
		
		Если Не ТекСтрока.Пустая Тогда
			ЗаполнитьЗначенияСвойств(Набор.Добавить(), ТекСтрока);
		КонецЕсли;
		
	КонецЦикла;
	
	Если Номенклатура <> Неопределено Тогда
		Набор.Записать();
	КонецЕсли;
	
	Для Индекс = 0 По ТаблицаДляОбновленияРесурсаВНаличии.Количество() - 1 Цикл
		
		Строка = ТаблицаДляОбновленияРесурсаВНаличии[Индекс];
		Набор.Очистить();
		Набор.Отбор.Номенклатура.Установить(Строка.Номенклатура);
		Набор.Отбор.Характеристика.Установить(Строка.Характеристика);
		Набор.Отбор.Склад.Установить(Строка.Склад);
		Набор.Отбор.Назначение.Установить(Строка.Назначение);
		Набор.Отбор.Состояние.Установить(Строка.Состояние);
		
		Если Не Строка.Пустая Тогда
			ЗаполнитьЗначенияСвойств(Набор.Добавить(), Строка);
		КонецЕсли;
		Набор.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

Функция ВременныеТаблицыДанныхРегистра(Запрос, ИмяТаблицыТовары = "ТоварыДляРаспределенияЗапасов", ИмяТаблицыКорректировки = "")
	
	Тексты = Новый Массив();
	Текст =
		"ВЫБРАТЬ
		|	Потребности.Номенклатура КАК Номенклатура,
		|	Потребности.Характеристика КАК Характеристика,
		|	Потребности.Склад КАК Склад,
		|	Потребности.Назначение КАК Назначение,
		|	Потребности.Заказ КАК Заказ,
		|	Потребности.ДатаСобытия КАК ДатаСобытия,
		|	ВЫБОР
		|		КОГДА Потребности.РезервироватьНаСкладеОстаток > 0
		|			ТОГДА Потребности.РезервироватьНаСкладеОстаток
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК РезервироватьНаСкладе,
		|	ВЫБОР
		|		КОГДА Потребности.РезервироватьПоМереПоступленияОстаток > 0
		|			ТОГДА Потребности.РезервироватьПоМереПоступленияОстаток
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК РезервироватьПоМереПоступления,
		|	ВЫБОР
		|		КОГДА Потребности.ОтложитьРезервированиеОстаток > 0
		|			ТОГДА Потребности.ОтложитьРезервированиеОстаток
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ОтложитьРезервирование,
		|	ВЫБОР
		|		КОГДА Потребности.КОбеспечениюОстаток > 0
		|			ТОГДА Потребности.КОбеспечениюОстаток
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК КОбеспечению,
		|	ВЫБОР
		|		КОГДА Потребности.НеОбеспечиватьОстаток > 0
		|			ТОГДА Потребности.НеОбеспечиватьОстаток
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК НеОбеспечивать
		|ПОМЕСТИТЬ ВсеПотребности
		|ИЗ
		|	РегистрНакопления.ЗапасыИПотребности.Остатки(,
		|		(Номенклатура, Характеристика, Склад, Назначение) В(
		|			ВЫБРАТЬ
		|				Товары.Номенклатура КАК Номенклатура,
		|				Товары.Характеристика КАК Характеристика,
		|				Товары.Склад КАК Склад,
		|				Товары.Назначение КАК Назначение
		|			ИЗ
		|				ТоварыДляРаспределенияЗапасовПереопределяемый КАК Товары)) КАК Потребности
		|ГДЕ
		|	Потребности.РезервироватьНаСкладеОстаток > 0
		|		ИЛИ Потребности.РезервироватьПоМереПоступленияОстаток > 0
		|		ИЛИ Потребности.ОтложитьРезервированиеОстаток > 0
		|		ИЛИ Потребности.КОбеспечениюОстаток > 0
		|		ИЛИ Потребности.НеОбеспечиватьОстаток > 0";
	
	Если ИмяТаблицыКорректировки <> "" Тогда
		
		ЗаменяемыйТекст = "ПОМЕСТИТЬ ВсеПотребности"; //@Query-part
		Текст = СтрЗаменить(Текст, ЗаменяемыйТекст, "");
		
		ТекстыНабора = Новый Массив();
		ТекстыНабора.Добавить(Текст);
		Текст =
			"ВЫБРАТЬ
			|	Корректировка.Номенклатура КАК Номенклатура,
			|	Корректировка.Характеристика КАК Характеристика,
			|	Корректировка.Склад КАК Склад,
			|	Корректировка.Назначение КАК Назначение,
			|	Корректировка.Заказ КАК Заказ,
			|	Корректировка.ДатаСобытия КАК ДатаСобытия,
			|	Корректировка.РезервироватьНаСкладе КАК РезервироватьНаСкладе,
			|	Корректировка.РезервироватьПоМереПоступления КАК РезервироватьПоМереПоступления,
			|	Корректировка.ОтложитьРезервирование КАК ОтложитьРезервирование,
			|	Корректировка.КОбеспечению КАК КОбеспечению,
			|	Корректировка.НеОбеспечивать КАК НеОбеспечивать
			|ИЗ
			|	КорректировкаДвиженийПереопределяемый КАК Корректировка
			|ГДЕ
			|	Корректировка.РезервироватьНаСкладе <> 0
			|		ИЛИ Корректировка.РезервироватьПоМереПоступления <> 0
			|		ИЛИ Корректировка.ОтложитьРезервирование <> 0
			|		ИЛИ Корректировка.КОбеспечению <> 0
			|		ИЛИ Корректировка.НеОбеспечивать <> 0";
		ТекстыНабора.Добавить(Текст);
		ТекстНабора = СтрСоединить(ТекстыНабора, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
		Текст =
			"ВЫБРАТЬ
			|	Набор.Номенклатура КАК Номенклатура,
			|	Набор.Характеристика КАК Характеристика,
			|	Набор.Склад КАК Склад,
			|	Набор.Назначение КАК Назначение,
			|	Набор.Заказ КАК Заказ,
			|	Набор.ДатаСобытия КАК ДатаСобытия,
			|	СУММА(Набор.РезервироватьНаСкладе) КАК РезервироватьНаСкладе,
			|	СУММА(Набор.РезервироватьПоМереПоступления) КАК РезервироватьПоМереПоступления,
			|	СУММА(Набор.ОтложитьРезервирование) КАК ОтложитьРезервирование,
			|	СУММА(Набор.КОбеспечению) КАК КОбеспечению,
			|	СУММА(Набор.НеОбеспечивать) КАК НеОбеспечивать
			|ПОМЕСТИТЬ ВсеПотребности
			|ИЗ
			|	НаборПереопределяемый КАК Набор
			|СГРУППИРОВАТЬ ПО
			|	Набор.Номенклатура,
			|	Набор.Характеристика,
			|	Набор.Склад,
			|	Набор.Назначение,
			|	Набор.Заказ,
			|	Набор.ДатаСобытия
			|ИМЕЮЩИЕ
			|	СУММА(Набор.РезервироватьНаСкладе) > 0
			|		ИЛИ СУММА(Набор.РезервироватьПоМереПоступления) > 0
			|		ИЛИ СУММА(Набор.ОтложитьРезервирование) > 0
			|		ИЛИ СУММА(Набор.КОбеспечению) > 0
			|		ИЛИ СУММА(Набор.НеОбеспечивать) > 0";
		Текст = СтрЗаменить(Текст, "НаборПереопределяемый", СтрШаблон("(%1)", ТекстНабора));
		
	КонецЕсли;
	Тексты.Добавить(Текст);
	
	Текст =
		"ВЫБРАТЬ
		|	Запасы.Номенклатура КАК Номенклатура,
		|	Запасы.Характеристика КАК Характеристика,
		|	Запасы.Склад КАК Склад,
		|	Запасы.ВНаличииОстаток КАК ВНаличии
		|ПОМЕСТИТЬ ЗапасыВНаличии
		|ИЗ
		|	РегистрНакопления.ЗапасыИПотребности.Остатки(,
		|		(Номенклатура, Характеристика, Склад) В(
		|			ВЫБРАТЬ
		|				Товары.Номенклатура КАК Номенклатура,
		|				Товары.Характеристика КАК Характеристика,
		|				Товары.Склад КАК Склад
		|			ИЗ
		|				ТоварыДляРаспределенияЗапасовПереопределяемый КАК Товары)) КАК Запасы
		|ГДЕ
		|	Запасы.ВНаличииОстаток <> 0
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад";
	Тексты.Добавить(Текст);
	
	Текст =
		"ВЫБРАТЬ
		|	0 КАК Раздел,
		|	Запасы.Номенклатура КАК Номенклатура,
		|	Запасы.Характеристика КАК Характеристика,
		|	Запасы.Склад КАК Склад,
		|	Запасы.Назначение КАК Назначение,
		|	НЕОПРЕДЕЛЕНО КАК Заказ,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаСобытия,
		|	Запасы.ВНаличииОстаток КАК Запас,
		|	Запасы.РезервироватьНаСкладеОстаток КАК Резерв
		|ПОМЕСТИТЬ ВсеЗапасы
		|ИЗ
		|	РегистрНакопления.ЗапасыИПотребности.Остатки(,
		|		(Номенклатура, Характеристика, Склад, Назначение) В(
		|			ВЫБРАТЬ
		|				Товары.Номенклатура КАК Номенклатура,
		|				Товары.Характеристика КАК Характеристика,
		|				Товары.Склад КАК Склад,
		|				Товары.Назначение КАК Назначение
		|			ИЗ
		|				ТоварыДляРаспределенияЗапасовПереопределяемый КАК Товары)) КАК Запасы
		|ГДЕ
		|	Запасы.ВНаличииОстаток <> 0
		|		ИЛИ Запасы.РезервироватьНаСкладеОстаток > 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	1 КАК Раздел,
		|	Запасы.Номенклатура КАК Номенклатура,
		|	Запасы.Характеристика КАК Характеристика,
		|	Запасы.Склад КАК Склад,
		|	Запасы.Назначение КАК Назначение,
		|	Запасы.Заказ КАК Заказ,
		|	Запасы.ДатаСобытия КАК ДатаСобытия,
		|	Запасы.ПоступитОстаток КАК Запас,
		|	0 КАК Резерв
		|ИЗ
		|	РегистрНакопления.ЗапасыИПотребности.Остатки(,
		|		(Номенклатура, Характеристика, Склад, Назначение) В(
		|			ВЫБРАТЬ
		|				Товары.Номенклатура КАК Номенклатура,
		|				Товары.Характеристика КАК Характеристика,
		|				Товары.Склад КАК Склад,
		|				Товары.Назначение КАК Назначение
		|			ИЗ
		|				ТоварыДляРаспределенияЗапасовПереопределяемый КАК Товары)) КАК Запасы
		|ГДЕ
		|	Запасы.ПоступитОстаток > 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	2 КАК Раздел,
		|	Запасы.Номенклатура КАК Номенклатура,
		|	Запасы.Характеристика КАК Характеристика,
		|	Запасы.Склад КАК Склад,
		|	Запасы.Назначение КАК Назначение,
		|	Запасы.Заказ КАК Заказ,
		|	Запасы.ДатаСобытия КАК ДатаСобытия,
		|	Запасы.ЗаказаноОстаток - Запасы.ПоступитОстаток КАК Запас,
		|	0 КАК Резерв
		|ИЗ
		|	РегистрНакопления.ЗапасыИПотребности.Остатки(,
		|		(Номенклатура, Характеристика, Склад, Назначение) В(
		|			ВЫБРАТЬ
		|				Товары.Номенклатура КАК Номенклатура,
		|				Товары.Характеристика КАК Характеристика,
		|				Товары.Склад КАК Склад,
		|				Товары.Назначение КАК Назначение
		|			ИЗ
		|				ТоварыДляРаспределенияЗапасовПереопределяемый КАК Товары)) КАК Запасы
		|ГДЕ
		|	Запасы.ЗаказаноОстаток - Запасы.ПоступитОстаток > 0";
	Если ИмяТаблицыКорректировки <> "" Тогда
		
		ЗаменяемыйТекст = "ПОМЕСТИТЬ ВсеЗапасы"; //@Query-part
		Текст = СтрЗаменить(Текст, ЗаменяемыйТекст, "");
		
		ТекстыНабора = Новый Массив();
		ТекстыНабора.Добавить(Текст);
		Текст =
			"ВЫБРАТЬ
			|	0 КАК Раздел,
			|	Корректировка.Номенклатура КАК Номенклатура,
			|	Корректировка.Характеристика КАК Характеристика,
			|	Корректировка.Склад КАК Склад,
			|	Корректировка.Назначение КАК Назначение,
			|	НЕОПРЕДЕЛЕНО КАК Заказ,
			|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаСобытия,
			|	Корректировка.ВНаличии КАК Запас,
			|	Корректировка.РезервироватьНаСкладе КАК Резерв
			|ИЗ
			|	КорректировкаДвиженийПереопределяемый КАК Корректировка
			|ГДЕ
			|	Корректировка.ВНаличии <> 0
			|		ИЛИ Корректировка.РезервироватьНаСкладе <> 0
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	1 КАК Раздел,
			|	Корректировка.Номенклатура КАК Номенклатура,
			|	Корректировка.Характеристика КАК Характеристика,
			|	Корректировка.Склад КАК Склад,
			|	Корректировка.Назначение КАК Назначение,
			|	Корректировка.Заказ КАК Заказ,
			|	Корректировка.ДатаСобытия КАК ДатаСобытия,
			|	Корректировка.Поступит КАК Запас,
			|	0 КАК Резерв
			|ИЗ
			|	КорректировкаДвиженийПереопределяемый КАК Корректировка
			|ГДЕ
			|	Корректировка.Поступит <> 0
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	2 КАК Раздел,
			|	Корректировка.Номенклатура КАК Номенклатура,
			|	Корректировка.Характеристика КАК Характеристика,
			|	Корректировка.Склад КАК Склад,
			|	Корректировка.Назначение КАК Назначение,
			|	Корректировка.Заказ КАК Заказ,
			|	Корректировка.ДатаСобытия КАК ДатаСобытия,
			|	Корректировка.Заказано - Корректировка.Поступит КАК Запас,
			|	0 КАК Резерв
			|ИЗ
			|	КорректировкаДвиженийПереопределяемый КАК Корректировка
			|ГДЕ
			|	Корректировка.Заказано - Корректировка.Поступит <> 0";
		ТекстыНабора.Добавить(Текст);
		ТекстНабора = СтрСоединить(ТекстыНабора, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
		Текст =
			"ВЫБРАТЬ
			|	Набор.Раздел КАК Раздел,
			|	Набор.Номенклатура КАК Номенклатура,
			|	Набор.Характеристика КАК Характеристика,
			|	Набор.Склад КАК Склад,
			|	Набор.Назначение КАК Назначение,
			|	Набор.Заказ КАК Заказ,
			|	Набор.ДатаСобытия КАК ДатаСобытия,
			|	СУММА(Набор.Запас) КАК Запас,
			|	СУММА(Набор.Резерв) КАК Резерв
			|ПОМЕСТИТЬ ВсеЗапасы
			|ИЗ
			|	НаборПереопределяемый КАК Набор
			|СГРУППИРОВАТЬ ПО
			|	Набор.Раздел,
			|	Набор.Номенклатура,
			|	Набор.Характеристика,
			|	Набор.Склад,
			|	Набор.Назначение,
			|	Набор.Заказ,
			|	Набор.ДатаСобытия
			|ИМЕЮЩИЕ
			|	СУММА(Набор.Запас) <> 0
			|			ИЛИ СУММА(Набор.Резерв) > 0";
		Текст = СтрЗаменить(Текст, "НаборПереопределяемый", СтрШаблон("(%1)", ТекстНабора));
		
	КонецЕсли;
	
	ТекстыСИндексированием = Новый Массив();
	ТекстыСИндексированием.Добавить(Текст);
	Текст =
		"ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение, Раздел"; //@Query-part
	ТекстыСИндексированием.Добавить(Текст);
	Текст = СтрСоединить(ТекстыСИндексированием, Символы.ПС);
	Тексты.Добавить(Текст);
	
	Текст =
		"ВЫБРАТЬ
		|	ЗапасыВНаличии.Номенклатура КАК Номенклатура,
		|	ЗапасыВНаличии.Характеристика КАК Характеристика,
		|	ЗапасыВНаличии.Склад КАК Склад,
		|	ЗапасыВНаличии.ВНаличии КАК ВНаличии
		|ПОМЕСТИТЬ ЕстьВНаличииЕстьДвиженияНетЗапаса
		|ИЗ
		|	ЗапасыВНаличии КАК ЗапасыВНаличии
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВсеЗапасы КАК Запасы
		|		ПО Запасы.Номенклатура = ЗапасыВНаличии.Номенклатура
		|		 И Запасы.Характеристика = ЗапасыВНаличии.Характеристика
		|		 И Запасы.Склад = ЗапасыВНаличии.Склад
		|		 И Запасы.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|		 И Запасы.Заказ = НЕОПРЕДЕЛЕНО
		|		 И Запасы.ДатаСобытия = ДАТАВРЕМЯ(1, 1, 1)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТоварыДляРаспределенияЗапасовПереопределяемый КАК Товары
		|		ПО Товары.Номенклатура = ЗапасыВНаличии.Номенклатура
		|		 И Товары.Характеристика = ЗапасыВНаличии.Характеристика
		|		 И Товары.Склад = ЗапасыВНаличии.Склад
		|		 И Товары.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|ГДЕ
		|	Запасы.Номенклатура ЕСТЬ NULL
		|		И НЕ Товары.Номенклатура ЕСТЬ NULL
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад";
	Тексты.Добавить(Текст);
	
	Текст =
		"ВЫБРАТЬ
		|	Сведения.Номенклатура КАК Номенклатура,
		|	Сведения.Характеристика КАК Характеристика,
		|	Сведения.Склад КАК Склад,
		|	Сведения.Назначение КАК Назначение,
		|	Сведения.ЗаказНаОтгрузку КАК Заказ,
		|	ВЫБОР
		|		КОГДА СУММА(Сведения.Зарезервировано) - МАКСИМУМ(ЕСТЬNULL(ОтгрузкаИзСобственногоРезерва.Отгрузка, 0)) > 0
		|			ТОГДА СУММА(Сведения.Зарезервировано) - МАКСИМУМ(ЕСТЬNULL(ОтгрузкаИзСобственногоРезерва.Отгрузка, 0))
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Зарезервировано
		|ПОМЕСТИТЬ ЗапасыРаспределенныеНаЗаказыРанее
		|ИЗ
		|	ТоварыДляРаспределенияЗапасовПереопределяемый КАК Товары
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОбеспечения КАК НастройкаКонтроляОбеспеченияХарактеристика
		|		ПО НастройкаКонтроляОбеспеченияХарактеристика.Склад = Товары.Склад
		|		 И НастройкаКонтроляОбеспеченияХарактеристика.Номенклатура = Товары.Номенклатура
		|		 И НастройкаКонтроляОбеспеченияХарактеристика.Характеристика = Товары.Характеристика
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОбеспечения КАК НастройкаКонтроляОбеспеченияНоменклатура
		|		ПО НастройкаКонтроляОбеспеченияНоменклатура.Склад = Товары.Склад
		|		 И НастройкаКонтроляОбеспеченияНоменклатура.Номенклатура = Товары.Номенклатура
		|		 И НастройкаКонтроляОбеспеченияНоменклатура.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
		|		 И НастройкаКонтроляОбеспеченияХарактеристика.Номенклатура ЕСТЬ NULL
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОбеспечения КАК НастройкаКонтроляОбеспечения
		|		ПО НастройкаКонтроляОбеспечения.Склад = Товары.Склад
		|		 И НастройкаКонтроляОбеспечения.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|		 И НастройкаКонтроляОбеспечения.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
		|		 И НастройкаКонтроляОбеспеченияХарактеристика.Номенклатура ЕСТЬ NULL
		|		 И НастройкаКонтроляОбеспеченияНоменклатура.Номенклатура ЕСТЬ NULL
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаспределениеЗапасов КАК Сведения
		|		ПО Сведения.Номенклатура = Товары.Номенклатура
		|		 И Сведения.Характеристика = Товары.Характеристика
		|		 И Сведения.Склад = Товары.Склад
		|		 И Сведения.Назначение = Товары.Назначение
		|		 И Сведения.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОбеспеченНаСкладе)
		|		 И ЕСТЬNULL(НастройкаКонтроляОбеспеченияХарактеристика.КонтролироватьСвободныеОстатки,
		|				ЕСТЬNULL(НастройкаКонтроляОбеспеченияНоменклатура.КонтролироватьСвободныеОстатки,
		|					ЕСТЬNULL(НастройкаКонтроляОбеспечения.КонтролироватьСвободныеОстатки, ЛОЖЬ)))
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ИзменениеОстаткаПоТоварамДляКонтроляОстатков КАК ОтгрузкаИзСобственногоРезерва
		|		ПО ОтгрузкаИзСобственногоРезерва.Номенклатура = Сведения.Номенклатура
		|		 И ОтгрузкаИзСобственногоРезерва.Характеристика = Сведения.Характеристика
		|		 И ОтгрузкаИзСобственногоРезерва.Склад = Сведения.Склад
		|		 И ОтгрузкаИзСобственногоРезерва.Назначение = Сведения.Назначение
		|		 И ОтгрузкаИзСобственногоРезерва.ЗаказНаОтгрузку = Сведения.ЗаказНаОтгрузку
		|
		|ГДЕ
		|	НЕ Сведения.Номенклатура ЕСТЬ NULL
 		|СГРУППИРОВАТЬ ПО
		|	Сведения.Номенклатура,
		|	Сведения.Характеристика,
		|	Сведения.Склад,
		|	Сведения.Назначение,
		|	Сведения.ЗаказНаОтгрузку
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение";
	Тексты.Добавить(Текст);
	
	Текст =
		"ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе) КАК Состояние,
		|	1 КАК ТипЗаписиРаспределенияЗапасов,
		|	ЗапасыВНаличии.Номенклатура КАК Номенклатура,
		|	ЗапасыВНаличии.Характеристика КАК Характеристика,
		|	ЗапасыВНаличии.Склад КАК Склад,
		|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
		|	ЗапасыВНаличии.ВНаличии КАК ВНаличии,
		|	ЕСТЬNULL(Сведения.Запас, 0) КАК Запас,
		|	ЕСТЬNULL(Сведения.Резерв, 0) КАК Резерв,
		|	ЕСТЬNULL(Сведения.Свободно, 0) КАК Свободно,
		|	ЕСТЬNULL(Сведения.Излишек, 0) КАК Излишек,
		|	ЛОЖЬ КАК Пустая
		|ИЗ
		|	ЗапасыВНаличии КАК ЗапасыВНаличии
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаспределениеЗапасов КАК Сведения
		|		ПО Сведения.Номенклатура = ЗапасыВНаличии.Номенклатура
		|		 И Сведения.Характеристика = ЗапасыВНаличии.Характеристика
		|		 И Сведения.Склад = ЗапасыВНаличии.Склад
		|		 И Сведения.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|		 И Сведения.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе)
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТоварыДляРаспределенияЗапасовПереопределяемый КАК Товары
		|		ПО ЗапасыВНаличии.Номенклатура = Товары.Номенклатура
		|		 И ЗапасыВНаличии.Характеристика = Товары.Характеристика
		|		 И ЗапасыВНаличии.Склад = Товары.Склад
		|		 И Товары.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|		
		|ГДЕ
		|	ЗапасыВНаличии.ВНаличии <> ЕСТЬNULL(Сведения.ВНаличии, 0)
				// Наличие не будет пересчитано по товару автоматически, так как в проводимом документе нет аналитики с пустым назначением.
		|		И Товары.Номенклатура ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе) КАК Состояние,
		|	1 КАК ТипЗаписиРаспределенияЗапасов,
		|	Сведения.Номенклатура КАК Номенклатура,
		|	Сведения.Характеристика КАК Характеристика,
		|	Сведения.Склад КАК Склад,
		|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
		|	0 КАК ВНаличии,
		|	Сведения.Запас КАК Запас,
		|	Сведения.Резерв КАК Резерв,
		|	Сведения.Свободно КАК Свободно,
		|	Сведения.Излишек КАК Излишек,
		|	Сведения.Запас = 0 И Сведения.Резерв = 0 И Сведения.Свободно = 0 И Сведения.Излишек = 0 КАК Пустая
		|ИЗ
		|	РегистрСведений.РаспределениеЗапасов КАК Сведения
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТоварыДляРаспределенияЗапасовПереопределяемый КАК Товары
		|		ПО Сведения.Номенклатура = Товары.Номенклатура
		|		 И Сведения.Характеристика = Товары.Характеристика
		|		 И Сведения.Склад = Товары.Склад
		|		 И Сведения.Назначение = Товары.Назначение
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ЗапасыВНаличии КАК ЗапасыВНаличии
		|		ПО ЗапасыВНаличии.Номенклатура = Сведения.Номенклатура
		|		 И ЗапасыВНаличии.Характеристика = Сведения.Характеристика
		|		 И ЗапасыВНаличии.Склад = Сведения.Склад
		|		
		|ГДЕ
		|	Сведения.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|		И Сведения.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе)
				// Строка не попала в предыдущий блок объединения.
		|		И ЗапасыВНаличии.Номенклатура ЕСТЬ NULL
		|		И Сведения.ВНаличии <> 0
				// Затронуты товары проводимого документа.
		|		И (Сведения.Номенклатура, Сведения.Характеристика, Сведения.Склад, Сведения.Назначение) В(
		|			ВЫБРАТЬ
		|				Фильтр.Номенклатура КАК Номенклатура,
		|				Фильтр.Характеристика КАК Характеристика,
		|				Фильтр.Склад КАК Склад,
		|				ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение
		|			ИЗ
		|				ТоварыДляРаспределенияЗапасовПереопределяемый КАК Фильтр)
				// Наличие не будет пересчитано по товару автоматически, так как в проводимом документе нет аналитики с пустым назначением.
		|		И Товары.Номенклатура ЕСТЬ NULL";
	Тексты.Добавить(Текст);

	Текст = СтрСоединить(Тексты, ОбщегоНазначения.РазделительПакетаЗапросов());
	Текст = СтрЗаменить(Текст, "ТоварыДляРаспределенияЗапасовПереопределяемый", ИмяТаблицыТовары);
	Запрос.Текст = СтрЗаменить(Текст, "КорректировкаДвиженийПереопределяемый", ИмяТаблицыКорректировки);
	Результат = Запрос.Выполнить().Выгрузить();
	Возврат Результат;
	
КонецФункции

Процедура ВременнаяТаблицаПотребностиРезервироватьПоМере(Запрос)
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Потребности.Номенклатура КАК Номенклатура,
		|	Потребности.Характеристика КАК Характеристика,
		|	Потребности.Склад КАК Склад,
		|	Потребности.Назначение КАК Назначение,
		|	Потребности.Заказ КАК Заказ,
		|	Потребности.ДатаСобытия КАК ДатаСобытия,
		|	Потребности.РезервироватьПоМереПоступления КАК РезервироватьПоМереПоступления,
		|	0 КАК РезервироватьПоМереПоступленияИтогНаДату
		|ИЗ
		|	ВсеПотребности КАК Потребности
		|ГДЕ
		|	Потребности.РезервироватьПоМереПоступления > 0
		|УПОРЯДОЧИТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение, Заказ, ДатаСобытия";
	
	ПотребностиРезервироватьПоМере = Запрос.Выполнить().Выгрузить();
	Заказ = Неопределено;
	Номенклатура = Неопределено;
	Характеристика = Неопределено;
	Склад = Неопределено;
	Назначение = Неопределено;
	
	Итог = 0;
	Для Индекс = 0 По ПотребностиРезервироватьПоМере.Количество() - 1 Цикл
		
		СтрокаТаблицы = ПотребностиРезервироватьПоМере[Индекс];
		
		Если Заказ <> СтрокаТаблицы.Заказ
				Или Номенклатура <> СтрокаТаблицы.Номенклатура
				Или Характеристика <> СтрокаТаблицы.Характеристика
				Или Склад <> СтрокаТаблицы.Склад
				Или Назначение <> СтрокаТаблицы.Назначение Тогда
					
					Итог = 0;
					Заказ = СтрокаТаблицы.Заказ;
					Номенклатура = СтрокаТаблицы.Номенклатура;
					Характеристика = СтрокаТаблицы.Характеристика;
					Склад = СтрокаТаблицы.Склад;
					Назначение = СтрокаТаблицы.Назначение;
					
		КонецЕсли;
		Итог = Итог + СтрокаТаблицы.РезервироватьПоМереПоступления;
		СтрокаТаблицы.РезервироватьПоМереПоступленияИтогНаДату = Итог;
		
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ПотребностиРезервироватьПоМере", ПотребностиРезервироватьПоМере);
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПотребностиРезервироватьПоМере.Номенклатура КАК Номенклатура,
		|	ПотребностиРезервироватьПоМере.Характеристика КАК Характеристика,
		|	ПотребностиРезервироватьПоМере.Склад КАК Склад,
		|	ПотребностиРезервироватьПоМере.Назначение КАК Назначение,
		|	ПотребностиРезервироватьПоМере.Заказ КАК Заказ,
		|	ПотребностиРезервироватьПоМере.ДатаСобытия КАК ДатаСобытия,
		|	ПотребностиРезервироватьПоМере.РезервироватьПоМереПоступления КАК РезервироватьПоМереПоступления,
		|	ПотребностиРезервироватьПоМере.РезервироватьПоМереПоступленияИтогНаДату КАК РезервироватьПоМереПоступленияИтогНаДату
		|ПОМЕСТИТЬ ПотребностиРезервироватьПоМере
		|ИЗ
		|	&ПотребностиРезервироватьПоМере КАК ПотребностиРезервироватьПоМере";
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ВременнаяТаблицаЗапасыИДоступностьПоДатам(Запрос)
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Потребности.Номенклатура КАК Номенклатура,
		|	Потребности.Характеристика КАК Характеристика,
		|	Потребности.Склад КАК Склад,
		|	Потребности.Назначение КАК Назначение,
		|	СУММА(Потребности.РезервироватьПоМереПоступления) КАК РезервироватьПоМереПоступления,
		|	СУММА(Потребности.КОбеспечению + Потребности.ОтложитьРезервирование) КАК КОбеспечению
		|ПОМЕСТИТЬ ПотребностиПоТовару
		|ИЗ
		|	ВсеПотребности КАК Потребности
		|ГДЕ
		|	Потребности.РезервироватьПоМереПоступления > 0
		|		ИЛИ Потребности.КОбеспечению + Потребности.ОтложитьРезервирование > 0
		|СГРУППИРОВАТЬ ПО
		|	Потребности.Номенклатура,
		|	Потребности.Характеристика,
		|	Потребности.Склад,
		|	Потребности.Назначение
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение
		|;
		|
		|/////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Запасы.Раздел КАК Раздел,
		|	Запасы.Номенклатура КАК Номенклатура,
		|	Запасы.Характеристика КАК Характеристика,
		|	Запасы.Склад КАК Склад,
		|	Запасы.Назначение КАК Назначение,
		|	Запасы.Заказ КАК Заказ,
		|	Запасы.ДатаСобытия КАК ДатаСобытия,
		|	ЕСТЬNULL(ЗапасыВНаличии.ВНаличии, 0) КАК ВНаличии,
		|	Запасы.Запас КАК Запас,
		|	Запасы.Резерв КАК Резерв,
		|	ЕСТЬNULL(Потребности.РезервироватьПоМереПоступления, 0) КАК РезервироватьПоМереПоступления,
		|	ЕСТЬNULL(Потребности.КОбеспечению, 0) КАК КОбеспечению,
		|	0 КАК Свободно,
		|	0 КАК Излишек
		|ИЗ
		|	ВсеЗапасы КАК Запасы
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ЗапасыВНаличии КАК ЗапасыВНаличии
		|		ПО ЗапасыВНаличии.Номенклатура = Запасы.Номенклатура
		|		 И ЗапасыВНаличии.Характеристика = Запасы.Характеристика
		|		 И ЗапасыВНаличии.Склад = Запасы.Склад
		|		 И Запасы.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|		 И Запасы.Раздел = 0
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПотребностиПоТовару КАК Потребности
		|		ПО Потребности.Номенклатура = Запасы.Номенклатура
		|		 И Потребности.Характеристика = Запасы.Характеристика
		|		 И Потребности.Склад = Запасы.Склад
		|		 И Потребности.Назначение = Запасы.Назначение
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	0 КАК Раздел,
		|	ЗапасыВНаличии.Номенклатура КАК Номенклатура,
		|	ЗапасыВНаличии.Характеристика КАК Характеристика,
		|	ЗапасыВНаличии.Склад КАК Склад,
		|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
		|	НЕОПРЕДЕЛЕНО КАК Заказ,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаСобытия,
		|	ЗапасыВНаличии.ВНаличии КАК ВНаличии,
		|	0 КАК Запас,
		|	0 КАК Резерв,
		|	ЕСТЬNULL(Потребности.РезервироватьПоМереПоступления, 0) КАК РезервироватьПоМереПоступления,
		|	ЕСТЬNULL(Потребности.КОбеспечению, 0) КАК КОбеспечению,
		|	0 КАК Свободно,
		|	0 КАК Излишек
		|ИЗ
		|	ЕстьВНаличииЕстьДвиженияНетЗапаса КАК ЗапасыВНаличии
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПотребностиПоТовару КАК Потребности
		|		ПО Потребности.Номенклатура = ЗапасыВНаличии.Номенклатура
		|		 И Потребности.Характеристика = ЗапасыВНаличии.Характеристика
		|		 И Потребности.Склад = ЗапасыВНаличии.Склад
		|		 И Потребности.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|УПОРЯДОЧИТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение, Раздел, ДатаСобытия, Заказ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВсеЗапасы;
		|УНИЧТОЖИТЬ ЗапасыВНаличии;
		|УНИЧТОЖИТЬ ПотребностиПоТовару";
		
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	ЗапасыИДоступностьПоДатам = ПакетЗапросов[ПакетЗапросов.ВГраница() - 3].Выгрузить();
	
	Номенклатура = Неопределено; Характеристика = Неопределено; Склад = Неопределено; Назначение = Неопределено;
	
	Для Индекс = 0 По ЗапасыИДоступностьПоДатам.Количество() - 1 Цикл
		
		СтрокаТаблицы = ЗапасыИДоступностьПоДатам[Индекс];
		Если Номенклатура <> СтрокаТаблицы.Номенклатура
				Или Характеристика <> СтрокаТаблицы.Характеристика
				Или Склад <> СтрокаТаблицы.Склад
				Или Назначение <> СтрокаТаблицы.Назначение Тогда
					
					Номенклатура = СтрокаТаблицы.Номенклатура;
					Характеристика = СтрокаТаблицы.Характеристика;
					Склад = СтрокаТаблицы.Склад;
					Назначение = СтрокаТаблицы.Назначение;
					
					РаспределитьВсего = СтрокаТаблицы.РезервироватьПоМереПоступления;
					РаспределитьИзлишекВсего = СтрокаТаблицы.КОбеспечению;
				
		КонецЕсли;
		
		РаспределитьНаСтроку = Мин(РаспределитьВсего, Макс(СтрокаТаблицы.Запас - СтрокаТаблицы.Резерв, 0));
		СтрокаТаблицы.Свободно = СтрокаТаблицы.Запас - СтрокаТаблицы.Резерв - РаспределитьНаСтроку;
		РаспределитьВсего = РаспределитьВсего - РаспределитьНаСтроку;
		
		РаспределитьИзлишекНаСтроку = Мин(РаспределитьИзлишекВсего, Макс(СтрокаТаблицы.Свободно, 0));
		СтрокаТаблицы.Излишек = СтрокаТаблицы.Свободно - РаспределитьИзлишекНаСтроку;
		РаспределитьИзлишекВсего = РаспределитьИзлишекВсего - РаспределитьИзлишекНаСтроку;
		
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ЗапасыИДоступностьПоДатам", ЗапасыИДоступностьПоДатам);
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗапасыИДоступностьПоДатам.Раздел КАК Раздел,
		|	ЗапасыИДоступностьПоДатам.Номенклатура КАК Номенклатура,
		|	ЗапасыИДоступностьПоДатам.Характеристика КАК Характеристика,
		|	ЗапасыИДоступностьПоДатам.Склад КАК Склад,
		|	ЗапасыИДоступностьПоДатам.Назначение КАК Назначение,
		|	ЗапасыИДоступностьПоДатам.Заказ КАК Заказ,
		|	ЗапасыИДоступностьПоДатам.ДатаСобытия КАК ДатаСобытия,
		|	ЗапасыИДоступностьПоДатам.ВНаличии КАК ВНаличии,
		|	ЗапасыИДоступностьПоДатам.Запас КАК Запас,
		|	ЗапасыИДоступностьПоДатам.Резерв КАК Резерв,
		|	ЗапасыИДоступностьПоДатам.Свободно КАК Свободно,
		|	ЗапасыИДоступностьПоДатам.Излишек КАК Излишек,
		|	ВЫБОР
		|		КОГДА ЗапасыИДоступностьПоДатам.Раздел = 0 ТОГДА
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе)
		|		КОГДА ЗапасыИДоступностьПоДатам.Раздел = 1 ТОГДА
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемоеПоступление)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ПоступлениеНеподтвержденное)
		|	КОНЕЦ КАК Состояние
		|ПОМЕСТИТЬ ЗапасыИДоступностьПоДатам
		|ИЗ
		|	&ЗапасыИДоступностьПоДатам КАК ЗапасыИДоступностьПоДатам
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение";
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ВременнаяТаблицаРезультатРаспределенияЗапасовНаПотребности(Запрос, ИмяТаблицыТовары = "ТоварыДляРаспределенияЗапасов", ИмяТаблицыРезультат = "РаспределениеЗапасовНаПотребности", ИмяТаблицыРеестрДокументов = "РегистрСведений.РеестрДокументов") Экспорт
	
	Текст =
		// Потребности.
		"ВЫБРАТЬ
		|	Набор.Номенклатура КАК Номенклатура,
		|	Набор.Характеристика КАК Характеристика,
		|	Набор.Склад КАК Склад,
		|	Набор.Назначение КАК Назначение,
		|	Набор.Заказ КАК Заказ,
		|	Набор.ДатаСобытия КАК ДатаСобытия,
		|	СУММА(Набор.РезервироватьПоМереПоступления) КАК РезервироватьПоМереПоступления,
		|	СУММА(Набор.РезервироватьПоМереПоступленияИтогНаДату) КАК РезервироватьПоМереПоступленияИтогНаДату,
		|	СУММА(Набор.КОбеспечению) КАК КОбеспечению
		|ПОМЕСТИТЬ ПотребностиОстатки
		|ИЗ(
		|	ВЫБРАТЬ
		|		Потребности.Номенклатура КАК Номенклатура,
		|		Потребности.Характеристика КАК Характеристика,
		|		Потребности.Склад КАК Склад,
		|		Потребности.Назначение КАК Назначение,
		|		Потребности.Заказ КАК Заказ,
		|		Потребности.ДатаСобытия КАК ДатаСобытия,
		|		Потребности.РезервироватьПоМереПоступления КАК РезервироватьПоМереПоступления,
		|		Потребности.РезервироватьПоМереПоступленияИтогНаДату КАК РезервироватьПоМереПоступленияИтогНаДату,
		|		0 КАК КОбеспечению
		|	ИЗ
		|		ПотребностиРезервироватьПоМере КАК Потребности
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Потребности.Номенклатура КАК Номенклатура,
		|		Потребности.Характеристика КАК Характеристика,
		|		Потребности.Склад КАК Склад,
		|		Потребности.Назначение КАК Назначение,
		|		Потребности.Заказ КАК Заказ,
		|		Потребности.ДатаСобытия КАК ДатаСобытия,
		|		0 КАК РезервироватьПоМереПоступления,
		|		0 КАК РезервироватьПоМереПоступленияИтогНаДату,
		|		Потребности.КОбеспечению + Потребности.ОтложитьРезервирование КАК КОбеспечению
		|	ИЗ
		|		ВсеПотребности КАК Потребности
		|	ГДЕ
		|		Потребности.КОбеспечению + Потребности.ОтложитьРезервирование > 0) КАК Набор
		|СГРУППИРОВАТЬ ПО
		|	Набор.Номенклатура,
		|	Набор.Характеристика,
		|	Набор.Склад,
		|	Набор.Назначение,
		|	Набор.Заказ,
		|	Набор.ДатаСобытия
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение
		|;
		|
		|////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Потребности.Заказ КАК Заказ
		|ПОМЕСТИТЬ РазличныеЗаказыПотребностей
		|ИЗ
		|	ПотребностиОстатки КАК Потребности
		|ИНДЕКСИРОВАТЬ ПО
		|	Заказ
		|;
		|
		|///////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗаказыРеестра.Ссылка КАК Ссылка,
		|	МИНИМУМ(ЗаказыРеестра.Приоритет) КАК Приоритет,
		|	МИНИМУМ(ЗаказыРеестра.ДатаДокументаИБ) КАК ДатаДокументаИБ
		|ПОМЕСТИТЬ ТаблицаЗаказыРеестра
		|ИЗ
		|	РазличныеЗаказыПотребностей КАК РазличныеЗаказыПотребностей
		|		ЛЕВОЕ СОЕДИНЕНИЕ РеестрДокументовПереопределяемый КАК ЗаказыРеестра
		|		ПО ЗаказыРеестра.Ссылка = РазличныеЗаказыПотребностей.Заказ
		|		И НЕ ЗаказыРеестра.ДополнительнаяЗапись
		|ГДЕ
		|	НЕ ЗаказыРеестра.Ссылка ЕСТЬ NULL
		|СГРУППИРОВАТЬ ПО
		|	ЗаказыРеестра.Ссылка
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		|
		|////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Потребности.Номенклатура КАК Номенклатура,
		|	Потребности.Характеристика КАК Характеристика,
		|	Потребности.Склад КАК Склад,
		|	Потребности.Назначение КАК Назначение,
		|	Потребности.Заказ КАК Заказ,
		|	СУММА(Потребности.РезервироватьПоМереПоступления) КАК РезервироватьПоМереПоступления,
		|	СУММА(Потребности.КОбеспечению) КАК КОбеспечению
		|ПОМЕСТИТЬ ПотребностиОстаткиГруппировкаПоЗаказам
		|ИЗ
		|	ПотребностиОстатки КАК Потребности
		|СГРУППИРОВАТЬ ПО
		|	Потребности.Номенклатура,
		|	Потребности.Характеристика,
		|	Потребности.Склад,
		|	Потребности.Назначение,
		|	Потребности.Заказ
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение
		|;
		|
		|////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Потребности.Номенклатура КАК Номенклатура,
		|	Потребности.Характеристика КАК Характеристика,
		|	Потребности.Склад КАК Склад,
		|	Потребности.Назначение КАК Назначение,
		|	СУММА(Потребности.РезервироватьПоМереПоступления) КАК РезервироватьПоМереПоступления,
		|	СУММА(Потребности.КОбеспечению) КАК КОбеспечению
		|ПОМЕСТИТЬ ПотребностиОстаткиГруппировкаПоТоварам
		|ИЗ
		|	ПотребностиОстаткиГруппировкаПоЗаказам КАК Потребности
		|СГРУППИРОВАТЬ ПО
		|	Потребности.Номенклатура,
		|	Потребности.Характеристика,
		|	Потребности.Склад,
		|	Потребности.Назначение
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение
		|;
		|
		|////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗапасыРаспределенныеРанее.Номенклатура КАК Номенклатура,
		|	ЗапасыРаспределенныеРанее.Характеристика КАК Характеристика,
		|	ЗапасыРаспределенныеРанее.Склад КАК Склад,
		|	ЗапасыРаспределенныеРанее.Назначение КАК Назначение,
		|	СУММА(ВЫБОР
		|		КОГДА ЗапасыРаспределенныеРанее.Зарезервировано > Потребности.РезервироватьПоМереПоступления
		|			ТОГДА Потребности.РезервироватьПоМереПоступления
		|		ИНАЧЕ ЗапасыРаспределенныеРанее.Зарезервировано
		|		КОНЕЦ) КАК Зарезервировано
		|ПОМЕСТИТЬ ЗапасыРаспределенныеНаЗаказыРанееГруппировкаПоТоварам
		|ИЗ
		|	ЗапасыРаспределенныеНаЗаказыРанее КАК ЗапасыРаспределенныеРанее
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПотребностиОстаткиГруппировкаПоЗаказам КАК Потребности
		|		ПО Потребности.Номенклатура = ЗапасыРаспределенныеРанее.Номенклатура
		|		 И Потребности.Характеристика = ЗапасыРаспределенныеРанее.Характеристика
		|		 И Потребности.Склад = ЗапасыРаспределенныеРанее.Склад
		|		 И Потребности.Назначение = ЗапасыРаспределенныеРанее.Назначение
		|		 И Потребности.Заказ = ЗапасыРаспределенныеРанее.Заказ
		|СГРУППИРОВАТЬ ПО
		|	ЗапасыРаспределенныеРанее.Номенклатура,
		|	ЗапасыРаспределенныеРанее.Характеристика,
		|	ЗапасыРаспределенныеРанее.Склад,
		|	ЗапасыРаспределенныеРанее.Назначение
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение
		|;
		|
		|////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Запасы.Номенклатура КАК Номенклатура,
		|	Запасы.Характеристика КАК Характеристика,
		|	Запасы.Склад КАК Склад,
		|	Запасы.Назначение КАК Назначение,
		|	СУММА(ВЫБОР
		|		КОГДА Запасы.Заказ = НЕОПРЕДЕЛЕНО ТОГДА
		|			Запасы.Запас
		|		ИНАЧЕ 0
		|	КОНЕЦ) КАК ЗапасНаСкладе,
		|	СУММА(ВЫБОР
		|		КОГДА Запасы.Заказ = НЕОПРЕДЕЛЕНО ТОГДА
		|			Запасы.Резерв
		|		ИНАЧЕ 0
		|	КОНЕЦ) КАК Резерв,
		|	СУММА(ВЫБОР
		|		КОГДА Запасы.Заказ = НЕОПРЕДЕЛЕНО ТОГДА
		|			Запасы.Свободно
		|		ИНАЧЕ 0
		|	КОНЕЦ) КАК СвободноНаСкладе,
		|	СУММА(ВЫБОР
		|		КОГДА Запасы.Заказ <> НЕОПРЕДЕЛЕНО ТОГДА
		|			Запасы.Запас
		|		ИНАЧЕ 0
		|	КОНЕЦ) КАК ЗапасВГрафике,
		|	СУММА(ВЫБОР
		|		КОГДА Запасы.Заказ <> НЕОПРЕДЕЛЕНО ТОГДА
		|			Запасы.Свободно
		|		ИНАЧЕ 0
		|	КОНЕЦ) КАК СвободноВГрафике
		|ПОМЕСТИТЬ ЗапасыИДоступностьПоДатамГруппировкаПоТоварам
		|ИЗ
		|	ЗапасыИДоступностьПоДатам КАК Запасы
		|СГРУППИРОВАТЬ ПО
		|	Запасы.Номенклатура,
		|	Запасы.Характеристика,
		|	Запасы.Склад,
		|	Запасы.Назначение
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение
		|;
		|
		|///////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Товары.Номенклатура КАК Номенклатура,
		|	Товары.Характеристика КАК Характеристика,
		|	Товары.Склад КАК Склад,
		|	Товары.Назначение КАК Назначение,
		// Потребности по товару.
		|	ЕСТЬNULL(ПотребностиПоТоварам.РезервироватьПоМереПоступления, 0) КАК РезервироватьПоМереПоступленияПоТовару,
		|	ЕСТЬNULL(ПотребностиПоТоварам.КОбеспечению, 0) КАК КОбеспечениюПоТовару,
		// Потребности по заказу.
		|	ЕСТЬNULL(ПотребностиПоЗаказам.РезервироватьПоМереПоступления, 0) КАК РезервироватьПоМереПоступленияПоЗаказу,
		|	ЕСТЬNULL(ПотребностиПоЗаказам.КОбеспечению, 0) КАК КОбеспечениюПоЗаказу,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ЗапасыРаспределенныеНаЗаказыРанее.Зарезервировано, 0) > ЕСТЬNULL(ПотребностиПоЗаказам.РезервироватьПоМереПоступления, 0)
		|			ТОГДА ЕСТЬNULL(ПотребностиПоЗаказам.РезервироватьПоМереПоступления, 0)
		|		ИНАЧЕ ЕСТЬNULL(ЗапасыРаспределенныеНаЗаказыРанее.Зарезервировано, 0)
		|	КОНЕЦ КАК РаспределеноПоЗаказу,
		// Потребности - строки заказов.
		|	Потребности.Заказ КАК ЗаказНаОтгрузку,
		|	Потребности.ДатаСобытия КАК ЖелаемаяДатаОтгрузки,
		|	ЕСТЬNULL(Потребности.РезервироватьПоМереПоступления, 0) КАК РезервироватьПоМереПоступления,
		|	ЕСТЬNULL(Потребности.РезервироватьПоМереПоступленияИтогНаДату, 0) КАК РезервироватьПоМереПоступленияИтогНаДату,
		|	ЕСТЬNULL(Потребности.КОбеспечению, 0) КАК КОбеспечению,
		// Запасы для распределения по товару.
		|	ЕСТЬNULL(ЗапасыПоТоварам.ЗапасНаСкладе - ЗапасыПоТоварам.Резерв, 0) КАК РаспределитьНаВсеСтроки,
		|	ЕСТЬNULL(ЗапасыПоТоварам.ЗапасНаСкладе, 0)
		|		- ЕСТЬNULL(ЗапасыПоТоварам.Резерв, 0)
		|		- ЕСТЬNULL(ЗапасыРаспределенныеНаЗаказыПоТоварам.Зарезервировано, 0) КАК СвободноПоТовару,
		|	ЕСТЬNULL(ЗапасыПоТоварам.ЗапасНаСкладе, 0)
		|		- ЕСТЬNULL(ЗапасыПоТоварам.Резерв, 0)
		|		- ЕСТЬNULL(ПотребностиПоТоварам.РезервироватьПоМереПоступления, 0) КАК СвободноПоТоваруНаСтрокиКОбеспечению,
		|	ЕСТЬNULL(ЗапасыПоТоварам.ЗапасВГрафике, 0) КАК ВсегоВГрафике,
		|	ЕСТЬNULL(ЗапасыПоТоварам.СвободноВГрафике, 0) КАК ВсегоВГрафикеНаСтрокиКОбеспечению,
		// Данные заполнения.
		|	ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.Обеспечить) КАК Состояние,
		|	0 КАК НеОбеспечено,
		// Порядок обхода заказов.
		|	ЗаказыРеестра.Приоритет.РеквизитДопУпорядочивания КАК Приоритет,
		|	
		|	ВЫБОР КОГДА Потребности.ДатаСобытия <> ДАТАВРЕМЯ(1, 1, 1) ТОГДА
		|				Потребности.ДатаСобытия
		|			ИНАЧЕ
		|				ДОБАВИТЬКДАТЕ(ЗаказыРеестра.ДатаДокументаИБ, ДЕНЬ, -1)
		|		КОНЕЦ КАК ПорядокОсновной,
		|		ЗаказыРеестра.ДатаДокументаИБ ПорядокДополнительный
		|ИЗ
		|	ТоварыДляРаспределенияЗапасовПереопределяемый КАК Товары
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПотребностиОстатки КАК Потребности
		|		ПО Потребности.Номенклатура = Товары.Номенклатура
		|		 И Потребности.Характеристика = Товары.Характеристика
		|		 И Потребности.Склад = Товары.Склад
		|		 И Потребности.Назначение = Товары.Назначение
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ЗапасыРаспределенныеНаЗаказыРанее КАК ЗапасыРаспределенныеНаЗаказыРанее
		|		ПО ЗапасыРаспределенныеНаЗаказыРанее.Номенклатура = Товары.Номенклатура
		|		 И ЗапасыРаспределенныеНаЗаказыРанее.Характеристика = Товары.Характеристика
		|		 И ЗапасыРаспределенныеНаЗаказыРанее.Склад = Товары.Склад
		|		 И ЗапасыРаспределенныеНаЗаказыРанее.Назначение = Товары.Назначение
		|		 И ЗапасыРаспределенныеНаЗаказыРанее.Заказ = Потребности.Заказ
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ЗапасыРаспределенныеНаЗаказыРанееГруппировкаПоТоварам КАК ЗапасыРаспределенныеНаЗаказыПоТоварам
		|		ПО ЗапасыРаспределенныеНаЗаказыПоТоварам.Номенклатура = Товары.Номенклатура
		|		 И ЗапасыРаспределенныеНаЗаказыПоТоварам.Характеристика = Товары.Характеристика
		|		 И ЗапасыРаспределенныеНаЗаказыПоТоварам.Склад = Товары.Склад
		|		 И ЗапасыРаспределенныеНаЗаказыПоТоварам.Назначение = Товары.Назначение
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПотребностиОстаткиГруппировкаПоЗаказам КАК ПотребностиПоЗаказам
		|		ПО ПотребностиПоЗаказам.Номенклатура = Товары.Номенклатура
		|		 И ПотребностиПоЗаказам.Характеристика = Товары.Характеристика
		|		 И ПотребностиПоЗаказам.Склад = Товары.Склад
		|		 И ПотребностиПоЗаказам.Назначение = Товары.Назначение
		|		 И ПотребностиПоЗаказам.Заказ = Потребности.Заказ
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПотребностиОстаткиГруппировкаПоТоварам КАК ПотребностиПоТоварам
		|		ПО ПотребностиПоТоварам.Номенклатура = Товары.Номенклатура
		|		 И ПотребностиПоТоварам.Характеристика = Товары.Характеристика
		|		 И ПотребностиПоТоварам.Склад = Товары.Склад
		|		 И ПотребностиПоТоварам.Назначение = Товары.Назначение
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ЗапасыИДоступностьПоДатамГруппировкаПоТоварам КАК ЗапасыПоТоварам
		|		ПО ЗапасыПоТоварам.Номенклатура = Товары.Номенклатура
		|		 И ЗапасыПоТоварам.Характеристика = Товары.Характеристика
		|		 И ЗапасыПоТоварам.Склад = Товары.Склад
		|		 И ЗапасыПоТоварам.Назначение = Товары.Назначение
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаЗаказыРеестра КАК ЗаказыРеестра
		|		ПО ЗаказыРеестра.Ссылка = Потребности.Заказ
		|		
		|УПОРЯДОЧИТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение, Приоритет, ПорядокОсновной, ПорядокДополнительный, ЗаказНаОтгрузку
		|;
		|
		|//////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ГрафикПоступления.Номенклатура КАК Номенклатура,
		|	ГрафикПоступления.Характеристика КАК Характеристика,
		|	ГрафикПоступления.Склад КАК Склад,
		|	ГрафикПоступления.Назначение КАК Назначение,
		|	ГрафикПоступления.Раздел КАК Раздел,
		|	ВЫБОР
		|		КОГДА Раздел = 1 ТОГДА
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОбеспеченКДате)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаетсяПоНеподтвержденномуЗаказу)
		|	КОНЕЦ КАК Состояние,
		|	ГрафикПоступления.Заказ КАК ЗаказНаПоступление,
		|	ГрафикПоступления.ДатаСобытия КАК ДатаПоступления,
		|	ЕСТЬNULL(ГрафикПоступления.Запас - ГрафикПоступления.Свободно, 0) КАК Распределить,
		|	ЕСТЬNULL(ГрафикПоступления.Свободно, 0) КАК РаспределитьНаСтрокиКОбеспечению
		|
		|ИЗ
		|	ТоварыДляРаспределенияЗапасовПереопределяемый КАК Товары
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ЗапасыИДоступностьПоДатам КАК ГрафикПоступления
		|		ПО ГрафикПоступления.Номенклатура = Товары.Номенклатура
		|		 И ГрафикПоступления.Характеристика = Товары.Характеристика
		|		 И ГрафикПоступления.Склад = Товары.Склад
		|		 И ГрафикПоступления.Назначение = Товары.Назначение
		|		 И ГрафикПоступления.Заказ <> НЕОПРЕДЕЛЕНО
		|УПОРЯДОЧИТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение, Раздел, ДатаПоступления, ЗаказНаПоступление
		|;
		|
		|////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ПотребностиОстатки;
		|УНИЧТОЖИТЬ РазличныеЗаказыПотребностей;
		|УНИЧТОЖИТЬ ТаблицаЗаказыРеестра;
		|УНИЧТОЖИТЬ ПотребностиОстаткиГруппировкаПоЗаказам;
		|УНИЧТОЖИТЬ ПотребностиОстаткиГруппировкаПоТоварам;
		|УНИЧТОЖИТЬ ЗапасыИДоступностьПоДатамГруппировкаПоТоварам;
		|УНИЧТОЖИТЬ ЗапасыРаспределенныеНаЗаказыРанее;
		|УНИЧТОЖИТЬ ЗапасыРаспределенныеНаЗаказыРанееГруппировкаПоТоварам";
	
	Текст = СтрЗаменить(Текст, "РеестрДокументовПереопределяемый", ИмяТаблицыРеестрДокументов);
	Запрос.Текст = СтрЗаменить(Текст, "ТоварыДляРаспределенияЗапасовПереопределяемый", ИмяТаблицыТовары);
	Пакет = Запрос.ВыполнитьПакет();

	Таблица = Пакет[Пакет.Вграница() - 9].Выгрузить();
	График = Пакет[Пакет.Вграница() - 8].Выгрузить();
	
	РезультатРаспределенияЗапасовНаПотребности = РегистрыСведений.РаспределениеЗапасов.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	РаспределитьЗапасыВТаблицу(РезультатРаспределенияЗапасовНаПотребности, Таблица, График, Ложь);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Таблица.Номенклатура КАК Номенклатура,
		|	Таблица.Характеристика КАК Характеристика,
		|	Таблица.Склад КАК Склад,
		|	Таблица.Назначение КАК Назначение,
		|	Таблица.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
		|	Таблица.ЖелаемаяДатаОтгрузки КАК ЖелаемаяДатаОтгрузки,
		|	Таблица.ЗаказНаПоступление КАК ЗаказНаПоступление,
		|	Таблица.ДатаПоступления КАК ДатаПоступления,
		|	Таблица.Состояние КАК Состояние,
		|	Таблица.Зарезервировано КАК Зарезервировано,
		|	Таблица.Обеспечено КАК Обеспечено,
		|	Таблица.НеОбеспечено КАК НеОбеспечено,
		|	Таблица.НеОбеспеченоКОбеспечению КАК НеОбеспеченоКОбеспечению
		|ПОМЕСТИТЬ РезультатРаспределенияЗапасовНаПотребности
		|ИЗ
		|	&РезультатРаспределенияЗапасовНаПотребности КАК Таблица";
		
	Запрос.УстановитьПараметр("РезультатРаспределенияЗапасовНаПотребности", РезультатРаспределенияЗапасовНаПотребности);
	Запрос.Выполнить();
	
КонецПроцедуры

Функция ДанныеДляЗаписиТоваровВПроизводительномРежиме(Запрос, ИмяТаблицыТовары = "ТоварыДляРаспределенияЗапасов", ИмяТаблицыРезультат = "")
	
	Текст =
		"ВЫБРАТЬ
		|	ЛОЖЬ КАК Пустая,
		|	Таблица.Номенклатура КАК Номенклатура,
		|	Таблица.Характеристика КАК Характеристика,
		|	Таблица.Склад КАК Склад,
		|	Таблица.Назначение КАК Назначение,
		|	ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемаяОтгрузка) Состояние,
		|	Таблица.Заказ КАК ЗаказНаОтгрузку,
		|	Таблица.ДатаСобытия КАК ЖелаемаяДатаОтгрузки,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказНаПоступление,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаПоступления,
		|	1 КАК ТипЗаписиРаспределенияЗапасов,
		|	Таблица.РезервироватьПоМереПоступления КАК РезервироватьПоМереПоступления,
		|	Таблица.КОбеспечению КАК КОбеспечениюБезРезерва,
		|	Таблица.ОтложитьРезервирование КАК ОтложитьРезервирование,
		|	Таблица.РезервироватьНаСкладе КАК Резервировать,
		|	Таблица.НеОбеспечивать КАК НеОбеспечивать,
		|	0 КАК Зарезервировано,
		|	0 КАК Обеспечено,
		|	ЛОЖЬ КАК РезервПревышаетОстатки,
		|	0 КАК НеОбеспечено,
		|	0 КАК Запас,
		|	0 КАК Резерв,
		|	0 КАК Свободно,
		|	0 КАК Излишек,
		|	0 КАК ВНаличии,
		|	ПотребностиРезервироватьПоМере.РезервироватьПоМереПоступленияИтогНаДату КАК РезервироватьПоМереПоступленияИтогНаДату,
		|	0 КАК НеОбеспеченоКОбеспечению
		|ПОМЕСТИТЬ ИмяТаблицыРезультатПереопределяемый
		|ИЗ
		|	ВсеПотребности КАК Таблица
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПотребностиРезервироватьПоМере КАК ПотребностиРезервироватьПоМере
		|		ПО ПотребностиРезервироватьПоМере.Номенклатура = Таблица.Номенклатура
		|		 И ПотребностиРезервироватьПоМере.Характеристика = Таблица.Характеристика
		|		 И ПотребностиРезервироватьПоМере.Склад = Таблица.Склад
		|		 И ПотребностиРезервироватьПоМере.Назначение = Таблица.Назначение
		|		 И ПотребностиРезервироватьПоМере.Заказ = Таблица.Заказ
		|		 И ПотребностиРезервироватьПоМере.ДатаСобытия = Таблица.ДатаСобытия
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЛОЖЬ КАК Пустая,
		|	Таблица.Номенклатура КАК Номенклатура,
		|	Таблица.Характеристика КАК Характеристика,
		|	Таблица.Склад КАК Склад,
		|	Таблица.Назначение КАК Назначение,
		|	ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.НеОбеспечивать) Состояние,
		|	Таблица.Заказ КАК ЗаказНаОтгрузку,
		|	Таблица.ДатаСобытия КАК ЖелаемаяДатаОтгрузки,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказНаПоступление,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаПоступления,
		|	1 КАК ТипЗаписиРаспределенияЗапасов,
		|	0 КАК РезервироватьПоМереПоступления,
		|	0 КАК КОбеспечениюБезРезерва,
		|	0 КАК ОтложитьРезервирование,
		|	0 КАК Резервировать,
		|	0 КАК НеОбеспечивать,
		|	0 КАК Зарезервировано,
		|	0 КАК Обеспечено,
		|	ЛОЖЬ КАК РезервПревышаетОстатки,
		|	Таблица.НеОбеспечивать КАК НеОбеспечено,
		|	0 КАК Запас,
		|	0 КАК Резерв,
		|	0 КАК Свободно,
		|	0 КАК Излишек,
		|	0 КАК ВНаличии,
		|	0 КАК РезервироватьПоМереПоступленияИтогНаДату,
		|	0 КАК НеОбеспеченоКОбеспечению
		|ИЗ
		|	ВсеПотребности КАК Таблица
		|ГДЕ
		|	Таблица.НеОбеспечивать > 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЛОЖЬ КАК Пустая,
		|	Таблица.Номенклатура КАК Номенклатура,
		|	Таблица.Характеристика КАК Характеристика,
		|	Таблица.Склад КАК Склад,
		|	Таблица.Назначение КАК Назначение,
		|	ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ВРезерве) Состояние,
		|	Таблица.Заказ КАК ЗаказНаОтгрузку,
		|	Таблица.ДатаСобытия КАК ЖелаемаяДатаОтгрузки,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказНаПоступление,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаПоступления,
		|	1 КАК ТипЗаписиРаспределенияЗапасов,
		|	0 КАК РезервироватьПоМереПоступления,
		|	0 КАК КОбеспечениюБезРезерва,
		|	0 КАК ОтложитьРезервирование,
		|	0 КАК Резервировать,
		|	0 КАК НеОбеспечивать,
		|	Таблица.РезервироватьНаСкладе КАК Зарезервировано,
		|	0 КАК Обеспечено,
		|	ЛОЖЬ КАК РезервПревышаетОстатки,
		|	0 КАК НеОбеспечено,
		|	0 КАК Запас,
		|	0 КАК Резерв,
		|	0 КАК Свободно,
		|	0 КАК Излишек,
		|	0 КАК ВНаличии,
		|	0 КАК РезервироватьПоМереПоступленияИтогНаДату,
		|	0 КАК НеОбеспеченоКОбеспечению
		|ИЗ
		|	ВсеПотребности КАК Таблица
		|ГДЕ
		|	Таблица.РезервироватьНаСкладе > 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЛОЖЬ КАК Пустая,
		|	Таблица.Номенклатура КАК Номенклатура,
		|	Таблица.Характеристика КАК Характеристика,
		|	Таблица.Склад КАК Склад,
		|	Таблица.Назначение КАК Назначение,
		|	Таблица.Состояние Состояние,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказНаОтгрузку,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ЖелаемаяДатаОтгрузки,
		|	Таблица.Заказ КАК ЗаказНаПоступление,
		|	Таблица.ДатаСобытия КАК ДатаПоступления,
		|	1 КАК ТипЗаписиРаспределенияЗапасов,
		|	0 КАК РезервироватьПоМереПоступления,
		|	0 КАК КОбеспечениюБезРезерва,
		|	0 КАК ОтложитьРезервирование,
		|	0 КАК Резервировать,
		|	0 КАК НеОбеспечивать,
		|	0 КАК Зарезервировано,
		|	0 КАК Обеспечено,
		|	ЛОЖЬ КАК РезервПревышаетОстатки,
		|	0 КАК НеОбеспечено,
		|	Таблица.Запас КАК Запас,
		|	Таблица.Резерв КАК Резерв,
		|	Таблица.Свободно КАК Свободно,
		|	Таблица.Излишек КАК Излишек,
		|	Таблица.ВНаличии КАК ВНаличии,
		|	0 КАК РезервироватьПоМереПоступленияИтогНаДату,
		|	0 КАК НеОбеспеченоКОбеспечению
		|ИЗ
		|	ЗапасыИДоступностьПоДатам КАК Таблица
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЛОЖЬ КАК Пустая,
		|	Таблица.Номенклатура КАК Номенклатура,
		|	Таблица.Характеристика КАК Характеристика,
		|	Таблица.Склад КАК Склад,
		|	Таблица.Назначение КАК Назначение,
		|	Таблица.Состояние Состояние,
		|	Таблица.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
		|	Таблица.ЖелаемаяДатаОтгрузки КАК ЖелаемаяДатаОтгрузки,
		|	Таблица.ЗаказНаПоступление КАК ЗаказНаПоступление,
		|	Таблица.ДатаПоступления КАК ДатаПоступления,
		|	0 КАК ТипЗаписиРаспределенияЗапасов,
		|	0 КАК РезервироватьПоМереПоступления,
		|	0 КАК КОбеспечениюБезРезерва,
		|	0 КАК ОтложитьРезервирование,
		|	0 КАК Резервировать,
		|	0 КАК НеОбеспечивать,
		|	Таблица.Зарезервировано КАК Зарезервировано,
		|	Таблица.Обеспечено КАК Обеспечено,
		|	ЛОЖЬ КАК РезервПревышаетОстатки,
		|	Таблица.НеОбеспечено КАК НеОбеспечено,
		|	0 КАК Запас,
		|	0 КАК Резерв,
		|	0 КАК Свободно,
		|	0 КАК Излишек,
		|	0 КАК ВНаличии,
		|	0 КАК РезервироватьПоМереПоступленияИтогНаДату,
		|	Таблица.НеОбеспеченоКОбеспечению КАК НеОбеспеченоКОбеспечению
		|ИЗ
		|	РезультатРаспределенияЗапасовНаПотребности КАК Таблица
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ИСТИНА КАК Пустая,
		|	Таблица.Номенклатура КАК Номенклатура,
		|	Таблица.Характеристика КАК Характеристика,
		|	Таблица.Склад КАК Склад,
		|	Таблица.Назначение КАК Назначение,
		|	НЕОПРЕДЕЛЕНО Состояние,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказНаОтгрузку,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ЖелаемаяДатаОтгрузки,
		|	НЕОПРЕДЕЛЕНО КАК ЗаказНаПоступление,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаПоступления,
		|	0 КАК ТипЗаписиРаспределенияЗапасов,
		|	0 КАК РезервироватьПоМереПоступления,
		|	0 КАК КОбеспечениюБезРезерва,
		|	0 КАК ОтложитьРезервирование,
		|	0 КАК Резервировать,
		|	0 КАК НеОбеспечивать,
		|	0 КАК Зарезервировано,
		|	0 КАК Обеспечено,
		|	ЛОЖЬ КАК РезервПревышаетОстатки,
		|	0 КАК НеОбеспечено,
		|	0 КАК Запас,
		|	0 КАК Резерв,
		|	0 КАК Свободно,
		|	0 КАК Излишек,
		|	0 КАК ВНаличии,
		|	0 КАК РезервироватьПоМереПоступленияИтогНаДату,
		|	0 КАК НеОбеспеченоКОбеспечению
		|ИЗ
		|	ТоварыДляРаспределенияЗапасовПереопределяемый КАК Таблица";
	
	ТекстУпорядочивания =
		"УПОРЯДОЧИТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение"; //@Query-part
	
	ТекстУдаленияТаблиц =
		"УНИЧТОЖИТЬ ВсеПотребности;
		|УНИЧТОЖИТЬ ПотребностиРезервироватьПоМере;
		|УНИЧТОЖИТЬ ЗапасыИДоступностьПоДатам;
		|УНИЧТОЖИТЬ РезультатРаспределенияЗапасовНаПотребности";
	
	Если ИмяТаблицыРезультат = "" Тогда
		Тексты = Новый Массив();
		Тексты.Добавить(Текст);
		Тексты.Добавить(ТекстУпорядочивания);
		Текст = СтрСоединить(Тексты, Символы.ПС);
	КонецЕсли;
	Тексты = Новый Массив();
	Тексты.Добавить(Текст);
	Тексты.Добавить(ТекстУдаленияТаблиц);
	Текст = СтрСоединить(Тексты, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	Текст = СтрЗаменить(Текст, "ТоварыДляРаспределенияЗапасовПереопределяемый", ИмяТаблицыТовары);
	Если ИмяТаблицыРезультат = "" Тогда
		ЗаменяемыйТекст = "ПОМЕСТИТЬ ИмяТаблицыРезультатПереопределяемый"; //@Query-part
		Текст = СтрЗаменить(Текст, ЗаменяемыйТекст, "");
		Запрос.Текст = Текст;
		Результат = Запрос.ВыполнитьПакет()[0].Выгрузить();
	Иначе
		Запрос.Текст = СтрЗаменить(Текст, "ИмяТаблицыРезультатПереопределяемый", ИмяТаблицыРезультат);
		Результат = Запрос.ВыполнитьПакет()[0];
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ТекстЗапросаРаспределенияЗапасов(ПолучатьГраницыПериодаОбеспечения = Истина, ИмяТаблицыТовары = "ТоварыДляРаспределенияЗапасов") Экспорт
	
	Тексты = Новый Массив();
	Если ПолучатьГраницыПериодаОбеспечения Тогда
		
		Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Товары.Номенклатура   КАК Номенклатура,
		|	Товары.Характеристика КАК Характеристика,
		|	Товары.Склад          КАК Склад
		|ПОМЕСТИТЬ РазличныеТоварыБезНазначений
		|ИЗ
		|	ТоварыДляРаспределенияЗапасовПереопределяемый КАК Товары
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад
		|;
		|
		|///////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Товары.Номенклатура                 КАК Номенклатура,
		|	Товары.Характеристика               КАК Характеристика,
		|	Товары.Склад                        КАК Склад,
		|	РасчетПереопределяемый.ДатаПоставки КАК ДатаПоставки,
		|	НЕ ЕСТЬNULL(НастройкаКонтроляОбеспеченияХарактеристика.КонтролироватьСвободныеОстатки,
		|				ЕСТЬNULL(НастройкаКонтроляОбеспеченияНоменклатура.КонтролироватьСвободныеОстатки,
		|					ЕСТЬNULL(НастройкаКонтроляОбеспечения.КонтролироватьСвободныеОстатки, ЛОЖЬ)))
		|		ИЛИ ВЫРАЗИТЬ(Товары.Номенклатура КАК Справочник.Номенклатура).ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа) КАК ПерераспределятьСкладскиеЗапасы
		|ПОМЕСТИТЬ ГраницыПериодаОбеспечения
		|ИЗ
		|	РазличныеТоварыБезНазначений КАК Товары
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РасчетПереопределяемый ПО ИСТИНА
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОбеспечения КАК НастройкаКонтроляОбеспеченияХарактеристика
		|		ПО НастройкаКонтроляОбеспеченияХарактеристика.Склад = Товары.Склад
		|		 И НастройкаКонтроляОбеспеченияХарактеристика.Номенклатура = Товары.Номенклатура
		|		 И НастройкаКонтроляОбеспеченияХарактеристика.Характеристика = Товары.Характеристика
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОбеспечения КАК НастройкаКонтроляОбеспеченияНоменклатура
		|		ПО НастройкаКонтроляОбеспеченияНоменклатура.Склад = Товары.Склад
		|		 И НастройкаКонтроляОбеспеченияНоменклатура.Номенклатура = Товары.Номенклатура
		|		 И НастройкаКонтроляОбеспеченияНоменклатура.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
		|		 И НастройкаКонтроляОбеспеченияХарактеристика.Номенклатура ЕСТЬ NULL
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОбеспечения КАК НастройкаКонтроляОбеспечения
		|		ПО НастройкаКонтроляОбеспечения.Склад = Товары.Склад
		|		 И НастройкаКонтроляОбеспечения.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|		 И НастройкаКонтроляОбеспечения.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
		|		 И НастройкаКонтроляОбеспеченияХарактеристика.Номенклатура ЕСТЬ NULL
		|		 И НастройкаКонтроляОбеспеченияНоменклатура.Номенклатура ЕСТЬ NULL
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад";
		Тексты.Добавить(Текст);
		
	КонецЕсли;
	
	Текст =
		"ВЫБРАТЬ
		|	Товары.Номенклатура          КАК Номенклатура,
		|	Товары.Характеристика        КАК Характеристика,
		|	Товары.Склад                 КАК Склад,
		|	Товары.Назначение            КАК Назначение,
		|	Сведения.ЗаказНаОтгрузку     КАК ЗаказНаОтгрузку,
		|	СУММА(Сведения.Зарезервировано) КАК Зарезервировано
		|ПОМЕСТИТЬ РаспределеноПоЗаказам
		|ИЗ
		|	ТоварыДляРаспределенияЗапасовПереопределяемый КАК Товары
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаспределениеЗапасов КАК Сведения
		|		ПО Сведения.Номенклатура  = Товары.Номенклатура
		|		 И Сведения.Характеристика = Товары.Характеристика
		|		 И Сведения.Склад          = Товары.Склад
		|		 И Сведения.Назначение     = Товары.Назначение
		|		 И Сведения.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОбеспеченНаСкладе)
		|ГДЕ
		|	НЕ Сведения.Номенклатура ЕСТЬ NULL
		|СГРУППИРОВАТЬ ПО
		|	Товары.Номенклатура,
		|	Товары.Характеристика,
		|	Товары.Склад,
		|	Товары.Назначение,
		|	Сведения.ЗаказНаОтгрузку
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение, ЗаказНаОтгрузку
		|;
		|
		|//////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Товары.Номенклатура          КАК Номенклатура,
		|	Товары.Характеристика        КАК Характеристика,
		|	Товары.Склад                 КАК Склад,
		|	Товары.Назначение            КАК Назначение,
		|	Сведения.ЗаказНаОтгрузку     КАК ЗаказНаОтгрузку,
		|	СУММА(Сведения.РезервироватьПоМереПоступления) КАК РезервироватьПоМереПоступления,
		|	СУММА(Сведения.КОбеспечениюБезРезерва) + СУММА(Сведения.ОтложитьРезервирование) КАК КОбеспечению
		|ПОМЕСТИТЬ ПотребностиПоЗаказамГруппировка
		|ИЗ
		|	ТоварыДляРаспределенияЗапасовПереопределяемый КАК Товары
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаспределениеЗапасов КАК Сведения
		|		ПО Сведения.Номенклатура  = Товары.Номенклатура
		|		 И Сведения.Характеристика = Товары.Характеристика
		|		 И Сведения.Склад          = Товары.Склад
		|		 И Сведения.Назначение     = Товары.Назначение
		|		 И Сведения.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемаяОтгрузка)
		|		 И (Сведения.РезервироватьПоМереПоступления > 0
		|				ИЛИ Сведения.КОбеспечениюБезРезерва > 0
		|				ИЛИ Сведения.ОтложитьРезервирование > 0)
		|ГДЕ
		|	НЕ Сведения.Номенклатура ЕСТЬ NULL
		|СГРУППИРОВАТЬ ПО
		|	Товары.Номенклатура,
		|	Товары.Характеристика,
		|	Товары.Склад,
		|	Товары.Назначение,
		|	Сведения.ЗаказНаОтгрузку
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение, ЗаказНаОтгрузку
		|;
		|
		|/////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Потребности.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку
		|ПОМЕСТИТЬ РазличныеЗаказыПотребностей
		|ИЗ
		|	ПотребностиПоЗаказамГруппировка КАК Потребности
		|ИНДЕКСИРОВАТЬ ПО
		|	ЗаказНаОтгрузку
		|;
		|
		|///////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗаказыРеестра.Ссылка КАК Ссылка,
		|	МИНИМУМ(ЗаказыРеестра.Приоритет) КАК Приоритет,
		|	МИНИМУМ(ЗаказыРеестра.ДатаДокументаИБ) КАК ДатаДокументаИБ
		|ПОМЕСТИТЬ ТаблицаЗаказыРеестра
		|ИЗ
		|	РазличныеЗаказыПотребностей КАК РазличныеЗаказыПотребностей
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК ЗаказыРеестра
		|		ПО ЗаказыРеестра.Ссылка = РазличныеЗаказыПотребностей.ЗаказНаОтгрузку
		|		И НЕ ЗаказыРеестра.ДополнительнаяЗапись
		|ГДЕ
		|	НЕ ЗаказыРеестра.Ссылка ЕСТЬ NULL
		|СГРУППИРОВАТЬ ПО
		|	ЗаказыРеестра.Ссылка
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		|
		|////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Потребности.Номенклатура    КАК Номенклатура,
		|	Потребности.Характеристика  КАК Характеристика,
		|	Потребности.Склад           КАК Склад,
		|	Потребности.Назначение      КАК Назначение,
		|	Потребности.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
		|	Потребности.РезервироватьПоМереПоступления КАК РезервироватьПоМереПоступления,
		|	Потребности.КОбеспечению КАК КОбеспечению,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(РаспределеноПоЗаказам.Зарезервировано, 0) - ЕСТЬNULL(ОтгрузкаИзСобственногоРезерва.Отгрузка, 0) < 0
		|			ТОГДА 0
		|		КОГДА Потребности.РезервироватьПоМереПоступления
		|				< ЕСТЬNULL(РаспределеноПоЗаказам.Зарезервировано, 0) - ЕСТЬNULL(ОтгрузкаИзСобственногоРезерва.Отгрузка, 0)
		|			ТОГДА Потребности.РезервироватьПоМереПоступления
		|		ИНАЧЕ ЕСТЬNULL(РаспределеноПоЗаказам.Зарезервировано, 0) - ЕСТЬNULL(ОтгрузкаИзСобственногоРезерва.Отгрузка, 0)
		|	КОНЕЦ КАК Зарезервировано
		|ПОМЕСТИТЬ ПотребностиПоЗаказам
		|ИЗ
		|	ПотребностиПоЗаказамГруппировка КАК Потребности
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РаспределеноПоЗаказам КАК РаспределеноПоЗаказам
		|		ПО РаспределеноПоЗаказам.Номенклатура    = Потребности.Номенклатура
		|		 И РаспределеноПоЗаказам.Характеристика  = Потребности.Характеристика
		|		 И РаспределеноПоЗаказам.Склад           = Потребности.Склад
		|		 И РаспределеноПоЗаказам.Назначение      = Потребности.Назначение
		|		 И РаспределеноПоЗаказам.ЗаказНаОтгрузку = Потребности.ЗаказНаОтгрузку
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ИзменениеОстаткаПоТоварамДляКонтроляОстатков КАК ОтгрузкаИзСобственногоРезерва
		|		ПО ОтгрузкаИзСобственногоРезерва.Номенклатура = Потребности.Номенклатура
		|		 И ОтгрузкаИзСобственногоРезерва.Характеристика = Потребности.Характеристика
		|		 И ОтгрузкаИзСобственногоРезерва.Склад = Потребности.Склад
		|		 И ОтгрузкаИзСобственногоРезерва.Назначение = Потребности.Назначение
		|		 И ОтгрузкаИзСобственногоРезерва.ЗаказНаОтгрузку = Потребности.ЗаказНаОтгрузку
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение, ЗаказНаОтгрузку
		|;
		|
		|/////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Потребности.Номенклатура    КАК Номенклатура,
		|	Потребности.Характеристика  КАК Характеристика,
		|	Потребности.Склад           КАК Склад,
		|	Потребности.Назначение      КАК Назначение,
		|	СУММА(ВЫБОР КОГДА ГраницыПериодаОбеспечения.ПерераспределятьСкладскиеЗапасы ТОГДА
		|				0
		|			ИНАЧЕ
		|				Потребности.Зарезервировано
		|		КОНЕЦ) КАК Зарезервировано,
		|	СУММА(Потребности.РезервироватьПоМереПоступления) КАК РезервироватьПоМереПоступления,
		|	СУММА(Потребности.КОбеспечению) КАК КОбеспечению
		|ПОМЕСТИТЬ ПотребностиПоТовару
		|ИЗ
		|	ПотребностиПоЗаказам КАК Потребности
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ГраницыПериодаОбеспечения КАК ГраницыПериодаОбеспечения
		|		ПО ГраницыПериодаОбеспечения.Номенклатура   = Потребности.Номенклатура
		|		 И ГраницыПериодаОбеспечения.Характеристика = Потребности.Характеристика
		|		 И ГраницыПериодаОбеспечения.Склад          = Потребности.Склад
		|СГРУППИРОВАТЬ ПО
		|	Потребности.Номенклатура,
		|	Потребности.Характеристика,
		|	Потребности.Склад,
		|	Потребности.Назначение
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение
		|;
		|
		|/////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Товары.Номенклатура         КАК Номенклатура,
		|	Товары.Характеристика       КАК Характеристика,
		|	Товары.Склад                КАК Склад,
		|	Товары.Назначение           КАК Назначение,
		|	Сведения.ЗаказНаПоступление КАК ЗаказНаПоступление,
		|	Сведения.ДатаПоступления    КАК ДатаПоступления,
		|	ВЫБОР КОГДА Сведения.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемоеПоступление) ТОГДА
		|				ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОбеспеченКДате)
		|			ИНАЧЕ
		|				ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаетсяПоНеподтвержденномуЗаказу)
		|		КОНЕЦ КАК Состояние,
		|	ЕСТЬNULL(Сведения.Запас - Сведения.Свободно, 0) КАК Распределить,
		|	ЕСТЬNULL(Сведения.Свободно, 0) КАК РаспределитьНаСтрокиКОбеспечению,
		|	ВЫБОР КОГДА Сведения.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемоеПоступление) ТОГДА
		|				1
		|			ИНАЧЕ
		|				2
		|		КОНЕЦ КАК ПорядокПоСостоянию
		|ПОМЕСТИТЬ ГрафикПоступления
		|ИЗ
		|	ТоварыДляРаспределенияЗапасовПереопределяемый КАК Товары
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПотребностиПоТовару КАК ЕстьСтрокиКОбеспечению
		|		ПО ЕстьСтрокиКОбеспечению.Номенклатура = Товары.Номенклатура
		|		 И ЕстьСтрокиКОбеспечению.Характеристика = Товары.Характеристика
		|		 И ЕстьСтрокиКОбеспечению.Склад = Товары.Склад
		|		 И ЕстьСтрокиКОбеспечению.Назначение = Товары.Назначение
		|		 И ЕстьСтрокиКОбеспечению.КОбеспечению > 0
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаспределениеЗапасов КАК Сведения
		|		ПО Сведения.Номенклатура    = Товары.Номенклатура
		|		 И Сведения.Характеристика  = Товары.Характеристика
		|		 И Сведения.Склад           = Товары.Склад
		|		 И Сведения.Назначение      = Товары.Назначение
		|		 И (Сведения.Запас - Сведения.Свободно > 0
		|			ИЛИ Сведения.Свободно > 0 И НЕ ЕстьСтрокиКОбеспечению.Номенклатура ЕСТЬ NULL)
		|		 И Сведения.Состояние В(
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемоеПоступление),
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ПоступлениеНеподтвержденное))
		|;
		|
		|//////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Сведения.Номенклатура        КАК Номенклатура,
		|	Сведения.Характеристика      КАК Характеристика,
		|	Сведения.Склад               КАК Склад,
		|	Сведения.Назначение          КАК Назначение,
		|	СУММА(Сведения.Распределить) КАК Распределить,
		|	СУММА(Сведения.РаспределитьНаСтрокиКОбеспечению) КАК РаспределитьНаСтрокиКОбеспечению
		|ПОМЕСТИТЬ ГрафикПоступленияПоТовару
		|ИЗ
		|	ГрафикПоступления КАК Сведения
		|СГРУППИРОВАТЬ ПО
		|	Сведения.Номенклатура,
		|	Сведения.Характеристика,
		|	Сведения.Склад,
		|	Сведения.Назначение
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение
		|;
		|
		|//////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Товары.Номенклатура                                             КАК Номенклатура,
		|	Товары.Характеристика                                           КАК Характеристика,
		|	Товары.Склад                                                    КАК Склад,
		|	Товары.Назначение                                               КАК Назначение,
		|	Сведения.ЗаказНаОтгрузку                                        КАК ЗаказНаОтгрузку,
		|	Сведения.ЖелаемаяДатаОтгрузки                                   КАК ЖелаемаяДатаОтгрузки,
		|	ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.Обеспечить) КАК Состояние,
		|	0                                                               КАК НеОбеспечено,
		|	ЕСТЬNULL(Сведения.РезервироватьПоМереПоступления, 0)            КАК РезервироватьПоМереПоступления,
		|	ЕСТЬNULL(Сведения.РезервироватьПоМереПоступленияИтогНаДату, 0)  КАК РезервироватьПоМереПоступленияИтогНаДату,
		|	ЕСТЬNULL(Сведения.КОбеспечениюБезРезерва + Сведения.ОтложитьРезервирование, 0) КАК КОбеспечению,
		|	ЕСТЬNULL(ПотребностиПоТовару.РезервироватьПоМереПоступления, 0) КАК РезервироватьПоМереПоступленияПоТовару,
		|	ЕСТЬNULL(ПотребностиПоЗаказам.РезервироватьПоМереПоступления, 0) КАК РезервироватьПоМереПоступленияПоЗаказу,
		|	ЕСТЬNULL(ПотребностиПоТовару.КОбеспечению, 0)                   КАК КОбеспечениюПоТовару,
		|	ЕСТЬNULL(ПотребностиПоЗаказам.КОбеспечению, 0)                  КАК КОбеспечениюПоЗаказу,
		|	ЕСТЬNULL(ПотребностиПоЗаказам.Зарезервировано, 0)               КАК РаспределеноПоЗаказу,
		|	ЕСТЬNULL(ОстатокНаСкладе.Запас - ОстатокНаСкладе.Резерв, 0)     КАК РаспределитьНаВсеСтроки,
		|	ЕСТЬNULL(ОстатокНаСкладе.Запас - ОстатокНаСкладе.Резерв, 0)
		|		- ЕСТЬNULL(ПотребностиПоТовару.Зарезервировано, 0)          КАК СвободноПоТовару,
		|	ЕСТЬNULL(ОстатокНаСкладе.Свободно, 0)                           КАК СвободноПоТоваруНаСтрокиКОбеспечению,
		|	ГрафикПоТовару.Распределить                                     КАК ВсегоВГрафике,
		|	ГрафикПоТовару.РаспределитьНаСтрокиКОбеспечению                 КАК ВсегоВГрафикеНаСтрокиКОбеспечению,
		// Порядок обхода заказов.
		|	ЗаказыРеестра.Приоритет.РеквизитДопУпорядочивания КАК Приоритет,
		|	
		|	ВЫБОР КОГДА Сведения.ЖелаемаяДатаОтгрузки <> ДАТАВРЕМЯ(1, 1, 1) ТОГДА
		|				Сведения.ЖелаемаяДатаОтгрузки
		|			ИНАЧЕ
		|				ДОБАВИТЬКДАТЕ(ЗаказыРеестра.ДатаДокументаИБ, ДЕНЬ, -1)
		|		КОНЕЦ КАК ПорядокОсновной,
		|		ЗаказыРеестра.ДатаДокументаИБ ПорядокДополнительный
		|ИЗ
		|	ТоварыДляРаспределенияЗапасовПереопределяемый КАК Товары
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПотребностиПоЗаказам КАК ПотребностиПоЗаказам
		|		ПО ПотребностиПоЗаказам.Номенклатура    = Товары.Номенклатура
		|		 И ПотребностиПоЗаказам.Характеристика  = Товары.Характеристика
		|		 И ПотребностиПоЗаказам.Склад           = Товары.Склад
		|		 И ПотребностиПоЗаказам.Назначение      = Товары.Назначение
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ГраницыПериодаОбеспечения КАК ГраницыПериодаОбеспечения
		|		ПО ГраницыПериодаОбеспечения.Номенклатура    = Товары.Номенклатура
		|		 И ГраницыПериодаОбеспечения.Характеристика  = Товары.Характеристика
		|		 И ГраницыПериодаОбеспечения.Склад           = Товары.Склад
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПотребностиПоТовару КАК ПотребностиПоТовару
		|		ПО ПотребностиПоТовару.Номенклатура    = Товары.Номенклатура
		|		 И ПотребностиПоТовару.Характеристика  = Товары.Характеристика
		|		 И ПотребностиПоТовару.Склад           = Товары.Склад
		|		 И ПотребностиПоТовару.Назначение      = Товары.Назначение
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаспределениеЗапасов КАК ОстатокНаСкладе
		|		ПО ОстатокНаСкладе.Номенклатура   = Товары.Номенклатура
		|		 И ОстатокНаСкладе.Характеристика = Товары.Характеристика
		|		 И ОстатокНаСкладе.Склад          = Товары.Склад
		|		 И ОстатокНаСкладе.Назначение     = Товары.Назначение
		|		 И ОстатокНаСкладе.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе)
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ГрафикПоступленияПоТовару КАК ГрафикПоТовару
		|		ПО ГрафикПоТовару.Номенклатура   = Товары.Номенклатура
		|		 И ГрафикПоТовару.Характеристика = Товары.Характеристика
		|		 И ГрафикПоТовару.Склад          = Товары.Склад
		|		 И ГрафикПоТовару.Назначение     = Товары.Назначение
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаспределениеЗапасов КАК Сведения
		|		ПО Сведения.ЗаказНаОтгрузку = ПотребностиПоЗаказам.ЗаказНаОтгрузку
		|		 И Сведения.Номенклатура    = ПотребностиПоЗаказам.Номенклатура
		|		 И Сведения.Характеристика  = ПотребностиПоЗаказам.Характеристика
		|		 И Сведения.Склад           = ПотребностиПоЗаказам.Склад
		|		 И Сведения.Назначение      = ПотребностиПоЗаказам.Назначение
		|		 И Сведения.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемаяОтгрузка)
		|		 И (Сведения.РезервироватьПоМереПоступления > 0 ИЛИ Сведения.КОбеспечениюБезРезерва > 0 ИЛИ Сведения.ОтложитьРезервирование > 0)
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаЗаказыРеестра КАК ЗаказыРеестра
		|		ПО ЗаказыРеестра.Ссылка = ПотребностиПоЗаказам.ЗаказНаОтгрузку
		|		
		|УПОРЯДОЧИТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение, Приоритет, ПорядокОсновной, ПорядокДополнительный, ЗаказНаОтгрузку
		|;
		|
		|//////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ГрафикПоступления.Номенклатура              КАК Номенклатура,
		|	ГрафикПоступления.Характеристика            КАК Характеристика,
		|	ГрафикПоступления.Склад                     КАК Склад,
		|	ГрафикПоступления.Назначение                КАК Назначение,
		|	ГрафикПоступления.Состояние                 КАК Состояние,
		|	ГрафикПоступления.ЗаказНаПоступление        КАК ЗаказНаПоступление,
		|	ГрафикПоступления.ДатаПоступления           КАК ДатаПоступления,
		|	ГрафикПоступления.Распределить              КАК Распределить,
		|	ГрафикПоступления.РаспределитьНаСтрокиКОбеспечению КАК РаспределитьНаСтрокиКОбеспечению,
		|	ГрафикПоступления.ПорядокПоСостоянию        КАК ПорядокПоСостоянию
		|ИЗ
		|	ГрафикПоступления КАК ГрафикПоступления
		|УПОРЯДОЧИТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение, ПорядокПоСостоянию, ДатаПоступления, ЗаказНаПоступление";
	Тексты.Добавить(Текст);
	Текст = СтрСоединить(Тексты, ОбщегоНазначения.РазделительПакетаЗапросов());
	Подстановки = ОбеспечениеВДокументахСервер.ПодстановкиГраницыОбеспечиваемогоПериода(
		"Товары.Номенклатура", "Товары.Характеристика", "Товары.Склад", "&НачалоТекущегоДня", "ИСТИНА");
	
	Текст = СтрЗаменить(Текст, "РасчетПереопределяемый.ДатаПоставки",               Подстановки.ПолеДатаПоставки);
	Текст = СтрЗаменить(Текст, "ЛЕВОЕ СОЕДИНЕНИЕ РасчетПереопределяемый ПО ИСТИНА", Подстановки.Соединения);
	Текст = СтрЗаменить(Текст, "ТоварыДляРаспределенияЗапасовПереопределяемый",     ИмяТаблицыТовары);
	Возврат Текст;
	
КонецФункции

Процедура РаспределитьЗапасыВТаблицу(Набор, Таблица, График, ДобавлятьПустуюЗаписьПоТовару) Экспорт
	
	Номенклатура   = Неопределено;
	Характеристика = Неопределено;
	Склад          = Неопределено;
	Назначение     = Неопределено;
	ВариантОбеспеченияКОбеспечению = Перечисления.ВариантыОбеспечения.КОбеспечению;
	ИндексВГрафике = 0;
	ИндексВГрафикеОбеспечить = 0;
	
	Свободно                = 0;
	РаспределитьНаВсеСтроки = 0;
	КОбеспечению            = 0;
	СвободноВГрафике        = 0;
	РезервироватьПоМереПоступления = 0;
	
	ВсегоСтрок = Таблица.Количество();
	Для Индекс = 0 По ВсегоСтрок - 1 Цикл
		
		ТекСтрока = Таблица[Индекс];
		
		Если Номенклатура <> ТекСтрока.Номенклатура
				Или Характеристика <> ТекСтрока.Характеристика
				Или Склад <> ТекСтрока.Склад
				Или Назначение <> ТекСтрока.Назначение Тогда
					
					Номенклатура   = ТекСтрока.Номенклатура;
					Характеристика = ТекСтрока.Характеристика;
					Склад          = ТекСтрока.Склад;
					Назначение     = ТекСтрока.Назначение;
					
					РаспределитьНаВсеСтроки = Макс(ТекСтрока.РаспределитьНаВсеСтроки, 0);
					Свободно                = Макс(ТекСтрока.СвободноПоТовару, 0);
					КОбеспечению            = ТекСтрока.КОбеспечениюПоТовару;
					СвободноНаСтрокиКОбеспечению = Макс(ТекСтрока.СвободноПоТоваруНаСтрокиКОбеспечению, 0);
					
					СвободноВГрафике = Мин(
						Макс(ТекСтрока.РезервироватьПоМереПоступленияПоТовару - РаспределитьНаВсеСтроки, 0),
						ТекСтрока.ВсегоВГрафике);
					СвободноВГрафикеНаСтрокиКОбеспечению = ТекСтрока.ВсегоВГрафикеНаСтрокиКОбеспечению;
					
					Если ДобавлятьПустуюЗаписьПоТовару Тогда
						НоваяЗапись = Набор.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекСтрока, "Номенклатура,Характеристика,Склад,Назначение");
						НоваяЗапись.Пустая = Истина;
					КонецЕсли;
					
					
		КонецЕсли;
		
		// Распределяем то, что уже было распределено на заказ ранее.
		РаспределитьНаСтроку = Мин(
			Макс(ТекСтрока.РаспределеноПоЗаказу - ТекСтрока.РезервироватьПоМереПоступленияИтогНаДату + ТекСтрока.РезервироватьПоМереПоступления, 0),
			ТекСтрока.РезервироватьПоМереПоступления);
		
		// Распределяем запас на строки Резервировать по мере поступления.
		Если РаспределитьНаСтроку < ТекСтрока.РезервироватьПоМереПоступления Тогда
			СвободноНаСтроку = Мин(Свободно, ТекСтрока.РезервироватьПоМереПоступления - РаспределитьНаСтроку);
			РаспределитьНаСтроку = РаспределитьНаСтроку + СвободноНаСтроку;
			Свободно = Свободно - СвободноНаСтроку;
		КонецЕсли;
		
		РаспределитьНаВсеСтроки = РаспределитьНаВсеСтроки - РаспределитьНаСтроку;
		СнятьРаспределение = Макс(0, -РаспределитьНаВсеСтроки);
		РаспределитьНаСтроку = РаспределитьНаСтроку - СнятьРаспределение;
		РаспределитьНаВсеСтроки = РаспределитьНаВсеСтроки + СнятьРаспределение;
		
		РаспределитьНаСтрокуВГрафике = 0;
		Если РаспределитьНаСтроку < ТекСтрока.РезервироватьПоМереПоступления Тогда
			РаспределитьНаСтрокуВГрафике = Мин(ТекСтрока.РезервироватьПоМереПоступления - РаспределитьНаСтроку, СвободноВГрафике);
			СвободноВГрафике = СвободноВГрафике - РаспределитьНаСтрокуВГрафике;
		КонецЕсли;
		
		НеОбеспечено = ТекСтрока.РезервироватьПоМереПоступления - РаспределитьНаСтроку - РаспределитьНаСтрокуВГрафике;
		
		// Распределяем свободный остаток на строки К обеспечению.
		ОбеспеченоНаСкладе = Мин(СвободноНаСтрокиКОбеспечению, ТекСтрока.КОбеспечению);
		СвободноНаСтрокиКОбеспечению = СвободноНаСтрокиКОбеспечению - ОбеспеченоНаСкладе;
		
		ОбеспеченоВГрафике = 0;
		Если ОбеспеченоНаСкладе < ТекСтрока.КОбеспечению Тогда
			ОбеспеченоВГрафике = Мин(ТекСтрока.КОбеспечению - ОбеспеченоНаСкладе, СвободноВГрафикеНаСтрокиКОбеспечению);
			СвободноВГрафикеНаСтрокиКОбеспечению = СвободноВГрафикеНаСтрокиКОбеспечению - ОбеспеченоВГрафике;
		КонецЕсли;
		
		НеОбеспеченоКОбеспечению = ТекСтрока.КОбеспечению - ОбеспеченоНаСкладе - ОбеспеченоВГрафике;
		НеОбеспечено = НеОбеспечено + НеОбеспеченоКОбеспечению;
		
		Если РаспределитьНаСтроку > 0 Или ОбеспеченоНаСкладе > 0 Тогда
			
			НоваяЗапись = Набор.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекСтрока,
				"Номенклатура,Характеристика,Склад,Назначение,ЗаказНаОтгрузку,ЖелаемаяДатаОтгрузки");
			НоваяЗапись.Состояние = Перечисления.РаспределениеЗапасовСостояния.ОбеспеченНаСкладе;
			НоваяЗапись.Зарезервировано = РаспределитьНаСтроку;
			НоваяЗапись.Обеспечено = ОбеспеченоНаСкладе;
			
		КонецЕсли;
		
		// Позиционирование на поступлениях текущей номенклатуры.
		Если РаспределитьНаСтрокуВГрафике > 0 Или ОбеспеченоВГрафике > 0 Тогда
			
			Пока Номенклатура <> График[ИндексВГрафике].Номенклатура
					Или Характеристика <> График[ИндексВГрафике].Характеристика
					Или Склад <> График[ИндексВГрафике].Склад
					Или Назначение <> График[ИндексВГрафике].Назначение Цикл
				ИндексВГрафике = ИндексВГрафике + 1;
				ИндексВГрафикеОбеспечить = ИндексВГрафике;
				Продолжить;
			КонецЦикла;
			
		КонецЕсли;
		
		НоваяЗапись = Неопределено;
		Пока РаспределитьНаСтрокуВГрафике > 0 Цикл
			
			Если График[ИндексВГрафике].Распределить = 0 Тогда
				ИндексВГрафике = ИндексВГрафике + 1;
				Продолжить;
			КонецЕсли;
			
			РаспределитьНаСтроку = Мин(График[ИндексВГрафике].Распределить, РаспределитьНаСтрокуВГрафике);
			График[ИндексВГрафике].Распределить = График[ИндексВГрафике].Распределить - РаспределитьНаСтроку;
			РаспределитьНаСтрокуВГрафике = РаспределитьНаСтрокуВГрафике - РаспределитьНаСтроку;
			
			НоваяЗапись = Набор.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекСтрока,
				"Номенклатура,Характеристика,Склад,Назначение,ЗаказНаОтгрузку,ЖелаемаяДатаОтгрузки");
			ЗаполнитьЗначенияСвойств(НоваяЗапись, График[ИндексВГрафике], "ЗаказНаПоступление,ДатаПоступления,Состояние");
			НоваяЗапись.Зарезервировано = РаспределитьНаСтроку;
			
		КонецЦикла;
		
		Пока ОбеспеченоВГрафике > 0 Цикл
			
			Если График[ИндексВГрафикеОбеспечить].РаспределитьНаСтрокиКОбеспечению = 0 Тогда
				ИндексВГрафикеОбеспечить = ИндексВГрафикеОбеспечить + 1;
				Продолжить;
			КонецЕсли;
			
			РаспределитьНаСтроку = Мин(График[ИндексВГрафикеОбеспечить].РаспределитьНаСтрокиКОбеспечению, ОбеспеченоВГрафике);
			График[ИндексВГрафикеОбеспечить].РаспределитьНаСтрокиКОбеспечению = График[ИндексВГрафикеОбеспечить].РаспределитьНаСтрокиКОбеспечению - РаспределитьНаСтроку;
			ОбеспеченоВГрафике = ОбеспеченоВГрафике - РаспределитьНаСтроку;
			
			Если НоваяЗапись = Неопределено
				Или НоваяЗапись.ЗаказНаПоступление <> График[ИндексВГрафикеОбеспечить].ЗаказНаПоступление
				Или НоваяЗапись.ДатаПоступления <> График[ИндексВГрафикеОбеспечить].ДатаПоступления
				Или НоваяЗапись.Состояние <> График[ИндексВГрафикеОбеспечить].Состояние Тогда
					НоваяЗапись = Набор.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекСтрока,
						"Номенклатура,Характеристика,Склад,Назначение,ЗаказНаОтгрузку,ЖелаемаяДатаОтгрузки");
					ЗаполнитьЗначенияСвойств(НоваяЗапись, График[ИндексВГрафикеОбеспечить], "ЗаказНаПоступление,ДатаПоступления,Состояние");
			КонецЕсли;
			НоваяЗапись.Обеспечено = РаспределитьНаСтроку;
			
		КонецЦикла;
		
		Если НеОбеспечено > 0 Тогда
			
			НоваяЗапись = Набор.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекСтрока,
				"Номенклатура,Характеристика,Склад,Назначение,ЗаказНаОтгрузку,ЖелаемаяДатаОтгрузки,Состояние");
			НоваяЗапись.НеОбеспечено = НеОбеспечено;
			НоваяЗапись.НеОбеспеченоКОбеспечению = НеОбеспеченоКОбеспечению;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СостояниеСУчетомОтраженияИзменений(Заказ, ТаблицаДвижений, МенеджерВременныхТаблиц, ТаблицаИмитацияРеестра) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	УстановитьПривилегированныйРежим(Истина);
	
	//
	
	Запрос.УстановитьПараметр("ТаблицаИмитацияРеестра", ТаблицаИмитацияРеестра);
	Запрос.УстановитьПараметр("Заказ", Заказ);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗаказыРеестра.ЗаказНаОтгрузку КАК Ссылка,
		|	ЗаказыРеестра.Приоритет КАК Приоритет,
		|	ЗаказыРеестра.ДатаДокумента КАК ДатаДокументаИБ
		|ПОМЕСТИТЬ ТаблицаИмитацияРеестра
		|ИЗ
		|	&ТаблицаИмитацияРеестра КАК ЗаказыРеестра
		|;
		|
		|///////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗаказыРеестра.Ссылка КАК Ссылка,
		|	ЗаказыРеестра.Приоритет КАК Приоритет,
		|	ЗаказыРеестра.ДатаДокументаИБ КАК ДатаДокументаИБ,
		|	ЛОЖЬ КАК ДополнительнаяЗапись
		|ПОМЕСТИТЬ РеестрДокументовСУчетомИзменений
		|ИЗ
		|	РегистрСведений.РеестрДокументов КАК ЗаказыРеестра
		|ГДЕ
		|	НЕ Ссылка В(
		|		ВЫБРАТЬ
		|			ЗаказыРеестра.Ссылка КАК Ссылка
		|		ИЗ
		|			ТаблицаИмитацияРеестра КАК ЗаказыРеестра)
		|		И НЕ ЗаказыРеестра.ДополнительнаяЗапись
		|		И ИСТИНА В(
		|			ВЫБРАТЬ ПЕРВЫЕ 1
		|				ИСТИНА
		|			ИЗ
		|				РегистрСведений.РаспределениеЗапасов КАК Сведения
		|			ГДЕ
		|				Сведения.ЗаказНаОтгрузку = ЗаказыРеестра.Ссылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗаказыРеестра.Ссылка КАК Ссылка,
		|	ЗаказыРеестра.Приоритет КАК Приоритет,
		|	ЗаказыРеестра.ДатаДокументаИБ КАК ДатаДокументаИБ,
		|	ЛОЖЬ КАК ДополнительнаяЗапись
		|ИЗ
		|	ТаблицаИмитацияРеестра КАК ЗаказыРеестра";
	
	Запрос.Выполнить();
	
	Запрос.УстановитьПараметр("НаборЗаписейРаспределениеЗапасовДвижения", ТаблицаДвижений);
	Запрос.УстановитьПараметр("Регистратор", Заказ);
	
	Если ЭтоПроизводительныйРежим() Тогда
		
		РаспределениеЗапасовДвижения.ЗапасыИПотребностиДвиженияПриЗаписиВоВременнуюТаблицу(Запрос, Ложь);
		РаспределениеЗапасовДвижения.ЗапасыИПотребностиИзменениеВоВременнуюТаблицу(Запрос);
		
		Запрос.Текст =
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	Таблица.Номенклатура КАК Номенклатура,
			|	Таблица.Характеристика КАК Характеристика,
			|	Таблица.Склад КАК Склад,
			|	Таблица.Назначение КАК Назначение
			|ПОМЕСТИТЬ ТоварыДляРаспределенияЗапасов
			|ИЗ
			|	ДвиженияПриЗаписи КАК Таблица";
		Запрос.Выполнить();
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	Набор.Номенклатура КАК Номенклатура,
			|	Набор.Характеристика КАК Характеристика,
			|	Набор.Склад КАК Склад,
			|	Набор.Назначение КАК Назначение,
			|	Набор.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
			|	СУММА(Набор.Отгрузка) КАК Отгрузка
			|ПОМЕСТИТЬ ИзменениеОстаткаПоТоварамДляКонтроляОстатков
			|ИЗ(
			|	ВЫБРАТЬ
			|		ИзмененияДвижений.Номенклатура КАК Номенклатура,
			|		ИзмененияДвижений.Характеристика КАК Характеристика,
			|		ИзмененияДвижений.Склад КАК Склад,
			|		ИзмененияДвижений.Назначение КАК Назначение,
			|		ИзмененияДвижений.Заказ КАК ЗаказНаОтгрузку,
			|		СУММА(ИзмененияДвижений.ВНаличииРасход + ИзмененияДвижений.РезервироватьНаСкладе) КАК Отгрузка
			|	ИЗ
			|		ЗапасыИПотребностиИзменение КАК ИзмененияДвижений
			|	СГРУППИРОВАТЬ ПО
			|		ИзмененияДвижений.Номенклатура,
			|		ИзмененияДвижений.Характеристика,
			|		ИзмененияДвижений.Склад,
			|		ИзмененияДвижений.Назначение,
			|		ИзмененияДвижений.Заказ
			|	ИМЕЮЩИЕ
			|		СУММА(ИзмененияДвижений.ВНаличииРасход + ИзмененияДвижений.РезервироватьНаСкладе) > 0
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		Задания.Номенклатура КАК Номенклатура,
			|		Задания.Характеристика КАК Характеристика,
			|		Задания.Склад КАК Склад,
			|		Задания.Назначение КАК Назначение,
			|		Задания.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
			|		Задания.КСнятиюРезерва КАК Отгрузка
			|	ИЗ
			|		ТоварыДляРаспределенияЗапасов КАК Товары
			|			
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗаданияКРаспределениюЗапасов КАК Задания
			|			ПО Задания.Номенклатура = Товары.Номенклатура
			|			 И Задания.Характеристика = Товары.Характеристика
			|			 И Задания.Склад = Товары.Склад
			|			 И Задания.Назначение = Товары.Назначение
			|	ГДЕ
			|		Задания.КСнятиюРезерва > 0) КАК Набор
			|СГРУППИРОВАТЬ ПО
			|	Набор.Номенклатура,
			|	Набор.Характеристика,
			|	Набор.Склад,
			|	Набор.Назначение,
			|	Набор.ЗаказНаОтгрузку
			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура, Характеристика, Склад, Назначение";
		Запрос.Выполнить();
		
		ВременныеТаблицыДанныхРегистра(Запрос, "ТоварыДляРаспределенияЗапасов", "ЗапасыИПотребностиИзменение");
		ВременнаяТаблицаПотребностиРезервироватьПоМере(Запрос);
		ВременнаяТаблицаЗапасыИДоступностьПоДатам(Запрос);
		ВременнаяТаблицаРезультатРаспределенияЗапасовНаПотребности(Запрос, "ТоварыДляРаспределенияЗапасов", "РаспределениеЗапасовНаПотребности", "РеестрДокументовСУчетомИзменений");
		ТаблицаРезультат = ДанныеДляЗаписиТоваровВПроизводительномРежиме(Запрос, "ТоварыДляРаспределенияЗапасов", "ИмитацияОтражения");
		Возврат;
		
	КонецЕсли;
	
	//
	
	РаспределениеЗапасовДвижения.ДвиженияПриЗаписиВоВременнуюТаблицу(Запрос, Ложь);
	РаспределениеЗапасовДвижения.РаспределениеЗапасовДвиженияИзменениеВоВременнуюТаблицу(Запрос);
	
	//
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Изменения.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку
		|ПОМЕСТИТЬ ИзмененныеЗаказыНаОтгрузку
		|ИЗ
		|	РаспределениеЗапасовДвиженияИзменение КАК Изменения
		|ГДЕ
		|	Изменения.РезервироватьПоМереПоступления <> 0 ИЛИ Изменения.КОбеспечению <> 0
		|ИНДЕКСИРОВАТЬ ПО
		|	ЗаказНаОтгрузку
		|;
		|
		|///////////////////////////////
		|ВЫБРАТЬ
		|	Набор.Номенклатура         КАК Номенклатура,
		|	Набор.Характеристика       КАК Характеристика,
		|	Набор.Склад                КАК Склад,
		|	Набор.Назначение           КАК Назначение,
		|	Набор.ЗаказНаОтгрузку      КАК ЗаказНаОтгрузку,
		|	Набор.ЖелаемаяДатаОтгрузки КАК ЖелаемаяДатаОтгрузки,
		|	СУММА(Набор.РезервироватьПоМереПоступления) КАК РезервироватьПоМереПоступления,
		|	СУММА(Набор.КОбеспечению) КАК КОбеспечению
		|ИЗ(
		|	ВЫБРАТЬ
		|		ДанныеИБ.Номенклатура         КАК Номенклатура,
		|		ДанныеИБ.Характеристика       КАК Характеристика,
		|		ДанныеИБ.Склад                КАК Склад,
		|		ДанныеИБ.Назначение           КАК Назначение,
		|		ДанныеИБ.ЗаказНаОтгрузку      КАК ЗаказНаОтгрузку,
		|		ДанныеИБ.ЖелаемаяДатаОтгрузки КАК ЖелаемаяДатаОтгрузки,
		|		ДанныеИБ.РезервироватьПоМереПоступления КАК РезервироватьПоМереПоступления,
		|		ДанныеИБ.КОбеспечениюБезРезерва КАК КОбеспечению
		|	ИЗ
		|		ИзмененныеЗаказыНаОтгрузку КАК ЗаказыНаОтгрузку
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РаспределениеЗапасовДвижения КАК ДанныеИБ
		|			ПО ДанныеИБ.ЗаказНаОтгрузку = ЗаказыНаОтгрузку.ЗаказНаОтгрузку
		|	ГДЕ
		|		(ДанныеИБ.РезервироватьПоМереПоступления <> 0
		|			ИЛИ ДанныеИБ.КОбеспечениюБезРезерва <> 0)
		|			И НЕ ДанныеИБ.Регистратор В(&Регистратор)
		|			И ДанныеИБ.Активность
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Движения.Номенклатура         КАК Номенклатура,
		|		Движения.Характеристика       КАК Характеристика,
		|		Движения.Склад                КАК Склад,
		|		Движения.Назначение           КАК Назначение,
		|		Движения.ЗаказНаОтгрузку      КАК ЗаказНаОтгрузку,
		|		Движения.ЖелаемаяДатаОтгрузки КАК ЖелаемаяДатаОтгрузки,
		|		Движения.РезервироватьПоМереПоступления КАК РезервироватьПоМереПоступления,
		|		Движения.КОбеспечению КАК КОбеспечению
		|	ИЗ
		|		ИзмененныеЗаказыНаОтгрузку КАК ЗаказыНаОтгрузку
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДвиженияПриЗаписи КАК Движения
		|			ПО Движения.ЗаказНаОтгрузку = ЗаказыНаОтгрузку.ЗаказНаОтгрузку
		|	ГДЕ
		|		Движения.РезервироватьПоМереПоступления <> 0 ИЛИ Движения.КОбеспечению <> 0) КАК Набор
		|СГРУППИРОВАТЬ ПО
		|	Набор.Номенклатура,
		|	Набор.Характеристика,
		|	Набор.Склад,
		|	Набор.Назначение,
		|	Набор.ЗаказНаОтгрузку,
		|	Набор.ЖелаемаяДатаОтгрузки
		|ИМЕЮЩИЕ
		|	СУММА(Набор.РезервироватьПоМереПоступления) > 0
		|		ИЛИ СУММА(Набор.КОбеспечению) > 0
		|УПОРЯДОЧИТЬ ПО
		|	ЗаказНаОтгрузку, Номенклатура, Характеристика, Склад, Назначение, ЖелаемаяДатаОтгрузки";
	ТаблицаКОбеспечению = Запрос.Выполнить().Выгрузить();
	ТаблицаКОбеспечению.Колонки.Добавить("РезервироватьПоМереПоступленияИтогНаДату", ОбщегоНазначения.ОписаниеТипаЧисло(15, 3));
	
	ЗаказНаОтгрузку = Неопределено;
	Номенклатура = Неопределено; Характеристика = Неопределено; Склад = Неопределено; Назначение = Неопределено;
	Итог = 0;
	
	Для Индекс = 0 По ТаблицаКОбеспечению.Количество() - 1 Цикл
		
		Строка = ТаблицаКОбеспечению[Индекс];
		
		Если ЗаказНаОтгрузку <> Строка.ЗаказНаОтгрузку
			Или Строка.Номенклатура <> Номенклатура
			Или Строка.Характеристика <> Строка.Номенклатура
			Или Строка.Склад <> Склад
			Или Строка.Назначение <> Назначение Тогда
			
			ЗаказНаОтгрузку     = Строка.ЗаказНаОтгрузку;
			Номенклатура        = Строка.Номенклатура;
			Характеристика      = Строка.Характеристика;
			Склад               = Строка.Склад;
			Назначение          = Строка.Назначение;
			Итог = 0;
			
		КонецЕсли;
		
		Итог = Итог + Строка.РезервироватьПоМереПоступления;
		Строка.РезервироватьПоМереПоступленияИтогНаДату = Итог;
		
	КонецЦикла;
	
	//
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Изменения.ЗаказНаПоступление КАК ЗаказНаПоступление
		|ПОМЕСТИТЬ ИзмененныеЗаказыНаПоступление
		|ИЗ
		|	РаспределениеЗапасовДвиженияИзменение КАК Изменения
		|ГДЕ
		|	Изменения.ПоступитКДате <> 0
		|		ИЛИ Изменения.ПоставкаНаСогласовании <> 0
		|		ИЛИ Изменения.ЗакрытьГрафикПоступления <> 0
		|ИНДЕКСИРОВАТЬ ПО
		|	ЗаказНаПоступление
		|;
		|
		|///////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеИБ.Номенклатура             КАК Номенклатура,
		|	ДанныеИБ.Характеристика           КАК Характеристика,
		|	ДанныеИБ.Склад                    КАК Склад,
		|	ДанныеИБ.Назначение               КАК Назначение,
		|	ДанныеИБ.ЗаказНаПоступление       КАК ЗаказНаПоступление,
		|	ДанныеИБ.ДатаПоступления          КАК ДатаПоступления,
		|	ДанныеИБ.ПоставкаНаСогласовании   КАК ПоставкаНаСогласовании,
		|	ДанныеИБ.ПоступитКДате            КАК ПоступитКДате,
		|	ДанныеИБ.ЗакрытьГрафикПоступления КАК ЗакрытьГрафикПоступления
		|ПОМЕСТИТЬ ДанныеСУчетомИзменений
		|ИЗ
		|	ИзмененныеЗаказыНаПоступление КАК ЗаказыНаПоступление
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РаспределениеЗапасовДвижения КАК ДанныеИБ
		|		ПО ДанныеИБ.ЗаказНаПоступление = ЗаказыНаПоступление.ЗаказНаПоступление
		|ГДЕ
		|	ДанныеИБ.Активность
		|		И НЕ ДанныеИБ.Регистратор В(&Регистратор)
		|		И (ДанныеИБ.ПоступитКДате <> 0
		|			ИЛИ ДанныеИБ.ПоставкаНаСогласовании <> 0
		|			ИЛИ ДанныеИБ.ЗакрытьГрафикПоступления <> 0)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДвиженияПриЗаписи.Номенклатура             КАК Номенклатура,
		|	ДвиженияПриЗаписи.Характеристика           КАК Характеристика,
		|	ДвиженияПриЗаписи.Склад                    КАК Склад,
		|	ДвиженияПриЗаписи.Назначение               КАК Назначение,
		|	ДвиженияПриЗаписи.ЗаказНаПоступление       КАК ЗаказНаПоступление,
		|	ДвиженияПриЗаписи.ДатаПоступления          КАК ДатаПоступления,
		|	ДвиженияПриЗаписи.ПоставкаНаСогласовании   КАК ПоставкаНаСогласовании,
		|	ДвиженияПриЗаписи.ПоступитКДате            КАК ПоступитКДате,
		|	ДвиженияПриЗаписи.ЗакрытьГрафикПоступления КАК ЗакрытьГрафикПоступления
		|ИЗ
		|	ДвиженияПриЗаписи КАК ДвиженияПриЗаписи
		|ГДЕ
		|	ДвиженияПриЗаписи.ПоступитКДате <> 0
		|		ИЛИ ДвиженияПриЗаписи.ПоставкаНаСогласовании <> 0
		|		ИЛИ ДвиженияПриЗаписи.ЗакрытьГрафикПоступления <> 0
		|ИНДЕКСИРОВАТЬ ПО
		|	ЗаказНаПоступление, Номенклатура, Характеристика, Склад, Назначение
		|;
		|
		|///////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Движения.ЗаказНаПоступление КАК ЗаказНаПоступление,
		|	Движения.Номенклатура       КАК Номенклатура,
		|	Движения.Характеристика     КАК Характеристика,
		|	Движения.Склад              КАК Склад,
		|	Движения.Назначение         КАК Назначение,
		|	
		|	СУММА(Движения.ПоставкаНаСогласовании + Движения.ПоступитКДате - Движения.ЗакрытьГрафикПоступления) КАК Количество
		|	
		|ПОМЕСТИТЬ ОстаткиПланируемыхПоступлений
		|ИЗ
		|	ДанныеСУчетомИзменений КАК Движения
		|СГРУППИРОВАТЬ ПО
		|	Движения.ЗаказНаПоступление,
		|	Движения.Номенклатура,
		|	Движения.Характеристика,
		|	Движения.Склад,
		|	Движения.Назначение
		|ИНДЕКСИРОВАТЬ ПО
		|	ЗаказНаПоступление, Номенклатура, Характеристика, Склад, Назначение
		|;
		|
		|////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Остатки.Номенклатура       КАК Номенклатура,
		|	Остатки.Характеристика     КАК Характеристика,
		|	Остатки.Склад              КАК Склад,
		|	Остатки.Назначение         КАК Назначение,
		|	Остатки.ЗаказНаПоступление КАК ЗаказНаПоступление,
		|
		|	ВЫБОР КОГДА Движения.ДатаПоступления <> ДАТАВРЕМЯ(1, 1, 1) ТОГДА
		|				ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемоеПоступление)
		|			ИНАЧЕ
		|				ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ПоступлениеНеподтвержденное)
		|		КОНЕЦ КАК Состояние,
		|	
		|	Движения.ДатаПоступления КАК ДатаПоступления,
		|	МАКСИМУМ(Остатки.Количество) КАК Распределить,
		|	СУММА(Движения.ПоставкаНаСогласовании + Движения.ПоступитКДате) КАК Запас
		|ИЗ
		|	ОстаткиПланируемыхПоступлений КАК Остатки
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеСУчетомИзменений КАК Движения
		|		ПО Движения.Номенклатура         = Остатки.Номенклатура
		|		 И Движения.Характеристика       = Остатки.Характеристика
		|		 И Движения.Склад                = Остатки.Склад
		|		 И Движения.Назначение           = Остатки.Назначение
		|		 И Движения.ЗаказНаПоступление   = Остатки.ЗаказНаПоступление
		|ГДЕ
		|	Остатки.Количество > 0
		|СГРУППИРОВАТЬ ПО
		|	Остатки.ЗаказНаПоступление,
		|	Остатки.Номенклатура,
		|	Остатки.Характеристика,
		|	Остатки.Склад,
		|	Остатки.Назначение,
		|	Движения.ДатаПоступления
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЗаказНаПоступление, Номенклатура, Характеристика, Склад, Назначение, ДатаПоступления УБЫВ";
	ТаблицаОжидаемыхПоступлений = Запрос.Выполнить().Выгрузить();
	
	ТаблицаОжидаемыхПоступлений.Колонки.Добавить("Пустая");
	ЗаказНаПоступление = Неопределено;
	Номенклатура = Неопределено; Характеристика = Неопределено; Склад = Неопределено; Назначение = Неопределено;
	Распределить = 0;
	
	Для Индекс = 0 По ТаблицаОжидаемыхПоступлений.Количество() - 1 Цикл
		
		Строка = ТаблицаОжидаемыхПоступлений[Индекс];
		
		Если ЗаказНаПоступление <> Строка.ЗаказНаПоступление
			Или Строка.Номенклатура <> Номенклатура
			Или Строка.Характеристика <> Строка.Номенклатура
			Или Строка.Склад <> Склад
			Или Строка.Назначение <> Назначение Тогда
			
			ЗаказНаПоступление  = Строка.ЗаказНаПоступление;
			Номенклатура        = Строка.Номенклатура;
			Характеристика      = Строка.Характеристика;
			Склад               = Строка.Склад;
			Назначение          = Строка.Назначение;
			Распределить = Строка.Распределить;
			
		КонецЕсли;
		
		Запас = Макс(Мин(Распределить, Строка.Запас), 0);
		Распределить = Распределить - Запас;
		Строка.Запас = Запас;
		Строка.Пустая = Запас = 0;
		
	КонецЦикла;
	
	ТаблицаОжидаемыхПоступлений = ТаблицаОжидаемыхПоступлений.Скопировать(Новый Структура("Пустая", Ложь));
	
	//
	
	Запрос.УстановитьПараметр("ТаблицаКОбеспечению", ТаблицаКОбеспечению);
	Запрос.УстановитьПараметр("ТаблицаОжидаемыхПоступлений", ТаблицаОжидаемыхПоступлений);
	Текст =
		"ВЫБРАТЬ
		|	Таблица.Номенклатура           КАК Номенклатура,
		|	Таблица.Характеристика         КАК Характеристика,
		|	Таблица.Склад                  КАК Склад,
		|	Таблица.Назначение             КАК Назначение,
		|	Таблица.ЗаказНаОтгрузку        КАК ЗаказНаОтгрузку,
		|	Таблица.ЖелаемаяДатаОтгрузки   КАК ЖелаемаяДатаОтгрузки,
		|	Таблица.КОбеспечению           КАК КОбеспечению,
		|	Таблица.РезервироватьПоМереПоступления КАК РезервироватьПоМереПоступления,
		|	Таблица.РезервироватьПоМереПоступленияИтогНаДату КАК РезервироватьПоМереПоступленияИтогНаДату
		|ПОМЕСТИТЬ ТаблицаКОбеспечениюСУчетомИзменений
		|ИЗ
		|	&ТаблицаКОбеспечению КАК Таблица
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение
		|;
		|
		|///////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Таблица.Номенклатура           КАК Номенклатура,
		|	Таблица.Характеристика         КАК Характеристика,
		|	Таблица.Склад                  КАК Склад
		|ПОМЕСТИТЬ РазличныеТоварыБезНазначений
		|ИЗ
		|	ТаблицаКОбеспечениюСУчетомИзменений КАК Таблица
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад
		|;
		|
		|/////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Товары.Номенклатура КАК Номенклатура,
		|	Товары.Характеристика КАК Характеристика,
		|	Товары.Склад КАК Склад,
		|	РасчетПереопределяемый.ГраницаПериода КАК ГраницаПериода
		|ПОМЕСТИТЬ ГраницыПериодаОбеспечения
		|ИЗ
		|	РазличныеТоварыБезНазначений КАК Товары
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РасчетПереопределяемый ПО ИСТИНА
		|		
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад
		|;
		|
		|/////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Таблица.Состояние              КАК Состояние,
		|	Таблица.Номенклатура           КАК Номенклатура,
		|	Таблица.Характеристика         КАК Характеристика,
		|	Таблица.Склад                  КАК Склад,
		|	Таблица.Назначение             КАК Назначение,
		|	Таблица.ЗаказНаПоступление     КАК ЗаказНаПоступление,
		|	Таблица.ДатаПоступления        КАК ДатаПоступления,
		|	Таблица.Запас                  КАК Запас
		|ПОМЕСТИТЬ ТаблицаОжидаемыхПоступленийСУчетомИзменений
		|ИЗ
		|	&ТаблицаОжидаемыхПоступлений КАК Таблица
		|;
		|
		|//////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИзмененияДвижений.Номенклатура          КАК Номенклатура,
		|	ИзмененияДвижений.Характеристика        КАК Характеристика,
		|	ИзмененияДвижений.Склад                 КАК Склад,
		|	ИзмененияДвижений.Назначение            КАК Назначение,
		|	СУММА(ИзмененияДвижений.Отгрузить)      КАК Отгрузить,
		|	СУММА(ИзмененияДвижений.Резервировать)  КАК Резервировать,
		|	СУММА(ИзмененияДвижений.Поступило)      КАК Поступило
		|ПОМЕСТИТЬ ИзмененияПоТовару
		|ИЗ
		|	РаспределениеЗапасовДвиженияИзменение КАК ИзмененияДвижений
		|СГРУППИРОВАТЬ ПО
		|	ИзмененияДвижений.Номенклатура,
		|	ИзмененияДвижений.Характеристика,
		|	ИзмененияДвижений.Склад,
		|	ИзмененияДвижений.Назначение
		|;
		|
		|////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемаяОтгрузка) КАК Состояние,
		|	Таблица.Номенклатура           КАК Номенклатура,
		|	Таблица.Характеристика         КАК Характеристика,
		|	Таблица.Склад                  КАК Склад,
		|	Таблица.Назначение             КАК Назначение,
		|	Таблица.ЗаказНаОтгрузку        КАК ЗаказНаОтгрузку,
		|	Таблица.ЖелаемаяДатаОтгрузки   КАК ЖелаемаяДатаОтгрузки,
		|	НЕОПРЕДЕЛЕНО                   КАК ЗаказНаПоступление,
		|	ДАТАВРЕМЯ(1, 1, 1)             КАК ДатаПоступления,
		|	ВЫБОР КОГДА Границы.ГраницаПериода = ДАТАВРЕМЯ(1,1,1)
		|					ИЛИ Границы.ГраницаПериода >= Таблица.ЖелаемаяДатаОтгрузки ТОГДА
		|				Таблица.РезервироватьПоМереПоступления
		|			ИНАЧЕ
		|				0
		|		КОНЕЦ КАК РезервироватьПоМереПоступления,
		|	ВЫБОР КОГДА Границы.ГраницаПериода = ДАТАВРЕМЯ(1,1,1)
		|					ИЛИ Границы.ГраницаПериода >= Таблица.ЖелаемаяДатаОтгрузки ТОГДА
		|				0
		|			ИНАЧЕ
		|				Таблица.РезервироватьПоМереПоступления
		|		КОНЕЦ КАК ОтложитьРезервирование,
		|	ВЫБОР КОГДА Границы.ГраницаПериода = ДАТАВРЕМЯ(1,1,1)
		|					ИЛИ Границы.ГраницаПериода >= Таблица.ЖелаемаяДатаОтгрузки ТОГДА
		|				Таблица.РезервироватьПоМереПоступленияИтогНаДату
		|			ИНАЧЕ
		|				0
		|		КОНЕЦ КАК РезервироватьПоМереПоступленияИтогНаДату,
		|	Таблица.КОбеспечению           КАК КОбеспечениюБезРезерва,
		|	0                              КАК Запас,
		|	0                              КАК Резерв,
		|	0                              КАК Свободно,
		|	0                              КАК Зарезервировано,
		|	0                              КАК Обеспечено
		|ПОМЕСТИТЬ РаспределениеЗапасовСУчетомИзменений
		|ИЗ
		|	ИзмененияПоТовару КАК ИзмененияПоТовару
		|		
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаКОбеспечениюСУчетомИзменений КАК Таблица
		|		ПО Таблица.Номенклатура = ИзмененияПоТовару.Номенклатура
		|		 И Таблица.Характеристика = ИзмененияПоТовару.Характеристика
		|		 И Таблица.Склад = ИзмененияПоТовару.Склад
		|		 И Таблица.Назначение = ИзмененияПоТовару.Назначение
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ГраницыПериодаОбеспечения КАК Границы
		|		ПО Границы.Номенклатура = ИзмененияПоТовару.Номенклатура
		|		 И Границы.Характеристика = ИзмененияПоТовару.Характеристика
		|		 И Границы.Склад = ИзмененияПоТовару.Склад
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Таблица.Состояние              КАК Состояние,
		|	Таблица.Номенклатура           КАК Номенклатура,
		|	Таблица.Характеристика         КАК Характеристика,
		|	Таблица.Склад                  КАК Склад,
		|	Таблица.Назначение             КАК Назначение,
		|	Таблица.ЗаказНаОтгрузку        КАК ЗаказНаОтгрузку,
		|	Таблица.ЖелаемаяДатаОтгрузки   КАК ЖелаемаяДатаОтгрузки,
		|	НЕОПРЕДЕЛЕНО                   КАК ЗаказНаПоступление,
		|	ДАТАВРЕМЯ(1, 1, 1)             КАК ДатаПоступления,
		|	Таблица.РезервироватьПоМереПоступления КАК РезервироватьПоМереПоступления,
		|	0                              КАК ОтложитьРезервирование,
		|	Таблица.РезервироватьПоМереПоступленияИтогНаДату КАК РезервироватьПоМереПоступленияИтогНаДату,
		|	Таблица.КОбеспечениюБезРезерва КАК КОбеспечениюБезРезерва,
		|	0                              КАК Запас,
		|	0                              КАК Резерв,
		|	0                              КАК Свободно,
		|	0                              КАК Зарезервировано,
		|	0                              КАК Обеспечено
		|ИЗ
		|	ИзмененияПоТовару КАК Изменения
		|		
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РаспределениеЗапасов КАК Таблица
		|		ПО Таблица.Номенклатура   = Изменения.Номенклатура
		|		 И Таблица.Характеристика = Изменения.Характеристика
		|		 И Таблица.Склад          = Изменения.Склад
		|		 И Таблица.Назначение     = Изменения.Назначение
		|		 И Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемаяОтгрузка)
		|		 И (Таблица.КОбеспечениюБезРезерва <> 0 ИЛИ Таблица.РезервироватьПоМереПоступления <> 0)
		|ГДЕ
		|	НЕ Таблица.ЗаказНаОтгрузку В(
		|		ВЫБРАТЬ
		|			ЗаказыНаОтгрузку.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку
		|		ИЗ
		|			ИзмененныеЗаказыНаОтгрузку КАК ЗаказыНаОтгрузку)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Таблица.Состояние              КАК Состояние,
		|	Таблица.Номенклатура           КАК Номенклатура,
		|	Таблица.Характеристика         КАК Характеристика,
		|	Таблица.Склад                  КАК Склад,
		|	Таблица.Назначение             КАК Назначение,
		|	НЕОПРЕДЕЛЕНО                   КАК ЗаказНаОтгрузку,
		|	ДАТАВРЕМЯ(1, 1, 1)             КАК ЖелаемаяДатаОтгрузки,
		|	Таблица.ЗаказНаПоступление     КАК ЗаказНаПоступление,
		|	Таблица.ДатаПоступления        КАК ДатаПоступления,
		|	0                              КАК РезервироватьПоМереПоступления,
		|	0                              КАК ОтложитьРезервирование,
		|	0                              КАК РезервироватьПоМереПоступленияИтогНаДату,
		|	0                              КАК КОбеспечениюБезРезерва,
		|	Таблица.Запас                  КАК Запас,
		|	0                              КАК Резерв,
		|	0                              КАК Свободно,
		|	0                              КАК Зарезервировано,
		|	0                              КАК Обеспечено
		|ИЗ
		|	ИзмененияПоТовару КАК ИзмененияПоТовару
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаОжидаемыхПоступленийСУчетомИзменений КАК Таблица
		|		ПО Таблица.Номенклатура = ИзмененияПоТовару.Номенклатура
		|		 И Таблица.Характеристика = ИзмененияПоТовару.Характеристика
		|		 И Таблица.Склад = ИзмененияПоТовару.Склад
		|		 И Таблица.Назначение = ИзмененияПоТовару.Назначение
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Таблица.Состояние              КАК Состояние,
		|	Таблица.Номенклатура           КАК Номенклатура,
		|	Таблица.Характеристика         КАК Характеристика,
		|	Таблица.Склад                  КАК Склад,
		|	Таблица.Назначение             КАК Назначение,
		|	НЕОПРЕДЕЛЕНО                   КАК ЗаказНаОтгрузку,
		|	ДАТАВРЕМЯ(1, 1, 1)             КАК ЖелаемаяДатаОтгрузки,
		|	Таблица.ЗаказНаПоступление     КАК ЗаказНаПоступление,
		|	Таблица.ДатаПоступления        КАК ДатаПоступления,
		|	0                              КАК РезервироватьПоМереПоступления,
		|	0                              КАК ОтложитьРезервирование,
		|	0                              КАК РезервироватьПоМереПоступленияИтогНаДату,
		|	0                              КАК КОбеспечениюБезРезерва,
		|	Таблица.Запас                  КАК Запас,
		|	0                              КАК Резерв,
		|	0                              КАК Свободно,
		|	0                              КАК Зарезервировано,
		|	0                              КАК Обеспечено
		|ИЗ
		|	ИзмененияПоТовару КАК Изменения
		|		
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РаспределениеЗапасов КАК Таблица
		|		ПО Таблица.Номенклатура   = Изменения.Номенклатура
		|		 И Таблица.Характеристика = Изменения.Характеристика
		|		 И Таблица.Склад          = Изменения.Склад
		|		 И Таблица.Назначение     = Изменения.Назначение
		|		 И Таблица.Состояние В(
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемоеПоступление),
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ПоступлениеНеподтвержденное))
		|ГДЕ
		|	НЕ Таблица.ЗаказНаПоступление В(
		|		ВЫБРАТЬ
		|			ЗаказыНаПоступление.ЗаказНаПоступление КАК ЗаказНаПоступление
		|		ИЗ
		|			ИзмененныеЗаказыНаПоступление КАК ЗаказыНаПоступление)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе) КАК Состояние,
		|	Таблица.Номенклатура        КАК Номенклатура,
		|	Таблица.Характеристика      КАК Характеристика,
		|	Таблица.Склад               КАК Склад,
		|	Таблица.Назначение          КАК Назначение,
		|	НЕОПРЕДЕЛЕНО                КАК ЗаказНаОтгрузку,
		|	ДАТАВРЕМЯ(1, 1, 1)          КАК ЖелаемаяДатаОтгрузки,
		|	НЕОПРЕДЕЛЕНО                КАК ЗаказНаПоступление,
		|	ДАТАВРЕМЯ(1, 1, 1)          КАК ДатаПоступления,
		|	0                           КАК РезервироватьПоМереПоступления,
		|	0                           КАК ОтложитьРезервирование,
		|	0                           КАК РезервироватьПоМереПоступленияИтогНаДату,
		|	0                           КАК КОбеспечениюБезРезерва,
		|	Таблица.Поступило - Таблица.Отгрузить + ЕСТЬNULL(ДанныеИБ.Запас, 0) КАК Запас,
		|	Таблица.Резервировать + ЕСТЬNULL(ДанныеИБ.Резерв, 0) КАК Резерв,
		|	0 КАК Свободно,
		|	0 КАК Зарезервировано,
		|	0 КАК Обеспечено
		|ИЗ
		|	ИзмененияПоТовару КАК Таблица
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаспределениеЗапасов КАК ДанныеИБ
		|		ПО ДанныеИБ.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе)
		|		 И ДанныеИБ.Номенклатура   = Таблица.Номенклатура
		|		 И ДанныеИБ.Характеристика = Таблица.Характеристика
		|		 И ДанныеИБ.Склад          = Таблица.Склад
		|		 И ДанныеИБ.Назначение     = Таблица.Назначение
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОбеспеченНаСкладе) КАК Состояние,
		|	Таблица.Номенклатура          КАК Номенклатура,
		|	Таблица.Характеристика        КАК Характеристика,
		|	Таблица.Склад                 КАК Склад,
		|	Таблица.Назначение            КАК Назначение,
		|	Сведения.ЗаказНаОтгрузку      КАК ЗаказНаОтгрузку,
		|	Сведения.ЖелаемаяДатаОтгрузки КАК ЖелаемаяДатаОтгрузки,
		|	НЕОПРЕДЕЛЕНО                  КАК ЗаказНаПоступление,
		|	ДАТАВРЕМЯ(1, 1, 1)            КАК ДатаПоступления,
		|	0                             КАК РезервироватьПоМереПоступления,
		|	0                             КАК ОтложитьРезервирование,
		|	0                             КАК РезервироватьПоМереПоступленияИтогНаДату,
		|	0                             КАК КОбеспечениюБезРезерва,
		|	0                             КАК Запас,
		|	0                             КАК Резерв,
		|	0                             КАК Свободно,
		|	Сведения.Зарезервировано      КАК Зарезервировано,
		|	Сведения.Обеспечено           КАК Обеспечено
		|ИЗ
		|	ИзмененияПоТовару КАК Таблица
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаспределениеЗапасов КАК Сведения
		|		ПО Сведения.Номенклатура   = Таблица.Номенклатура
		|		 И Сведения.Характеристика = Таблица.Характеристика
		|		 И Сведения.Склад          = Таблица.Склад
		|		 И Сведения.Назначение     = Таблица.Назначение
		|		 И Сведения.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОбеспеченНаСкладе)
		|ГДЕ
		|	НЕ Сведения.Номенклатура ЕСТЬ NULL
		|	
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение, Состояние, ЗаказНаОтгрузку, ЖелаемаяДатаОтгрузки, ЗаказНаПоступление, ДатаПоступления
		|;
		|///////////////////////////////////////////
		|УНИЧТОЖИТЬ ТаблицаОжидаемыхПоступленийСУчетомИзменений;
		|УНИЧТОЖИТЬ ГраницыПериодаОбеспечения;
		|УНИЧТОЖИТЬ РазличныеТоварыБезНазначений";
	
	Подстановки = ОбеспечениеВДокументахСервер.ПодстановкиГраницыОбеспечиваемогоПериода(
		"Товары.Номенклатура", "Товары.Характеристика", "Товары.Склад", "&НачалоТекущегоДня", "ИСТИНА");
	
	Текст = СтрЗаменить(Текст, "РасчетПереопределяемый.ГраницаПериода",             Подстановки.Поле);
	Текст = СтрЗаменить(Текст, "ЛЕВОЕ СОЕДИНЕНИЕ РасчетПереопределяемый ПО ИСТИНА", Подстановки.Соединения);
	Запрос.УстановитьПараметр("НачалоТекущегоДня", НачалоДня(ТекущаяДатаСеанса()));
	
	Запрос.Текст = Текст;
	Запрос.УстановитьПараметр("НачалоТекущегоДня", НачалоДня(ТекущаяДатаСеанса()));
	Запрос.Выполнить();
	
	// Расчет свободного остатка
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	РазличныеТовары.Номенклатура                                         КАК Номенклатура,
		|	РазличныеТовары.Характеристика                                       КАК Характеристика,
		|	РазличныеТовары.Склад                                                КАК Склад,
		|	РазличныеТовары.Назначение                                           КАК Назначение,
		|	СУММА(ЕСТЬNULL(ОжидаемаяОтгрузка.РезервироватьПоМереПоступления, 0)) КАК Количество
		|ПОМЕСТИТЬ Потребности
		|ИЗ
		|	ИзмененияПоТовару КАК РазличныеТовары
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РаспределениеЗапасовСУчетомИзменений КАК ОжидаемаяОтгрузка
		|		ПО ОжидаемаяОтгрузка.Номенклатура    = РазличныеТовары.Номенклатура
		|		 И ОжидаемаяОтгрузка.Характеристика  = РазличныеТовары.Характеристика
		|		 И ОжидаемаяОтгрузка.Склад           = РазличныеТовары.Склад
		|		 И ОжидаемаяОтгрузка.Назначение      = РазличныеТовары.Назначение
		|		 И ОжидаемаяОтгрузка.Состояние       = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемаяОтгрузка)
		|		 И ОжидаемаяОтгрузка.РезервироватьПоМереПоступления > 0
		|
		|СГРУППИРОВАТЬ ПО
		|	РазличныеТовары.Номенклатура,
		|	РазличныеТовары.Характеристика,
		|	РазличныеТовары.Склад,
		|	РазличныеТовары.Назначение
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение
		|;
		|
		|/////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Потребности.Количество         КАК Количество,
		|	Остатки.Состояние              КАК Состояние,
		|	1                              КАК ТипЗаписиРаспределенияЗапасов,
		|	Остатки.Номенклатура           КАК Номенклатура,
		|	Остатки.Характеристика         КАК Характеристика,
		|	Остатки.Склад                  КАК Склад,
		|	Остатки.Назначение             КАК Назначение,
		|	Остатки.ЗаказНаПоступление     КАК ЗаказНаПоступление,
		|	Остатки.ДатаПоступления        КАК ДатаПоступления,
		|	Остатки.Запас                  КАК Запас,
		|	Остатки.Резерв                 КАК Резерв,
		|	Остатки.Свободно               КАК Свободно,
		|	ВЫБОР КОГДА Остатки.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе) ТОГДА
		|					1
		|				КОГДА Остатки.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемоеПоступление) ТОГДА
		|					2
		|				КОГДА Остатки.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ПоступлениеНеподтвержденное) ТОГДА
		|					3
		|		КОНЕЦ КАК Порядок
		|ИЗ
		|	Потребности КАК Потребности
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РаспределениеЗапасовСУчетомИзменений КАК Остатки
		|		ПО Остатки.Номенклатура    = Потребности.Номенклатура
		|		 И Остатки.Характеристика  = Потребности.Характеристика
		|		 И Остатки.Склад           = Потребности.Склад
		|		 И Остатки.Назначение      = Потребности.Назначение
		|		 И Остатки.Состояние       В(
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе),
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемоеПоступление),
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ПоступлениеНеподтвержденное))
		|УПОРЯДОЧИТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение, Порядок, ДатаПоступления, ЗаказНаПоступление
		|;
		|
		|/////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ Потребности";
	
	Пакет = Запрос.ВыполнитьПакет();
	
	Выборка = Пакет[1].Выбрать();
	
	Количество = 0;
	
	ТаблицаОстатков = РегистрыСведений.РаспределениеЗапасов.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	
	Номенклатура   = Неопределено;
	Характеристика = Неопределено;
	Склад          = Неопределено;
	Назначение     = Неопределено;
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.Номенклатура <> Номенклатура
				Или Выборка.Характеристика <> Характеристика
				Или Выборка.Склад <> Склад
				Или Выборка.Назначение <> Назначение Тогда
				
				Количество = Выборка.Количество;
				
		КонецЕсли;
		
		КоличествоРаспределить = Мин(Количество, Макс(Выборка.Запас - Выборка.Резерв, 0));
		Свободно = Выборка.Запас - Выборка.Резерв - КоличествоРаспределить;
		Количество = Количество - КоличествоРаспределить;
		
		Если Выборка.Запас <> 0
				Или Выборка.Резерв <> 0
				Или Свободно <> 0 Тогда
				
			НоваяСтрока = ТаблицаОстатков.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.Свободно = Свободно;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ТаблицаОстатков", ТаблицаОстатков);
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Таблица.Состояние            КАК Состояние,
		|	Таблица.Номенклатура         КАК Номенклатура,
		|	Таблица.Характеристика       КАК Характеристика,
		|	Таблица.Склад                КАК Склад,
		|	Таблица.Назначение           КАК Назначение,
		|	Таблица.ЗаказНаПоступление   КАК ЗаказНаПоступление,
		|	Таблица.ДатаПоступления      КАК ДатаПоступления,
		|	Таблица.Запас                КАК Запас,
		|	Таблица.Резерв               КАК Резерв,
		|	Таблица.Свободно             КАК Свободно
		|ПОМЕСТИТЬ ТаблицаОстатков
		|ИЗ
		|	&ТаблицаОстатков КАК Таблица
		|;
		|
		|///////////////////////////////////////
		|ВЫБРАТЬ
		|	Таблица.Состояние КАК Состояние,
		|	Таблица.Номенклатура КАК Номенклатура,
		|	Таблица.Характеристика КАК Характеристика,
		|	Таблица.Склад КАК Склад,
		|	Таблица.Назначение КАК Назначение,
		|	Таблица.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
		|	Таблица.ЖелаемаяДатаОтгрузки КАК ЖелаемаяДатаОтгрузки,
		|	Таблица.ЗаказНаПоступление КАК ЗаказНаПоступление,
		|	Таблица.ДатаПоступления КАК ДатаПоступления,
		|	Таблица.РезервироватьПоМереПоступления КАК РезервироватьПоМереПоступления,
		|	Таблица.ОтложитьРезервирование КАК ОтложитьРезервирование,
		|	Таблица.РезервироватьПоМереПоступленияИтогНаДату КАК РезервироватьПоМереПоступленияИтогНаДату,
		|	Таблица.КОбеспечениюБезРезерва КАК КОбеспечениюБезРезерва,
		|	Таблица.Запас КАК Запас,
		|	Таблица.Резерв КАК Резерв,
		|	ЕСТЬNULL(ТаблицаОстатков.Свободно, 0) КАК Свободно,
		|	Таблица.Зарезервировано КАК Зарезервировано,
		|	Таблица.Обеспечено КАК Обеспечено
		|ПОМЕСТИТЬ РаспределениеЗапасовСУчетомИзмененийСвободно
		|ИЗ
		|	РаспределениеЗапасовСУчетомИзменений КАК Таблица
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОстатков КАК ТаблицаОстатков
		|		ПО ТаблицаОстатков.Состояние = Таблица.Состояние
		|		 И ТаблицаОстатков.Номенклатура = Таблица.Номенклатура
		|		 И ТаблицаОстатков.Характеристика = Таблица.Характеристика
		|		 И ТаблицаОстатков.Склад = Таблица.Склад
		|		 И ТаблицаОстатков.Назначение = Таблица.Назначение
		|		 И ТаблицаОстатков.ЗаказНаПоступление = Таблица.ЗаказНаПоступление
		|		 И ТаблицаОстатков.ДатаПоступления = Таблица.ДатаПоступления
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение, Состояние,
		|	ЗаказНаОтгрузку, ЖелаемаяДатаОтгрузки, ЗаказНаПоступление, ДатаПоступления";
	Запрос.Выполнить();
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Набор.Номенклатура КАК Номенклатура,
		|	Набор.Характеристика КАК Характеристика,
		|	Набор.Склад КАК Склад,
		|	Набор.Назначение КАК Назначение,
		|	Набор.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
		|	СУММА(Набор.Отгрузка) КАК Отгрузка
		|ПОМЕСТИТЬ ИзменениеОстаткаПоТоварамДляКонтроляОстатков
		|ИЗ(
		|	ВЫБРАТЬ
		|		ИзмененияДвижений.Номенклатура КАК Номенклатура,
		|		ИзмененияДвижений.Характеристика КАК Характеристика,
		|		ИзмененияДвижений.Склад КАК Склад,
		|		ИзмененияДвижений.Назначение КАК Назначение,
		|		ИзмененияДвижений.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
		|		СУММА(ИзмененияДвижений.Отгрузить + ИзмененияДвижений.Резервировать) КАК Отгрузка
		|	ИЗ
		|		РаспределениеЗапасовДвиженияИзменение КАК ИзмененияДвижений
		|	СГРУППИРОВАТЬ ПО
		|		ИзмененияДвижений.Номенклатура,
		|		ИзмененияДвижений.Характеристика,
		|		ИзмененияДвижений.Склад,
		|		ИзмененияДвижений.Назначение,
		|		ИзмененияДвижений.ЗаказНаОтгрузку
		|	ИМЕЮЩИЕ
		|		СУММА(ИзмененияДвижений.Отгрузить + ИзмененияДвижений.Резервировать) > 0
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Задания.Номенклатура КАК Номенклатура,
		|		Задания.Характеристика КАК Характеристика,
		|		Задания.Склад КАК Склад,
		|		Задания.Назначение КАК Назначение,
		|		Задания.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
		|		Задания.КСнятиюРезерва КАК Отгрузка
		|	ИЗ
		|		ИзмененияПоТовару КАК Товары
		|			
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗаданияКРаспределениюЗапасов КАК Задания
		|			ПО Задания.Номенклатура = Товары.Номенклатура
		|			 И Задания.Характеристика = Товары.Характеристика
		|			 И Задания.Склад = Товары.Склад
		|			 И Задания.Назначение = Товары.Назначение
		|	ГДЕ
		|		Задания.КСнятиюРезерва > 0) КАК Набор
		|СГРУППИРОВАТЬ ПО
		|	Набор.Номенклатура,
		|	Набор.Характеристика,
		|	Набор.Склад,
		|	Набор.Назначение,
		|	Набор.ЗаказНаОтгрузку
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение";
	
	Запрос.Выполнить();
	
	// Распределение запасов
	Набор = РегистрыСведений.РаспределениеЗапасов.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	Текст = ТекстЗапросаРаспределенияЗапасов();
	Текст = СтрЗаменить(Текст, "РегистрСведений.РаспределениеЗапасов", "РаспределениеЗапасовСУчетомИзмененийСвободно");
	Текст = СтрЗаменить(Текст, "РегистрСведений.РеестрДокументов", "РеестрДокументовСУчетомИзменений");
	Запрос.Текст = СтрЗаменить(Текст, "ТоварыДляРаспределенияЗапасов", "ИзмененияПоТовару");
	Пакет = Запрос.ВыполнитьПакет();
	
	Таблица = Пакет[10].Выгрузить();
	График = Пакет[11].Выгрузить();
	
	РаспределитьЗапасыВТаблицу(Набор, Таблица, График, Ложь);
	
	Запрос.УстановитьПараметр("Набор", Набор);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Таблица.Состояние            КАК Состояние,
		|	Таблица.Номенклатура         КАК Номенклатура,
		|	Таблица.Характеристика       КАК Характеристика,
		|	Таблица.Склад                КАК Склад,
		|	Таблица.Назначение           КАК Назначение,
		|	Таблица.ЗаказНаОтгрузку      КАК ЗаказНаОтгрузку,
		|	Таблица.ЖелаемаяДатаОтгрузки КАК ЖелаемаяДатаОтгрузки,
		|	Таблица.ЗаказНаПоступление   КАК ЗаказНаПоступление,
		|	Таблица.ДатаПоступления      КАК ДатаПоступления,
		|	Таблица.Зарезервировано      КАК Зарезервировано,
		|	Таблица.Обеспечено           КАК Обеспечено,
		|	Таблица.НеОбеспечено         КАК НеОбеспечено,
		|	Таблица.НеОбеспеченоКОбеспечению КАК НеОбеспеченоКОбеспечению
		|ПОМЕСТИТЬ Набор
		|ИЗ
		|	&Набор КАК Таблица";
	Запрос.Выполнить();
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЛОЖЬ КАК РезервПревышаетОстатки,
		|	Таблица.Состояние            КАК Состояние,
		|	Таблица.Номенклатура         КАК Номенклатура,
		|	Таблица.Характеристика       КАК Характеристика,
		|	Таблица.Склад                КАК Склад,
		|	Таблица.Назначение           КАК Назначение,
		|	Таблица.ЗаказНаОтгрузку      КАК ЗаказНаОтгрузку,
		|	Таблица.ЖелаемаяДатаОтгрузки КАК ЖелаемаяДатаОтгрузки,
		|	Таблица.ЗаказНаПоступление   КАК ЗаказНаПоступление,
		|	Таблица.ДатаПоступления      КАК ДатаПоступления,
		|	Таблица.Зарезервировано      КАК Зарезервировано,
		|	Таблица.Обеспечено           КАК Обеспечено,
		|	Таблица.НеОбеспечено         КАК НеОбеспечено,
		|	0                            КАК Запас,
		|	0                            КАК Свободно,
		|	0                            КАК Резерв,
		|	Таблица.НеОбеспеченоКОбеспечению КАК НеОбеспеченоКОбеспечению,
		|	0 КАК КОбеспечениюБезРезерва,
		|	0 КАК РезервироватьПоМереПоступления,
		|	0 КАК ОтложитьРезервирование
		|ПОМЕСТИТЬ ИмитацияОтражения
		|ИЗ
		|	Набор КАК Таблица
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЛОЖЬ КАК РезервПревышаетОстатки,
		|	Таблица.Состояние            КАК Состояние,
		|	Таблица.Номенклатура         КАК Номенклатура,
		|	Таблица.Характеристика       КАК Характеристика,
		|	Таблица.Склад                КАК Склад,
		|	Таблица.Назначение           КАК Назначение,
		|	НЕОПРЕДЕЛЕНО                 КАК ЗаказНаОтгрузку,
		|	ДАТАВРЕМЯ(1, 1, 1)           КАК ЖелаемаяДатаОтгрузки,
		|	Таблица.ЗаказНаПоступление   КАК ЗаказНаПоступление,
		|	Таблица.ДатаПоступления      КАК ДатаПоступления,
		|	0                            КАК Зарезервировано,
		|	0                            КАК Обеспечено,
		|	0                            КАК НеОбеспечено,
		|	Таблица.Запас                КАК Запас,
		|	Таблица.Свободно             КАК Свободно,
		|	Таблица.Резерв               КАК Резерв,
		|	НЕОПРЕДЕЛЕНО                 КАК НеОбеспеченоКОбеспечению,
		|	0 КАК КОбеспечениюБезРезерва,
		|	0 КАК РезервироватьПоМереПоступления,
		|	0 КАК ОтложитьРезервирование
		|
		|ИЗ
		|	ТаблицаОстатков КАК Таблица
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЛОЖЬ КАК РезервПревышаетОстатки,
		|	ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ВРезерве) КАК Состояние,
		|	Движения.Номенклатура         КАК Номенклатура,
		|	Движения.Характеристика       КАК Характеристика,
		|	Движения.Склад                КАК Склад,
		|	Движения.Назначение           КАК Назначение,
		|	Движения.ЗаказНаОтгрузку      КАК ЗаказНаОтгрузку,
		|	Движения.ЖелаемаяДатаОтгрузки КАК ЖелаемаяДатаОтгрузки,
		|	НЕОПРЕДЕЛЕНО                  КАК ЗаказНаПоступление,
		|	ДАТАВРЕМЯ(1, 1, 1)            КАК ДатаПоступления,
		|	Движения.Резервировать        КАК Зарезервировано,
		|	0                             КАК Обеспечено,
		|	0                             КАК НеОбеспечено,
		|	0                             КАК Запас,
		|	0                             КАК Свободно,
		|	0                             КАК Резерв,
		|	НЕОПРЕДЕЛЕНО                  КАК НеОбеспеченоКОбеспечению,
		|	0 КАК КОбеспечениюБезРезерва,
		|	0 КАК РезервироватьПоМереПоступления,
		|	0 КАК ОтложитьРезервирование
		|ИЗ
		|	ИзмененияПоТовару КАК Изменения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДвиженияПриЗаписи КАК Движения
		|		ПО Движения.Номенклатура = Изменения.Номенклатура
		|		 И Движения.Характеристика = Изменения.Характеристика
		|		 И Движения.Склад = Изменения.Склад
		|		 И Движения.Назначение = Изменения.Назначение
		|ГДЕ
		|	Движения.Резервировать <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЛОЖЬ КАК РезервПревышаетОстатки,
		|	ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.НеОбеспечивать) КАК Состояние,
		|	Движения.Номенклатура         КАК Номенклатура,
		|	Движения.Характеристика       КАК Характеристика,
		|	Движения.Склад                КАК Склад,
		|	Движения.Назначение           КАК Назначение,
		|	Движения.ЗаказНаОтгрузку      КАК ЗаказНаОтгрузку,
		|	Движения.ЖелаемаяДатаОтгрузки КАК ЖелаемаяДатаОтгрузки,
		|	НЕОПРЕДЕЛЕНО                  КАК ЗаказНаПоступление,
		|	ДАТАВРЕМЯ(1, 1, 1)            КАК ДатаПоступления,
		|	0                             КАК Зарезервировано,
		|	0                             КАК Обеспечено,
		|	Движения.НеОбеспечивать       КАК НеОбеспечено,
		|	0                             КАК Запас,
		|	0                             КАК Свободно,
		|	0                             КАК Резерв,
		|	НЕОПРЕДЕЛЕНО                  КАК НеОбеспеченоКОбеспечению,
		|	0 КАК КОбеспечениюБезРезерва,
		|	0 КАК РезервироватьПоМереПоступления,
		|	0 КАК ОтложитьРезервирование
		|ИЗ
		|	ИзмененияПоТовару КАК Изменения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДвиженияПриЗаписи КАК Движения
		|		ПО Движения.Номенклатура = Изменения.Номенклатура
		|		 И Движения.Характеристика = Изменения.Характеристика
		|		 И Движения.Склад = Изменения.Склад
		|		 И Движения.Назначение = Изменения.Назначение
		|ГДЕ
		|	Движения.НеОбеспечивать <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЛОЖЬ КАК РезервПревышаетОстатки,
		|	Сведения.Состояние            КАК Состояние,
		|	Сведения.Номенклатура         КАК Номенклатура,
		|	Сведения.Характеристика       КАК Характеристика,
		|	Сведения.Склад                КАК Склад,
		|	Сведения.Назначение           КАК Назначение,
		|	Сведения.ЗаказНаОтгрузку      КАК ЗаказНаОтгрузку,
		|	Сведения.ЖелаемаяДатаОтгрузки КАК ЖелаемаяДатаОтгрузки,
		|	Сведения.ЗаказНаПоступление   КАК ЗаказНаПоступление,
		|	Сведения.ДатаПоступления      КАК ДатаПоступления,
		|	Сведения.Зарезервировано      КАК Зарезервировано,
		|	Сведения.Обеспечено           КАК Обеспечено,
		|	Сведения.НеОбеспечено         КАК НеОбеспечено,
		|	0                             КАК Запас,
		|	0                             КАК Свободно,
		|	0                             КАК Резерв,
		|	Сведения.НеОбеспеченоКОбеспечению КАК НеОбеспеченоКОбеспечению,
		|	Сведения.КОбеспечениюБезРезерва КАК КОбеспечениюБезРезерва,
		|	Сведения.РезервироватьПоМереПоступления КАК РезервироватьПоМереПоступления,
		|	Сведения.ОтложитьРезервирование КАК ОтложитьРезервирование
		|ИЗ
		|	РегистрСведений.РаспределениеЗапасов КАК Сведения
		|		ЛЕВОЕ СОЕДИНЕНИЕ ИзмененияПоТовару КАК Изменения
		|		ПО Изменения.Номенклатура = Сведения.Номенклатура
		|		 И Изменения.Характеристика = Сведения.Характеристика
		|		 И Изменения.Склад = Сведения.Склад
		|		 И Изменения.Назначение = Сведения.Назначение
		|ГДЕ
		|	Сведения.ЗаказНаОтгрузку В(&Заказ)
		|		И Изменения.Номенклатура ЕСТЬ NULL
		|		И Сведения.Состояние В(
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОбеспеченНаСкладе),
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОбеспеченКДате),
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемаяОтгрузка),
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.Обеспечить),
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ВРезерве),
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.НеОбеспечивать),
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаетсяПоНеподтвержденномуЗаказу))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЛОЖЬ КАК РезервПревышаетОстатки,
		|	Сведения.Состояние            КАК Состояние,
		|	Сведения.Номенклатура         КАК Номенклатура,
		|	Сведения.Характеристика       КАК Характеристика,
		|	Сведения.Склад                КАК Склад,
		|	Сведения.Назначение           КАК Назначение,
		|	Сведения.ЗаказНаОтгрузку      КАК ЗаказНаОтгрузку,
		|	Сведения.ЖелаемаяДатаОтгрузки КАК ЖелаемаяДатаОтгрузки,
		|	Сведения.ЗаказНаПоступление   КАК ЗаказНаПоступление,
		|	Сведения.ДатаПоступления      КАК ДатаПоступления,
		|	0                             КАК Зарезервировано,
		|	0                             КАК Обеспечено,
		|	0                             КАК НеОбеспечено,
		|	Сведения.Запас                КАК Запас,
		|	Сведения.Свободно             КАК Свободно,
		|	Сведения.Резерв               КАК Резерв,
		|	Сведения.НеОбеспеченоКОбеспечению КАК НеОбеспеченоКОбеспечению,
		|	0 КАК КОбеспечениюБезРезерва,
		|	0 КАК РезервироватьПоМереПоступления,
		|	0 КАК ОтложитьРезервирование
		|ИЗ
		|	РегистрСведений.РаспределениеЗапасов КАК Сведения
		|		ЛЕВОЕ СОЕДИНЕНИЕ ИзмененияПоТовару КАК Изменения
		|		ПО Изменения.Номенклатура = Сведения.Номенклатура
		|		 И Изменения.Характеристика = Сведения.Характеристика
		|		 И Изменения.Склад = Сведения.Склад
		|		 И Изменения.Назначение = Сведения.Назначение
		|ГДЕ
		|	Изменения.Номенклатура ЕСТЬ NULL
		|		И Сведения.Состояние В(
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемоеПоступление),
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ПоступлениеНеподтвержденное),
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе))
		|		И ИСТИНА В(
		|			ВЫБРАТЬ ПЕРВЫЕ 1
		|				ИСТИНА КАК ЕстьЗаписи
		|			ИЗ
		|				ТаблицаКОбеспечениюСУчетомИзменений КАК Таблица
		|			ГДЕ
		|				Таблица.Номенклатура = Сведения.Номенклатура
		|					И Таблица.Характеристика = Сведения.Характеристика
		|					И Таблица.Склад = Сведения.Склад
		|					И Таблица.Назначение = Сведения.Назначение)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЛОЖЬ КАК РезервПревышаетОстатки,
		|	Сведения.Состояние            КАК Состояние,
		|	Сведения.Номенклатура         КАК Номенклатура,
		|	Сведения.Характеристика       КАК Характеристика,
		|	Сведения.Склад                КАК Склад,
		|	Сведения.Назначение           КАК Назначение,
		|	Сведения.ЗаказНаОтгрузку      КАК ЗаказНаОтгрузку,
		|	Сведения.ЖелаемаяДатаОтгрузки КАК ЖелаемаяДатаОтгрузки,
		|	Сведения.ЗаказНаПоступление   КАК ЗаказНаПоступление,
		|	Сведения.ДатаПоступления      КАК ДатаПоступления,
		|	0                             КАК Зарезервировано,
		|	0                             КАК Обеспечено,
		|	0                             КАК НеОбеспечено,
		|	0                             КАК Запас,
		|	0                             КАК Свободно,
		|	0                             КАК Резерв,
		|	0                             КАК НеОбеспеченоКОбеспечению,
		|	Сведения.КОбеспечениюБезРезерва КАК КОбеспечениюБезРезерва,
		|	Сведения.РезервироватьПоМереПоступления КАК РезервироватьПоМереПоступления,
		|	Сведения.ОтложитьРезервирование КАК ОтложитьРезервирование
		|	
		|ИЗ
		|	РаспределениеЗапасовСУчетомИзменений КАК Сведения
		|ГДЕ
		|	Сведения.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемаяОтгрузка)
		|;
		|УНИЧТОЖИТЬ ТаблицаКОбеспечениюСУчетомИзменений";
		
	Запрос.Выполнить();
	
КонецПроцедуры


Процедура ОбновитьИнформациюОДоступностиТоваровДляВнешнихПользователей() Экспорт

	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.ОбновлениеДанныхОДоступностиТоваровДляВнешнихПользователей);
	
	УстановитьПривилегированныйРежим(Истина);

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоДня", НачалоДня(ТекущаяДатаСеанса()));
	
	Тексты = Новый Массив();
	Если РаспределениеЗапасов.ЭтоПроизводительныйРежим() Тогда
		ТекстЗапроса =
			"ВЫБРАТЬ
			|	&НачалоДня КАК Период,
			|	Запасы.Номенклатура КАК Номенклатура,
			|	Запасы.Характеристика КАК Характеристика,
			|	Запасы.Склад КАК Склад,
			|	ВЫБОР
			|		КОГДА Запасы.ВНаличииОстаток - Запасы.РезервироватьНаСкладеОстаток < 0
			|			ТОГДА
			|				Запасы.ВНаличииОстаток - Запасы.РезервироватьНаСкладеОстаток
			|		КОГДА Запасы.ВНаличииОстаток - Запасы.РезервироватьНаСкладеОстаток - Запасы.РезервироватьПоМереПоступленияОстаток > 0
			|			ТОГДА
			|				Запасы.ВНаличииОстаток - Запасы.РезервироватьНаСкладеОстаток - Запасы.РезервироватьПоМереПоступленияОстаток
			|		ИНАЧЕ 0
			|	КОНЕЦ КАК Доступно
			|ИЗ
			|	РегистрНакопления.ЗапасыИПотребности.Остатки(,
			|		Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
			|			И Номенклатура.ТипНоменклатуры В(
			|			ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
			|			ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))) КАК Запасы";
	Иначе
		ТекстЗапроса =
			"ВЫБРАТЬ
			|	&НачалоДня КАК Период,
			|	РаспределениеЗапасов.Номенклатура КАК Номенклатура,
			|	РаспределениеЗапасов.Характеристика КАК Характеристика,
			|	РаспределениеЗапасов.Склад КАК Склад,
			|	РаспределениеЗапасов.Свободно КАК Доступно
			|ИЗ
			|	РегистрСведений.РаспределениеЗапасов КАК РаспределениеЗапасов
			|ГДЕ
			|	РаспределениеЗапасов.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
			|		И РаспределениеЗапасов.Номенклатура.ТипНоменклатуры В(
			|			ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
			|			ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
			|		И РаспределениеЗапасов.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе)
			|		И РаспределениеЗапасов.Свободно <> 0";
	КонецЕсли;
	Тексты.Добавить(ТекстЗапроса);
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ВЫБОР КОГДА РаспределениеЗапасов.ДатаПоступления > &НачалоДня ТОГДА
		|			РаспределениеЗапасов.ДатаПоступления
		|		ИНАЧЕ
		|			&НачалоДня
		|		КОНЕЦ КАК Период,
		|	РаспределениеЗапасов.Номенклатура КАК Номенклатура,
		|	РаспределениеЗапасов.Характеристика Характеристика,
		|	РаспределениеЗапасов.Склад КАК Склад,
		|	РаспределениеЗапасов.Свободно КАК Доступно
		|ИЗ
		|	РегистрСведений.РаспределениеЗапасов КАК РаспределениеЗапасов
		|ГДЕ
		|	РаспределениеЗапасов.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|		И РаспределениеЗапасов.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемоеПоступление)
		|		И РаспределениеЗапасов.Номенклатура.ТипНоменклатуры В(
		|			ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
		|			ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
		|		И РаспределениеЗапасов.Свободно <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	&НачалоДня КАК Период,
		|	СтарыеДанные.Номенклатура КАК Номенклатура,
		|	СтарыеДанные.Характеристика КАК Характеристика,
		|	СтарыеДанные.Склад КАК Склад,
		|	0 КАК Доступно
		|ИЗ
		|	РегистрСведений.ДоступностьТоваровДляВнешнихПользователей КАК СтарыеДанные";
	Тексты.Добавить(ТекстЗапроса);
	ТекстЗапроса = СтрСоединить(Тексты, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	
	Текст =
		"ВЫБРАТЬ
		|	ВложенныйЗапрос.Период КАК Период,
		|	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
		|	ВложенныйЗапрос.Характеристика КАК Характеристика,
		|	ВложенныйЗапрос.Склад КАК Склад,
		|	СУММА(ВложенныйЗапрос.Доступно) КАК Доступно
		|ИЗ
		|	ВложенныйЗапросПереопределяемый КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Номенклатура,
		|	ВложенныйЗапрос.Характеристика,
		|	ВложенныйЗапрос.Склад,
		|	ВложенныйЗапрос.Период
		|
		|УПОРЯДОЧИТЬ ПО
		|	Номенклатура,
		|	Характеристика,
		|	Склад,
		|	Период";
	Запрос.Текст = СтрЗаменить(Текст, "ВложенныйЗапросПереопределяемый", СтрШаблон("(%1)", ТекстЗапроса));
	
	СтрокаОстатка = Запрос.Выполнить().Выбрать();
	
	Ключ = Новый Структура("Номенклатура,Характеристика,Склад");
	Итог = 0;
	НаборЗаписей = РегистрыСведений.ДоступностьТоваровДляВнешнихПользователей.СоздатьНаборЗаписей();
	Пока СтрокаОстатка.Следующий() Цикл
		
		Если СтрокаОстатка.Номенклатура <> Ключ.Номенклатура
			Или СтрокаОстатка.Характеристика <> Ключ.Характеристика
			Или СтрокаОстатка.Склад <> Ключ.Склад Тогда
			
			Если Ключ.Номенклатура <> Неопределено Тогда
				НаборЗаписей.Записать();
				НаборЗаписей.Очистить();
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(Ключ, СтрокаОстатка);
			Итог = 0;
			НаборЗаписей.Отбор.Номенклатура.Установить(Ключ.Номенклатура, Истина);
			НаборЗаписей.Отбор.Характеристика.Установить(Ключ.Характеристика, Истина);
			НаборЗаписей.Отбор.Склад.Установить(Ключ.Склад, Истина);
			
		КонецЕсли;
		Итог = СтрокаОстатка.Доступно + Итог;
		
		Если Итог > 0 Тогда
			Запись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, СтрокаОстатка);
			Запись.Доступно = Итог;
		КонецЕсли;
		
	КонецЦикла;
	
	Если Ключ.Номенклатура <> Неопределено Тогда
		НаборЗаписей.Записать();
	КонецЕсли;
	
КонецПроцедуры

Функция ОбновлениеИБПоТовару(Товар) Экспорт
	
	Набор = РегистрыСведений.РаспределениеЗапасов.СоздатьНаборЗаписей();
	Набор.Отбор.Номенклатура.Установить(Товар.Номенклатура);
	Набор.Отбор.Характеристика.Установить(Товар.Характеристика);
	Набор.Отбор.Склад.Установить(Товар.Склад);
	Набор.Отбор.Назначение.Установить(Товар.Назначение);
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Номенклатура",   Товар.Номенклатура);
	Запрос.УстановитьПараметр("Характеристика", Товар.Характеристика);
	Запрос.УстановитьПараметр("Склад",          Товар.Склад);
	Запрос.УстановитьПараметр("Назначение",     Товар.Назначение);
	
	Текст =
		"ВЫБРАТЬ
		|	&Номенклатура   КАК Номенклатура,
		|	&Характеристика КАК Характеристика,
		|	&Склад          КАК Склад
		|ПОМЕСТИТЬ РазличныеТоварыБезНазначений
		|;
		|/////////////////////////////////
		|
		|ВЫБРАТЬ
		|	РасчетПереопределяемый.ГраницаПериода КАК ГраницаПериода,
		|	РасчетПереопределяемый.ДатаПоставки КАК ДатаПоставки
		|ИЗ
		|	РазличныеТоварыБезНазначений КАК Товары
		|		ЛЕВОЕ СОЕДИНЕНИЕ РасчетПереопределяемый ПО ИСТИНА";
	Подстановки = ОбеспечениеВДокументахСервер.ПодстановкиГраницыОбеспечиваемогоПериода(
		"Товары.Номенклатура", "Товары.Характеристика", "Товары.Склад", "&НачалоТекущегоДня", "ИСТИНА");
	
	Текст = СтрЗаменить(Текст, "РасчетПереопределяемый.ГраницаПериода",             Подстановки.Поле);
	Текст = СтрЗаменить(Текст, "РасчетПереопределяемый.ДатаПоставки",               Подстановки.ПолеДатаПоставки);
	Текст = СтрЗаменить(Текст, "ЛЕВОЕ СОЕДИНЕНИЕ РасчетПереопределяемый ПО ИСТИНА", Подстановки.Соединения);
	Запрос.Текст = Текст;
	
	НачалоДня = НачалоДня(ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("НачалоТекущегоДня", НачалоДня);
	Выборка = Запрос.Выполнить().Выбрать();
	
	ГраницаПериода = Неопределено;
	ДатаПоставки = НачалоДня;
	Если Выборка.Следующий() Тогда
		Если ЗначениеЗаполнено(Выборка.ГраницаПериода) Тогда
			ГраницаПериода = Выборка.ГраницаПериода;
		КонецЕсли;
		Если ЗначениеЗаполнено(Выборка.ДатаПоставки) Тогда
			ДатаПоставки = Выборка.ДатаПоставки;
		КонецЕсли;
	КонецЕсли;
	Запрос.УстановитьПараметр("ДатаПоставки", ДатаПоставки);
	Запрос.УстановитьПараметр("ГраницаПериода", ГраницаПериода);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ГруппировкаЗаписей.ЗаказНаОтгрузку      КАК ЗаказНаОтгрузку,
		|	ГруппировкаЗаписей.ЖелаемаяДатаОтгрузки КАК ЖелаемаяДатаОтгрузки,
		|	ГруппировкаЗаписей.Резервировать        КАК Резервировать,
		|	ГруппировкаЗаписей.РезервироватьПоМереПоступления КАК РезервироватьПоМереПоступления,
		|	ГруппировкаЗаписей.КОбеспечению         КАК КОбеспечению,
		|	ВЫБОР КОГДА ГруппировкаЗаписей.НеОбеспечивать < ГруппировкаЗаписей.ЗакрытьГрафикОтгрузки ТОГДА
		|				0
		|			ИНАЧЕ
		|				ГруппировкаЗаписей.НеОбеспечивать - ГруппировкаЗаписей.ЗакрытьГрафикОтгрузки
		|		КОНЕЦ КАК НеОбеспечивать
		|ПОМЕСТИТЬ ДвиженияПоЗаказамНаОтгрузку
		|ИЗ(
		|	ВЫБРАТЬ
		|		Движения.ЗаказНаОтгрузку              КАК ЗаказНаОтгрузку,
		|		Движения.ЖелаемаяДатаОтгрузки         КАК ЖелаемаяДатаОтгрузки,
		|		СУММА(Движения.Резервировать)         КАК Резервировать,
		|		СУММА(Движения.РезервироватьПоМереПоступления) КАК РезервироватьПоМереПоступления,
		|		СУММА(Движения.КОбеспечениюБезРезерва) КАК КОбеспечению,
		|		СУММА(Движения.НеОбеспечивать)        КАК НеОбеспечивать,
		|		СУММА(Движения.ЗакрытьГрафикОтгрузки) КАК ЗакрытьГрафикОтгрузки
		|	ИЗ
		|		РегистрНакопления.РаспределениеЗапасовДвижения КАК Движения
		|	ГДЕ
		|		Движения.Активность
		|			И Движения.ЗаказНаОтгрузку <> НЕОПРЕДЕЛЕНО
		|			И Движения.Номенклатура    = &Номенклатура
		|			И Движения.Характеристика  = &Характеристика
		|			И Движения.Склад           = &Склад
		|			И Движения.Назначение      = &Назначение
		|	СГРУППИРОВАТЬ ПО
		|		Движения.ЗаказНаОтгрузку,
		|		Движения.ЖелаемаяДатаОтгрузки) КАК ГруппировкаЗаписей
		|ИНДЕКСИРОВАТЬ ПО
		|	ЗаказНаОтгрузку
		|;
		|
		|/////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗаказыРеестра.Ссылка КАК Ссылка,
		|	МАКСИМУМ(ЗаказыРеестра.Приоритет.РеквизитДопУпорядочивания) КАК Приоритет,
		|	МАКСИМУМ(ВЫБОР
		|					КОГДА Движения.ЖелаемаяДатаОтгрузки <> ДАТАВРЕМЯ(1, 1, 1)
		|						ТОГДА Движения.ЖелаемаяДатаОтгрузки
		|					ИНАЧЕ ДОБАВИТЬКДАТЕ(ЗаказыРеестра.ДатаДокументаИБ, ДЕНЬ, -1)
		|				КОНЕЦ) КАК ПорядокОсновной,
		|	МАКСИМУМ(ЗаказыРеестра.ДатаДокументаИБ) ПорядокДополнительный
		|ПОМЕСТИТЬ ДанныеРеестра
		|ИЗ
		|	ДвиженияПоЗаказамНаОтгрузку КАК Движения
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК ЗаказыРеестра
		|		ПО ЗаказыРеестра.Ссылка = Движения.ЗаказНаОтгрузку
		|			И НЕ ЗаказыРеестра.ДополнительнаяЗапись
		|ГДЕ
		|	НЕ ЗаказыРеестра.Ссылка ЕСТЬ NULL
		|СГРУППИРОВАТЬ ПО
		|	ЗаказыРеестра.Ссылка
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		|
		|//////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемаяОтгрузка) КАК Состояние,
		|	1                             КАК ТипЗаписиРаспределенияЗапасов,
		|	Движения.ЗаказНаОтгрузку      КАК ЗаказНаОтгрузку,
		|	ДанныеРеестра.Приоритет            КАК Приоритет,
		|	ДанныеРеестра.ПорядокОсновной      КАК ПорядокОсновной,
		|	ДанныеРеестра.ПорядокДополнительный КАК ПорядокДополнительный,
		|	Движения.ЖелаемаяДатаОтгрузки КАК ЖелаемаяДатаОтгрузки,
		|	НЕОПРЕДЕЛЕНО                  КАК ЗаказНаПоступление,
		|	ДАТАВРЕМЯ(1, 1, 1)            КАК ДатаПоступления,
		|	0                             КАК Запас,
		|	0                             КАК Свободно,
		|	Движения.Резервировать        КАК Резервировать,
		|	ВЫБОР КОГДА &ГраницаПериода = НЕОПРЕДЕЛЕНО ИЛИ &ГраницаПериода >= Движения.ЖелаемаяДатаОтгрузки ТОГДА
		|				Движения.РезервироватьПоМереПоступления
		|			ИНАЧЕ
		|				0
		|		КОНЕЦ КАК РезервироватьПоМереПоступления,
		|	Движения.КОбеспечению         КАК КОбеспечениюБезРезерва,
		|	ВЫБОР КОГДА &ГраницаПериода = НЕОПРЕДЕЛЕНО ИЛИ &ГраницаПериода >= Движения.ЖелаемаяДатаОтгрузки ТОГДА
		|				0
		|			ИНАЧЕ
		|				Движения.РезервироватьПоМереПоступления
		|		КОНЕЦ КАК ОтложитьРезервирование,
		|	Движения.НеОбеспечивать       КАК НеОбеспечивать,
		|	0                             КАК Зарезервировано,
		|	0                             КАК НеОбеспечено
		|ИЗ
		|	ДвиженияПоЗаказамНаОтгрузку КАК Движения
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеРеестра КАК ДанныеРеестра
		|		ПО ДанныеРеестра.Ссылка = Движения.ЗаказНаОтгрузку
		|ГДЕ
		|	Движения.КОбеспечению <> 0
		|		ИЛИ Движения.РезервироватьПоМереПоступления <> 0
		|		ИЛИ Движения.Резервировать <> 0
		|		ИЛИ Движения.НеОбеспечивать <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ВРезерве) КАК Состояние,
		|	1                             КАК ТипЗаписиРаспределенияЗапасов,
		|	Движения.ЗаказНаОтгрузку      КАК ЗаказНаОтгрузку,
		|	ДанныеРеестра.Приоритет            КАК Приоритет,
		|	ДанныеРеестра.ПорядокОсновной      КАК ПорядокОсновной,
		|	ДанныеРеестра.ПорядокДополнительный КАК ПорядокДополнительный,
		|	Движения.ЖелаемаяДатаОтгрузки КАК ЖелаемаяДатаОтгрузки,
		|	НЕОПРЕДЕЛЕНО                  КАК ЗаказНаПоступление,
		|	ДАТАВРЕМЯ(1, 1, 1)            КАК ДатаПоступления,
		|	0                             КАК Запас,
		|	0                             КАК Свободно,
		|	0                             КАК Резервировать,
		|	0                             КАК РезервироватьПоМереПоступления,
		|	0                             КАК КОбеспечениюБезРезерва,
		|	0                             КАК ОтложитьРезервирование,
		|	0                             КАК НеОбеспечивать,
		|	Движения.Резервировать        КАК Зарезервировано,
		|	0                             КАК НеОбеспечено
		|ИЗ
		|	ДвиженияПоЗаказамНаОтгрузку КАК Движения
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеРеестра КАК ДанныеРеестра
		|		ПО ДанныеРеестра.Ссылка = Движения.ЗаказНаОтгрузку
		|ГДЕ
		|	Движения.Резервировать <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.НеОбеспечивать) КАК Состояние,
		|	1                             КАК ТипЗаписиРаспределенияЗапасов,
		|	Движения.ЗаказНаОтгрузку      КАК ЗаказНаОтгрузку,
		|	ДанныеРеестра.Приоритет            КАК Приоритет,
		|	ДанныеРеестра.ПорядокОсновной      КАК ПорядокОсновной,
		|	ДанныеРеестра.ПорядокДополнительный КАК ПорядокДополнительный,
		|	Движения.ЖелаемаяДатаОтгрузки КАК ЖелаемаяДатаОтгрузки,
		|	НЕОПРЕДЕЛЕНО                  КАК ЗаказНаПоступление,
		|	ДАТАВРЕМЯ(1, 1, 1)            КАК ДатаПоступления,
		|	0                             КАК Запас,
		|	0                             КАК Свободно,
		|	0                             КАК Резервировать,
		|	0                             КАК РезервироватьПоМереПоступления,
		|	0                             КАК КОбеспечениюБезРезерва,
		|	0                             КАК ОтложитьРезервирование,
		|	0                             КАК НеОбеспечивать,
		|	0                             КАК Зарезервировано,
		|	Движения.НеОбеспечивать       КАК НеОбеспечено
		|ИЗ
		|	ДвиженияПоЗаказамНаОтгрузку КАК Движения
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеРеестра КАК ДанныеРеестра
		|		ПО ДанныеРеестра.Ссылка = Движения.ЗаказНаОтгрузку
		|ГДЕ
		|	Движения.НеОбеспечивать <> 0
		|УПОРЯДОЧИТЬ ПО
		|	ЗаказНаОтгрузку, ЖелаемаяДатаОтгрузки";
	
	ТаблицаЗаказыНаОтгрузку = Запрос.Выполнить().Выгрузить();
	ВсегоСтрок = ТаблицаЗаказыНаОтгрузку.Количество();
	ТаблицаЗаказыНаОтгрузку.Колонки.Добавить("РезервироватьПоМереПоступленияИтогНаДату", ОбщегоНазначения.ОписаниеТипаЧисло(15, 3));
	
	ЗаказНаОтгрузку = Неопределено;
	Итог = 0;
	Для Индекс = 0 По ВсегоСтрок - 1 Цикл
		
		Строка = ТаблицаЗаказыНаОтгрузку[Индекс];
		Если Строка.ЗаказНаОтгрузку <> ЗаказНаОтгрузку Тогда
			ЗаказНаОтгрузку = Строка.ЗаказНаОтгрузку;
			Итог = 0;
		КонецЕсли;
		
		Если Строка.РезервироватьПоМереПоступления > 0 Тогда
			Итог = Итог + Строка.РезервироватьПоМереПоступления;
			Строка.РезервироватьПоМереПоступленияИтогНаДату = Итог;
		КонецЕсли;
		
	КонецЦикла;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Движения.ЗаказНаПоступление КАК ЗаказНаПоступление,
		|	
		|	СУММА(ЕСТЬNULL(
		|			Движения.ПоставкаНаСогласовании
		|				+ Движения.ПоступитКДате
		|				- Движения.ЗакрытьГрафикПоступления, 0)) КАК Количество
		|	
		|ПОМЕСТИТЬ ОстаткиПланируемыхПоступлений
		|ИЗ
		|	РегистрНакопления.РаспределениеЗапасовДвижения КАК Движения
		|ГДЕ
		|	Движения.Активность
		|		И Движения.ЗаказНаПоступление <> НЕОПРЕДЕЛЕНО
		|		И Движения.Номенклатура       = &Номенклатура
		|		И Движения.Характеристика     = &Характеристика
		|		И Движения.Склад              = &Склад
		|		И Движения.Назначение         = &Назначение
		|СГРУППИРОВАТЬ ПО
		|	Движения.ЗаказНаПоступление
		|;
		|
		|////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Остатки.ЗаказНаПоступление КАК ЗаказНаПоступление,
		|	ВЫБОР КОГДА Движения.ДатаПоступления <> ДАТАВРЕМЯ(1, 1, 1) ТОГДА
		|				1
		|			ИНАЧЕ
		|				0
		|		КОНЕЦ КАК ПорядокПоСостоянию,
		|	ВЫБОР КОГДА Движения.ДатаПоступления <> ДАТАВРЕМЯ(1, 1, 1) ТОГДА
		|				ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемоеПоступление)
		|			ИНАЧЕ
		|				ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ПоступлениеНеподтвержденное)
		|		КОНЕЦ КАК Состояние,
		|	1 КАК ТипЗаписиРаспределенияЗапасов,
		|	Движения.ДатаПоступления КАК ДатаПоступления,
		|	МАКСИМУМ(Остатки.Количество) КАК КоличествоВсего,
		|	СУММА(Движения.ПоставкаНаСогласовании + Движения.ПоступитКДате) КАК Запас
		|ИЗ
		|	ОстаткиПланируемыхПоступлений КАК Остатки
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РаспределениеЗапасовДвижения КАК Движения
		|		ПО Движения.Номенклатура         = &Номенклатура
		|		 И Движения.Характеристика       = &Характеристика
		|		 И Движения.Склад                = &Склад
		|		 И Движения.Назначение           = &Назначение
		|		 И Движения.ЗаказНаПоступление   = Остатки.ЗаказНаПоступление
		|		 И Движения.ЗаказНаОтгрузку      = НЕОПРЕДЕЛЕНО       // для использования индекса
		|		 И Движения.ЖелаемаяДатаОтгрузки = ДАТАВРЕМЯ(1, 1, 1) // для использования индекса
		|		 И Движения.Активность
		|ГДЕ
		|	Остатки.Количество > 0
		|СГРУППИРОВАТЬ ПО
		|	Движения.ДатаПоступления,
		|	Остатки.ЗаказНаПоступление
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЗаказНаПоступление, ПорядокПоСостоянию, ДатаПоступления УБЫВ";
	
	ТаблицаЗаказыНаПоступление = Запрос.Выполнить().Выгрузить();
	ВсегоСтрок = ТаблицаЗаказыНаПоступление.Количество();
	ТаблицаЗаказыНаПоступление.Колонки.Добавить("ПустаяЗапись", Новый ОписаниеТипов("Булево"));
	
	ЗаказНаПоступление = Неопределено;
	Распределить = 0;
	Для Индекс = 0 По ВсегоСтрок - 1 Цикл
		
		Строка = ТаблицаЗаказыНаПоступление[Индекс];
		Если Строка.ЗаказНаПоступление <> ЗаказНаПоступление Тогда
			ЗаказНаПоступление = Строка.ЗаказНаПоступление;
			Распределить = Строка.КоличествоВсего;
		КонецЕсли;
		
		Запас = Макс(Мин(Распределить, Строка.Запас), 0);
		Распределить = Распределить - Запас;
		Строка.ПустаяЗапись = Запас = 0;
		Строка.Запас = Запас;
		
	КонецЦикла;
	ТаблицаЗаказыНаПоступление = ТаблицаЗаказыНаПоступление.Скопировать(Новый Структура("ПустаяЗапись", Ложь));
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СУММА(Движения.Отгрузить)      КАК Отгрузить,
		|	СУММА(Движения.Резервировать)  КАК Резервировать,
		|	СУММА(Движения.Поступило)      КАК Поступило
		|ПОМЕСТИТЬ ТаблицаПоТовару
		|ИЗ
		|	РегистрНакопления.РаспределениеЗапасовДвижения КАК Движения
		|ГДЕ
		|	Движения.Активность
		|		И Движения.Номенклатура       = &Номенклатура
		|		И Движения.Характеристика     = &Характеристика
		|		И Движения.Склад              = &Склад
		|		И Движения.Назначение         = &Назначение
		|;
		|
		|/////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(Движения.Отгрузить)      КАК Отгрузить,
		|	СУММА(Движения.Поступило)      КАК Поступило
		|ПОМЕСТИТЬ ТаблицаВНаличии
		|ИЗ
		|	РегистрНакопления.РаспределениеЗапасовДвижения КАК Движения
		|ГДЕ
		|	Движения.Активность
		|		И &Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|		И Движения.Номенклатура       = &Номенклатура
		|		И Движения.Характеристика     = &Характеристика
		|		И Движения.Склад              = &Склад
		|ИМЕЮЩИЕ
		|	СУММА(Движения.Отгрузить) <> 0
		|		ИЛИ СУММА(Движения.Поступило) <> 0
		|;
		|
		|/////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе) КАК Состояние,
		|	1                                                                    КАК ТипЗаписиРаспределенияЗапасов,
		|	ЕСТЬNULL(ТаблицаВНаличии.Поступило - ТаблицаВНаличии.Отгрузить, 0)   КАК ВНаличии,
		|	ЕСТЬNULL(Таблица.Поступило - Таблица.Отгрузить, 0)                   КАК Запас,
		|	ЕСТЬNULL(Таблица.Резервировать, 0)                                   КАК Резерв
		|ИЗ
		|	ТаблицаПоТовару КАК Таблица
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаВНаличии
		|		ПО ИСТИНА
		|ГДЕ
		|	ЕСТЬNULL(Таблица.Поступило - Таблица.Отгрузить, 0) <> 0
		|		ИЛИ ЕСТЬNULL(Таблица.Резервировать, 0) <> 0
		|		ИЛИ ЕСТЬNULL(ТаблицаВНаличии.Поступило - ТаблицаВНаличии.Отгрузить, 0) <> 0";
	
	ТаблицаОстатокНаСкладе = Запрос.Выполнить().Выгрузить();
	
	Потребность = 0;
	ПотребностьКОбеспечению = 0;
	Для Каждого Строка Из ТаблицаЗаказыНаОтгрузку Цикл
		Потребность = Потребность + Строка.РезервироватьПоМереПоступления;
		ПотребностьКОбеспечению = ПотребностьКОбеспечению + Строка.КОбеспечениюБезРезерва + Строка.ОтложитьРезервирование;
	КонецЦикла;
	
	ТаблицаЗаказыНаПоступление.Сортировать("ПорядокПоСостоянию,ДатаПоступления,ЗаказНаПоступление", Новый СравнениеЗначений());
	
	ТаблицаОстатокНаСкладе.Колонки.Добавить("Свободно", ОбщегоНазначения.ОписаниеТипаЧисло(15, 3));
	ТаблицаОстатокНаСкладе.Колонки.Добавить("Излишек", ОбщегоНазначения.ОписаниеТипаЧисло(15, 3));
	КоличествоРаспределитьСоСклада = 0;
	КоличествоОбеспеченоСоСклада = 0;
	Если ТаблицаОстатокНаСкладе.Количество() > 0 Тогда
		
		Строка = ТаблицаОстатокНаСкладе[0];
		КоличествоРаспределитьСоСклада = Мин(Потребность, Макс(Строка.Запас - Строка.Резерв, 0));
		Строка.Свободно = Строка.Запас - Строка.Резерв - КоличествоРаспределитьСоСклада;
		Потребность = Потребность - КоличествоРаспределитьСоСклада;
		КоличествоОбеспеченоСоСклада = Мин(ПотребностьКОбеспечению, Макс(Строка.Свободно, 0));
		ПотребностьКОбеспечению = ПотребностьКОбеспечению - КоличествоОбеспеченоСоСклада;
		Строка.Излишек = Строка.Свободно - КоличествоОбеспеченоСоСклада;
		
	КонецЕсли;
	
	ТаблицаЗаказыНаПоступление.Колонки.Добавить("Свободно", ОбщегоНазначения.ОписаниеТипаЧисло(15, 3));
	ТаблицаЗаказыНаПоступление.Колонки.Добавить("Излишек", ОбщегоНазначения.ОписаниеТипаЧисло(15, 3));
	ВсегоСтрок = ТаблицаЗаказыНаПоступление.Количество();
	Для Счетчик = 0 По ВсегоСтрок - 1 Цикл
		
		Строка = ТаблицаЗаказыНаПоступление[Счетчик];
		КоличествоРаспределитьИзГрафика = Мин(Потребность, Макс(Строка.Запас, 0));
		Строка.Свободно = Строка.Запас - КоличествоРаспределитьИзГрафика;
		Потребность = Потребность - КоличествоРаспределитьИзГрафика;
		КоличествоОбеспеченоИзГрафика = Мин(ПотребностьКОбеспечению, Макс(Строка.Свободно, 0));
		ПотребностьКОбеспечению = ПотребностьКОбеспечению - КоличествоОбеспеченоИзГрафика;
		Строка.Излишек = Строка.Свободно - КоличествоОбеспеченоИзГрафика;
		
	КонецЦикла;
	
	ТаблицаЗаказыНаОтгрузку.Сортировать("Приоритет,ПорядокОсновной,ПорядокДополнительный,ЗаказНаОтгрузку", Новый СравнениеЗначений());
	ВсегоСтрок = ТаблицаЗаказыНаОтгрузку.Количество();
	
	ИндексГрафик = -1;
	ИндексГрафикОбеспечить = -1;
	КоличествоВГрафике =0;
	КоличествоВГрафикеОбеспечить =0;
	
	Для Счетчик = 0 По ВсегоСтрок - 1 Цикл
		
		Строка = ТаблицаЗаказыНаОтгрузку[Счетчик];
		Если Строка.РезервироватьПоМереПоступления > 0 Или Строка.КОбеспечениюБезРезерва Или Строка.ОтложитьРезервирование Тогда
			
			Потребность = Строка.РезервироватьПоМереПоступления;
			КРаспределениюСоСклада = Мин(Потребность, КоличествоРаспределитьСоСклада);
			КоличествоРаспределитьСоСклада = КоличествоРаспределитьСоСклада - КРаспределениюСоСклада;
			Потребность = Потребность - КРаспределениюСоСклада;
			
			ПотребностьКОбеспечению = Строка.ОтложитьРезервирование + Строка.КОбеспечениюБезРезерва;
			КОбеспечениюСоСклада = Мин(ПотребностьКОбеспечению, КоличествоОбеспеченоСоСклада);
			КоличествоОбеспеченоСоСклада = КоличествоОбеспеченоСоСклада - КОбеспечениюСоСклада;
			ПотребностьКОбеспечению = ПотребностьКОбеспечению - КОбеспечениюСоСклада;
			
			Если КРаспределениюСоСклада Или КОбеспечениюСоСклада > 0 Тогда
				СтрокаНабора = Набор.Добавить();
				СтрокаНабора.Состояние = Перечисления.РаспределениеЗапасовСостояния.ОбеспеченНаСкладе;
				СтрокаНабора.Зарезервировано = КРаспределениюСоСклада;
				СтрокаНабора.Обеспечено = КОбеспечениюСоСклада;
				ЗаполнитьЗначенияСвойств(СтрокаНабора, Строка, "ЗаказНаОтгрузку,ЖелаемаяДатаОтгрузки");
			КонецЕсли;
			
			СтрокаНабора = Неопределено;
			Пока Потребность > 0
				И (КоличествоВГрафике > 0 Или ИндексГрафик < ТаблицаЗаказыНаПоступление.Количество() - 1) Цикл
				
				Пока КоличествоВГрафике = 0 И ИндексГрафик < ТаблицаЗаказыНаПоступление.Количество() - 1 Цикл
					ИндексГрафик = ИндексГрафик + 1;
					КоличествоВГрафике = ТаблицаЗаказыНаПоступление[ИндексГрафик].Запас - ТаблицаЗаказыНаПоступление[ИндексГрафик].Свободно;
				КонецЦикла;
				
				КРаспределениюИзГрафика = Мин(Потребность, КоличествоВГрафике);
				КоличествоВГрафике = КоличествоВГрафике - КРаспределениюИзГрафика;
				Потребность = Потребность - КРаспределениюИзГрафика;
				
				Если КРаспределениюИзГрафика > 0 Тогда
					СтрокаНабора = Набор.Добавить();
					Если ТаблицаЗаказыНаПоступление[ИндексГрафик].Состояние = Перечисления.РаспределениеЗапасовСостояния.ОжидаемоеПоступление Тогда
						СтрокаНабора.Состояние = Перечисления.РаспределениеЗапасовСостояния.ОбеспеченКДате;
					Иначе
						СтрокаНабора.Состояние = Перечисления.РаспределениеЗапасовСостояния.ОжидаетсяПоНеподтвержденномуЗаказу;
					КонецЕсли;
					СтрокаНабора.Зарезервировано = КРаспределениюИзГрафика;
					ЗаполнитьЗначенияСвойств(СтрокаНабора, Строка, "ЗаказНаОтгрузку,ЖелаемаяДатаОтгрузки");
					ЗаполнитьЗначенияСвойств(СтрокаНабора, ТаблицаЗаказыНаПоступление[ИндексГрафик],
						"ЗаказНаПоступление,ДатаПоступления");
				КонецЕсли;
				
			КонецЦикла;
			
			Пока ПотребностьКОбеспечению > 0
				И (КоличествоВГрафикеОбеспечить > 0 Или ИндексГрафикОбеспечить < ТаблицаЗаказыНаПоступление.Количество() - 1) Цикл
				
				Пока КоличествоВГрафикеОбеспечить = 0 И ИндексГрафикОбеспечить < ТаблицаЗаказыНаПоступление.Количество() - 1 Цикл
					ИндексГрафикОбеспечить = ИндексГрафикОбеспечить + 1;
					КоличествоВГрафикеОбеспечить = ТаблицаЗаказыНаПоступление[ИндексГрафикОбеспечить].Свободно;
				КонецЦикла;
				
				КРаспределениюИзГрафикаОбеспечить = Мин(ПотребностьКОбеспечению, КоличествоВГрафикеОбеспечить);
				КоличествоВГрафикеОбеспечить = КоличествоВГрафикеОбеспечить - КРаспределениюИзГрафикаОбеспечить;
				ПотребностьКОбеспечению = ПотребностьКОбеспечению - КРаспределениюИзГрафикаОбеспечить;
				
				Если КРаспределениюИзГрафикаОбеспечить > 0 Тогда
					
					Если СтрокаНабора = Неопределено
						Или СтрокаНабора.ЗаказНаПоступление <> ТаблицаЗаказыНаПоступление[ИндексГрафикОбеспечить].ЗаказНаПоступление
						Или СтрокаНабора.ДатаПоступления <> ТаблицаЗаказыНаПоступление[ИндексГрафикОбеспечить].ДатаПоступления Тогда
						
						СтрокаНабора = Набор.Добавить();
						Если ТаблицаЗаказыНаПоступление[ИндексГрафикОбеспечить].Состояние = Перечисления.РаспределениеЗапасовСостояния.ОжидаемоеПоступление Тогда
							СтрокаНабора.Состояние = Перечисления.РаспределениеЗапасовСостояния.ОбеспеченКДате;
						Иначе
							СтрокаНабора.Состояние = Перечисления.РаспределениеЗапасовСостояния.ОжидаетсяПоНеподтвержденномуЗаказу;
						КонецЕсли;
						ЗаполнитьЗначенияСвойств(СтрокаНабора, Строка, "ЗаказНаОтгрузку,ЖелаемаяДатаОтгрузки");
						ЗаполнитьЗначенияСвойств(СтрокаНабора, ТаблицаЗаказыНаПоступление[ИндексГрафикОбеспечить],
							"ЗаказНаПоступление,ДатаПоступления");
						
					КонецЕсли;
					
					СтрокаНабора.Обеспечено = КРаспределениюИзГрафикаОбеспечить;
					
				КонецЕсли;
				
			КонецЦикла;
			
			Если Потребность > 0 Или ПотребностьКОбеспечению > 0 Тогда
				СтрокаНабора = Набор.Добавить();
				СтрокаНабора.Состояние = Перечисления.РаспределениеЗапасовСостояния.Обеспечить;
				СтрокаНабора.НеОбеспечено = Потребность + ПотребностьКОбеспечению;
				ЗаполнитьЗначенияСвойств(СтрокаНабора, Строка, "ЗаказНаОтгрузку,ЖелаемаяДатаОтгрузки");
				СтрокаНабора.НеОбеспеченоКОбеспечению = ПотребностьКОбеспечению;
			КонецЕсли;
		
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого Строка Из ТаблицаЗаказыНаОтгрузку Цикл
		СтрокаНабора = Набор.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаНабора, Строка);
	КонецЦикла;
	Для Каждого Строка Из ТаблицаЗаказыНаПоступление Цикл
		СтрокаНабора = Набор.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаНабора, Строка);
	КонецЦикла;
	Для Каждого Строка Из ТаблицаОстатокНаСкладе Цикл
		СтрокаНабора = Набор.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаНабора, Строка);
	КонецЦикла;
	
	Для Каждого СтрокаНабора Из Набор Цикл
		СтрокаНабора.Номенклатура = Товар.Номенклатура;
		СтрокаНабора.Характеристика = Товар.Характеристика;
		СтрокаНабора.Склад = Товар.Склад;
		СтрокаНабора.Назначение = Товар.Назначение;
	КонецЦикла;
	Возврат Набор;
	
КонецФункции

// Параметры:
//  Запрос - Запрос - запрос, хранящий параметры используемые в списке запросов
//  ТекстыЗапроса - СписокЗначений - список текстов запросов и их имен.
//  Документ - ДокументОбъект - записываемый документ.
//  ЭтоПроизводительныйРежим - Булево - контроль выполняется в производительном режиме.
Процедура ДобавитьТекстЗапросаКонтроль(Запрос, ТекстыЗапроса, Документ, ЭтоПроизводительныйРежим) Экспорт
	
	Если ЭтоПроизводительныйРежим Тогда
		
		ТекстЗапроса =
			"ВЫБРАТЬ
			|	ИзмененияДвижений.Номенклатура КАК Номенклатура,
			|	ИзмененияДвижений.Характеристика КАК Характеристика,
			|	ИзмененияДвижений.Склад КАК Склад,
			|	ИзмененияДвижений.Назначение КАК Назначение,
			|	СУММА(ИзмененияДвижений.ВНаличииРасход) КАК Отгрузка,
			|	СУММА(ИзмененияДвижений.РезервироватьНаСкладе - ИзмененияДвижений.ВНаличии) КАК Расход
			|ПОМЕСТИТЬ ИзмененияДляКонтроляОбеспечения
			|ИЗ
			|	ЗапасыИПотребностиИзменение КАК ИзмененияДвижений
			|		
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОбеспечения КАК НастройкаХарактеристика
			|		ПО НастройкаХарактеристика.Склад          = ИзмененияДвижений.Склад
			|		 И НастройкаХарактеристика.Номенклатура   = ИзмененияДвижений.Номенклатура
			|		 И НастройкаХарактеристика.Характеристика = ИзмененияДвижений.Характеристика
			|		
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОбеспечения КАК НастройкаНоменклатура
			|		ПО НастройкаНоменклатура.Склад        = ИзмененияДвижений.Склад
			|		 И НастройкаНоменклатура.Номенклатура = ИзмененияДвижений.Номенклатура
			|		 И НастройкаНоменклатура.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
			|		 И НастройкаХарактеристика.Склад ЕСТЬ NULL
			|		
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОбеспечения КАК НастройкаСклад
			|		ПО НастройкаСклад.Склад          = ИзмененияДвижений.Склад
			|		 И НастройкаСклад.Номенклатура   = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
			|		 И НастройкаСклад.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
			|		 И НастройкаХарактеристика.Склад ЕСТЬ NULL
			|		 И НастройкаНоменклатура.Склад ЕСТЬ NULL
			|ГДЕ
			|	ЕСТЬNULL(НастройкаХарактеристика.КонтролироватьСвободныеОстатки,
			|		ЕСТЬNULL(НастройкаНоменклатура.КонтролироватьСвободныеОстатки,
			|			НастройкаСклад.КонтролироватьСвободныеОстатки))
			|		И ИзмененияДвижений.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
			|СГРУППИРОВАТЬ ПО
			|	ИзмененияДвижений.Номенклатура,
			|	ИзмененияДвижений.Характеристика,
			|	ИзмененияДвижений.Склад,
			|	ИзмененияДвижений.Назначение
			|ИМЕЮЩИЕ
			|	СУММА(ИзмененияДвижений.РезервироватьНаСкладе + ИзмененияДвижений.ВНаличииРасход) > 0
			|		ИЛИ СУММА(ИзмененияДвижений.ВНаличииРасход) > 0
			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура, Характеристика, Склад, Назначение";
		ТекстыЗапроса.Добавить(ТекстЗапроса, "ИзмененияДляКонтроляОбеспечения");
		
		ТекстЗапроса =
			"ВЫБРАТЬ
			|	Набор.Номенклатура КАК Номенклатура,
			|	Набор.Характеристика КАК Характеристика,
			|	Набор.Склад КАК Склад,
			|	Набор.Назначение КАК Назначение,
			|	СУММА(Набор.Зарезервировано) КАК Зарезервировано
			|ПОМЕСТИТЬ РезервыПоЗаказамДляКонтроляОбеспечения
			|ИЗ(
			|	ВЫБРАТЬ
			|		НаборПоЗаказам.Номенклатура КАК Номенклатура,
			|		НаборПоЗаказам.Характеристика КАК Характеристика,
			|		НаборПоЗаказам.Склад КАК Склад,
			|		НаборПоЗаказам.Назначение КАК Назначение,
			|		НаборПоЗаказам.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
			|		СУММА(НаборПоЗаказам.Зарезервировано) КАК Зарезервировано
			|	ИЗ(
			|		ВЫБРАТЬ
			|			РаспределениеЗапасов.Номенклатура КАК Номенклатура,
			|			РаспределениеЗапасов.Характеристика КАК Характеристика,
			|			РаспределениеЗапасов.Склад КАК Склад,
			|			РаспределениеЗапасов.Назначение КАК Назначение,
			|			РаспределениеЗапасов.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
			|			РаспределениеЗапасов.Зарезервировано КАК Зарезервировано
			|		ИЗ
			|			ИзмененияДляКонтроляОбеспечения КАК ИзмененияДвижений
			|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РаспределениеЗапасов КАК РаспределениеЗапасов
			|				ПО РаспределениеЗапасов.Номенклатура = ИзмененияДвижений.Номенклатура
			|				 И РаспределениеЗапасов.Характеристика = ИзмененияДвижений.Характеристика
			|				 И РаспределениеЗапасов.Склад = ИзмененияДвижений.Склад
			|				 И РаспределениеЗапасов.Назначение = ИзмененияДвижений.Назначение
			|		ГДЕ
			|			РаспределениеЗапасов.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОбеспеченНаСкладе)
			|				И РаспределениеЗапасов.Зарезервировано > 0
			|				И ИСТИНА В(
			|					ВЫБРАТЬ ПЕРВЫЕ 1
			|						ИСТИНА КАК ЕстьЗаписи
			|					ИЗ
			|						ЗапасыИПотребностиПередЗаписью КАК Заказы
			|					ГДЕ
			|						Заказы.Номенклатура = РаспределениеЗапасов.Номенклатура
			|						 И Заказы.Характеристика = РаспределениеЗапасов.Характеристика
			|						 И Заказы.Склад = РаспределениеЗапасов.Склад
			|						 И Заказы.Назначение = РаспределениеЗапасов.Назначение
			|						 И Заказы.Заказ = РаспределениеЗапасов.ЗаказНаОтгрузку)
			|		
			|		ОБЪЕДИНИТЬ ВСЕ
			|		
			|		ВЫБРАТЬ
			|			ЗаданияПоЗаказам.Номенклатура КАК Номенклатура,
			|			ЗаданияПоЗаказам.Характеристика КАК Характеристика,
			|			ЗаданияПоЗаказам.Склад КАК Склад,
			|			ЗаданияПоЗаказам.Назначение КАК Назначение,
			|			ЗаданияПоЗаказам.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
			|			-ЗаданияПоЗаказам.КСнятиюРезерва КАК Зарезервировано
			|		ИЗ
			|			ИзмененияДляКонтроляОбеспечения КАК ИзмененияДвижений
			|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗаданияКРаспределениюЗапасов КАК ЗаданияПоЗаказам
			|				ПО ЗаданияПоЗаказам.Номенклатура = ИзмененияДвижений.Номенклатура
			|				 И ЗаданияПоЗаказам.Характеристика = ИзмененияДвижений.Характеристика
			|				 И ЗаданияПоЗаказам.Склад = ИзмененияДвижений.Склад
			|				 И ЗаданияПоЗаказам.Назначение = ИзмененияДвижений.Назначение
			|		ГДЕ
			|			ЗаданияПоЗаказам.КСнятиюРезерва > 0
			|				И ИСТИНА В(
			|					ВЫБРАТЬ ПЕРВЫЕ 1
			|						ИСТИНА КАК ЕстьЗаписи
			|					ИЗ
			|						ЗапасыИПотребностиПередЗаписью КАК Заказы
			|					ГДЕ
			|						Заказы.Номенклатура = ЗаданияПоЗаказам.Номенклатура
			|						 И Заказы.Характеристика = ЗаданияПоЗаказам.Характеристика
			|						 И Заказы.Склад = ЗаданияПоЗаказам.Склад
			|						 И Заказы.Назначение = ЗаданияПоЗаказам.Назначение
			|						 И Заказы.Заказ = ЗаданияПоЗаказам.ЗаказНаОтгрузку)) КАК НаборПоЗаказам
			|	СГРУППИРОВАТЬ ПО
			|		НаборПоЗаказам.Номенклатура,
			|		НаборПоЗаказам.Характеристика,
			|		НаборПоЗаказам.Склад,
			|		НаборПоЗаказам.Назначение,
			|		НаборПоЗаказам.ЗаказНаОтгрузку
			|	ИМЕЮЩИЕ
			|		СУММА(НаборПоЗаказам.Зарезервировано) > 0) КАК Набор
			|СГРУППИРОВАТЬ ПО
			|	Набор.Номенклатура,
			|	Набор.Характеристика,
			|	Набор.Склад,
			|	Набор.Назначение";
		ТекстыЗапроса.Добавить(ТекстЗапроса, "РезервыПоЗаказамДляКонтроляОбеспечения");
		
		ТекстЗапроса =
			"ВЫБРАТЬ
			|	ИзмененияДвижений.Номенклатура КАК Номенклатура,
			|	ИзмененияДвижений.Характеристика КАК Характеристика,
			|	ИзмененияДвижений.Склад КАК Склад,
			|	ИзмененияДвижений.Назначение КАК Назначение,
			|	ИзмененияДвижений.Расход - ЕСТЬNULL(РезервыПоЗаказам.Зарезервировано, 0) КАК Расход,
			|	ИзмененияДвижений.Отгрузка - ЕСТЬNULL(РезервыПоЗаказам.Зарезервировано, 0) КАК Отгрузка,
			|	ЕСТЬNULL(РезервыПоЗаказам.Зарезервировано, 0) КАК Зарезервировано
			|ПОМЕСТИТЬ ИзмененияИРезервыДляКонтроляОбеспечения
			|ИЗ
			|	ИзмененияДляКонтроляОбеспечения КАК ИзмененияДвижений
			|		ЛЕВОЕ СОЕДИНЕНИЕ РезервыПоЗаказамДляКонтроляОбеспечения КАК РезервыПоЗаказам
			|		ПО РезервыПоЗаказам.Номенклатура = ИзмененияДвижений.Номенклатура
			|		 И РезервыПоЗаказам.Характеристика = ИзмененияДвижений.Характеристика
			|		 И РезервыПоЗаказам.Склад = ИзмененияДвижений.Склад
			|		 И РезервыПоЗаказам.Назначение = ИзмененияДвижений.Назначение
			|ГДЕ
			|	ИзмененияДвижений.Расход - ЕСТЬNULL(РезервыПоЗаказам.Зарезервировано, 0) > 0
			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура, Характеристика, Склад, Назначение";
		ТекстыЗапроса.Добавить(ТекстЗапроса, "ИзмененияИРезервыДляКонтроляОбеспечения");
		
		ТекстЗапроса =
			"ВЫБРАТЬ
			|	Набор.Номенклатура КАК Номенклатура,
			|	Набор.Характеристика КАК Характеристика,
			|	Набор.Склад КАК Склад,
			|	Набор.Назначение КАК Назначение,
			|	СУММА(Набор.Расход) КАК Расход,
			|	СУММА(Набор.Отгрузка) КАК Отгрузка,
			|	СУММА(Набор.Свободно) КАК Свободно
			|ПОМЕСТИТЬ ОшибкиКонтроляДляКонтроляОбеспечения
			|ИЗ(
			|	ВЫБРАТЬ
			|		Остатки.Номенклатура КАК Номенклатура,
			|		Остатки.Характеристика КАК Характеристика,
			|		Остатки.Склад КАК Склад,
			|		Остатки.Назначение КАК Назначение,
			|		0 КАК Расход,
			|		0 КАК Отгрузка,
			|		Остатки.ВНаличииОстаток
			|			- Остатки.РезервироватьНаСкладеОстаток
			|			- Остатки.РезервироватьПоМереПоступленияОстаток КАК Свободно
			|	ИЗ
			|		РегистрНакопления.ЗапасыИПотребности.Остатки(,
			|			(Номенклатура, Характеристика, Склад, Назначение) В(
			|				ВЫБРАТЬ
			|					Таблица.Номенклатура КАК Номенклатура,
			|					Таблица.Характеристика КАК Характеристика,
			|					Таблица.Склад КАК Склад,
			|					Таблица.Назначение КАК Назначение
			|				ИЗ
			|					ИзмененияИРезервыДляКонтроляОбеспечения КАК Таблица)) КАК Остатки
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		Остатки.Номенклатура КАК Номенклатура,
			|		Остатки.Характеристика КАК Характеристика,
			|		Остатки.Склад КАК Склад,
			|		Остатки.Назначение КАК Назначение,
			|		0 КАК Расход,
			|		0 КАК Отгрузка,
			|		Остатки.РезервироватьПоМереПоступленияОстаток КАК Свободно
			|	ИЗ
			|		РегистрНакопления.ЗапасыИПотребности.Остатки(,
			|			(Номенклатура, Характеристика, Склад, Назначение, Заказ) В(
			|				ВЫБРАТЬ
			|					Таблица.Номенклатура КАК Номенклатура,
			|					Таблица.Характеристика КАК Характеристика,
			|					Таблица.Склад КАК Склад,
			|					Таблица.Назначение КАК Назначение,
			|					Таблица.Заказ КАК Заказ
			|				ИЗ
			|					ЗапасыИПотребностиЗатронутыеЗаказы КАК Таблица)
			|			И (Номенклатура, Характеристика, Склад, Назначение) В(
			|				ВЫБРАТЬ
			|					Таблица.Номенклатура КАК Номенклатура,
			|					Таблица.Характеристика КАК Характеристика,
			|					Таблица.Склад КАК Склад,
			|					Таблица.Назначение КАК Назначение
			|				ИЗ
			|					ИзмененияИРезервыДляКонтроляОбеспечения КАК Таблица)) КАК Остатки
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		ИзмененияИРезервы.Номенклатура КАК Номенклатура,
			|		ИзмененияИРезервы.Характеристика КАК Характеристика,
			|		ИзмененияИРезервы.Склад КАК Склад,
			|		ИзмененияИРезервы.Назначение КАК Назначение,
			|		ИзмененияИРезервы.Отгрузка КАК Отгрузка,
			|		ИзмененияИРезервы.Расход КАК Расход,
			|		0 КАК Свободно
			|	ИЗ
			|		ИзмененияИРезервыДляКонтроляОбеспечения КАК ИзмененияИРезервы) КАК Набор
			|СГРУППИРОВАТЬ ПО
			|	Набор.Номенклатура,
			|	Набор.Характеристика,
			|	Набор.Склад,
			|	Набор.Назначение
			|ИМЕЮЩИЕ
			|	СУММА(Набор.Свободно) < 0";
		ТекстыЗапроса.Добавить(ТекстЗапроса, "ОшибкиКонтроляДляКонтроляОбеспечения");
		
		ТекстЗапроса =
			"ВЫБРАТЬ
			|	ОшибкиКонтроля.Номенклатура КАК Номенклатура,
			|	ОшибкиКонтроля.Характеристика КАК Характеристика,
			|	ОшибкиКонтроля.Склад КАК Склад,
			|	ОшибкиКонтроля.Назначение КАК Назначение,
			|	ОшибкиКонтроля.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
			|	ЛОЖЬ КАК ИгнорироватьРезервыПриКонтролеОстатков,
			|	ОшибкиКонтроля.Свободно КАК Свободно,
			|	ВЫБОР
			|		КОГДА ОшибкиКонтроля.Отгрузка > ОшибкиКонтроля.Расход
			|			ТОГДА
			|				ВЫБОР
			|					КОГДА -ОшибкиКонтроля.Свободно > ОшибкиКонтроля.Отгрузка
			|						ТОГДА ОшибкиКонтроля.Отгрузка
			|					ИНАЧЕ -ОшибкиКонтроля.Свободно
			|				КОНЕЦ
			|		КОГДА -ОшибкиКонтроля.Свободно > ОшибкиКонтроля.Расход
			|			ТОГДА ОшибкиКонтроля.Расход
			|		ИНАЧЕ -ОшибкиКонтроля.Свободно
			|	КОНЕЦ КАК КоличествоСвободно
			|ИЗ
			|	ОшибкиКонтроляДляКонтроляОбеспечения КАК ОшибкиКонтроля";
		ТекстыЗапроса.Добавить(ТекстЗапроса, "ОбеспечениеРезультатыКонтроля");
		
		ТипДокумента = ТипЗнч(Документ.Ссылка);
		Если ТипДокумента = Тип("ДокументСсылка.ВозвратТоваровОтКлиента")
				Или ТипДокумента = Тип("ДокументСсылка.КорректировкаРеализации")
				
				Или ТипДокумента = Тип("ДокументСсылка.ПоступлениеТоваровОтХранителя") Тогда
					
					Возврат;
		КонецЕсли;
		
		Если Не Запрос.Параметры.Свойство("МерныеТипыЕдиницИзмерений") Тогда
			Запрос.УстановитьПараметр("МерныеТипыЕдиницИзмерений",
				Справочники.УпаковкиЕдиницыИзмерения.МерныеТипыЕдиницИзмерений());
		КонецЕсли;
		
		ТекстЗапроса =
			"ВЫБРАТЬ
			|	ИзмененияДвижений.Номенклатура КАК Номенклатура,
			|	ИзмененияДвижений.Характеристика КАК Характеристика,
			|	ИзмененияДвижений.Склад КАК Склад,
			|	ИзмененияДвижений.Назначение КАК Назначение,
			|	МАКСИМУМ(ИзмененияДвижений.ВНаличииПриход > 0) КАК ЭтоУвеличениеПрихода
			|ПОМЕСТИТЬ АналитикиДляКонтроляПревышенияОбособленногоОбеспечения
			|ИЗ
			|	ЗапасыИПотребностиИзменение КАК ИзмененияДвижений
			|		
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОбеспечения КАК НастройкаХарактеристика
			|		ПО НастройкаХарактеристика.Склад          = ИзмененияДвижений.Склад
			|		 И НастройкаХарактеристика.Номенклатура   = ИзмененияДвижений.Номенклатура
			|		 И НастройкаХарактеристика.Характеристика = ИзмененияДвижений.Характеристика
			|		
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОбеспечения КАК НастройкаНоменклатура
			|		ПО НастройкаНоменклатура.Склад        = ИзмененияДвижений.Склад
			|		 И НастройкаНоменклатура.Номенклатура = ИзмененияДвижений.Номенклатура
			|		 И НастройкаНоменклатура.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
			|		 И НастройкаХарактеристика.Склад ЕСТЬ NULL
			|		
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОбеспечения КАК НастройкаСклад
			|		ПО НастройкаСклад.Склад          = ИзмененияДвижений.Склад
			|		 И НастройкаСклад.Номенклатура   = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
			|		 И НастройкаСклад.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
			|		 И НастройкаХарактеристика.Склад ЕСТЬ NULL
			|		 И НастройкаНоменклатура.Склад ЕСТЬ NULL
			|		
			|ГДЕ
			|	ИзмененияДвижений.КонтролироватьПревышениеОбособленногоОбеспечения
			|		И (ЕСТЬNULL(НастройкаХарактеристика.КонтролироватьСвободныеОстатки,
			|				ЕСТЬNULL(НастройкаНоменклатура.КонтролироватьСвободныеОстатки,
			|					НастройкаСклад.КонтролироватьСвободныеОстатки))
			|			ИЛИ ИзмененияДвижений.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа))
			|СГРУППИРОВАТЬ ПО
			|	ИзмененияДвижений.Номенклатура,
			|	ИзмененияДвижений.Характеристика,
			|	ИзмененияДвижений.Склад,
			|	ИзмененияДвижений.Назначение
			|ИМЕЮЩИЕ
			|	СУММА(ИзмененияДвижений.ВНаличииПриход) > 0
			|		ИЛИ СУММА(ИзмененияДвижений.Поступит) > 0
			|		ИЛИ СУММА(ИзмененияДвижений.ВНаличииРасход
			|					+ ИзмененияДвижений.РезервироватьНаСкладе
			|					+ ИзмененияДвижений.РезервироватьПоМереПоступления
			|					+ ИзмененияДвижений.ОтложитьРезервирование
			|					+ ИзмененияДвижений.КОбеспечению) < 0
			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура, Характеристика, Склад, Назначение";
		ТекстыЗапроса.Добавить(ТекстЗапроса, "АналитикиДляКонтроляПревышенияОбособленногоОбеспечения");
		
		ТекстЗапроса =
			"ВЫБРАТЬ
			|	Остатки.Номенклатура КАК Номенклатура,
			|	Остатки.Характеристика КАК Характеристика,
			|	Остатки.Склад КАК Склад,
			|	Остатки.Назначение КАК Назначение,
			|	Остатки.ВНаличииОстаток КАК ВНаличииОстаток,
			|	Остатки.ПоступитОстаток КАК ЗаказаноОстаток,
			|	Остатки.РезервироватьНаСкладеОстаток
			|		+ Остатки.РезервироватьПоМереПоступленияОстаток
			|		+ Остатки.ОтложитьРезервированиеОстаток
			|		+ Остатки.КОбеспечениюОстаток КАК ВсеПотребностиОстаток
			|ПОМЕСТИТЬ ЗапасыИПотребностиДляКонтроляОбособленногоОбеспечения
			|ИЗ
			|	РегистрНакопления.ЗапасыИПотребности.Остатки(,
			|		(Номенклатура, Характеристика, Склад, Назначение) В(
			|			ВЫБРАТЬ
			|				Аналитики.Номенклатура КАК Номенклатура,
			|				Аналитики.Характеристика КАК Характеристика,
			|				Аналитики.Склад КАК Склад,
			|				Аналитики.Назначение КАК Назначение
			|			ИЗ
			|				АналитикиДляКонтроляПревышенияОбособленногоОбеспечения КАК Аналитики)) КАК Остатки
			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура, Характеристика, Склад, Назначение";
		ТекстыЗапроса.Добавить(ТекстЗапроса, "ЗапасыИПотребностиДляКонтроляОбособленногоОбеспечения");
		
		ТекстЗапроса =
			"ВЫБРАТЬ
			|	Остатки.Номенклатура КАК Номенклатура,
			|	Остатки.Характеристика КАК Характеристика,
			|	Остатки.Склад КАК Склад,
			|	Остатки.Назначение КАК Назначение,
			|	Остатки.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
			|	ВЫБОР
			|		КОГДА Аналитики.ЭтоУвеличениеПрихода
			|				И Остатки.ВНаличииОстаток - Остатки.ВсеПотребностиОстаток
			|					> ВЫБОР
			|						КОГДА НЕ Остатки.Номенклатура.ЕдиницаИзмерения.ТипИзмеряемойВеличины
			|									В (&МерныеТипыЕдиницИзмерений)
			|							ТОГДА 0
			|						ИНАЧЕ Остатки.ВсеПотребностиОстаток * ДопустимоеОтклонение.Значение / 100
			|					КОНЕЦ
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК ОшибкаНаличиеБольшеПотребности,
			|	Остатки.ВНаличииОстаток - Остатки.ВсеПотребностиОстаток КАК ВеличинаНаличиеБольшеПотребности,
			|	Остатки.ВНаличииОстаток + Остатки.ЗаказаноОстаток - Остатки.ВсеПотребностиОстаток КАК ВеличинаЗаказатьМеньшеНуля
			|ИЗ
			|	ЗапасыИПотребностиДляКонтроляОбособленногоОбеспечения КАК Остатки
			|		
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикиДляКонтроляПревышенияОбособленногоОбеспечения КАК Аналитики
			|		ПО Аналитики.Номенклатура = Остатки.Номенклатура
			|		 И Аналитики.Характеристика = Остатки.Характеристика
			|		 И Аналитики.Склад = Остатки.Склад
			|		 И Аналитики.Назначение = Остатки.Назначение
			|		
			|		ЛЕВОЕ СОЕДИНЕНИЕ Константа.ДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров КАК ДопустимоеОтклонение
			|		ПО ИСТИНА
			|ГДЕ
			|	Остатки.ВНаличииОстаток + Остатки.ЗаказаноОстаток - Остатки.ВсеПотребностиОстаток
			|		> ВЫБОР
			|			КОГДА НЕ Остатки.Номенклатура.ЕдиницаИзмерения.ТипИзмеряемойВеличины
			|						В (&МерныеТипыЕдиницИзмерений)
			|				ТОГДА 0
			|			ИНАЧЕ
			|				Остатки.ВсеПотребностиОстаток * ДопустимоеОтклонение.Значение / 100
			|		КОНЕЦ";
		ТекстыЗапроса.Добавить(ТекстЗапроса, "ОбеспечениеРезультатыКонтроляПревышенияОбособленногоОбеспечения");
		
	Иначе
		ТекстЗапроса =
			"ВЫБРАТЬ
			|	ВЫРАЗИТЬ(ИзмененияДвижений.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
			|	ИзмененияДвижений.Характеристика КАК Характеристика,
			|	ИзмененияДвижений.Склад КАК Склад,
			|	ИзмененияДвижений.Назначение КАК Назначение,
			|	МАКСИМУМ(ИзмененияДвижений.КонтролироватьПревышениеОбособленногоОбеспечения) КАК КонтролироватьПревышениеОбособленногоОбеспечения,
			|	СУММА(ИзмененияДвижений.Отгрузить) КАК Отгрузить,
			|	СУММА(ИзмененияДвижений.Резервировать) КАК Резервировать,
			|	СУММА(ИзмененияДвижений.РезервироватьПоМереПоступления) КАК РезервироватьПоМереПоступления,
			|	СУММА(ИзмененияДвижений.КОбеспечению) КАК КОбеспечению,
			|	СУММА(ИзмененияДвижений.Поступило) КАК Поступило,
			|	СУММА(ИзмененияДвижений.ПоступитКДате) КАК ПоступитКДате,
			|	СУММА(ИзмененияДвижений.ЗакрытьГрафикПоступления) КАК ЗакрытьГрафикПоступления
			|ПОМЕСТИТЬ ОбеспечениеРезультатыКонтроляДвиженияСгруппированные
			|ИЗ
			|	ДвиженияРаспределениеЗапасовДвиженияИзменение КАК ИзмененияДвижений
			|СГРУППИРОВАТЬ ПО
			|	ИзмененияДвижений.Номенклатура,
			|	ИзмененияДвижений.Характеристика,
			|	ИзмененияДвижений.Склад,
			|	ИзмененияДвижений.Назначение
			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура, Характеристика, Склад, Назначение";
		ТекстыЗапроса.Добавить(ТекстЗапроса, "ОбеспечениеРезультатыКонтроляДвиженияСгруппированные");
		
		ТекстЗапроса =
			"ВЫБРАТЬ
			|	ИзмененияДвижений.Номенклатура                  КАК Номенклатура,
			|	ИзмененияДвижений.Характеристика                КАК Характеристика,
			|	ИзмененияДвижений.Склад                         КАК Склад,
			|	ИзмененияДвижений.Назначение                    КАК Назначение,
			|	ИзмененияДвижений.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
			|	РаспределениеЗапасов.Свободно,
			|	РаспределениеЗапасов.Запас,
			|	РаспределениеЗапасов.Резерв,
			|	ИзмененияДвижений.Отгрузить,
			|	ИзмененияДвижений.Резервировать,
			|	ВЫБОР КОГДА ИзмененияДвижений.Отгрузить > ИзмененияДвижений.Отгрузить + ИзмененияДвижений.Резервировать ТОГДА
			|			ВЫБОР
			|				КОГДА ИзмененияДвижений.Отгрузить > -(РаспределениеЗапасов.Запас - РаспределениеЗапасов.Резерв)
			|					ТОГДА -(РаспределениеЗапасов.Запас - РаспределениеЗапасов.Резерв)
			|				ИНАЧЕ ИзмененияДвижений.Отгрузить
			|			КОНЕЦ
			|		КОГДА ИзмененияДвижений.Отгрузить + ИзмененияДвижений.Резервировать > -(РаспределениеЗапасов.Запас - РаспределениеЗапасов.Резерв) ТОГДА
			|					-(РаспределениеЗапасов.Запас - РаспределениеЗапасов.Резерв)
			|				ИНАЧЕ
			|					ИзмененияДвижений.Отгрузить + ИзмененияДвижений.Резервировать
			|		КОНЕЦ КАК КоличествоЗапас,
			|	
			|	ВЫБОР КОГДА ИзмененияДвижений.Отгрузить > ИзмененияДвижений.Отгрузить + ИзмененияДвижений.Резервировать ТОГДА
			|			ВЫБОР
			|				КОГДА ИзмененияДвижений.Отгрузить > -РаспределениеЗапасов.Свободно
			|					ТОГДА -РаспределениеЗапасов.Свободно
			|				ИНАЧЕ ИзмененияДвижений.Отгрузить
			|			КОНЕЦ
			|		
			|		КОГДА ИзмененияДвижений.Отгрузить + ИзмененияДвижений.Резервировать > -РаспределениеЗапасов.Свободно ТОГДА
			|					-РаспределениеЗапасов.Свободно
			|				ИНАЧЕ
			|					ИзмененияДвижений.Отгрузить + ИзмененияДвижений.Резервировать
			|		КОНЕЦ КАК КоличествоСвободно
			|ИЗ
			|	
			|	ОбеспечениеРезультатыКонтроляДвиженияСгруппированные КАК ИзмененияДвижений
			|		
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОбеспечения КАК НастройкаХарактеристика
			|		ПО НастройкаХарактеристика.Склад          = ИзмененияДвижений.Склад
			|		 И НастройкаХарактеристика.Номенклатура   = ИзмененияДвижений.Номенклатура
			|		 И НастройкаХарактеристика.Характеристика = ИзмененияДвижений.Характеристика
			|		
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОбеспечения КАК НастройкаНоменклатура
			|		ПО НастройкаНоменклатура.Склад        = ИзмененияДвижений.Склад
			|		 И НастройкаНоменклатура.Номенклатура = ИзмененияДвижений.Номенклатура
			|		 И НастройкаНоменклатура.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
			|		 И НастройкаХарактеристика.Склад ЕСТЬ NULL
			|		
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОбеспечения КАК НастройкаСклад
			|		ПО НастройкаСклад.Склад          = ИзмененияДвижений.Склад
			|		 И НастройкаСклад.Номенклатура   = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
			|		 И НастройкаСклад.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
			|		 И НастройкаХарактеристика.Склад ЕСТЬ NULL
			|		 И НастройкаНоменклатура.Склад ЕСТЬ NULL
			|		
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаспределениеЗапасов КАК РаспределениеЗапасов
			|		ПО РаспределениеЗапасов.Номенклатура         = ИзмененияДвижений.Номенклатура
			|		 И РаспределениеЗапасов.Характеристика       = ИзмененияДвижений.Характеристика
			|		 И РаспределениеЗапасов.Склад                = ИзмененияДвижений.Склад
			|		 И РаспределениеЗапасов.Назначение           = ИзмененияДвижений.Назначение
			|		 И РаспределениеЗапасов.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе)
			|ГДЕ
			|	(ИзмененияДвижений.Отгрузить > 0 ИЛИ (ИзмененияДвижений.Отгрузить + ИзмененияДвижений.Резервировать) > 0)
			|		И ЕСТЬNULL(НастройкаХарактеристика.КонтролироватьСвободныеОстатки,
			|			ЕСТЬNULL(НастройкаНоменклатура.КонтролироватьСвободныеОстатки,
			|				НастройкаСклад.КонтролироватьСвободныеОстатки))
			|		И ИзмененияДвижений.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
			|		И РаспределениеЗапасов.Свободно < 0";
		ТекстыЗапроса.Добавить(ТекстЗапроса, "ОбеспечениеРезультатыКонтроля");
		
		ТипДокумента = ТипЗнч(Документ.Ссылка);
		Если ТипДокумента = Тип("ДокументСсылка.ВозвратТоваровОтКлиента")
				Или ТипДокумента = Тип("ДокументСсылка.КорректировкаРеализации")
				
				Или ТипДокумента = Тип("ДокументСсылка.ПоступлениеТоваровОтХранителя") Тогда
					
					Возврат;
		КонецЕсли;
		
		Если Не Запрос.Параметры.Свойство("МерныеТипыЕдиницИзмерений") Тогда
			Запрос.УстановитьПараметр("МерныеТипыЕдиницИзмерений",
				Справочники.УпаковкиЕдиницыИзмерения.МерныеТипыЕдиницИзмерений());
		КонецЕсли;
		
		ТекстЗапроса =
			"ВЫБРАТЬ
			|	ИзмененияДвижений.Номенклатура                  КАК Номенклатура,
			|	ИзмененияДвижений.Характеристика                КАК Характеристика,
			|	ИзмененияДвижений.Склад                         КАК Склад,
			|	ИзмененияДвижений.Назначение                    КАК Назначение,
			|	ИзмененияДвижений.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
			|	ВЫБОР
			|		КОГДА МАКСИМУМ(ИзмененияДвижений.Поступило) > 0
			|				И СУММА(
			|					ВЫБОР
			|						КОГДА РаспределениеЗапасов.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе)
			|							ТОГДА РаспределениеЗапасов.Запас
			|						ИНАЧЕ 0
			|					КОНЕЦ
			|						- РаспределениеЗапасов.РезервироватьПоМереПоступления
			|						- РаспределениеЗапасов.КОбеспечениюБезРезерва
			|						- РаспределениеЗапасов.ОтложитьРезервирование
			|						- РаспределениеЗапасов.Резервировать)
			|					> СУММА(
			|						ВЫБОР
			|							КОГДА НЕ ИзмененияДвижений.Номенклатура.ЕдиницаИзмерения.ТипИзмеряемойВеличины
			|										В (&МерныеТипыЕдиницИзмерений)
			|								ТОГДА 0
			|							ИНАЧЕ
			|								РаспределениеЗапасов.РезервироватьПоМереПоступления
			|								+ РаспределениеЗапасов.КОбеспечениюБезРезерва
			|								+ РаспределениеЗапасов.ОтложитьРезервирование
			|								+ РаспределениеЗапасов.Резервировать
			|						КОНЕЦ
			|						* ДопустимоеОтклонение.Значение / 100)
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ
			|			ЛОЖЬ
			|		КОНЕЦ КАК ОшибкаНаличиеБольшеПотребности,
			|	СУММА(
			|		ВЫБОР
			|			КОГДА РаспределениеЗапасов.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе)
			|				ТОГДА РаспределениеЗапасов.Запас
			|			ИНАЧЕ 0
			|		КОНЕЦ
			|			- РаспределениеЗапасов.РезервироватьПоМереПоступления
			|			- РаспределениеЗапасов.КОбеспечениюБезРезерва
			|			- РаспределениеЗапасов.ОтложитьРезервирование
			|			- РаспределениеЗапасов.Резервировать) КАК ВеличинаНаличиеБольшеПотребности,
			|	СУММА(
			|		РаспределениеЗапасов.Запас
			|			- РаспределениеЗапасов.РезервироватьПоМереПоступления
			|			- РаспределениеЗапасов.КОбеспечениюБезРезерва
			|			- РаспределениеЗапасов.ОтложитьРезервирование
			|			- РаспределениеЗапасов.Резервировать) КАК ВеличинаЗаказатьМеньшеНуля
			|ИЗ
			|	ОбеспечениеРезультатыКонтроляДвиженияСгруппированные КАК ИзмененияДвижений
			|		
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОбеспечения КАК НастройкаХарактеристика
			|		ПО НастройкаХарактеристика.Склад          = ИзмененияДвижений.Склад
			|		 И НастройкаХарактеристика.Номенклатура   = ИзмененияДвижений.Номенклатура
			|		 И НастройкаХарактеристика.Характеристика = ИзмененияДвижений.Характеристика
			|		
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОбеспечения КАК НастройкаНоменклатура
			|		ПО НастройкаНоменклатура.Склад        = ИзмененияДвижений.Склад
			|		 И НастройкаНоменклатура.Номенклатура = ИзмененияДвижений.Номенклатура
			|		 И НастройкаНоменклатура.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
			|		 И НастройкаХарактеристика.Склад ЕСТЬ NULL
			|		
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОбеспечения КАК НастройкаСклад
			|		ПО НастройкаСклад.Склад          = ИзмененияДвижений.Склад
			|		 И НастройкаСклад.Номенклатура   = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
			|		 И НастройкаСклад.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
			|		 И НастройкаХарактеристика.Склад ЕСТЬ NULL
			|		 И НастройкаНоменклатура.Склад ЕСТЬ NULL
			|		
			|		
			|		ЛЕВОЕ СОЕДИНЕНИЕ Константа.ДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров КАК ДопустимоеОтклонение
			|		ПО ИСТИНА
			|		
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаспределениеЗапасов КАК РаспределениеЗапасов
			|		ПО РаспределениеЗапасов.Номенклатура = ИзмененияДвижений.Номенклатура
			|		И РаспределениеЗапасов.Характеристика = ИзмененияДвижений.Характеристика
			|		И РаспределениеЗапасов.Склад = ИзмененияДвижений.Склад
			|		И РаспределениеЗапасов.Назначение = ИзмененияДвижений.Назначение
			|		И РаспределениеЗапасов.Состояние В(
			|						ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе),
			|						ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемоеПоступление),
			|						ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемаяОтгрузка))
			|ГДЕ
			|	ИзмененияДвижений.КонтролироватьПревышениеОбособленногоОбеспечения
			|		И (ИзмененияДвижений.Поступило > 0
			|			ИЛИ ИзмененияДвижений.ПоступитКДате > 0
			|			ИЛИ ИзмененияДвижений.Отгрузить
			|					+ ИзмененияДвижений.Резервировать
			|					+ ИзмененияДвижений.РезервироватьПоМереПоступления
			|					+ ИзмененияДвижений.КОбеспечению < 0)
			|		И (ЕСТЬNULL(НастройкаХарактеристика.КонтролироватьСвободныеОстатки,
			|				ЕСТЬNULL(НастройкаНоменклатура.КонтролироватьСвободныеОстатки,
			|					НастройкаСклад.КонтролироватьСвободныеОстатки))
			|			ИЛИ ИзмененияДвижений.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа))
			|СГРУППИРОВАТЬ ПО
			|	ИзмененияДвижений.Номенклатура,
			|	ИзмененияДвижений.Характеристика,
			|	ИзмененияДвижений.Склад,
			|	ИзмененияДвижений.Назначение
			|ИМЕЮЩИЕ
			|	СУММА(
			|		РаспределениеЗапасов.Запас
			|			- РаспределениеЗапасов.РезервироватьПоМереПоступления
			|			- РаспределениеЗапасов.КОбеспечениюБезРезерва
			|			- РаспределениеЗапасов.ОтложитьРезервирование
			|			- РаспределениеЗапасов.Резервировать)
			|		> СУММА(
			|			ВЫБОР
			|				КОГДА НЕ ИзмененияДвижений.Номенклатура.ЕдиницаИзмерения.ТипИзмеряемойВеличины
			|							В (&МерныеТипыЕдиницИзмерений)
			|					ТОГДА 0
			|				ИНАЧЕ
			|					РаспределениеЗапасов.РезервироватьПоМереПоступления
			|						+ РаспределениеЗапасов.КОбеспечениюБезРезерва
			|						+ РаспределениеЗапасов.ОтложитьРезервирование
			|						+ РаспределениеЗапасов.Резервировать
			|			КОНЕЦ
			|			* ДопустимоеОтклонение.Значение / 100)";
		ТекстыЗапроса.Добавить(ТекстЗапроса, "ОбеспечениеРезультатыКонтроляПревышенияОбособленногоОбеспечения");
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СообщитьОбОшибкахПроведения(Объект, Отказ, РезультатыКонтроля) Экспорт
	
	Для Каждого ОшибкаКонтроля Из РезультатыКонтроля Цикл
		
		ТекстНазначениеХарактеристика    = "";
		ТекстНазначение                  = "";
		ТекстБезНазначенияХарактеристика = "";
		ТекстБезНазначения               = "";
		Количество = 0;
		ЕдиницаИзмерения = ОшибкаКонтроля.ЕдиницаИзмерения;
		
		ТекстНазначениеХарактеристика = НСтр("ru = 'Для отгрузки/резервирования товара ""%1"",""%2"" на складе ""%3"" по назначению ""%4"" недостаточно свободного остатка в количестве %5 %6.'");
		ТекстНазначение = НСтр("ru = 'Для отгрузки/резервирования обособленного товара ""%1"" на складе ""%2"" по назначению ""%3"" недостаточно свободного остатка в количестве %4 %5'");
		ТекстБезНазначенияХарактеристика = НСтр("ru = 'Для отгрузки/резервирования необособленного товара ""%1"",""%2"" на складе ""%3"" недостаточно свободного остатка в количестве %4 %5.'");
		ТекстБезНазначения = НСтр("ru = 'Для отгрузки/резервирования необособленного товара ""%1"" на складе ""%2"" недостаточно свободного остатка в количестве %3 %4'");
		Количество = ОшибкаКонтроля.КоличествоСвободно;
		
		Если ЗначениеЗаполнено(ОшибкаКонтроля.Назначение) Тогда
			
			Если ЗначениеЗаполнено(ОшибкаКонтроля.Характеристика) Тогда
				ТекстСообщения = СтрШаблон(ТекстНазначениеХарактеристика,
					ОшибкаКонтроля.Номенклатура,
					ОшибкаКонтроля.Характеристика,
					ОшибкаКонтроля.Склад,
					ОшибкаКонтроля.Назначение,
					Количество,
					ЕдиницаИзмерения);
			Иначе
				ТекстСообщения = СтрШаблон(ТекстНазначение,
					ОшибкаКонтроля.Номенклатура,
					ОшибкаКонтроля.Склад,
					ОшибкаКонтроля.Назначение,
					Количество,
					ЕдиницаИзмерения);
			КонецЕсли;
			
		Иначе
			
			Если ЗначениеЗаполнено(ОшибкаКонтроля.Характеристика) Тогда
				ТекстСообщения = СтрШаблон(ТекстБезНазначенияХарактеристика,
					ОшибкаКонтроля.Номенклатура,
					ОшибкаКонтроля.Характеристика,
					ОшибкаКонтроля.Склад,
					Количество,
					ЕдиницаИзмерения);
			Иначе
				ТекстСообщения = СтрШаблон(ТекстБезНазначения,
					ОшибкаКонтроля.Номенклатура,
					ОшибкаКонтроля.Склад,
					Количество,
					ЕдиницаИзмерения);
			КонецЕсли;
			
		КонецЕсли;
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект, Неопределено, Неопределено, Отказ);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СообщитьОбОшибкахПроведенияПревышениеОбособленногоОбеспечения(Объект, Отказ, РезультатыКонтроля) Экспорт
	
	Для Каждого ОшибкаКонтроля Из РезультатыКонтроля Цикл
		
		ТекстНазначениеХарактеристика = "";
		ТекстНазначение = "";
		ТекстБезНазначенияХарактеристика = "";
		ТекстБезНазначения = "";
		Количество = 0;
		ЕдиницаИзмерения = ОшибкаКонтроля.ЕдиницаИзмерения;
		
		Если ОшибкаКонтроля.ОшибкаНаличиеБольшеПотребности Тогда
			Количество = ОшибкаКонтроля.ВеличинаНаличиеБольшеПотребности;
			Если ТипЗнч(ОшибкаКонтроля.Склад) = Тип("СправочникСсылка.Склады") Тогда
				ТекстНазначениеХарактеристика = НСтр("ru = 'Номенклатура: %1, Характеристика: %2, Склад: %3, Назначение: %4. 
					|Приходуется для обеспечения больше чем требуется на %5 %6.'");
				ТекстНазначение = НСтр("ru = 'Номенклатура: %1, Склад: %2, Назначение: %3. 
					|Приходуется для обеспечения больше чем требуется на %4 %5.'");
			Иначе
				ТекстНазначениеХарактеристика = НСтр("ru = 'Номенклатура: %1, Характеристика: %2, Подразделение: %3, Назначение: %4. 
					|Приходуется для обеспечения больше чем требуется на %5 %6.'");
				ТекстНазначение = НСтр("ru = 'Номенклатура: %1, Подразделение: %2, Назначение: %3. 
					|Приходуется для обеспечения больше чем требуется на %4 %5.'");
			КонецЕсли;
		Иначе
			Количество = ОшибкаКонтроля.ВеличинаЗаказатьМеньшеНуля;
			Если ТипЗнч(ОшибкаКонтроля.Склад) = Тип("СправочникСсылка.Склады") Тогда
				ТекстНазначениеХарактеристика = НСтр("ru = 'Номенклатура: %1, Характеристика: %2, Склад: %3, Назначение: %4. 
					|Заказано для обеспечения больше чем требуется на %5 %6.'");
				ТекстНазначение = НСтр("ru = 'Номенклатура: %1, Склад: %2, Назначение: %3. 
					|Заказано для обеспечения больше чем требуется на %4 %5.'");
			Иначе
				ТекстНазначениеХарактеристика = НСтр("ru = 'Номенклатура: %1, Характеристика: %2, Подразделение: %3, Назначение: %4. 
					|Заказано для обеспечения больше чем требуется на %5 %6.'");
				ТекстНазначение = НСтр("ru = 'Номенклатура: %1, Подразделение: %2, Назначение: %3. 
					|Заказано для обеспечения больше чем требуется на %4 %5.'");
			КонецЕсли;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ОшибкаКонтроля.Характеристика) Тогда
			ТекстСообщения = СтрШаблон(ТекстНазначениеХарактеристика,
				ОшибкаКонтроля.Номенклатура,
				ОшибкаКонтроля.Характеристика,
				ОшибкаКонтроля.Склад,
				ОшибкаКонтроля.Назначение,
				Количество,
				ЕдиницаИзмерения);
		Иначе
			ТекстСообщения = СтрШаблон(ТекстНазначение,
				ОшибкаКонтроля.Номенклатура,
				ОшибкаКонтроля.Склад,
				ОшибкаКонтроля.Назначение,
				Количество,
				ЕдиницаИзмерения);
		КонецЕсли;
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Объект, Неопределено, Неопределено, Отказ);
		
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////////////////////////////
// Обслуживание регистров состояний заказов.

Процедура ДобавитьВременныеТаблицыАктуализацииСостояний(Запрос, ФильтрПереопределяемый, Постфикс) Экспорт
	
	ИменаРегистров = Новый Массив();
	ИменаРегистров.Добавить("РегистрСведений.СостоянияЗаказовКлиентов");
	ИменаРегистров.Добавить("РегистрСведений.СостоянияВнутреннихЗаказов");
	
	Тексты = Новый Массив();
	
	Для Каждого ИмяРегистра Из ИменаРегистров Цикл
		
		ИмяРегистра = СтрЗаменить(ИмяРегистра, "РегистрСведений.", "");
		Параметры = РегистрыСведений[ИмяРегистра].ПараметрыАктуализацииСостоянийЗаказов();
		Текст = Параметры.ПравилоОтбораЗаписей;
		Текст = СтрЗаменить(Текст, "ФильтрПереопределяемый",  ФильтрПереопределяемый);
		Текст = СтрЗаменить(Текст, "ТаблицаПереопределяемый", ИмяРегистра + Постфикс);
		Тексты.Добавить(Текст);
		
	КонецЦикла;
	
	Запрос.Текст = СтрСоединить(Тексты, ОбщегоНазначения.РазделительПакетаЗапросов());
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ТаблицаИзмененийРаспределениеЗапасовДляРасчетаСостояний(Запрос) Экспорт
	
	ИменаРегистров = Новый Массив();
	ИменаРегистров.Добавить("РегистрСведений.СостоянияЗаказовКлиентов");
	ИменаРегистров.Добавить("РегистрСведений.СостоянияВнутреннихЗаказов");
	
	Тексты = Новый Массив();
	
	Для Каждого ИмяРегистра Из ИменаРегистров Цикл
		
		ИмяРегистра = СтрЗаменить(ИмяРегистра, "РегистрСведений.", "");
		Параметры = РегистрыСведений[ИмяРегистра].ПараметрыАктуализацииСостоянийЗаказов();
		Текст = Параметры.ФункцияСравненияЗаписейВоВременнуюТаблицу;
		Текст = СтрЗаменить(Текст, "ПередЗаписьюПереопределяемый", ИмяРегистра + "ПередЗаписью");
		Текст = СтрЗаменить(Текст, "ПриЗаписиПереопределяемый",    ИмяРегистра + "ПриЗаписи");
		Текст = СтрЗаменить(Текст, "ИзменениеПереопределяемый",    ИмяРегистра + "Изменение");
		Тексты.Добавить(Текст);
		
	КонецЦикла;
	
	Для Каждого ИмяРегистра Из ИменаРегистров Цикл
		
		ИмяРегистра = СтрЗаменить(ИмяРегистра, "РегистрСведений.", "");
		
		Текст = "УНИЧТОЖИТЬ " + ИмяРегистра + "ПередЗаписью";
		Тексты.Добавить(Текст);
		
		Текст = "УНИЧТОЖИТЬ " + ИмяРегистра + "ПриЗаписи";
		Тексты.Добавить(Текст);
		
	КонецЦикла;
	
	Запрос.Текст = СтрСоединить(Тексты, ОбщегоНазначения.РазделительПакетаЗапросов());
	Запрос.ВыполнитьПакет();
	
КонецПроцедуры

Процедура ИсключитьСсылкиТребующиеОтложенногоОбновления(Ссылки) Экспорт
	
	Если ОбновлениеИнформационнойБазы.ОтложенноеОбновлениеЗавершено() Тогда
		Возврат;
	КонецЕсли;
	
	НаборСостоянияЗаказовКлиентов = РегистрыСведений.СостоянияЗаказовКлиентов.СоздатьНаборЗаписей();
	НаборСостоянияВнутреннихЗаказов = РегистрыСведений.СостоянияВнутреннихЗаказов.СоздатьНаборЗаписей();
	НаборСостоянияЗаказовКлиентов.Отбор.Заказ.Использование = Истина;
	НаборСостоянияВнутреннихЗаказов.Отбор.Заказ.Использование = Истина;
	
	ВсегоЭлементов = Ссылки.Количество();
	Для Счетчик = 1 По ВсегоЭлементов Цикл
		Индекс = ВсегоЭлементов - Счетчик;
		Ссылка = Ссылки[Индекс];
		
		Набор = Неопределено;
		Если НаборСостоянияЗаказовКлиентов.Отбор.Заказ.ТипЗначения.СодержитТип(ТипЗнч(Ссылка)) Тогда
			Набор = НаборСостоянияЗаказовКлиентов;
			Набор.Отбор.Заказ.Значение = Ссылка;
		ИначеЕсли НаборСостоянияВнутреннихЗаказов.Отбор.Заказ.ТипЗначения.СодержитТип(ТипЗнч(Ссылка)) Тогда
			Набор = НаборСостоянияВнутреннихЗаказов;
			Набор.Отбор.Заказ.Значение = Ссылка;
		КонецЕсли;
		
		Если Набор <> Неопределено Тогда
			РезультатПроверки = ОбновлениеИнформационнойБазы.ОбъектОбработан(Набор);
			Если Не РезультатПроверки.Обработан Тогда
				Ссылки.Удалить(Индекс);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ДосчитыватьРегистрРегламентнымЗаданием() Экспорт
	Возврат Не ОбщегоНазначения.ИнформационнаяБазаФайловая()
		И Не (РаботаВМоделиСервиса.РазделениеВключено() И РаботаВМоделиСервиса.ДоступноИспользованиеРазделенныхДанных())
		Или БлокироватьРаспределениеЗапасов();
КонецФункции

Функция БлокироватьРаспределениеЗапасов() Экспорт
	ПараметрЗапускаПриложения = ПараметрыСеанса.ПараметрыКлиентаНаСервере.Получить("ПараметрЗапуска");
	Возврат СтрНайти(ПараметрЗапускаПриложения, "БлокироватьРаспределениеЗапасов") > 0;
КонецФункции

Функция ОшибкиПроверкиКорректностиЗаписейРегистраСведенийРаспределениеЗапасов(ПроверятьПроведение, ПроверятьРаспределение, Товар) Экспорт
	
	Тексты = Новый Массив();
	
	Если Товар = Неопределено Тогда
		
		Текст =
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	Таблица.Номенклатура КАК Номенклатура,
			|	Таблица.Характеристика КАК Характеристика,
			|	Таблица.Склад КАК Склад,
			|	Таблица.Назначение КАК Назначение
			|ПОМЕСТИТЬ ТаблицаТовары
			|ИЗ
			|	РегистрНакопления.РаспределениеЗапасовДвижения КАК Таблица
			|ГДЕ
			|	Таблица.Активность
			|
			|ОБЪЕДИНИТЬ
			|
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	Таблица.Номенклатура КАК Номенклатура,
			|	Таблица.Характеристика КАК Характеристика,
			|	Таблица.Склад КАК Склад,
			|	Таблица.Назначение КАК Назначение
			|ИЗ
			|	РегистрСведений.РаспределениеЗапасов КАК Таблица
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Номенклатура, Характеристика, Склад, Назначение";
		
	Иначе
		
		Текст =
			"ВЫБРАТЬ
			|	&Номенклатура КАК Номенклатура,
			|	&Характеристика КАК Характеристика,
			|	&Склад КАК Склад,
			|	&Назначение КАК Назначение
			|ПОМЕСТИТЬ
			|	ТаблицаТовары";
		
	КонецЕсли;
	
	Тексты.Добавить(Текст);
	
	Если ПроверятьПроведение Тогда
		
		Текст =
			"ВЫБРАТЬ
			|	Набор.Номенклатура КАК Номенклатура,
			|	Набор.Характеристика КАК Характеристика,
			|	Набор.Склад КАК Склад,
			|	Набор.Назначение КАК Назначение,
			|	НЕОПРЕДЕЛЕНО КАК ЗаказНаПоступление,
			|	ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе) КАК Состояние,
			|	СУММА(Набор.ЗапасДвижения) КАК ЗапасДвижения,
			|	СУММА(Набор.ЗапасСведения) КАК ЗапасСведения
			|ИЗ(
			|	ВЫБРАТЬ
			|		Таблица.Номенклатура КАК Номенклатура,
			|		Таблица.Характеристика КАК Характеристика,
			|		Таблица.Склад КАК Склад,
			|		Таблица.Назначение КАК Назначение,
			|		Таблица.Поступило - Таблица.Отгрузить КАК ЗапасДвижения,
			|		0 КАК ЗапасСведения
			|	ИЗ
			|		РегистрНакопления.РаспределениеЗапасовДвижения КАК Таблица
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТовары КАК Фильтр
			|			ПО Фильтр.Номенклатура = Таблица.Номенклатура
			|			 И Фильтр.Характеристика = Таблица.Характеристика
			|			 И Фильтр.Склад = Таблица.Склад
			|			 И Фильтр.Назначение = Таблица.Назначение
			|	ГДЕ
			|		Таблица.Активность
			|			И (Таблица.Поступило <> 0 ИЛИ Таблица.Отгрузить <> 0)
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		Таблица.Номенклатура КАК Номенклатура,
			|		Таблица.Характеристика КАК Характеристика,
			|		Таблица.Склад КАК Склад,
			|		Таблица.Назначение КАК Назначение,
			|		0 КАК ЗапасДвижения,
			|		Таблица.Запас КАК ЗапасСведения
			|	ИЗ
			|		РегистрСведений.РаспределениеЗапасов КАК Таблица
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТовары КАК Фильтр
			|			ПО Фильтр.Номенклатура = Таблица.Номенклатура
			|			 И Фильтр.Характеристика = Таблица.Характеристика
			|			 И Фильтр.Склад = Таблица.Склад
			|			 И Фильтр.Назначение = Таблица.Назначение
			|	ГДЕ
			|		Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе)) КАК Набор
			|	СГРУППИРОВАТЬ ПО
			|		Набор.Номенклатура,
			|		Набор.Характеристика,
			|		Набор.Склад,
			|		Набор.Назначение
			|ИМЕЮЩИЕ
			|	СУММА(Набор.ЗапасДвижения) <> СУММА(Набор.ЗапасСведения)
			|;
			|
			|/////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Набор.Номенклатура КАК Номенклатура,
			|	Набор.Характеристика КАК Характеристика,
			|	Набор.Склад КАК Склад,
			|	Набор.Назначение КАК Назначение,
			|	Набор.ЗаказНаПоступление КАК ЗаказНаПоступление,
			|	ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемоеПоступление) КАК Состояние,
			|	ВЫБОР КОГДА СУММА(Набор.ЗапасДвижения) < 0 ТОГДА 0 ИНАЧЕ СУММА(Набор.ЗапасДвижения) КОНЕЦ КАК ЗапасДвижения,
			|	СУММА(Набор.ЗапасСведения) КАК ЗапасСведения
			|ИЗ(
			|	ВЫБРАТЬ
			|		Таблица.Номенклатура КАК Номенклатура,
			|		Таблица.Характеристика КАК Характеристика,
			|		Таблица.Склад КАК Склад,
			|		Таблица.Назначение КАК Назначение,
			|		Таблица.ЗаказНаПоступление КАК ЗаказНаПоступление,
			|		Таблица.ПоступитКДате + Таблица.ПоставкаНаСогласовании - Таблица.ЗакрытьГрафикПоступления КАК ЗапасДвижения,
			|		0 КАК ЗапасСведения
			|	ИЗ
			|		РегистрНакопления.РаспределениеЗапасовДвижения КАК Таблица
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТовары КАК Фильтр
			|			ПО Фильтр.Номенклатура = Таблица.Номенклатура
			|			 И Фильтр.Характеристика = Таблица.Характеристика
			|			 И Фильтр.Склад = Таблица.Склад
			|			 И Фильтр.Назначение = Таблица.Назначение
			|	ГДЕ
			|		Таблица.Активность
			|			И (Таблица.ПоступитКДате + Таблица.ПоставкаНаСогласовании <> 0 ИЛИ Таблица.ЗакрытьГрафикПоступления <> 0)
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		Таблица.Номенклатура КАК Номенклатура,
			|		Таблица.Характеристика КАК Характеристика,
			|		Таблица.Склад КАК Склад,
			|		Таблица.Назначение КАК Назначение,
			|		Таблица.ЗаказНаПоступление КАК ЗаказНаПоступление,
			|		0 КАК ЗапасДвижения,
			|		Таблица.Запас КАК ЗапасСведения
			|	ИЗ
			|		РегистрСведений.РаспределениеЗапасов КАК Таблица
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТовары КАК Фильтр
			|			ПО Фильтр.Номенклатура = Таблица.Номенклатура
			|			 И Фильтр.Характеристика = Таблица.Характеристика
			|			 И Фильтр.Склад = Таблица.Склад
			|			 И Фильтр.Назначение = Таблица.Назначение
			|	ГДЕ
			|		Таблица.Состояние В(
			|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемоеПоступление),
			|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ПоступлениеНеПодтвержденное))) КАК Набор
			|	СГРУППИРОВАТЬ ПО
			|		Набор.Номенклатура,
			|		Набор.Характеристика,
			|		Набор.Склад,
			|		Набор.Назначение,
			|		Набор.ЗаказНаПоступление
			|	ИМЕЮЩИЕ
			|		ВЫБОР КОГДА СУММА(Набор.ЗапасДвижения) < 0 ТОГДА 0 ИНАЧЕ СУММА(Набор.ЗапасДвижения) КОНЕЦ <> СУММА(Набор.ЗапасСведения)
			|;
			|/////////////////////////////////////////////////////////////
			|
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	Набор.Номенклатура КАК Номенклатура,
			|	Набор.Характеристика КАК Характеристика,
			|	Набор.Склад КАК Склад,
			|	Набор.Назначение КАК Назначение,
			|	Набор.ЗаказНаПоступление КАК ЗаказНаПоступление,
			|	&ТекстНесоответствиеДат КАК Состояние,
			|	0 КАК ЗапасДвижения,
			|	0 КАК ЗапасСведения
			|ИЗ(
			|	ВЫБРАТЬ
			|		Таблица.Номенклатура КАК Номенклатура,
			|		Таблица.Характеристика КАК Характеристика,
			|		Таблица.Склад КАК Склад,
			|		Таблица.Назначение КАК Назначение,
			|		Таблица.ЗаказНаПоступление КАК ЗаказНаПоступление,
			|		СУММА(ЕСТЬNULL(Движения.ПоступитКДате + Движения.ПоставкаНаСогласовании, 0)) КАК ЗапасДвижения,
			|		МАКСИМУМ(Таблица.Запас) КАК ЗапасСведения
			|	ИЗ
			|		РегистрСведений.РаспределениеЗапасов КАК Таблица
			|			
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТовары КАК Фильтр
			|			ПО Фильтр.Номенклатура = Таблица.Номенклатура
			|			 И Фильтр.Характеристика = Таблица.Характеристика
			|			 И Фильтр.Склад = Таблица.Склад
			|			 И Фильтр.Назначение = Таблица.Назначение
			|			
			|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РаспределениеЗапасовДвижения КАК Движения
			|			ПО Движения.Активность
			|			 И Движения.Номенклатура = Таблица.Номенклатура
			|			 И Движения.Характеристика = Таблица.Характеристика
			|			 И Движения.Склад = Таблица.Склад
			|			 И Движения.Назначение = Таблица.Назначение
			|			 И Движения.ЗаказНаПоступление = Таблица.ЗаказНаПоступление
			|			 И Движения.ДатаПоступления = Таблица.ДатаПоступления
			|			 И Движения.Назначение = Таблица.Назначение
			|	ГДЕ
			|		Таблица.Состояние В(
			|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ПоступлениеНеПодтвержденное),
			|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемоеПоступление))
			|СГРУППИРОВАТЬ ПО
			|	Таблица.Номенклатура,
			|	Таблица.Характеристика,
			|	Таблица.Склад,
			|	Таблица.Назначение,
			|	Таблица.ЗаказНаПоступление,
			|	Таблица.ДатаПоступления
			|ИМЕЮЩИЕ
			|	СУММА(ЕСТЬNULL(Движения.ПоступитКДате + Движения.ПоставкаНаСогласовании, 0)) < МАКСИМУМ(Таблица.Запас)) КАК Набор
			|;
			|/////////////////////////////////////////////////////////////
			|
			|ВЫБРАТЬ
			|	Набор.Номенклатура КАК Номенклатура,
			|	Набор.Характеристика КАК Характеристика,
			|	Набор.Склад КАК Склад,
			|	Набор.Назначение КАК Назначение,
			|	Набор.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
			|	Набор.ЖелаемаяДатаОтгрузки КАК ЖелаемаяДатаОтгрузки,
			|	ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемаяОтгрузка) КАК Состояние,
			|	СУММА(Набор.РезервироватьДвижения) КАК РезервироватьДвижения,
			|	СУММА(Набор.НеОбеспечиватьДвижения) КАК НеОбеспечиватьДвижения,
			|	СУММА(Набор.ЗакрытьГрафикОтгрузкиДвижения) КАК ЗакрытьГрафикОтгрузкиДвижения,
			|	СУММА(Набор.КОбеспечениюДвижения) КАК КОбеспечениюДвижения,
			|	СУММА(Набор.РезервироватьПоМереПоступленияДвижения) КАК РезервироватьПоМереПоступленияДвижения,
			|	СУММА(Набор.РезервироватьСведения) КАК РезервироватьСведения,
			|	СУММА(Набор.НеОбеспечиватьСведения) КАК НеОбеспечиватьСведения,
			|	СУММА(Набор.РезервироватьПоМереПоступленияСведения) КАК РезервироватьПоМереПоступленияСведения,
			|	СУММА(Набор.КОбеспечениюСведения) КАК КОбеспечениюСведения
			|ИЗ(
			|	ВЫБРАТЬ
			|		Таблица.Номенклатура КАК Номенклатура,
			|		Таблица.Характеристика КАК Характеристика,
			|		Таблица.Склад КАК Склад,
			|		Таблица.Назначение КАК Назначение,
			|		Таблица.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
			|		Таблица.ЖелаемаяДатаОтгрузки КАК ЖелаемаяДатаОтгрузки,
			|		Таблица.Резервировать КАК РезервироватьДвижения,
			|		Таблица.НеОбеспечивать КАК НеОбеспечиватьДвижения,
			|		Таблица.ЗакрытьГрафикОтгрузки КАК ЗакрытьГрафикОтгрузкиДвижения,
			|		Таблица.РезервироватьПоМереПоступления КАК РезервироватьПоМереПоступленияДвижения,
			|		Таблица.КОбеспечениюБезРезерва КАК КОбеспечениюДвижения,
			|		0 КАК РезервироватьСведения,
			|		0 КАК НеОбеспечиватьСведения,
			|		0 КАК РезервироватьПоМереПоступленияСведения,
			|		0 КАК КОбеспечениюСведения
			|	ИЗ
			|		РегистрНакопления.РаспределениеЗапасовДвижения КАК Таблица
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТовары КАК Фильтр
			|			ПО Фильтр.Номенклатура = Таблица.Номенклатура
			|			 И Фильтр.Характеристика = Таблица.Характеристика
			|			 И Фильтр.Склад = Таблица.Склад
			|			 И Фильтр.Назначение = Таблица.Назначение
			|	ГДЕ
			|		Таблица.Активность
			|			И (Таблица.Резервировать <> 0 ИЛИ Таблица.РезервироватьПоМереПоступления <> 0 ИЛИ Таблица.КОбеспечениюБезРезерва <> 0 ИЛИ Таблица.НеОбеспечивать <> 0 ИЛИ Таблица.ЗакрытьГрафикОтгрузки <> 0)
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		Таблица.Номенклатура КАК Номенклатура,
			|		Таблица.Характеристика КАК Характеристика,
			|		Таблица.Склад КАК Склад,
			|		Таблица.Назначение КАК Назначение,
			|		Таблица.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
			|		Таблица.ЖелаемаяДатаОтгрузки КАК ЖелаемаяДатаОтгрузки,
			|		0 КАК РезервироватьДвижения,
			|		0 КАК НеОбеспечиватьДвижения,
			|		0 КАК ЗакрытьГрафикОтгрузкиДвижения,
			|		0 КАК РезервироватьПоМереПоступленияДвижения,
			|		0 КАК КОбеспечениюДвижения,
			|		Таблица.Резервировать КАК РезервироватьСведения,
			|		Таблица.НеОбеспечивать КАК НеОбеспечиватьСведения,
			|		Таблица.РезервироватьПоМереПоступления + Таблица.ОтложитьРезервирование КАК РезервироватьПоМереПоступленияСведения,
			|		Таблица.КОбеспечениюБезРезерва КАК КОбеспечениюСведения
			|	ИЗ
			|		РегистрСведений.РаспределениеЗапасов КАК Таблица
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТовары КАК Фильтр
			|			ПО Фильтр.Номенклатура = Таблица.Номенклатура
			|			 И Фильтр.Характеристика = Таблица.Характеристика
			|			 И Фильтр.Склад = Таблица.Склад
			|			 И Фильтр.Назначение = Таблица.Назначение
			|	ГДЕ
			|		Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемаяОтгрузка)) КАК Набор
			|СГРУППИРОВАТЬ ПО
			|	Набор.Номенклатура,
			|	Набор.Характеристика,
			|	Набор.Склад,
			|	Набор.Назначение,
			|	Набор.ЗаказНаОтгрузку,
			|	Набор.ЖелаемаяДатаОтгрузки
			|ИМЕЮЩИЕ
			|	СУММА(Набор.РезервироватьДвижения) <> СУММА(Набор.РезервироватьСведения)
			|		ИЛИ ВЫБОР КОГДА СУММА(Набор.НеОбеспечиватьДвижения) < СУММА(Набор.ЗакрытьГрафикОтгрузкиДвижения) ТОГДА
			|					0
			|				ИНАЧЕ
			|					СУММА(Набор.НеОбеспечиватьДвижения) - СУММА(Набор.ЗакрытьГрафикОтгрузкиДвижения)
			|			КОНЕЦ <> СУММА(Набор.НеОбеспечиватьСведения)
			|		ИЛИ СУММА(Набор.РезервироватьПоМереПоступленияДвижения) <> СУММА(Набор.РезервироватьПоМереПоступленияСведения)
			|		ИЛИ СУММА(Набор.КОбеспечениюДвижения) <> СУММА(Набор.КОбеспечениюСведения)
			|;
			|/////////////////////////////////////////////////////////////
			|
			|ВЫБРАТЬ
			|	Набор.Номенклатура КАК Номенклатура,
			|	Набор.Характеристика КАК Характеристика,
			|	Набор.Склад КАК Склад,
			|	Набор.Назначение КАК Назначение,
			|	Набор.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
			|	Набор.ЖелаемаяДатаОтгрузки КАК ЖелаемаяДатаОтгрузки,
			|	ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ВРезерве) КАК Состояние,
			|	СУММА(Набор.РезервироватьДвижения) КАК РезервироватьДвижения,
			|	СУММА(Набор.РезервироватьСведения) КАК РезервироватьСведения
			|ИЗ(
			|	ВЫБРАТЬ
			|		Таблица.Номенклатура КАК Номенклатура,
			|		Таблица.Характеристика КАК Характеристика,
			|		Таблица.Склад КАК Склад,
			|		Таблица.Назначение КАК Назначение,
			|		Таблица.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
			|		Таблица.ЖелаемаяДатаОтгрузки КАК ЖелаемаяДатаОтгрузки,
			|		Таблица.Резервировать КАК РезервироватьДвижения,
			|		0 КАК РезервироватьСведения
			|	ИЗ
			|		РегистрНакопления.РаспределениеЗапасовДвижения КАК Таблица
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТовары КАК Фильтр
			|			ПО Фильтр.Номенклатура = Таблица.Номенклатура
			|			 И Фильтр.Характеристика = Таблица.Характеристика
			|			 И Фильтр.Склад = Таблица.Склад
			|			 И Фильтр.Назначение = Таблица.Назначение
			|	ГДЕ
			|		Таблица.Активность
			|			И Таблица.Резервировать <> 0
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		Таблица.Номенклатура КАК Номенклатура,
			|		Таблица.Характеристика КАК Характеристика,
			|		Таблица.Склад КАК Склад,
			|		Таблица.Назначение КАК Назначение,
			|		Таблица.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
			|		Таблица.ЖелаемаяДатаОтгрузки КАК ЖелаемаяДатаОтгрузки,
			|		0 КАК РезервироватьДвижения,
			|		Таблица.Зарезервировано КАК РезервироватьСведения
			|	ИЗ
			|		РегистрСведений.РаспределениеЗапасов КАК Таблица
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТовары КАК Фильтр
			|			ПО Фильтр.Номенклатура = Таблица.Номенклатура
			|			 И Фильтр.Характеристика = Таблица.Характеристика
			|			 И Фильтр.Склад = Таблица.Склад
			|			 И Фильтр.Назначение = Таблица.Назначение
			|	ГДЕ
			|		Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ВРезерве)) КАК Набор
			|СГРУППИРОВАТЬ ПО
			|	Набор.Номенклатура,
			|	Набор.Характеристика,
			|	Набор.Склад,
			|	Набор.Назначение,
			|	Набор.ЗаказНаОтгрузку,
			|	Набор.ЖелаемаяДатаОтгрузки
			|ИМЕЮЩИЕ
			|	СУММА(Набор.РезервироватьДвижения) <> СУММА(Набор.РезервироватьСведения)
			|;
			|/////////////////////////////////////////////////////////////
			|
			|ВЫБРАТЬ
			|	Набор.Номенклатура КАК Номенклатура,
			|	Набор.Характеристика КАК Характеристика,
			|	Набор.Склад КАК Склад,
			|	Набор.Назначение КАК Назначение,
			|	ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ВРезерве) КАК Состояние,
			|	СУММА(Набор.РезервироватьДвижения) КАК РезервироватьДвижения,
			|	СУММА(Набор.РезервСведения) КАК РезервСведения
			|ИЗ(
			|	ВЫБРАТЬ
			|		Таблица.Номенклатура КАК Номенклатура,
			|		Таблица.Характеристика КАК Характеристика,
			|		Таблица.Склад КАК Склад,
			|		Таблица.Назначение КАК Назначение,
			|		Таблица.Резервировать КАК РезервироватьДвижения,
			|		0 КАК РезервСведения
			|	ИЗ
			|		РегистрНакопления.РаспределениеЗапасовДвижения КАК Таблица
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТовары КАК Фильтр
			|			ПО Фильтр.Номенклатура = Таблица.Номенклатура
			|			 И Фильтр.Характеристика = Таблица.Характеристика
			|			 И Фильтр.Склад = Таблица.Склад
			|			 И Фильтр.Назначение = Таблица.Назначение
			|	ГДЕ
			|		Таблица.Активность
			|			И Таблица.Резервировать <> 0
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		Таблица.Номенклатура КАК Номенклатура,
			|		Таблица.Характеристика КАК Характеристика,
			|		Таблица.Склад КАК Склад,
			|		Таблица.Назначение КАК Назначение,
			|		0 КАК РезервироватьДвижения,
			|		Таблица.Резерв КАК РезервСведения
			|	ИЗ
			|		РегистрСведений.РаспределениеЗапасов КАК Таблица
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТовары КАК Фильтр
			|			ПО Фильтр.Номенклатура = Таблица.Номенклатура
			|			 И Фильтр.Характеристика = Таблица.Характеристика
			|			 И Фильтр.Склад = Таблица.Склад
			|			 И Фильтр.Назначение = Таблица.Назначение
			|	ГДЕ
			|		Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе)) КАК Набор
			|СГРУППИРОВАТЬ ПО
			|	Набор.Номенклатура,
			|	Набор.Характеристика,
			|	Набор.Склад,
			|	Набор.Назначение
			|ИМЕЮЩИЕ
			|	СУММА(Набор.РезервироватьДвижения) <> СУММА(Набор.РезервСведения)
			|;
			|/////////////////////////////////////////////////////////////
			|
			|ВЫБРАТЬ
			|	Набор.Номенклатура КАК Номенклатура,
			|	Набор.Характеристика КАК Характеристика,
			|	Набор.Склад КАК Склад,
			|	Набор.Назначение КАК Назначение,
			|	Набор.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
			|	Набор.ЖелаемаяДатаОтгрузки КАК ЖелаемаяДатаОтгрузки,
			|	ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.НеОбеспечивать) КАК Состояние,
			|	СУММА(Набор.НеОбеспечиватьДвижения) КАК НеОбеспечиватьДвижения,
			|	СУММА(Набор.ЗакрытьГрафикОтгрузкиДвижения) КАК ЗакрытьГрафикОтгрузкиДвижения,
			|	СУММА(Набор.НеОбеспечиватьСведения) КАК НеОбеспечиватьСведения
			|ИЗ(
			|	ВЫБРАТЬ
			|		Таблица.Номенклатура КАК Номенклатура,
			|		Таблица.Характеристика КАК Характеристика,
			|		Таблица.Склад КАК Склад,
			|		Таблица.Назначение КАК Назначение,
			|		Таблица.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
			|		Таблица.ЖелаемаяДатаОтгрузки КАК ЖелаемаяДатаОтгрузки,
			|		Таблица.НеОбеспечивать КАК НеОбеспечиватьДвижения,
			|		Таблица.ЗакрытьГрафикОтгрузки КАК ЗакрытьГрафикОтгрузкиДвижения,
			|		0 КАК НеОбеспечиватьСведения
			|	ИЗ
			|		РегистрНакопления.РаспределениеЗапасовДвижения КАК Таблица
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТовары КАК Фильтр
			|			ПО Фильтр.Номенклатура = Таблица.Номенклатура
			|			 И Фильтр.Характеристика = Таблица.Характеристика
			|			 И Фильтр.Склад = Таблица.Склад
			|			 И Фильтр.Назначение = Таблица.Назначение
			|	ГДЕ
			|		Таблица.Активность
			|			И (Таблица.НеОбеспечивать <> 0 ИЛИ Таблица.ЗакрытьГрафикОтгрузки <> 0)
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		Таблица.Номенклатура КАК Номенклатура,
			|		Таблица.Характеристика КАК Характеристика,
			|		Таблица.Склад КАК Склад,
			|		Таблица.Назначение КАК Назначение,
			|		Таблица.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
			|		Таблица.ЖелаемаяДатаОтгрузки КАК ЖелаемаяДатаОтгрузки,
			|		0 КАК НеОбеспечиватьДвижения,
			|		0 КАК ЗакрытьГрафикОтгрузкиДвижения,
			|		Таблица.НеОбеспечено КАК НеОбеспечиватьСведения
			|	ИЗ
			|		РегистрСведений.РаспределениеЗапасов КАК Таблица
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТовары КАК Фильтр
			|			ПО Фильтр.Номенклатура = Таблица.Номенклатура
			|			 И Фильтр.Характеристика = Таблица.Характеристика
			|			 И Фильтр.Склад = Таблица.Склад
			|			 И Фильтр.Назначение = Таблица.Назначение
			|	ГДЕ
			|		Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.НеОбеспечивать)) КАК Набор
			|СГРУППИРОВАТЬ ПО
			|	Набор.Номенклатура,
			|	Набор.Характеристика,
			|	Набор.Склад,
			|	Набор.Назначение,
			|	Набор.ЗаказНаОтгрузку,
			|	Набор.ЖелаемаяДатаОтгрузки
			|ИМЕЮЩИЕ
			|	ВЫБОР КОГДА СУММА(Набор.НеОбеспечиватьДвижения) < СУММА(Набор.ЗакрытьГрафикОтгрузкиДвижения) ТОГДА
			|					0
			|				ИНАЧЕ
			|					СУММА(Набор.НеОбеспечиватьДвижения) - СУММА(Набор.ЗакрытьГрафикОтгрузкиДвижения)
			|			КОНЕЦ <> СУММА(Набор.НеОбеспечиватьСведения)
			|;
			|/////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Набор.Номенклатура КАК Номенклатура,
			|	Набор.Характеристика КАК Характеристика,
			|	Набор.Склад КАК Склад,
			|	&ТекстВНаличии КАК Состояние,
			|	СУММА(Набор.ВНаличииДвижения) КАК ВНаличииДвижения,
			|	СУММА(Набор.ВНаличииСведения) КАК ВНаличииСведения
			|ИЗ(
			|	ВЫБРАТЬ
			|		Таблица.Номенклатура КАК Номенклатура,
			|		Таблица.Характеристика КАК Характеристика,
			|		Таблица.Склад КАК Склад,
			|		Таблица.Поступило - Таблица.Отгрузить КАК ВНаличииДвижения,
			|		0 КАК ВНаличииСведения
			|	ИЗ
			|		РегистрНакопления.РаспределениеЗапасовДвижения КАК Таблица
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТовары КАК Фильтр
			|			ПО Фильтр.Номенклатура = Таблица.Номенклатура
			|			 И Фильтр.Характеристика = Таблица.Характеристика
			|			 И Фильтр.Склад = Таблица.Склад
			|			 И Фильтр.Назначение = Таблица.Назначение
			|	ГДЕ
			|		Таблица.Активность
			|			И Таблица.Поступило <> 0 ИЛИ Таблица.Отгрузить <> 0
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		Таблица.Номенклатура КАК Номенклатура,
			|		Таблица.Характеристика КАК Характеристика,
			|		Таблица.Склад КАК Склад,
			|		0 КАК ВНаличииДвижения,
			|		Таблица.ВНаличии КАК ВНаличииСведения
			|	ИЗ
			|		РегистрСведений.РаспределениеЗапасов КАК Таблица
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТовары КАК Фильтр
			|			ПО Фильтр.Номенклатура = Таблица.Номенклатура
			|			 И Фильтр.Характеристика = Таблица.Характеристика
			|			 И Фильтр.Склад = Таблица.Склад
			|			 И Фильтр.Назначение = Таблица.Назначение
			|	ГДЕ
			|		Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе)) КАК Набор
			|СГРУППИРОВАТЬ ПО
			|	Набор.Номенклатура,
			|	Набор.Характеристика,
			|	Набор.Склад
			|ИМЕЮЩИЕ
			|	СУММА(Набор.ВНаличииДвижения) <> СУММА(Набор.ВНаличииСведения)
			|;
			|
			|/////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Набор.Номенклатура КАК Номенклатура,
			|	Набор.Характеристика КАК Характеристика,
			|	Набор.Склад КАК Склад,
			|	Набор.Назначение КАК Назначение,
			|	&ТекстСвободно КАК Состояние,
			|	СУММА(Набор.РезервироватьПоМереПоступленияСведения) КАК КОбеспечениюСведения,
			|	СУММА(Набор.ЗапасСведения) КАК ЗапасСведения,
			|	СУММА(Набор.ЗапасНаСкладеСведения) КАК ЗапасНаСкладеСведения,
			|	СУММА(Набор.СвободноСведения) КАК СвободноСведения
			|ИЗ(
			|	ВЫБРАТЬ
			|		Таблица.Номенклатура КАК Номенклатура,
			|		Таблица.Характеристика КАК Характеристика,
			|		Таблица.Склад КАК Склад,
			|		Таблица.Назначение КАК Назначение,
			|		Таблица.РезервироватьПоМереПоступления КАК РезервироватьПоМереПоступленияСведения,
			|		0 КАК ЗапасНаСкладеСведения,
			|		0 КАК ЗапасСведения,
			|		0 КАК СвободноСведения
			|	ИЗ
			|		РегистрСведений.РаспределениеЗапасов КАК Таблица
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТовары КАК Фильтр
			|			ПО Фильтр.Номенклатура = Таблица.Номенклатура
			|			 И Фильтр.Характеристика = Таблица.Характеристика
			|			 И Фильтр.Склад = Таблица.Склад
			|			 И Фильтр.Назначение = Таблица.Назначение
			|	ГДЕ
			|		Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемаяОтгрузка)
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		Таблица.Номенклатура КАК Номенклатура,
			|		Таблица.Характеристика КАК Характеристика,
			|		Таблица.Склад КАК Склад,
			|		Таблица.Назначение КАК Назначение,
			|		0 КАК РезервироватьПоМереПоступленияСведения,
			|		ВЫБОР КОГДА Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе) ТОГДА
			|					Таблица.Запас - Таблица.Резерв
			|				ИНАЧЕ
			|					0
			|			КОНЕЦ КАК ЗапасНаСкладеСведения,
			|		ВЫБОР КОГДА Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе) ТОГДА
			|					0
			|				ИНАЧЕ
			|					Таблица.Запас
			|			КОНЕЦ КАК ЗапасСведения,
			|		Таблица.Свободно КАК СвободноСведения
			|	ИЗ
			|		РегистрСведений.РаспределениеЗапасов КАК Таблица
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТовары КАК Фильтр
			|			ПО Фильтр.Номенклатура = Таблица.Номенклатура
			|			 И Фильтр.Характеристика = Таблица.Характеристика
			|			 И Фильтр.Склад = Таблица.Склад
			|			 И Фильтр.Назначение = Таблица.Назначение
			|	ГДЕ
			|		Таблица.Состояние В(
			|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе),
			|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемоеПоступление),
			|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ПоступлениеНеПодтвержденное))) КАК Набор
			|СГРУППИРОВАТЬ ПО
			|	Набор.Номенклатура,
			|	Набор.Характеристика,
			|	Набор.Склад,
			|	Набор.Назначение
			|ИМЕЮЩИЕ
			|	ВЫБОР КОГДА СУММА(Набор.ЗапасНаСкладеСведения) < 0 ТОГДА
			|				0
			|			КОГДА СУММА(Набор.РезервироватьПоМереПоступленияСведения) > СУММА(Набор.ЗапасНаСкладеСведения) ТОГДА
			|				СУММА(Набор.ЗапасНаСкладеСведения)
			|			ИНАЧЕ
			|				СУММА(Набор.РезервироватьПоМереПоступленияСведения)
			|		КОНЕЦ // Распределено на складе
			|		+ ВЫБОР КОГДА СУММА(Набор.РезервироватьПоМереПоступленияСведения)
			|					- ВЫБОР КОГДА СУММА(Набор.ЗапасНаСкладеСведения) < 0 ТОГДА
			|								0
			|								КОГДА СУММА(Набор.РезервироватьПоМереПоступленияСведения) > СУММА(Набор.ЗапасНаСкладеСведения) ТОГДА
			|									СУММА(Набор.ЗапасНаСкладеСведения)
			|							ИНАЧЕ
			|								СУММА(Набор.РезервироватьПоМереПоступленияСведения)
			|						КОНЕЦ > СУММА(Набор.ЗапасСведения) ТОГДА
			|				СУММА(Набор.ЗапасСведения)
			|			ИНАЧЕ
			|				СУММА(Набор.РезервироватьПоМереПоступленияСведения)
			|					- ВЫБОР КОГДА СУММА(Набор.ЗапасНаСкладеСведения) < 0 ТОГДА
			|								0
			|								КОГДА СУММА(Набор.РезервироватьПоМереПоступленияСведения) > СУММА(Набор.ЗапасНаСкладеСведения) ТОГДА
			|									СУММА(Набор.ЗапасНаСкладеСведения)
			|							ИНАЧЕ
			|								СУММА(Набор.РезервироватьПоМереПоступленияСведения)
			|						КОНЕЦ
			|		КОНЕЦ // Распределено в заказах
			|		<> СУММА(Набор.ЗапасСведения) + СУММА(Набор.ЗапасНаСкладеСведения) - СУММА(Набор.СвободноСведения)
			|;
			|
			|/////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	Таблица.Номенклатура КАК Номенклатура,
			|	Таблица.Характеристика КАК Характеристика,
			|	Таблица.Склад КАК Склад,
			|	Таблица.Назначение КАК Назначение,
			|	Таблица.Состояние КАК СостояниеБлиже,
			|	Таблица.ЗаказНаПоступление КАК ЗаказНаПоступлениеБлиже,
			|	Таблица.ДатаПоступления КАК ДатаПоступленияБлиже,
			|	ВсеЧтоДальше.Состояние КАК СостояниеДальше,
			|	ВсеЧтоДальше.ЗаказНаПоступление КАК ЗаказНаПоступлениеДальше,
			|	ВсеЧтоДальше.ДатаПоступления КАК ДатаПоступленияДальше,
			|	&ТекстНарушенПорядокРаспределенияЗапаса КАК Состояние
			|ИЗ
			|	РегистрСведений.РаспределениеЗапасов КАК Таблица
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТовары КАК Фильтр
			|		ПО Фильтр.Номенклатура = Таблица.Номенклатура
			|		 И Фильтр.Характеристика = Таблица.Характеристика
			|		 И Фильтр.Склад = Таблица.Склад
			|		 И Фильтр.Назначение = Таблица.Назначение
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РаспределениеЗапасов КАК ВсеЧтоДальше
			|		ПО ВсеЧтоДальше.Номенклатура = Таблица.Номенклатура
			|		 И ВсеЧтоДальше.Характеристика = Таблица.Характеристика
			|		 И ВсеЧтоДальше.Склад = Таблица.Склад
			|		 И ВсеЧтоДальше.Назначение = Таблица.Назначение
			|		 И ВсеЧтоДальше.Состояние В(
			|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе),
			|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемоеПоступление),
			|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ПоступлениеНеПодтвержденное))
			|		 И ВЫБОР КОГДА Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе) ТОГДА
			|				ВсеЧтоДальше.Состояние В(
			|					ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемоеПоступление),
			|					ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ПоступлениеНеПодтвержденное))
			|			КОГДА Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемоеПоступление) ТОГДА
			|				ВсеЧтоДальше.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ПоступлениеНеПодтвержденное)
			|					ИЛИ ВсеЧтоДальше.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемоеПоступление)
			|						И ВсеЧтоДальше.ДатаПоступления > Таблица.ДатаПоступления
			|							ИЛИ ВсеЧтоДальше.ДатаПоступления = Таблица.ДатаПоступления
			|								И ВсеЧтоДальше.ЗаказНаПоступление > Таблица.ЗаказНаПоступление
			|			КОГДА Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ПоступлениеНеПодтвержденное) ТОГДА
			|				ВсеЧтоДальше.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ПоступлениеНеПодтвержденное)
			|					И ВсеЧтоДальше.ЗаказНаПоступление > Таблица.ЗаказНаПоступление
			|			КОНЕЦ
			|
			|ГДЕ
			|	Таблица.Состояние В(
			|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе),
			|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемоеПоступление),
			|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ПоступлениеНеПодтвержденное))
			|		И Таблица.Свободно > 0 И ВсеЧтоДальше.Запас - ВсеЧтоДальше.Резерв - ВсеЧтоДальше.Свободно > 0
			|;
			|/////////////////////////////////////////////////////////////
			|
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	Таблица.Номенклатура КАК Номенклатура,
			|	Таблица.Характеристика КАК Характеристика,
			|	Таблица.Склад КАК Склад,
			|	Таблица.Назначение КАК Назначение,
			|	Таблица.ЗаказНаПоступление КАК ЗаказНаПоступление,
			|	&ТекстНарушенПорядокРасчетаГрафикаПоступленийПоДатам КАК Состояние
			|ИЗ
			|	РегистрСведений.РаспределениеЗапасов КАК Таблица
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТовары КАК Фильтр
			|		ПО Фильтр.Номенклатура = Таблица.Номенклатура
			|		 И Фильтр.Характеристика = Таблица.Характеристика
			|		 И Фильтр.Склад = Таблица.Склад
			|		 И Фильтр.Назначение = Таблица.Назначение
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РаспределениеЗапасовДвижения КАК ВсеЧтоДальше
			|		ПО ВсеЧтоДальше.Номенклатура = Таблица.Номенклатура
			|		 И ВсеЧтоДальше.Характеристика = Таблица.Характеристика
			|		 И ВсеЧтоДальше.Склад = Таблица.Склад
			|		 И ВсеЧтоДальше.Назначение = Таблица.Назначение
			|		 И ВсеЧтоДальше.ЗаказНаПоступление = Таблица.ЗаказНаПоступление
			|		 И ВсеЧтоДальше.ПоступитКДате > 0
			|		 И ВсеЧтоДальше.ДатаПоступления > Таблица.ДатаПоступления
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаспределениеЗапасов КАК ВсеЧтоДальшеСведения
			|		ПО ВсеЧтоДальшеСведения.Номенклатура = Таблица.Номенклатура
			|		 И ВсеЧтоДальшеСведения.Характеристика = Таблица.Характеристика
			|		 И ВсеЧтоДальшеСведения.Склад = Таблица.Склад
			|		 И ВсеЧтоДальшеСведения.Назначение = Таблица.Назначение
			|		 И ВсеЧтоДальшеСведения.ЗаказНаПоступление = Таблица.ЗаказНаПоступление
			|		 И ВсеЧтоДальшеСведения.ДатаПоступления = ВсеЧтоДальше.ДатаПоступления
			|		 И ВсеЧтоДальшеСведения.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемоеПоступление)
			|
			|ГДЕ
			|	Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемоеПоступление)
			|		И Таблица.Запас > 0
			|СГРУППИРОВАТЬ ПО
			|	Таблица.Номенклатура,
			|	Таблица.Характеристика,
			|	Таблица.Склад,
			|	Таблица.Назначение,
			|	Таблица.ЗаказНаПоступление,
			|	Таблица.ДатаПоступления,
			|	ВсеЧтоДальше.ДатаПоступления
			|ИМЕЮЩИЕ
			|	СУММА(ВсеЧтоДальше.ПоступитКДате) > МАКСИМУМ(ЕСТЬNULL(ВсеЧтоДальшеСведения.Запас, 0))";
		
		Тексты.Добавить(Текст);
		
	КонецЕсли;
	
	Текст =
		"ВЫБРАТЬ
		|	Таблица.Состояние КАК Состояние,
		|	Таблица.Номенклатура КАК Номенклатура,
		|	Таблица.Характеристика КАК Характеристика,
		|	Таблица.Склад КАК Склад,
		|	Таблица.Назначение КАК Назначение,
		|	Таблица.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
		|	Таблица.ЖелаемаяДатаОтгрузки КАК ЖелаемаяДатаОтгрузки,
		|	Таблица.ЗаказНаПоступление КАК ЗаказНаПоступление,
		|	Таблица.ДатаПоступления КАК ДатаПоступления,
		|	Таблица.ТипЗаписиРаспределенияЗапасов КАК ТипЗаписиРаспределенияЗапасов,
		|	Таблица.КОбеспечениюБезРезерва КАК КОбеспечению,
		|	Таблица.РезервироватьПоМереПоступления КАК РезервироватьПоМереПоступления,
		|	Таблица.Резервировать КАК Резервировать,
		|	Таблица.НеОбеспечивать КАК НеОбеспечивать,
		|	Таблица.Зарезервировано КАК Зарезервировано,
		|	Таблица.Обеспечено КАК Обеспечено,
		|	Таблица.РезервПревышаетОстатки КАК РезервПревышаетОстатки,
		|	Таблица.НеОбеспечено КАК НеОбеспечено,
		|	Таблица.Запас КАК Запас,
		|	Таблица.Резерв КАК Резерв,
		|	Таблица.Свободно КАК Свободно,
		|	Таблица.ВНаличии КАК ВНаличии,
		|	Таблица.ОтложитьРезервирование КАК ОтложитьРезервирование
		|ИЗ
		|	РегистрСведений.РаспределениеЗапасов КАК Таблица
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТовары КАК Фильтр
		|		ПО Фильтр.Номенклатура = Таблица.Номенклатура
		|		 И Фильтр.Характеристика = Таблица.Характеристика
		|		 И Фильтр.Склад = Таблица.Склад
		|		 И Фильтр.Назначение = Таблица.Назначение
		|ГДЕ
		|	ВЫБОР КОГДА Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемаяОтгрузка) ТОГДА
		|			Таблица.РезервироватьПоМереПоступления = 0 И Таблица.КОбеспечениюБезРезерва = 0 И Таблица.Резервировать = 0 И Таблица.НеОбеспечивать = 0 И Таблица.ОтложитьРезервирование = 0
		|				ИЛИ Таблица.Зарезервировано <> 0 ИЛИ Таблица.Обеспечено <> 0 ИЛИ Таблица.НеОбеспечено <> 0 ИЛИ Таблица.Запас <> 0 ИЛИ Таблица.Резерв <> 0 ИЛИ Таблица.Свободно <> 0 ИЛИ Таблица.ВНаличии <> 0
		|				ИЛИ Таблица.ЗаказНаОтгрузку = НЕОПРЕДЕЛЕНО ИЛИ Таблица.ЗаказНаПоступление <> НЕОПРЕДЕЛЕНО ИЛИ Таблица.ДатаПоступления <> ДАТАВРЕМЯ(1,1,1) ИЛИ Таблица.ТипЗаписиРаспределенияЗапасов <> 1
		|		КОГДА Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе) ТОГДА
		|			Таблица.Запас = 0 И Таблица.Свободно = 0 И Таблица.Резерв = 0 И ВЫБОР КОГДА Таблица.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) ТОГДА Таблица.ВНаличии = 0 ИНАЧЕ ИСТИНА КОНЕЦ
		|				ИЛИ Таблица.Зарезервировано <> 0 ИЛИ Таблица.Обеспечено <> 0 ИЛИ Таблица.КОбеспечениюБезРезерва <> 0 ИЛИ Таблица.РезервироватьПоМереПоступления <> 0 ИЛИ Таблица.Резервировать <> 0 ИЛИ Таблица.НеОбеспечивать <> 0 ИЛИ Таблица.НеОбеспечено <> 0 ИЛИ Таблица.ОтложитьРезервирование <> 0
		|				ИЛИ Таблица.ЗаказНаОтгрузку <> НЕОПРЕДЕЛЕНО ИЛИ Таблица.ЖелаемаяДатаОтгрузки <> ДАТАВРЕМЯ(1,1,1) ИЛИ Таблица.ЗаказНаПоступление <> НЕОПРЕДЕЛЕНО ИЛИ Таблица.ДатаПоступления <> ДАТАВРЕМЯ(1,1,1) ИЛИ Таблица.ТипЗаписиРаспределенияЗапасов <> 1
		|		КОГДА Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемоеПоступление) ТОГДА
		|			Таблица.Запас = 0 И Таблица.Свободно = 0
		|				ИЛИ Таблица.ВНаличии <> 0 ИЛИ Таблица.Резерв <> 0 ИЛИ Таблица.Зарезервировано <> 0 ИЛИ Таблица.Обеспечено <> 0 ИЛИ Таблица.РезервироватьПоМереПоступления <> 0 ИЛИ Таблица.КОбеспечениюБезРезерва <> 0 ИЛИ Таблица.Резервировать <> 0 ИЛИ Таблица.НеОбеспечивать <> 0 ИЛИ Таблица.НеОбеспечено <> 0 ИЛИ Таблица.ОтложитьРезервирование <> 0
		|				ИЛИ Таблица.ЗаказНаОтгрузку <> НЕОПРЕДЕЛЕНО ИЛИ Таблица.ЖелаемаяДатаОтгрузки <> ДАТАВРЕМЯ(1,1,1) ИЛИ Таблица.ЗаказНаПоступление = НЕОПРЕДЕЛЕНО ИЛИ Таблица.ДатаПоступления = ДАТАВРЕМЯ(1,1,1) ИЛИ Таблица.ТипЗаписиРаспределенияЗапасов <> 1
		|		КОГДА Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ПоступлениеНеПодтвержденное) ТОГДА
		|			Таблица.Запас = 0 И Таблица.Свободно = 0
		|				ИЛИ Таблица.ВНаличии <> 0 ИЛИ Таблица.Резерв <> 0 ИЛИ Таблица.Зарезервировано <> 0 ИЛИ Таблица.Обеспечено <> 0 ИЛИ Таблица.КОбеспечениюБезРезерва <> 0 ИЛИ Таблица.РезервироватьПоМереПоступления <> 0 ИЛИ Таблица.Резервировать <> 0 ИЛИ Таблица.НеОбеспечивать <> 0 ИЛИ Таблица.НеОбеспечено <> 0 ИЛИ Таблица.ОтложитьРезервирование <> 0
		|				ИЛИ Таблица.ЗаказНаОтгрузку <> НЕОПРЕДЕЛЕНО ИЛИ Таблица.ЖелаемаяДатаОтгрузки <> ДАТАВРЕМЯ(1,1,1) ИЛИ Таблица.ЗаказНаПоступление = НЕОПРЕДЕЛЕНО ИЛИ Таблица.ДатаПоступления <> ДАТАВРЕМЯ(1,1,1) ИЛИ Таблица.ТипЗаписиРаспределенияЗапасов <> 1
		|		КОГДА Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ВРезерве) ТОГДА
		|			Таблица.Зарезервировано = 0
		|				ИЛИ Таблица.Обеспечено <> 0 ИЛИ Таблица.Запас <> 0 ИЛИ Таблица.Свободно <> 0 ИЛИ Таблица.ВНаличии <> 0 ИЛИ Таблица.Резерв <> 0 ИЛИ Таблица.РезервироватьПоМереПоступления <> 0 ИЛИ Таблица.КОбеспечениюБезРезерва <> 0 ИЛИ Таблица.Резервировать <> 0 ИЛИ Таблица.НеОбеспечивать <> 0 ИЛИ Таблица.НеОбеспечено <> 0 ИЛИ Таблица.ОтложитьРезервирование <> 0
		|				ИЛИ Таблица.ЗаказНаОтгрузку = НЕОПРЕДЕЛЕНО ИЛИ Таблица.ЗаказНаПоступление <> НЕОПРЕДЕЛЕНО ИЛИ Таблица.ДатаПоступления <> ДАТАВРЕМЯ(1,1,1) ИЛИ Таблица.ТипЗаписиРаспределенияЗапасов <> 1
		|		КОГДА Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.НеОбеспечивать) ТОГДА
		|			Таблица.НеОбеспечено = 0
		|				ИЛИ Таблица.Запас <> 0 ИЛИ Таблица.Свободно <> 0 ИЛИ Таблица.ВНаличии <> 0 ИЛИ Таблица.Резерв <> 0 ИЛИ Таблица.Зарезервировано <> 0 ИЛИ Таблица.Обеспечено <> 0 ИЛИ Таблица.РезервироватьПоМереПоступления <> 0 ИЛИ Таблица.КОбеспечениюБезРезерва <> 0 ИЛИ Таблица.Резервировать <> 0 ИЛИ Таблица.НеОбеспечивать <> 0 ИЛИ Таблица.ОтложитьРезервирование <> 0
		|				ИЛИ Таблица.ЗаказНаОтгрузку = НЕОПРЕДЕЛЕНО ИЛИ Таблица.ЗаказНаПоступление <> НЕОПРЕДЕЛЕНО ИЛИ Таблица.ДатаПоступления <> ДАТАВРЕМЯ(1,1,1) ИЛИ Таблица.ТипЗаписиРаспределенияЗапасов <> 1
		|		КОГДА Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОбеспеченКДате) ТОГДА
		|			Таблица.Зарезервировано = 0 И Таблица.Обеспечено = 0
		|				ИЛИ Таблица.Запас <> 0 ИЛИ Таблица.Свободно <> 0 ИЛИ Таблица.ВНаличии <> 0 ИЛИ Таблица.Резерв <> 0 ИЛИ Таблица.КОбеспечениюБезРезерва <> 0 ИЛИ Таблица.Резервировать <> 0 ИЛИ Таблица.НеОбеспечивать <> 0 ИЛИ Таблица.НеОбеспечено <> 0 ИЛИ Таблица.ОтложитьРезервирование <> 0
		|				ИЛИ Таблица.ЗаказНаОтгрузку = НЕОПРЕДЕЛЕНО ИЛИ Таблица.ЗаказНаПоступление = НЕОПРЕДЕЛЕНО ИЛИ Таблица.ДатаПоступления = ДАТАВРЕМЯ(1,1,1) ИЛИ Таблица.ТипЗаписиРаспределенияЗапасов <> 0
		|		КОГДА Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.Обеспечить) ТОГДА
		|			Таблица.НеОбеспечено = 0
		|				ИЛИ Таблица.Запас <> 0 ИЛИ Таблица.Свободно <> 0 ИЛИ Таблица.ВНаличии <> 0 ИЛИ Таблица.Резерв <> 0 ИЛИ Таблица.РезервироватьПоМереПоступления <> 0 ИЛИ Таблица.КОбеспечениюБезРезерва <> 0 ИЛИ Таблица.Резервировать <> 0 ИЛИ Таблица.НеОбеспечивать <> 0 ИЛИ Таблица.Зарезервировано <> 0 ИЛИ Таблица.Обеспечено <> 0 ИЛИ Таблица.ОтложитьРезервирование <> 0
		|				ИЛИ Таблица.ЗаказНаОтгрузку = НЕОПРЕДЕЛЕНО ИЛИ Таблица.ЗаказНаПоступление <> НЕОПРЕДЕЛЕНО ИЛИ Таблица.ДатаПоступления <> ДАТАВРЕМЯ(1,1,1) ИЛИ Таблица.ТипЗаписиРаспределенияЗапасов <> 0
		|		КОГДА Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОбеспеченНаСкладе) ТОГДА
		|			Таблица.Зарезервировано = 0 И Таблица.Обеспечено = 0
		|				ИЛИ Таблица.Запас <> 0 ИЛИ Таблица.Свободно <> 0 ИЛИ Таблица.ВНаличии <> 0 ИЛИ Таблица.Резерв <> 0 ИЛИ Таблица.РезервироватьПоМереПоступления <> 0 ИЛИ Таблица.КОбеспечениюБезРезерва <> 0 ИЛИ Таблица.Резервировать <> 0 ИЛИ Таблица.НеОбеспечивать <> 0 ИЛИ Таблица.НеОбеспечено <> 0 ИЛИ Таблица.ОтложитьРезервирование <> 0
		|				ИЛИ Таблица.ЗаказНаОтгрузку = НЕОПРЕДЕЛЕНО ИЛИ Таблица.ЗаказНаПоступление <> НЕОПРЕДЕЛЕНО ИЛИ Таблица.ДатаПоступления <> ДАТАВРЕМЯ(1,1,1) ИЛИ Таблица.ТипЗаписиРаспределенияЗапасов <> 0
		|		КОГДА Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаетсяПоНеподтвержденномуЗаказу) ТОГДА
		|			Таблица.Зарезервировано = 0 И Таблица.Обеспечено = 0
		|				ИЛИ Таблица.Запас <> 0 ИЛИ Таблица.Свободно <> 0 ИЛИ Таблица.ВНаличии <> 0 ИЛИ Таблица.Резерв <> 0 ИЛИ Таблица.РезервироватьПоМереПоступления <> 0 ИЛИ Таблица.КОбеспечениюБезРезерва <> 0 ИЛИ Таблица.Резервировать <> 0 ИЛИ Таблица.НеОбеспечивать <> 0 ИЛИ Таблица.НеОбеспечено <> 0 ИЛИ Таблица.ОтложитьРезервирование <> 0
		|				ИЛИ Таблица.ЗаказНаОтгрузку = НЕОПРЕДЕЛЕНО ИЛИ Таблица.ЗаказНаПоступление = НЕОПРЕДЕЛЕНО ИЛИ Таблица.ДатаПоступления <> ДАТАВРЕМЯ(1,1,1) ИЛИ Таблица.ТипЗаписиРаспределенияЗапасов <> 0
		|		ИНАЧЕ
		|			ИСТИНА
		|		КОНЕЦ";
		
	Тексты.Добавить(Текст);
	
	Если ПроверятьРаспределение Тогда
		
		Текст =
			"ВЫБРАТЬ
			|	Набор.Номенклатура КАК Номенклатура,
			|	Набор.Характеристика КАК Характеристика,
			|	Набор.Склад КАК Склад,
			|	Набор.Назначение КАК Назначение,
			|	Набор.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
			|	Набор.ЖелаемаяДатаОтгрузки КАК ЖелаемаяДатаОтгрузки,
			|	ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.Обеспечить) КАК Состояние,
			|	СУММА(Набор.КОбеспечениюДвижения) КАК КОбеспечениюДвижения,
			|	СУММА(Набор.КОбеспечениюСведения) КАК КОбеспечениюСведения
			|ИЗ(
			|ВЫБРАТЬ
			|		Таблица.Номенклатура КАК Номенклатура,
			|		Таблица.Характеристика КАК Характеристика,
			|		Таблица.Склад КАК Склад,
			|		Таблица.Назначение КАК Назначение,
			|		Таблица.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
			|		Таблица.ЖелаемаяДатаОтгрузки КАК ЖелаемаяДатаОтгрузки,
			|		Таблица.КОбеспечениюБезРезерва + Таблица.РезервироватьПоМереПоступления + Таблица.ОтложитьРезервирование КАК КОбеспечениюДвижения,
			|		0 КАК КОбеспечениюСведения
			|	ИЗ
			|		РегистрСведений.РаспределениеЗапасов КАК Таблица
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТовары КАК Фильтр
			|			ПО Фильтр.Номенклатура = Таблица.Номенклатура
			|			 И Фильтр.Характеристика = Таблица.Характеристика
			|			 И Фильтр.Склад = Таблица.Склад
			|			 И Фильтр.Назначение = Таблица.Назначение
			|	ГДЕ
			|		Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемаяОтгрузка)
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|	ВЫБРАТЬ
			|		Таблица.Номенклатура КАК Номенклатура,
			|		Таблица.Характеристика КАК Характеристика,
			|		Таблица.Склад КАК Склад,
			|		Таблица.Назначение КАК Назначение,
			|		Таблица.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
			|		Таблица.ЖелаемаяДатаОтгрузки КАК ЖелаемаяДатаОтгрузки,
			|		0 КАК КОбеспечениюДвижения,
			|		Таблица.НеОбеспечено + Таблица.Зарезервировано + Таблица.Обеспечено КАК КОбеспечениюСведения
			|	ИЗ
			|		РегистрСведений.РаспределениеЗапасов КАК Таблица
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТовары КАК Фильтр
			|			ПО Фильтр.Номенклатура = Таблица.Номенклатура
			|			 И Фильтр.Характеристика = Таблица.Характеристика
			|			 И Фильтр.Склад = Таблица.Склад
			|			 И Фильтр.Назначение = Таблица.Назначение
			|	ГДЕ
			|		Таблица.Состояние В(
			|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.Обеспечить),
			|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОбеспеченКДате),
			|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаетсяПоНеподтвержденномуЗаказу),
			|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОбеспеченНаСкладе))) КАК Набор
			|СГРУППИРОВАТЬ ПО
			|	Набор.Номенклатура,
			|	Набор.Характеристика,
			|	Набор.Склад,
			|	Набор.Назначение,
			|	Набор.ЗаказНаОтгрузку,
			|	Набор.ЖелаемаяДатаОтгрузки
			|ИМЕЮЩИЕ
			|	СУММА(Набор.КОбеспечениюДвижения) <> СУММА(Набор.КОбеспечениюСведения)
			|;
			|/////////////////////////////////////////////////////////////
			|
			|ВЫБРАТЬ
			|	Набор.Номенклатура КАК Номенклатура,
			|	Набор.Характеристика КАК Характеристика,
			|	Набор.Склад КАК Склад,
			|	Набор.Назначение КАК Назначение,
			|	ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОбеспеченНаСкладе) КАК Состояние,
			|	СУММА(Набор.РаспределеноДвижения) КАК РаспределеноДвижения,
			|	СУММА(Набор.РаспределеноСведения) КАК РаспределеноСведения
			|ИЗ(
			|ВЫБРАТЬ
			|		Таблица.Номенклатура КАК Номенклатура,
			|		Таблица.Характеристика КАК Характеристика,
			|		Таблица.Склад КАК Склад,
			|		Таблица.Назначение КАК Назначение,
			|		Таблица.Запас - Таблица.Свободно - Таблица.Резерв КАК РаспределеноДвижения,
			|		0 КАК РаспределеноСведения
			|	ИЗ
			|		РегистрСведений.РаспределениеЗапасов КАК Таблица
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТовары КАК Фильтр
			|			ПО Фильтр.Номенклатура = Таблица.Номенклатура
			|			 И Фильтр.Характеристика = Таблица.Характеристика
			|			 И Фильтр.Склад = Таблица.Склад
			|			 И Фильтр.Назначение = Таблица.Назначение
			|	ГДЕ
			|		Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе)
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|	ВЫБРАТЬ
			|		Таблица.Номенклатура КАК Номенклатура,
			|		Таблица.Характеристика КАК Характеристика,
			|		Таблица.Склад КАК Склад,
			|		Таблица.Назначение КАК Назначение,
			|		0 КАК РаспределеноДвижения,
			|		Таблица.Зарезервировано КАК РаспределеноСведения
			|	ИЗ
			|		РегистрСведений.РаспределениеЗапасов КАК Таблица
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТовары КАК Фильтр
			|			ПО Фильтр.Номенклатура = Таблица.Номенклатура
			|			 И Фильтр.Характеристика = Таблица.Характеристика
			|			 И Фильтр.Склад = Таблица.Склад
			|			 И Фильтр.Назначение = Таблица.Назначение
			|	ГДЕ
			|		Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОбеспеченНаСкладе)) КАК Набор
			|СГРУППИРОВАТЬ ПО
			|	Набор.Номенклатура,
			|	Набор.Характеристика,
			|	Набор.Склад,
			|	Набор.Назначение
			|ИМЕЮЩИЕ
			|	СУММА(Набор.РаспределеноДвижения) <> СУММА(Набор.РаспределеноСведения)
			|;
			|/////////////////////////////////////////////////////////////
			|
			|ВЫБРАТЬ
			|	Набор.Номенклатура КАК Номенклатура,
			|	Набор.Характеристика КАК Характеристика,
			|	Набор.Склад КАК Склад,
			|	Набор.Назначение КАК Назначение,
			|	Набор.ЗаказНаПоступление КАК ЗаказНаПоступление,
			|	Набор.ДатаПоступления КАК ДатаПоступления,
			|	ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОбеспеченКДате) КАК Состояние,
			|	СУММА(Набор.РаспределеноДвижения) КАК РаспределеноДвижения,
			|	СУММА(Набор.РаспределеноСведения) КАК РаспределеноСведения
			|ИЗ(
			|ВЫБРАТЬ
			|		Таблица.Номенклатура КАК Номенклатура,
			|		Таблица.Характеристика КАК Характеристика,
			|		Таблица.Склад КАК Склад,
			|		Таблица.Назначение КАК Назначение,
			|		Таблица.ЗаказНаПоступление КАК ЗаказНаПоступление,
			|		Таблица.ДатаПоступления КАК ДатаПоступления,
			|		Таблица.Запас - Таблица.Свободно КАК РаспределеноДвижения,
			|		0 КАК РаспределеноСведения
			|	ИЗ
			|		РегистрСведений.РаспределениеЗапасов КАК Таблица
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТовары КАК Фильтр
			|			ПО Фильтр.Номенклатура = Таблица.Номенклатура
			|			 И Фильтр.Характеристика = Таблица.Характеристика
			|			 И Фильтр.Склад = Таблица.Склад
			|			 И Фильтр.Назначение = Таблица.Назначение
			|	ГДЕ
			|		Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемоеПоступление)
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|	ВЫБРАТЬ
			|		Таблица.Номенклатура КАК Номенклатура,
			|		Таблица.Характеристика КАК Характеристика,
			|		Таблица.Склад КАК Склад,
			|		Таблица.Назначение КАК Назначение,
			|		Таблица.ЗаказНаПоступление КАК ЗаказНаПоступление,
			|		Таблица.ДатаПоступления КАК ДатаПоступления,
			|		0 КАК РаспределеноДвижения,
			|		Таблица.Зарезервировано КАК РаспределеноСведения
			|	ИЗ
			|		РегистрСведений.РаспределениеЗапасов КАК Таблица
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТовары КАК Фильтр
			|			ПО Фильтр.Номенклатура = Таблица.Номенклатура
			|			 И Фильтр.Характеристика = Таблица.Характеристика
			|			 И Фильтр.Склад = Таблица.Склад
			|			 И Фильтр.Назначение = Таблица.Назначение
			|	ГДЕ
			|		Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОбеспеченКДате)) КАК Набор
			|СГРУППИРОВАТЬ ПО
			|	Набор.Номенклатура,
			|	Набор.Характеристика,
			|	Набор.Склад,
			|	Набор.Назначение,
			|	Набор.ЗаказНаПоступление,
			|	Набор.ДатаПоступления
			|ИМЕЮЩИЕ
			|	СУММА(Набор.РаспределеноДвижения) <> СУММА(Набор.РаспределеноСведения)
			|;
			|/////////////////////////////////////////////////////////////
			|
			|ВЫБРАТЬ
			|	Набор.Номенклатура КАК Номенклатура,
			|	Набор.Характеристика КАК Характеристика,
			|	Набор.Склад КАК Склад,
			|	Набор.Назначение КАК Назначение,
			|	Набор.ЗаказНаПоступление КАК ЗаказНаПоступление,
			|	Набор.ДатаПоступления КАК ДатаПоступления,
			|	ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаетсяПоНеподтвержденномуЗаказу) КАК Состояние,
			|	СУММА(Набор.РаспределеноДвижения) КАК РаспределеноДвижения,
			|	СУММА(Набор.РаспределеноСведения) КАК РаспределеноСведения
			|ИЗ(
			|ВЫБРАТЬ
			|		Таблица.Номенклатура КАК Номенклатура,
			|		Таблица.Характеристика КАК Характеристика,
			|		Таблица.Склад КАК Склад,
			|		Таблица.Назначение КАК Назначение,
			|		Таблица.ЗаказНаПоступление КАК ЗаказНаПоступление,
			|		Таблица.ДатаПоступления КАК ДатаПоступления,
			|		Таблица.Запас - Таблица.Свободно КАК РаспределеноДвижения,
			|		0 КАК РаспределеноСведения
			|	ИЗ
			|		РегистрСведений.РаспределениеЗапасов КАК Таблица
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТовары КАК Фильтр
			|			ПО Фильтр.Номенклатура = Таблица.Номенклатура
			|			 И Фильтр.Характеристика = Таблица.Характеристика
			|			 И Фильтр.Склад = Таблица.Склад
			|			 И Фильтр.Назначение = Таблица.Назначение
			|	ГДЕ
			|		Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ПоступлениеНеПодтвержденное)
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|	ВЫБРАТЬ
			|		Таблица.Номенклатура КАК Номенклатура,
			|		Таблица.Характеристика КАК Характеристика,
			|		Таблица.Склад КАК Склад,
			|		Таблица.Назначение КАК Назначение,
			|		Таблица.ЗаказНаПоступление КАК ЗаказНаПоступление,
			|		Таблица.ДатаПоступления КАК ДатаПоступления,
			|		0 КАК РаспределеноДвижения,
			|		Таблица.Зарезервировано КАК РаспределеноСведения
			|	ИЗ
			|		РегистрСведений.РаспределениеЗапасов КАК Таблица
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТовары КАК Фильтр
			|			ПО Фильтр.Номенклатура = Таблица.Номенклатура
			|			 И Фильтр.Характеристика = Таблица.Характеристика
			|			 И Фильтр.Склад = Таблица.Склад
			|			 И Фильтр.Назначение = Таблица.Назначение
			|	ГДЕ
			|		Таблица.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаетсяПоНеподтвержденномуЗаказу)) КАК Набор
			|СГРУППИРОВАТЬ ПО
			|	Набор.Номенклатура,
			|	Набор.Характеристика,
			|	Набор.Склад,
			|	Набор.Назначение,
			|	Набор.ЗаказНаПоступление,
			|	Набор.ДатаПоступления
			|ИМЕЮЩИЕ
			|	СУММА(Набор.РаспределеноДвижения) <> СУММА(Набор.РаспределеноСведения)";
		
		Тексты.Добавить(Текст);
		
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Текст = СтрСоединить(Тексты, ОбщегоНазначения.РазделительПакетаЗапросов());
	Если Товар <> Неопределено Тогда
		Запрос.УстановитьПараметр("Номенклатура", Товар.Номенклатура);
		Запрос.УстановитьПараметр("Характеристика", Товар.Характеристика);
		Запрос.УстановитьПараметр("Склад", Товар.Склад);
		Запрос.УстановитьПараметр("Назначение", Товар.Назначение);
	КонецЕсли;
	Запрос.УстановитьПараметр("ТекстНесоответствиеДат", НСтр("ru = 'Даты поступления отсутствуют в движениях'"));
	Запрос.УстановитьПараметр("ТекстСвободно", НСтр("ru = 'Свободный остаток'"));
	Запрос.УстановитьПараметр("ТекстНарушенПорядокРаспределенияЗапаса", НСтр("ru = 'Нарушен порядок распределения запаса'"));
	Запрос.УстановитьПараметр("ТекстВНаличии", НСтр("ru = 'В наличии'"));
	Запрос.УстановитьПараметр("ТекстНарушенПорядокРасчетаГрафикаПоступленийПоДатам", НСтр("ru = 'Нарушен порядок расчета графика поступления по датам'"));
	Пакет = Запрос.ВыполнитьПакет();
	Возврат Пакет;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СообщитьОНеудачнойОбработкеНоменклатуры()
	
	ТекстСообщения = НСтр("ru = 'Задание распределения запасов завершилось неудачно по причине: %Причина%'");
	ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	СобытиеЖурнала = НСтр("ru = 'Распределение запасов'", ОбщегоНазначения.КодОсновногоЯзыка());
	ЗаписьЖурналаРегистрации(СобытиеЖурнала,
	УровеньЖурналаРегистрации.Ошибка, Метаданные.РегистрыСведений.РаспределениеЗапасов,, ТекстСообщения);
	
КонецПроцедуры

#КонецОбласти
