#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.КодКлассификатора) Тогда
		
		Элементы.Список.ТекущаяСтрока = Справочники.КлассификаторПолномочийФНСМЧД002.НайтиПолномочиеПоКоду(
			Параметры.КодКлассификатора);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Структура = Новый Структура;
		Структура.Вставить("КодКлассификатора", ТекущиеДанные.КодКлассификатора);
		Структура.Вставить("Полномочие",        ТекущиеДанные.Полномочие);
		
		Закрыть(Структура);
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьКлассификатор(Команда)
		
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриПолученииОтветаНаВопросОЗагрузкеКлассификатораФНС_МЧД002",
		ЭтотОбъект);
	ЗаголовокВопроса = НСтр("ru = 'Обновление классификатора'");
	ТекстВопроса = НСтр("ru = 'Обновить классификатор полномочий ФНС (МЧД 002) сейчас?'");
	Кнопки = Новый СписокЗначений;
	Кнопки.Добавить(КодВозвратаДиалога.Да);
	Кнопки.Добавить(КодВозвратаДиалога.Отмена);
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, Кнопки, , Кнопки[0].Значение, ЗаголовокВопроса);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПриПолученииОтветаНаВопросОЗагрузкеКлассификатораФНС_МЧД002(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ДлительнаяОперация = ОбновитьКлассификаторНаСервере();
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ВыполнитьДействиеЗавершение", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаСервере
Функция ОбновитьКлассификаторНаСервере()
	
	ПараметрыОперации = СинхронизацияЭДО.ЗаполнитьПараметрыСинхронизацииКлассификатораПолномочийФНС_МЧД002();
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания =
		НСтр("ru = 'Синхронизация классификатора полномочий ФНС (МЧД 002).'");
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"СинхронизацияЭДО.СинхронизироватьКлассификаторПолномочийФНС_МЧД002", ПараметрыОперации, ПараметрыВыполнения);
	
КонецФункции

&НаКлиенте
Процедура ВыполнитьДействиеЗавершение(РезультатЗадания, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗадания = Неопределено Тогда // Задание было отменено
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Загрузка отменена.'"));
		Возврат;
		
	КонецЕсли;
	
	Если РезультатЗадания.Статус = "Выполнено" Тогда
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Загрузка завершена успешно.'"));
		Элементы.Список.Обновить();
		
	Иначе
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Ошибка синхронизации.
														|Подробности см. в журнале регистрации.'"));
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти