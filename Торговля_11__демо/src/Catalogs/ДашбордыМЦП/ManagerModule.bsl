#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
#Если НЕ МобильныйАвтономныйСервер Тогда

#Область СлужебныеПроцедурыИФункции
	
#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "Справочники.ДашбордыМЦП.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "11.5.10.18";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Многопоточный = Истина;
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("24ea20d6-737c-4669-b793-ed13a2a0b1cd");
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "Справочники.ДашбордыМЦП.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru = 'Создает дашборды МЦП по пользовательским настройкам.'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Справочники.Пользователи.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.Справочники.ДашбордыМЦП.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");

КонецПроцедуры

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт

	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаОбъектов = "Справочник.Пользователи";
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("ИдентификаторПользователяИБ");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиСсылки();

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Пользователи.Ссылка КАК Пользователь,
	|	Пользователи.ИдентификаторПользователяИБ КАК ИдентификаторПользователяИБ
	|ИЗ
	|	Справочник.Пользователи КАК Пользователи
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДашбордыМЦП КАК ДашбордыМЦП
	|		ПО Пользователи.Ссылка = ДашбордыМЦП.Автор
	|			И (ДашбордыМЦП.ТолькоДляАвтора)
	|ГДЕ
	|	НЕ Пользователи.ПометкаУдаления
	|	И ДашбордыМЦП.Ссылка ЕСТЬ NULL";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ПользователиМЦП = Новый Массив;
	
	Пока Выборка.Следующий() Цикл
	
		ПользовательИБ = ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(
			Выборка.ИдентификаторПользователяИБ);
		Если ПользовательИБ = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Настройка_ВариантыАнализа = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"НастройкиМонитораЦелевыхПоказателей", "ВариантыАнализаПользовательскиеНастройки", , , ПользовательИБ.Имя);
		Если ТипЗнч(Настройка_ВариантыАнализа) <> Тип("ХранилищеЗначения") Тогда
			Продолжить;
		Иначе
			ВариантыАнализа = Настройка_ВариантыАнализа.Получить();
			Если ТипЗнч(ВариантыАнализа) <> Тип("ТаблицаЗначений") Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		ПользователиМЦП.Добавить(Выборка.Пользователь);
		
	КонецЦикла;
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, ПользователиМЦП);
	
КонецПроцедуры

// Создает дашборды МЦП по настройке вывода показателей МЦП каждого пользователя
//
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяОбъекта = "Справочник.Пользователи";
	
	НастройкиПоУмолчанию = МониторингЦелевыхПоказателейКлиентСервер.СтандартныеНастройки();
	
	ОтборВариантовАнализа = Новый Структура("Доступность", Истина);
	
	ПользователиМЦП = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	Если Не ПользователиМЦП.Количество() Тогда
		Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПользователиМЦП", ПользователиМЦП.ВыгрузитьКолонку("Ссылка"));
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Пользователи.Ссылка КАК Пользователь,
	|	Пользователи.Наименование КАК ПользовательНаименование,
	|	Пользователи.ИдентификаторПользователяИБ КАК ИдентификаторПользователяИБ
	|ИЗ
	|	Справочник.Пользователи КАК Пользователи
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДашбордыМЦП КАК ДашбордыМЦП
	|		ПО Пользователи.Ссылка = ДашбордыМЦП.Автор
	|			И (ДашбордыМЦП.ТолькоДляАвтора)
	|ГДЕ
	|	Пользователи.Ссылка В (&ПользователиМЦП)";
	
	Выборка = Запрос.Выполнить().Выбрать();

	Пока Выборка.Следующий() Цикл
	
		ПользовательИБ = ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(
			Выборка.ИдентификаторПользователяИБ);
		
		Настройка_ВариантыАнализа = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"НастройкиМонитораЦелевыхПоказателей", "ВариантыАнализаПользовательскиеНастройки", , , ПользовательИБ.Имя); // ХранилищеЗначения
		ВариантыАнализа = Настройка_ВариантыАнализа.Получить();
		
		НачатьТранзакцию();
		
		Попытка
			
			СправочникОбъект = СоздатьЭлемент();
			СправочникОбъект.Наименование = Выборка.ПользовательНаименование;
			СправочникОбъект.Автор = Выборка.Пользователь;
			СправочникОбъект.ТолькоДляАвтора = Истина;
			
			СправочникОбъект.ВариантыАнализа.Загрузить(ВариантыАнализа.Скопировать(ОтборВариантовАнализа));
			
			ВариантГруппировкиПоказателей = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
				"НастройкиМонитораЦелевыхПоказателей", "ВариантГруппировкиПоказателей");
			Если ВариантГруппировкиПоказателей = Неопределено Тогда
				ВариантГруппировкиПоказателей = НастройкиПоУмолчанию.ВариантГруппировкиПоказателей;
			КонецЕсли;
			Если ВариантГруппировкиПоказателей = "ПоВажности" Тогда
				ВариантГруппировкиПоказателей = "ПоСостоянию";
			КонецЕсли;
			СправочникОбъект.ВариантГруппировкиПоказателей = ВариантГруппировкиПоказателей;
			
			КоличествоКолонокМонитораПоказателей = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
				"НастройкиМонитораЦелевыхПоказателей", "КоличествоКолонокМонитораПоказателей");
			Если КоличествоКолонокМонитораПоказателей = Неопределено Тогда
				КоличествоКолонокМонитораПоказателей = НастройкиПоУмолчанию.КоличествоКолонокМонитораПоказателей;
			КонецЕсли;
			СправочникОбъект.КоличествоКолонок = КоличествоКолонокМонитораПоказателей;
			
			ПроизвольныйМасштаб = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
				"НастройкиМонитораЦелевыхПоказателей", "ПроизвольныйМасштаб");
			Если ПроизвольныйМасштаб = Неопределено Тогда
				ПроизвольныйМасштаб = НастройкиПоУмолчанию.ПроизвольныйМасштаб;
			КонецЕсли;
			СправочникОбъект.Масштаб = ПроизвольныйМасштаб;
			
			ПериодАвтообновления = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
				"НастройкиМонитораЦелевыхПоказателей", "ПериодАвтообновления");
			Если ПериодАвтообновления = Неопределено Тогда
				ПериодАвтообновления = НастройкиПоУмолчанию.ПериодАвтообновления;
			КонецЕсли;
			СправочникОбъект.ПериодАвтообновления = ПериодАвтообновления;
			
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(СправочникОбъект);
			ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Пользователь);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			ОтменитьТранзакцию();
			ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), ПустаяСсылка());
		КонецПопытки;
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
#КонецЕсли