
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.ТипДокумента = 1 Тогда
		
		Элементы.НачатьПриемку.Видимость = Истина;
		Элементы.НачатьОтгрузку.Видимость = Ложь;
		
		Распоряжение = Параметры.СтруктураЗаполнения.Распоряжение;
		ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры.СтруктураЗаполнения);
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТоварыКПоступлениюОстатки.Номенклатура КАК Номенклатура,
		|	ТоварыКПоступлениюОстатки.Характеристика КАК Характеристика,
		|	ТоварыКПоступлениюОстатки.Назначение КАК Назначение,
		|	ТоварыКПоступлениюОстатки.Серия КАК Серия,
		|	ТоварыКПоступлениюОстатки.Склад КАК Склад,
		|	ТоварыКПоступлениюОстатки.КОформлениюОрдеровОстаток КАК КоличествоУпаковок,
		|	ТоварыКПоступлениюОстатки.Номенклатура.ЕдиницаИзмерения КАК Упаковка
		|ИЗ
		|	РегистрНакопления.ТоварыКПоступлению.Остатки(, ДокументПоступления = &ДокументПоступления) КАК ТоварыКПоступлениюОстатки
		|ГДЕ
		|	ТоварыКПоступлениюОстатки.КОформлениюОрдеровОстаток > 0";
		
		Запрос.УстановитьПараметр("ДокументПоступления", Распоряжение);
		
		РезультатЗапроса = Запрос.Выполнить().Выгрузить();
		Товары.Загрузить(РезультатЗапроса);
		
	Иначе
		
		Элементы.НачатьПриемку.Видимость = Ложь;
		Элементы.НачатьОтгрузку.Видимость = Истина;
		
		ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры.СтруктураЗаполнения);
		РаспоряжениеОтгрузка = Параметры.СтруктураЗаполнения.Распоряжение;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТоварыКОтгрузкеОстатки.Склад КАК Склад,
		|	ТоварыКОтгрузкеОстатки.Получатель КАК Получатель,
		|	ТоварыКОтгрузкеОстатки.ДокументОтгрузки КАК ДокументОтгрузки,
		|	ТоварыКОтгрузкеОстатки.Номенклатура КАК Номенклатура,
		|	ТоварыКОтгрузкеОстатки.Характеристика КАК Характеристика,
		|	ТоварыКОтгрузкеОстатки.Назначение КАК Назначение,
		|	ТоварыКОтгрузкеОстатки.Серия КАК Серия,
		|	ТоварыКОтгрузкеОстатки.КОтгрузкеОстаток КАК КоличествоУпаковок,
		|	ТоварыКОтгрузкеОстатки.Номенклатура.ЕдиницаИзмерения КАК Упаковка
		|ИЗ
		|	РегистрНакопления.ТоварыКОтгрузке.Остатки(, ДокументОтгрузки = &ДокументОтгрузки) КАК ТоварыКОтгрузкеОстатки
		|ГДЕ
		|	ТоварыКОтгрузкеОстатки.КОтгрузкеОстаток > 0";
		
		Запрос.УстановитьПараметр("ДокументОтгрузки", РаспоряжениеОтгрузка);
		
		РезультатЗапроса = Запрос.Выполнить().Выгрузить();
		Товары.Загрузить(РезультатЗапроса);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьПриемку(Команда)
	
	Если Товары.Количество() = 0 Тогда
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Текст = НСтр("ru = 'Нет товаров для приемки'");
		СообщениеПользователю.Сообщить();
		Возврат;
	КонецЕсли;
	
	СтруктураЗаполнения = ПолучитьСтруктуруЗаполненияОрдера();
	
	ЗаполнитьЗначенияСвойств(СтруктураЗаполнения, ЭтаФорма);
	
	Описание = Новый ОписаниеОповещения("ЗакрытьФорму", ЭтаФорма);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТипДокумента", 1);
	
	ПараметрыФормы.Вставить("СтруктураЗаполнения", СтруктураЗаполнения);
	
	ОткрытьФорму(
	"Обработка.МобильноеРабочееМестоКладовщика.Форма.ПриходныйОрдер",ПараметрыФормы,
	ЭтаФорма,,,,Описание,
	РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Номенклатура", Элемент.ТекущиеДанные.Номенклатура);
	ПараметрыФормы.Вставить("Характеристика", Элемент.ТекущиеДанные.Характеристика);
	ПараметрыФормы.Вставить("Серия", Элемент.ТекущиеДанные.Серия);
	ПараметрыФормы.Вставить("Назначение", Элемент.ТекущиеДанные.Назначение);
	ПараметрыФормы.Вставить("Упаковка", Элемент.ТекущиеДанные.Упаковка);
	ПараметрыФормы.Вставить("Количество", Элемент.ТекущиеДанные.КоличествоУпаковок);
	ПараметрыФормы.Вставить("Режим", "Просмотр");
	ПараметрыФормы.Вставить("НомерСтроки", Элемент.ТекущаяСтрока); 
	ПараметрыФормы.Вставить("Склад", Склад);
	ПараметрыФормы.Вставить("Помещение", Помещение);
	
	ОткрытьФорму(
	"Обработка.МобильноеРабочееМестоКладовщика.Форма.КарточкаТовара",ПараметрыФормы,
	ЭтаФорма,,,,,
	РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ЭтаФорма.Открыта() Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСтруктуруЗаполненияОрдера()
	
	Возврат Обработки.МобильноеРабочееМестоКладовщика.СтруктураЗаполненияПриходногоОрдера();
	
КонецФункции 

&НаСервереБезКонтекста
Функция ПолучитьСтруктуруЗаполненияРасходногоОрдера()
	
	Возврат СкладыСервер.ПараметрыПереоформленияРасходныхОрдеров();
	
КонецФункции

&НаКлиенте
Процедура НачатьОтгрузку(Команда)
	
	Если Товары.Количество() = 0 Тогда
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Текст = НСтр("ru = 'Нет товаров для отгрузки'");
		СообщениеПользователю.Сообщить();
		Возврат;
	КонецЕсли;
	
	СтруктураЗаполнения = ПолучитьСтруктуруЗаполненияРасходногоОрдера();
	
	ЗаполнитьЗначенияСвойств(СтруктураЗаполнения, ЭтаФорма);
	СтруктураЗаполнения.РаспоряженияНаОтгрузку.Добавить(РаспоряжениеОтгрузка);
	
	Описание = Новый ОписаниеОповещения("ЗакрытьФорму", ЭтаФорма);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СтруктураЗаполнения", СтруктураЗаполнения);
	
	ОткрытьФорму(
	"Обработка.МобильноеРабочееМестоКладовщика.Форма.РасходныйОрдер",ПараметрыФормы,
	ЭтаФорма,,,,Описание,
	РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры
