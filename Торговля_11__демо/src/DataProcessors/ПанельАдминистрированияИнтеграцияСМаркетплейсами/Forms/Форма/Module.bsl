#Область ОписаниеПеременных

&НаКлиенте
Перем ЭтоОткрытиеФормы;

&НаКлиенте
Перем ОбновитьИнтерфейс;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
			
КонецПроцедуры   

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЭтоОткрытиеФормы = Истина;
	
	ОбновитьЗначениеРеквизитов("Константа_ИспользоватьИнтеграциюСЯндексМаркет");
	ОбновитьЗначениеРеквизитов("Константа_ИспользоватьИнтеграциюСOzon"); 
	
	ИспользоватьИнтеграциюСЯндексМаркетПриИзменении(Элементы.ИспользоватьИнтеграциюСЯндексМаркет);
	ИспользоватьИнтеграциюСOzonПриИзменении(Элементы.ИспользоватьИнтеграциюСOzon);
	
	ЭтоОткрытиеФормы = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	ОбновитьИнтерфейсПрограммы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИспользоватьИнтеграциюСЯндексМаркетПриИзменении(Элемент)
	
	Подключаемый_ПриИзмененииРеквизита(Элемент, Не ЭтоОткрытиеФормы);
	
	Элементы.ОткрытьНастройкиПодключенияЯндексМаркет.Доступность = Константа_ИспользоватьИнтеграциюСЯндексМаркет;
	Элементы.ОткрытьНастройкиОбменаДанными.Доступность = Константа_ИспользоватьИнтеграциюСЯндексМаркет;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьИнтеграциюСOzonПриИзменении(Элемент)
	
	Подключаемый_ПриИзмененииРеквизита(Элемент, Не ЭтоОткрытиеФормы);
	
	Элементы.ОткрытьНастройкиOzon.Доступность = Константа_ИспользоватьИнтеграциюСOzon;
	Элементы.ОткрытьНастройкиПодключенияOzon.Доступность = Константа_ИспользоватьИнтеграциюСOzon;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОткрытьНастройкиПодключенияЯндексМаркет(Команда)
	
		ЕстьАктуальныеНастройки  = ЕстьАктуальныеНастройкиЯндексМаркетФулфилмент();
		
		Если ЕстьАктуальныеНастройки Тогда
			
			ОткрытьФорму(
			"Обработка.ЯндексМаркетВитринаФулфилмент.Форма.ТекущееСостояниеАвторизации",
			,,Истина);
			
		Иначе
			
			ОткрытьФорму(
			"Обработка.ЯндексМаркетВитринаФулфилмент.Форма.ПодключениеКСервису",
			,,Истина);
				
		КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНастройкиОбменаДанными(Команда)
	
	ОткрытьФорму(
	"Обработка.ЯндексМаркетВитринаФулфилмент.Форма.НастройкиОбменаДанными",,,Истина);

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНастройкиПодключенияOzon(Команда)

	ОчиститьСообщения();
	ОткрытьФорму("Обработка.УправлениеПродажамиНаOzon.Форма.СписокПодключенийКСервису",, ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНастройкиСинхронизацииДанныхOzon(Команда)

	ОчиститьСообщения();
	ОткрытьФорму("Обработка.УправлениеПродажамиНаOzon.Форма.НастройкиИнтеграции");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Клиент

&НаКлиенте
Процедура Подключаемый_ПриИзмененииРеквизита(Элемент, ОбновлятьИнтерфейс = Истина, ВнешнееИзменение = Ложь)
	
	КонстантаИмя = ПриИзмененииРеквизитаСервер(Элемент.Имя);
	
	Если ОбновлятьИнтерфейс Тогда
		ОбновитьИнтерфейс = Истина;
		ПодключитьОбработчикОжидания("ОбновитьИнтерфейсПрограммы", 2, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИнтерфейсПрограммы()
	
	Если ОбновитьИнтерфейс = Истина Тогда
		ОбновитьИнтерфейс = Ложь;
		ОбщегоНазначенияКлиент.ОбновитьИнтерфейсПрограммы();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ВызовСервера

&НаСервере
Функция ПриИзмененииРеквизитаСервер(ИмяЭлемента)
	
	РеквизитПутьКДанным = Элементы[ИмяЭлемента].ПутьКДанным;
	
	КонстантаИмя = СохранитьЗначениеРеквизита(РеквизитПутьКДанным,  Новый Структура);
	
	ОбновитьЗначениеРеквизитов(РеквизитПутьКДанным);
	
	ОбновитьПовторноИспользуемыеЗначения();
	
	Возврат КонстантаИмя;
	
КонецФункции

#КонецОбласти

#Область Сервер

&НаСервере
Функция СохранитьЗначениеРеквизита(РеквизитПутьКДанным, Результат)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если РеквизитПутьКДанным = "" Тогда
		Возврат "";
	КонецЕсли;
	
	// Определение имени константы.
	КонстантаИмя = "";
	Если НРег(Лев(РеквизитПутьКДанным, 10)) = НРег("Константа_") Тогда
		КонстантаИмя = Сред(РеквизитПутьКДанным, 11);
	КонецЕсли;
	
	// Сохранения значения константы.
	Если КонстантаИмя <> "" Тогда
		КонстантаМенеджер = Константы[КонстантаИмя];
		
		Если НРег(Лев(РеквизитПутьКДанным, 10)) = НРег("Константа_") Тогда
			КонстантаЗначение = ЭтотОбъект[РеквизитПутьКДанным];  
		КонецЕсли;
		
		Если КонстантаМенеджер.Получить() <> КонстантаЗначение Тогда
			КонстантаМенеджер.Установить(КонстантаЗначение);
		КонецЕсли;
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат КонстантаИмя;
	
КонецФункции

&НаСервере
Процедура ОбновитьЗначениеРеквизитов(РеквизитПутьКДанным = "")
	
	УстановитьПривилегированныйРежим(Истина);
	
	КонстантаИмя = "";
	Если НРег(Лев(РеквизитПутьКДанным, 10)) = НРег("Константа_") Тогда
		КонстантаИмя = Сред(РеквизитПутьКДанным, 11);
	КонецЕсли;
	
	Если КонстантаИмя <> "" Тогда
		ЭтотОбъект[РеквизитПутьКДанным] = Константы[КонстантаИмя].Получить();
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

&НаСервере
Функция ЕстьАктуальныеНастройкиЯндексМаркетФулфилмент()
	
	ЕстьАктуальныеНастройки = Ложь;
	
	Организация = ИнтеграцияСЯндексМаркетСервер.ТекущиеДанныеАвторизацииОрганизация();
	
	Если Организация <> Неопределено И ТипЗнч(Организация) = Тип("СправочникСсылка.Организации") Тогда
		ДанныеАвторизации = ИнтеграцияСЯндексМаркетСервер.ТекущиеДанныеАвторизации(Организация);
		Если ДанныеАвторизации.Свойство("access_token") 
			И ДанныеАвторизации.Свойство("access_token_expires")
			И ДанныеАвторизации.Свойство("refresh_token") Тогда
			
				ЕстьАктуальныеНастройки = Истина;
				
		КонецЕсли;
	КонецЕсли;
	
	Возврат ЕстьАктуальныеНастройки;
	
КонецФункции

#КонецОбласти

#КонецОбласти
