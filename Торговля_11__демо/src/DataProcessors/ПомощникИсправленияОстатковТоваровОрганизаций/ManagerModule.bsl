#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Выполняет методы модуля объекта обработки помощника исправления.
//
// Параметры:
//  Параметры - Структура - Структура с полями:
//		* СтруктураОбъектаОбработки - Структура - Структура, из которой будет восстановлен объект обработки. Поля структуры должны совпадать с
//			полями метаданных обработки ПомощникИсправленияОстатковТоваровОрганизаций.
//		* ИменаМетодов - Массив - Массив строк с именами методов, которые нужно выполнить.
//		* ПараметрыМетодов - Структура - Структура, где ключи совпадают с именами методов, а значения содержат массивы параметров этих методов.
//  АдресРезультата - Строка - Адрес во временное хранилище, по которому нужно поместить структуру объекта после выполнения всех методов.
//  ОбработкаОбъект	- ОбработкаОбъект.ПомощникИсправленияОстатковТоваровОрганизаций - объект обработки, если его не нужно восстанавливать из
//		структуры.
//
Процедура ВыполнитьМетодыОбъекта(Параметры, АдресРезультата = Неопределено, ОбработкаОбъект = Неопределено) Экспорт
	
	Если ОбработкаОбъект = Неопределено Тогда
		ОбработкаОбъект = ОбработкаОбъект(Параметры.СтруктураОбъектаОбработки);
	КонецЕсли;
	
	КраткоеПредставлениеОшибки = "";
	
	Методы = СтрРазделить(Параметры.ИменаМетодов, ",");
	Для Каждого Метод Из Методы Цикл
		Попытка
			Метод = СокрЛП(Метод);
			Если Метод = "ОбновитьДанныеПоОтрицательнымОстаткамИСальдо" Тогда
				ОбработкаОбъект.ОбновитьДанныеПоОтрицательнымОстаткамИСальдо();
			ИначеЕсли Метод = "ЗаполнитьМесяцИсправления" Тогда
				ОбработкаОбъект.ЗаполнитьМесяцИсправления();
			ИначеЕсли Метод = "СформироватьНовыеРезервы" Тогда
				ОбработкаОбъект.СформироватьНовыеРезервы();
			ИначеЕсли Метод = "СформироватьНовыеРезервыПоНовымНастройкам" Тогда
				ОбработкаОбъект.СформироватьНовыеРезервыПоНовымНастройкам();
			ИначеЕсли Метод = "СформироватьИсправленияРазвернутогоСальдо" Тогда
				ОбработкаОбъект.СформироватьИсправленияРазвернутогоСальдо();
			ИначеЕсли Метод = "ТипыЗачетаОстатковРазвернутогоСальдо" Тогда
				ОбработкаОбъект.ТипыЗачетаОстатковРазвернутогоСальдо();
			ИначеЕсли Метод = "ПерезаполнитьВидыЗапасов" Тогда
				ОбработкаОбъект.ПерезаполнитьВидыЗапасов();
			ИначеЕсли Метод = "ЗаполнитьНовыеНастройкиПередачи" Тогда
				ОбработкаОбъект.ЗаполнитьНовыеНастройкиПередачи();
			ИначеЕсли Метод = "ЗаполнитьРаспоряженияНаОформлениеОприходований" Тогда
				ОбработкаОбъект.ЗаполнитьРаспоряженияНаОформлениеОприходований();
			ИначеЕсли Метод = "ЗаполнитьТребуетсяПереформированиеРезервов" Тогда
				ОбработкаОбъект.ЗаполнитьТребуетсяПереформированиеРезервов();
			ИначеЕсли Метод = "ЗаполнитьТребуетсяИсправлениеРазвернутогоСальдо" Тогда
				ОбработкаОбъект.ЗаполнитьТребуетсяИсправлениеРазвернутогоСальдо();
			ИначеЕсли Метод = "СформироватьИсправительныеСчетаФактуры" Тогда
				ОбработкаОбъект.СформироватьИсправительныеСчетаФактуры();
			ИначеЕсли Метод = "СформироватьРезультатКонтроляОформленияДокументовТовародвижения" Тогда
				ОбработкаОбъект.СформироватьРезультатКонтроляОформленияДокументовТовародвижения(
					Параметры.ПараметрыМетодов.СформироватьРезультатКонтроляОформленияДокументовТовародвижения[0],
					Параметры.ПараметрыМетодов.СформироватьРезультатКонтроляОформленияДокументовТовародвижения[1]);
			ИначеЕсли Метод = "Инициализировать" Тогда
				ОбработкаОбъект.Инициализировать();
			Иначе
				ТекстИсключения = НСтр("ru = 'Работа с методом %Метод% не поддерживается'");
				ТекстИсключения = СтрЗаменить(ТекстИсключения, "%Метод%", Метод);
				ВызватьИсключение ТекстИсключения;
			КонецЕсли;
		Исключение
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			КраткоеПредставлениеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
			Прервать;
		КонецПопытки;	
	КонецЦикла;
	
	Если ЗначениеЗаполнено(АдресРезультата) Тогда
		ПоместитьВоВременноеХранилище(ОбработкаОбъект.СтруктураДанных(), АдресРезультата);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КраткоеПредставлениеОшибки) Тогда
		ВызватьИсключение КраткоеПредставлениеОшибки;
	КонецЕсли;
	
КонецПроцедуры

// Запрос детальные остатки на конец месяца исправления.
// 
// Параметры:
//  МесяцИсправления - Дата
// 
// Возвращаемое значение:
//  Запрос - Запрос детальные остатки на конец месяца исправления
//
Функция ЗапросДетальныеОстаткиНаКонецМесяцаИсправления(МесяцИсправления) Экспорт
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	СформироватьВТКонтролируемыеВидыЗапасов(МенеджерВременныхТаблиц);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТоварыОрганизацийОстатки.Организация КАК Организация,
		|	ТоварыОрганизацийОстатки.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	ТоварыОрганизацийОстатки.ВидЗапасов КАК ВидЗапасов,
		|	ТоварыОрганизацийОстатки.НомерГТД КАК НомерГТД,
		|	ТоварыОрганизацийОстатки.КоличествоОстаток КАК КоличествоОстаток
		|ПОМЕСТИТЬ ОстаткиНаКонецПериодаВсе
		|ИЗ
		|	РегистрНакопления.ТоварыОрганизаций.Остатки(
		|			&ГраницаИсправления,
		|			ВидЗапасов В
		|				(ВЫБРАТЬ
		|					ВТКонтролируемыеВидыЗапасов.Ссылка
		|				ИЗ
		|					ВТКонтролируемыеВидыЗапасов КАК ВТКонтролируемыеВидыЗапасов)) КАК ТоварыОрганизацийОстатки
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РезервыТоваровОрганизацийОстатки.Организация,
		|	РезервыТоваровОрганизацийОстатки.АналитикаУчетаНоменклатуры,
		|	РезервыТоваровОрганизацийОстатки.ВидЗапасов,
		|	РезервыТоваровОрганизацийОстатки.НомерГТД,
		|	РезервыТоваровОрганизацийОстатки.КоличествоОстаток
		|ИЗ
		|	РегистрНакопления.РезервыТоваровОрганизаций.Остатки(
		|			&ГраницаИсправления,
		|			ВидЗапасов В
		|				(ВЫБРАТЬ
		|					ВТКонтролируемыеВидыЗапасов.Ссылка
		|				ИЗ
		|					ВТКонтролируемыеВидыЗапасов КАК ВТКонтролируемыеВидыЗапасов)) КАК РезервыТоваровОрганизацийОстатки
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Организация,
		|	АналитикаУчетаНоменклатуры,
		|	ВидЗапасов,
		|	НомерГТД
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТоварыОрганизаций.Регистратор КАК Регистратор,
		|	ТоварыОрганизаций.Организация КАК Организация,
		|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	ТоварыОрганизаций.ВидЗапасов КАК ВидЗапасов,
		|	ТоварыОрганизаций.НомерГТД КАК НомерГТД,
		|	СУММА(ВЫБОР
		|			КОГДА ТоварыОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|					И ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Номенклатура.ВестиУчетПоГТД
		|				ТОГДА -1
		|			ИНАЧЕ 1
		|		КОНЕЦ * ТоварыОрганизаций.Количество) КАК Количество
		|ПОМЕСТИТЬ ТаможенныеДекларацииИмпорт
		|ИЗ
		|	РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
		|ГДЕ
		|	&ИспользуетсяРаздельноеОформлениеЗакупок
		|	И ТоварыОрганизаций.ХозяйственнаяОперация В(&ОперацииТаможеннойДекларацииИмпорт)
		|	И ТоварыОрганизаций.НомерГТД = ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
		|	И (ТоварыОрганизаций.Организация, ТоварыОрганизаций.АналитикаУчетаНоменклатуры, ТоварыОрганизаций.ВидЗапасов) В
		|			(ВЫБРАТЬ
		|				ОстаткиНаКонецПериодаВсе.Организация,
		|				ОстаткиНаКонецПериодаВсе.АналитикаУчетаНоменклатуры,
		|				ОстаткиНаКонецПериодаВсе.ВидЗапасов
		|			ИЗ
		|				ОстаткиНаКонецПериодаВсе)
		|	И ТоварыОрганизаций.Период <= &ПериодИсправления
		|
		|СГРУППИРОВАТЬ ПО
		|	ТоварыОрганизаций.Регистратор,
		|	ТоварыОрганизаций.Организация,
		|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры,
		|	ТоварыОрганизаций.ВидЗапасов,
		|	ТоварыОрганизаций.НомерГТД
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаможенныеДекларацииИмпорт.Регистратор КАК Регистратор,
		|	ТаможенныеДекларацииИмпорт.Организация КАК Организация,
		|	ТаможенныеДекларацииИмпорт.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	ТаможенныеДекларацииИмпорт.ВидЗапасов КАК ВидЗапасов,
		|	ТаможенныеДекларацииИмпорт.НомерГТД КАК НомерГТД,
		|	ТаможеннаяДекларацияИмпортТовары.ДокументПоступления КАК Распоряжение
		|ПОМЕСТИТЬ ДокументыПоступления
		|ИЗ
		|	ТаможенныеДекларацииИмпорт КАК ТаможенныеДекларацииИмпорт
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ТаможеннаяДекларацияИмпорт.Товары КАК ТаможеннаяДекларацияИмпортТовары
		|		ПО ТаможеннаяДекларацияИмпортТовары.Ссылка = ТаможенныеДекларацииИмпорт.Регистратор
		|			И ТаможеннаяДекларацияИмпортТовары.Номенклатура = ТаможенныеДекларацииИмпорт.АналитикаУчетаНоменклатуры.Номенклатура
		|			И ТаможеннаяДекларацияИмпортТовары.Характеристика = ТаможенныеДекларацииИмпорт.АналитикаУчетаНоменклатуры.Характеристика
		|			И ТаможеннаяДекларацияИмпортТовары.Серия = ТаможенныеДекларацииИмпорт.АналитикаУчетаНоменклатуры.Серия
		|			И ТаможеннаяДекларацияИмпортТовары.Склад = ТаможенныеДекларацииИмпорт.АналитикаУчетаНоменклатуры.МестоХранения
		|ГДЕ
		|	ТаможеннаяДекларацияИмпортТовары.ДокументПоступления.ХозяйственнаяОперация В(&ОперацииРаздельногоОформленияЗакупок)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОстаткиНаКонецПериодаВсе.Организация КАК Организация,
		|	ОстаткиНаКонецПериодаВсе.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	ОстаткиНаКонецПериодаВсе.ВидЗапасов КАК ВидЗапасов,
		|	ОстаткиНаКонецПериодаВсе.НомерГТД КАК НомерГТД,
		|	ОстаткиНаКонецПериодаВсе.КоличествоОстаток КАК КоличествоОстаток
		|ПОМЕСТИТЬ ОстаткиНаКонецПериода
		|ИЗ
		|	ОстаткиНаКонецПериодаВсе КАК ОстаткиНаКонецПериодаВсе
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Данные.Организация,
		|	Данные.АналитикаУчетаНоменклатуры,
		|	Данные.ВидЗапасов,
		|	Данные.НомерГТД,
		|	СУММА(Данные.Количество)
		|ИЗ
		|	(ВЫБРАТЬ
		|		ТаможенныеДекларацииИмпорт.Организация КАК Организация,
		|		ТаможенныеДекларацииИмпорт.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|		ТаможенныеДекларацииИмпорт.ВидЗапасов КАК ВидЗапасов,
		|		ТаможенныеДекларацииИмпорт.НомерГТД КАК НомерГТД,
		|		ТаможенныеДекларацииИмпорт.Количество КАК Количество
		|	ИЗ
		|		ТаможенныеДекларацииИмпорт КАК ТаможенныеДекларацииИмпорт
		|	ГДЕ
		|		ТаможенныеДекларацииИмпорт.Регистратор В
		|				(ВЫБРАТЬ
		|					ДокументыПоступления.Регистратор
		|				ИЗ
		|					ДокументыПоступления КАК ДокументыПоступления)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ДокументыПоступления.Организация,
		|		ДокументыПоступления.АналитикаУчетаНоменклатуры,
		|		ДокументыПоступления.ВидЗапасов,
		|		ДокументыПоступления.НомерГТД,
		|		-МИНИМУМ(ЕСТЬNULL(ТоварыОрганизаций.Количество, 0))
		|	ИЗ
		|		ДокументыПоступления КАК ДокументыПоступления
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
		|			ПО (ТоварыОрганизаций.Организация = ДокументыПоступления.Организация)
		|				И (ТоварыОрганизаций.АналитикаУчетаНоменклатуры = ДокументыПоступления.АналитикаУчетаНоменклатуры)
		|				И (ТоварыОрганизаций.ВидЗапасов = ДокументыПоступления.ВидЗапасов)
		|				И (ТоварыОрганизаций.Регистратор.Распоряжение = ДокументыПоступления.Распоряжение)
		|				И (ТоварыОрганизаций.Период <= &ПериодИсправления)
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ДокументыПоступления.Организация,
		|		ДокументыПоступления.АналитикаУчетаНоменклатуры,
		|		ДокументыПоступления.ВидЗапасов,
		|		ДокументыПоступления.НомерГТД,
		|		ТоварыОрганизаций.Регистратор,
		|		ТоварыОрганизаций.НомерСтроки) КАК Данные
		|
		|СГРУППИРОВАТЬ ПО
		|	Данные.Организация,
		|	Данные.АналитикаУчетаНоменклатуры,
		|	Данные.ВидЗапасов,
		|	Данные.НомерГТД,
		|	Данные.АналитикаУчетаНоменклатуры.Номенклатура.ВестиУчетПоГТД
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Организация,
		|	АналитикаУчетаНоменклатуры,
		|	ВидЗапасов,
		|	НомерГТД
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ОстаткиНаКонецПериодаВсе
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ТаможенныеДекларацииИмпорт
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ДокументыПоступления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОстаткиНаКонецПериода.Организация КАК Организация,
		|	ОстаткиНаКонецПериода.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	ОстаткиНаКонецПериода.ВидЗапасов КАК ВидЗапасов,
		|	ОстаткиНаКонецПериода.НомерГТД КАК НомерГТД,
		|	СУММА(ОстаткиНаКонецПериода.КоличествоОстаток) КАК КоличествоОстаток
		|ПОМЕСТИТЬ ДетальныеОстаткиНаКонецМесяцаИсправления
		|ИЗ
		|	ОстаткиНаКонецПериода КАК ОстаткиНаКонецПериода
		|
		|СГРУППИРОВАТЬ ПО
		|	ОстаткиНаКонецПериода.Организация,
		|	ОстаткиНаКонецПериода.АналитикаУчетаНоменклатуры,
		|	ОстаткиНаКонецПериода.ВидЗапасов,
		|	ОстаткиНаКонецПериода.НомерГТД
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ОстаткиНаКонецПериода";                    

	Запрос.УстановитьПараметр("ГраницаИсправления", 					 Новый Граница(КонецМесяца(МесяцИсправления), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ПериодИсправления",  				     КонецМесяца(МесяцИсправления));
	Запрос.УстановитьПараметр("ИспользуетсяРаздельноеОформлениеЗакупок", ЗапасыСервер.ВыполнятьСторноТаможенныхДеклараций());
	Запрос.УстановитьПараметр("ОперацииРаздельногоОформленияЗакупок",    ЗакупкиСервер.ХозяйственныеОперацииРаздельнойЗакупкиБезОтборов());
	Запрос.УстановитьПараметр("ОперацииТаможеннойДекларацииИмпорт",      ЗакупкиСервер.ХозяйственныеОперацииТаможеннойДекларацииИмпорт());

	Возврат Запрос;

КонецФункции

// Сформировать ВТКонтролируемые виды запасов.
// 
// Параметры:
//  МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Менеджер временных таблиц
//
Процедура СформироватьВТКонтролируемыеВидыЗапасов(МенеджерВременныхТаблиц) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВидыЗапасов.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТКонтролируемыеВидыЗапасов
	|ИЗ
	|	Справочник.ВидыЗапасов КАК ВидыЗапасов
	|ГДЕ
	|	ВидыЗапасов.ТипЗапасов В (&КонтролируемыеТипыЗапасов)";
	Запрос.Параметры.Вставить("КонтролируемыеТипыЗапасов", Перечисления.ТипыЗапасов.КонтролируемыеТипыЗапасов());
	Запрос.Выполнить();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОбработкаОбъект(СтруктураОбъектаОбработки)
	
	ОбработкаОбъект = Обработки.ПомощникИсправленияОстатковТоваровОрганизаций.Создать();
	Для Каждого ПолеСтруктуры Из СтруктураОбъектаОбработки Цикл
		Если ТипЗнч(ПолеСтруктуры.Значение) = Тип("ТаблицаЗначений") Тогда
			ОбработкаОбъект[ПолеСтруктуры.Ключ].Загрузить(ПолеСтруктуры.Значение);
		Иначе
			ОбработкаОбъект[ПолеСтруктуры.Ключ] = ПолеСтруктуры.Значение;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ОбработкаОбъект;
	
КонецФункции

#КонецОбласти

#КонецЕсли