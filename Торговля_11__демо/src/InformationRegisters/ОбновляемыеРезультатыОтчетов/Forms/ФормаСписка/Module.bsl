
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Параметры.Свойство("Пользователь", Пользователь) Тогда
		РегистрыСведений.ОбновляемыеРезультатыОтчетов.СообщитьПользователю(НСтр(
			"ru = 'Список доступен только из формы отчета или панели отчетов!'"), Отказ);
		Возврат;
	КонецЕсли;
	
	Параметры.Свойство("ВыводитьДашбордыМЦП", ВыводитьДашбордыМЦП);
	
	ОбновитьСписокОфлайнОтчетов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	#Если МобильныйКлиент Тогда
	ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиФормы.Нет;
	Элементы.ГруппаКнопокМобильныйКлиент.Видимость = Истина;
	Элементы.СписокОфлайнОтчетов.ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиЭлементаФормы.Нет;
	
	Если ОсновнойСерверДоступен() = Ложь Тогда
		Элементы.ГруппаСохранитьОфлайнОтчеты.Доступность = Ложь;
		Элементы.СписокОфлайнОтчетовСохранитьОфлайнОтчет.Доступность = Ложь;
	КонецЕсли;
	#КонецЕсли
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтметитьВсе(Команда)
	
	Для Каждого СтрокаОтчет Из СписокОфлайнОтчетов Цикл
		СтрокаОтчет.Пометка = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсе(Команда)
	
	Для Каждого СтрокаОтчет Из СписокОфлайнОтчетов Цикл
		СтрокаОтчет.Пометка = Ложь;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьОфлайнОтчеты(Команда)
	
	ИдентификаторыСтрок = Новый Массив;
	Для Каждого СтрокаОтчет Из СписокОфлайнОтчетов Цикл
		Если СтрокаОтчет.Пометка Тогда
			ИдентификаторыСтрок.Добавить(СтрокаОтчет.ПолучитьИдентификатор());
		КонецЕсли;
	КонецЦикла;
	
	Если ИдентификаторыСтрок.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеОбновленияОфлайнОтчетов", ЭтотОбъект);
	ДлительнаяОперация = Неопределено;
	ПараметрыОжидания = Неопределено;
	
	#Если МобильныйКлиент Тогда
		Выполнить("ДлительнаяОперация = СохранитьОфлайнОтчетыНаСервере(ИдентификаторыСтрок)");
		Выполнить("ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект)");
		Выполнить("ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОписаниеОповещения, ПараметрыОжидания)");
	#Иначе
		ДлительнаяОперация = СохранитьОфлайнОтчетыНаСервере(ИдентификаторыСтрок);
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОписаниеОповещения, ПараметрыОжидания);
	#КонецЕсли
	
КонецПроцедуры


#Если НЕ МобильныйАвтономныйСервер Тогда

&НаСервере
Функция СохранитьОфлайнОтчетыНаСервере(ИдентификаторыСтрок)
	
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("Пользователь", Пользователь);
	ПараметрыЗаполнения.Вставить("ОбновитьДашбордыМЦП", ВыводитьДашбордыМЦП);
	
	ОфлайнОтчеты = СписокОфлайнОтчетов.Выгрузить(, "Пользователь, Отчет, Вариант, ХешПользовательскойНастройки");
	ОфлайнОтчеты.Очистить();
	Для Каждого ИдентификаторСтроки Из ИдентификаторыСтрок Цикл
		СтрокаОтчет = СписокОфлайнОтчетов.НайтиПоИдентификатору(ИдентификаторСтроки);
		НоваяСтрока = ОфлайнОтчеты.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаОтчет);
	КонецЦикла;
	ПараметрыЗаполнения.Вставить("ОфлайнОтчеты", ОфлайнОтчеты);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Обновление офлайн-отчетов пользователя'");
	
	РезультатВыполнения = ДлительныеОперации.ВыполнитьВФоне(
		"РегистрыСведений.ОбновляемыеРезультатыОтчетов.ОбновитьОфлайнОтчетыПользователя",
		ПараметрыЗаполнения, ПараметрыВыполнения);
	
	Возврат РезультатВыполнения;
	
КонецФункции

#КонецЕсли

&НаКлиенте
Процедура ПослеОбновленияОфлайнОтчетов(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Статус = "Ошибка" Тогда
		ТекстСообщения = НСтр("ru = 'Произошла ошибка обновления офлайн-отчетов:'") + Символы.ПС
			+ Результат.КраткоеПредставлениеОшибки;
	Иначе
		ТекстСообщения = НСтр("ru = 'Офлайн-отчеты пользователя актуализированы.'");
	КонецЕсли
	;
	
	Сообщить(ТекстСообщения);
	
	ОбновитьСписокОфлайнОтчетов();
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьОфлайнОтчеты(Команда)
	
	ИдентификаторыСтрок = Новый Массив;
	Для Каждого СтрокаОтчет Из СписокОфлайнОтчетов Цикл
		Если СтрокаОтчет.Пометка Тогда
			ИдентификаторыСтрок.Добавить(СтрокаОтчет.ПолучитьИдентификатор());
		КонецЕсли;
	КонецЦикла;

	Если ИдентификаторыСтрок.Количество() > 0 Тогда
		УдалитьОфлайнОтчетыНаСервере(ИдентификаторыСтрок);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УдалитьОфлайнОтчетыНаСервере(ИдентификаторыСтрок)
	
	Для Каждого ИдентификаторСтроки Из ИдентификаторыСтрок Цикл
		СтрокаОтчет = СписокОфлайнОтчетов.НайтиПоИдентификатору(ИдентификаторСтроки);
		МенеджерЗаписи = РегистрыСведений.ОбновляемыеРезультатыОтчетов.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, СтрокаОтчет);
		МенеджерЗаписи.Прочитать();
		Если МенеджерЗаписи.Выбран() Тогда
			МенеджерЗаписи.Удалить();
		КонецЕсли;
		СписокОфлайнОтчетов.Удалить(СтрокаОтчет);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОфлайнОтчет(Команда)
	
	СтруктураЗаписи = Новый Структура("Пользователь,Отчет,Вариант,ХешПользовательскойНастройки,ДатаАктуальности");
	
	СтрокаОтчет = Элементы.СписокОфлайнОтчетов.ТекущиеДанные;
	ЗаполнитьЗначенияСвойств(СтруктураЗаписи, СтрокаОтчет);
	
	ОткрытьФорму("РегистрСведений.ОбновляемыеРезультатыОтчетов.ФормаЗаписи",
		Новый Структура("СтруктураЗаписи", СтруктураЗаписи), ЭтаФорма, УникальныйИдентификатор);

КонецПроцедуры

&НаКлиенте
Процедура СохранитьОфлайнОтчет(Команда)
	
	ИдентификаторыСтрок = Новый Массив;
	ИдентификаторыСтрок.Добавить(Элементы.СписокОфлайнОтчетов.ТекущаяСтрока);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеОбновленияОфлайнОтчетов", ЭтотОбъект);
	ДлительнаяОперация = Неопределено;
	ПараметрыОжидания = Неопределено;
	
	#Если МобильныйКлиент Тогда
		Выполнить("ДлительнаяОперация = СохранитьОфлайнОтчетыНаСервере(ИдентификаторыСтрок)");
		Выполнить("ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект)");
		Выполнить("ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОписаниеОповещения, ПараметрыОжидания)");
	#Иначе
		ДлительнаяОперация = СохранитьОфлайнОтчетыНаСервере(ИдентификаторыСтрок);
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОписаниеОповещения, ПараметрыОжидания);
	#КонецЕсли
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыСписокОфлайнОтчетов

&НаКлиенте
Процедура СписокОфлайнОтчетовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СтрокаОтчет = СписокОфлайнОтчетов.НайтиПоИдентификатору(ВыбраннаяСтрока);
	СтрокаОтчет.Пометка = Не СтрокаОтчет.Пометка;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокОфлайнОтчетовПередУдалением(Элемент, Отказ)
	
	ИдентификаторыСтрок = Новый Массив;
	ИдентификаторыСтрок.Добавить(Элементы.СписокОфлайнОтчетов.ТекущаяСтрока);
	
	УдалитьОфлайнОтчетыНаСервере(ИдентификаторыСтрок);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьСписокОфлайнОтчетов()

	СписокОфлайнОтчетов.Загрузить(РегистрыСведений.ОбновляемыеРезультатыОтчетов.СписокОфлайнОтчетовПользователя(
		Пользователь, ВыводитьДашбордыМЦП));

КонецПроцедуры

#КонецОбласти