
#Область ОбработчикиСобытийФормы
 
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	СтруктураЗаписи = Неопределено;
	Если Не Параметры.Свойство("СтруктураЗаписи", СтруктураЗаписи) Тогда
		РегистрыСведений.ОбновляемыеРезультатыОтчетов.СообщитьПользователю(НСтр(
			"ru = 'Просмотр офлайн отчета доступен только из списка офлайн отчетов!'"), Отказ);
		Возврат;
	КонецЕсли;

	ЗаполнитьЗначенияСвойств(ЭтаФорма, СтруктураЗаписи);

	УстановитьПривилегированныйРежим(Истина);

	Запись = РегистрыСведений.ОбновляемыеРезультатыОтчетов.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(Запись, Параметры.СтруктураЗаписи);
	Запись.Прочитать();
	Если Не Запись.Выбран() Тогда
		РегистрыСведений.ОбновляемыеРезультатыОтчетов.СообщитьПользователю(НСтр(
			"ru = 'Не найдена запись по указанным параметрам!'"), Отказ);
	ИначеЕсли Запись.ОшибкаОбновленияОтчета Тогда
		РегистрыСведений.ОбновляемыеРезультатыОтчетов.СообщитьПользователю(НСтр(
			"ru = 'Результат отчета не был сформирован!'"), Отказ);
	КонецЕсли;
	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	РезультатОтчета = Запись.РезультатОтчета.Получить();
	Если ТипЗнч(РезультатОтчета) = Тип("ТабличныйДокумент") Тогда
		ТабДокумент.Вывести(РезультатОтчета);
	Иначе
		РегистрыСведений.ОбновляемыеРезультатыОтчетов.СообщитьПользователю(НСтр(
			"ru = 'Ошибка при чтении результата отчета - некорректные данные'"), Отказ);
	КонецЕсли;

	Если Не Отказ Тогда
		Запись.ДатаПоследнегоПросмотра = ТекущаяДатаСеанса();
		Запись.Записать();
	КонецЕсли;

	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти